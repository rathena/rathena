//===== rAthena Script =======================================
//= Sample: Live Dialogue
//===== By: ==================================================
//= Lupus
//===== Last Updated: ========================================
//= 20180906
//===== Description: ========================================= 
//= An example of an NPC with live dialogue.
//= Note: This relies on Global_Functions.txt to run.
//============================================================

prontera,167,177,5	script	Luppy	1107,{
	mes "[Luppy]";

	// Say a random greeting from Global_Functions.txt
	mes callfunc("F_Hi");

	// Say a compliment according to player's gender
	// 1st string is for FEMALE, 2nd for MALE
	mes callfunc("F_SexMes","What a beautiful lady!","What a handsome man!");

	// Add some random greeting and goodbye into the menu
	if (select(callfunc("F_Hi"), callfunc("F_Bye")) != 1) {
		mes "[Luppy]";
		// Add some random goodbye from Global_Functions.txt
		mes callfunc("F_Bye");
		close;
	}

	mes "[Luppy]";
	// Give a random prize from set list of items
	if (@gotstuff){
		// Again, say stuff according to player's gender
		mes "I like "+callfunc("F_SexMes","smiling ladies!","bloody pirates!");

		// Show one of 3 emotion from the list (we added ,1 to show emotion over PLAYER's head)
		emotion callfunc("F_Rand",ET_SCISSOR,ET_CHUP,ET_GOODBOY), playerattached();
		close;
	}

	// We set a temp var to give present just once. Player can get more by relogging.
	@gotstuff = 1;

	// Get item ID from the list of presents: Apple, Mastela Fruit, Yggdrasil Seed or Orange Juice
	.@itemIDfromList = callfunc("F_Rand",512,522,608,620);

	// Again, say stuff according to player's gender
	mes "Hey, "+callfunc("F_SexMes","sister!","brother!")+" I have "+getitemname(.@itemIDfromList)+" for you!";

	// Get the item from the list
	getitem .@itemIDfromList,1;
	close;
}
