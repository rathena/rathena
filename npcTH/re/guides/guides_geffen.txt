//===== rAthena Script ======================================= 
//= Geffen Guides
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Guides for the city of Geffen.
//===== Additional Comments: ================================= 
//= 1.0 First version, Renewal guide.
//= 1.1 Fixed and optimized. [Euphy]
//= 1.2 Navigation system update. [Euphy]
//============================================================ 

geffen,203,116,4	script	Guide#01geffen::GuideGeffen	705,{
	cutin "gef_soldier",2;
	mes "[Geffen Guide]";
	mes "Welcome to ^8B4513Geffen^000000,";
	mes "the City of Magic.";
	mes "Do you need help navigating the city?";
	F_Navi("[Geffen Guide]");
	next;
	while (1) {
		switch(select("[ Main Facilities ]:[ Merchants & Helpers ]:Remove Marks from Mini-Map:Cancel")) {
		case 1:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ Magic Academy ]:[ Geffen Tower ]:[ Fountain ]:[ Tool Shop ]:[ Buy & Repair Equipment ]:[ Inn ]:[ Tavern ]:[ Christopher's Forge ]:Previous Menu")) {
				case 1:
					mes "[Geffen Guide]";
					mes "The "+F_Navi("Magic Academy","geffen,61,180","^B9062F");
					mes "handles all magic-related affairs,";
					mes "including job changes.";
					mes "Let me mark its location";
					mes "on your mini-map.";
					mes "Would you like to check any other locations?";
					viewpoint 1,61,180,0,0xFF0000;
					next;
					break;
				case 2:
					callsub L_Mark, F_Navi("Geffen Tower","geffen,120,114","^0000FF");
					viewpoint 1,120,114,1,0x0000FF;
					next;
					break;
				case 3:
					callsub L_Mark, F_Navi("Fountain","geffen,126,48","^FF5400");
					viewpoint 1,126,48,2,0xFFB400;
					next;
					break;
				case 4:
					callsub L_Mark, F_Navi("Tool Shop","geffen,44,86","^006400");
					viewpoint 1,44,86,3,0xAAFF00;
					next;
					break;
				case 5:
					mes "[Geffen Guide]";
					mes "If you'd like to ";
					mes F_Navi("Buy or Repair Equipment","geffen,99,140","^7F3300");
					mes "then this is the place you'll want to visit.";
					mes "Let me mark its location";
					mes "on your mini-map.";
					mes "Would you like to check any other locations?";
					viewpoint 1,99,140,4,0xD2691E;
					next;
					break;
				case 6:
					callsub L_Mark, F_Navi("Inn","geffen,172,174","^800080");
					viewpoint 1,172,174,5,0xDA70D6;
					next;
					break;
				case 7:
					callsub L_Mark, F_Navi("Tavern","geffen,138,138","^B9062F");
					viewpoint 1,138,138,6,0xFF0000;
					next;
					break;
				case 8:
					mes "[Geffen Guide]";
					mes F_Navi("Christopher's Forge","geffen,182,59","^0000FF")+" is";
					mes "where you can purchase";
					mes "Refining materials.";
					mes "It used to be the old Blacksmith Guild.";
					viewpoint 1,182,59,6,0x0000FF;
					next;
					break;
				case 9:
					set .@loop,0;
					break;
				}
			}
			break;
		case 2:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ Eden Teleport Officer ]:[ Kafra Employee ]:[ Pet Groomer ]:[ Chef Assistant ]:[ Dimensional Arcanist Guide ]:Previous Menu")) {
				case 1:
					callsub L_Mark, F_Navi("Eden Teleport Officers","geffen,132,66","^B9062F");
					viewpoint 1,61,180,7,0xFF0000;
					viewpoint 1,132,66,8,0xFF0000;
					next;
					break;
				case 2:
					callsub L_Mark, "^FF5400Kafra Employees^000000";
					viewpoint 1,203,123,10,0xFFB400;
					viewpoint 1,120,62,11,0xFFB400;
					next;
					break;
				case 3:
					callsub L_Mark, F_Navi("Pet Groomer","geffen,193,152","^006400");
					viewpoint 1,193,152,12,0xAAFF00;
					next;
					break;
				case 4:
					callsub L_Mark, F_Navi("Chef Assistant","geffen,196,111","^7F3300");
					viewpoint 1,196,111,13,0xD2691E;
					next;
					break;
				case 5:
					callsub L_Mark, F_Navi("Dimensional Arcanist Guide","geffen,140,196","^800080");
					viewpoint 1,140,196,14,0xDA70D6;
					next;
					break;
				case 6:
					set .@loop,0;
					break;
				}
			}
			break;
		case 3:
			mes "[Geffen Guide]";
			mes "Sure, I'll remove all marks from your mini-map.";
			mes "Is there anything else I can do for you?";
			viewpoint 2,1,1,0,0xFFFF00;
			viewpoint 2,1,1,1,0x000000;
			viewpoint 2,1,1,2,0xFF0000;
			viewpoint 2,1,1,3,0xFFFF00;
			viewpoint 2,1,1,4,0xFFFF00;
			viewpoint 2,1,1,5,0xFFFF00;
			viewpoint 2,1,1,6,0xFFFF00;
			viewpoint 2,1,1,7,0xFFFF00;
			viewpoint 2,1,1,8,0xFFFF00;
			viewpoint 2,1,1,9,0xFFFF00;
			viewpoint 2,1,1,10,0xFFFF00;
			viewpoint 2,1,1,11,0xFFFF00;
			viewpoint 2,1,1,12,0xFFFF00;
			viewpoint 2,1,1,13,0xFFFF00;
			viewpoint 2,1,1,14,0xFFFF00;
			next;
			break;
		case 4:
			mes "[Geffen Guide]";
			mes "Enjoy your stay.";
			close2;
			cutin "gef_soldier",255;
			end;
		}
	}
	end;
L_Mark:
	mes "[Geffen Guide]";
	mes "Let me mark the location of the";
	mes getarg(0)+"^000000";
	mes "on your mini-map.";
	mes "Would you like to check any other locations?";
	return;
}

geffen,118,62,0	duplicate(GuideGeffen)	Guide#02geffen	705
geffen,36,123,6	duplicate(GuideGeffen)	Guide#03geffen	705
geffen,123,203,4	duplicate(GuideGeffen)	Guide#04geffen	705
