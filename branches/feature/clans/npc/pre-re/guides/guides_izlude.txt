//===== rAthena Script =======================================
//= Izlude Guides
//===== By: ==================================================
//= kobra_k88; L0ne_W0lf
//===== Current Version: =====================================
//= 1.5a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Guides for the City of Izlude
//===== Additional Comments: =================================
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.3 Updated the guide according to how it is on iRO, thanks [MasterOfMuppets]
//=     to Muad_Dib
//= 1.4 Fitted the many lines into 1 or 2, and corrected some typos [erKURITA]
//= 1.5 Rescripted to official 10.3 standards. [L0ne_W0lf]
//= 1.5a Fixed some errors with the loop[Samuray22]
//	-Thanks to $ephiroth.
//============================================================

izlude,121,87,6	script	Guide#iz	105,{
	cutin "prt_soldier",2;
	mes "[Izlude Guide]";
	mes "Welcome to Izlude,";
	mes "Prontera's satellite city.";
	mes "If you need any guidance";
	mes "around Izlude, feel free";
	mes "to ask me at anytime.";
	set .@loop1,1;
	while(.@loop1) {
		next;
		switch(select("City Guide:Remove Marks from Mini-Map:Notice.:Cancel")) {
		case 1:
			mes "[Izlude Guide]";
			mes "Please select";
			mes "a location from";
			mes "the following menu.";
			if (.@compass_check == 0) {
				mes "Would you like me";
				mes "to mark locations";
				mes "on your Mini-Map?";
				next;
				if (select("Yes:No") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
				
				switch(select("^FF0000Swordman Association^000000:Swordman Hall:Arena:Izlude Marina:Weapon Shop:Tool Shop:Cancel")) {
				case 1:
					mes "[Izlude Guide]";
					mes "The Swordman Association";
					mes "is located on an island that is";
					mes "in west Izlude. If you're thinking of changing jobs to Swordman,";
					mes "you should check it out.";
					if (.@compass_check)
						viewpoint 1,52,140,0,0xFF0000;
					break;
				case 2:
					mes "[Izlude Guide]";
					mes "The Swordman Hall";
					mes "is located in the eastern";
					mes "island connected to Izlude.";
					if (.@compass_check)
						viewpoint 1,214,130,1,0x00FF00;
					break;
				case 3:
					mes "[Izlude Guide]";
					mes "Izlude's famous";
					mes "Arena is located at the";
					mes "northern end of Izlude.";
					if (.@compass_check)
						viewpoint 1,128,225,2,0x00FF00;
					break;
				case 4:
					mes "[Izlude Guide]";
					mes "You can find the";
					mes "Marina in the northeast";
					mes "part of Izlude. There, you can";
					mes "ride a ship which will take you";
					mes "to Alberta or Byalan Island.";
					if (.@compass_check)
						viewpoint 1,200,180,3,0xFF0000;
					break;
				case 5:
					mes "[Izlude Guide]";
					mes "You can easily";
					mes "find the Weapon Shop";
					mes "in northwest Izlude.";
					if (.@compass_check)
						viewpoint 1,111,149,4,0xFF00FF;
					break;
				case 6:
					mes "[Izlude Guide]";
					mes "The Tool Shop shouldn't";
					mes "be too hard to find in the";
					mes "northeast part of Izlude.";
					if (.@compass_check)
						viewpoint 1,148,148,5,0xFF00FF;
					break;
				case 7:
					mes "[Izlude Guide]";
					mes "Please ask me to ''Remove";
					mes "Marks from Mini-Map'' if you";
					mes "no longer wish to have the";
					mes "location marks displayed";
					mes "on your Mini-Map.";
					set .@loop2,0;
					break;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,0,0x00FF00;
			viewpoint 2,237,41,1,0x0000FF;
			viewpoint 2,46,345,2,0x00FF00;
			viewpoint 2,175,220,3,0xFF0000;
			viewpoint 2,134,221,4,0xFF0000;
			viewpoint 2,204,214,5,0xFF0000;
			set .@compass_check,0;
			break;
		case 3:
			mes "[Izlude Guide]";
			mes "Advances in sorcery and";
			mes "technology have allowed";
			mes "us to update our information";
			mes "system, enabling up to mark";
			mes "locations on your Mini-Map";
			mes "for easier navigation.";
			next;
			mes "[Izlude Guide]";
			mes "Your Mini-Map is located";
			mes "in the upper right corner";
			mes "of the screen. If you can't";
			mes "see it, press the Ctrl + Tab";
			mes "keys or click the ''Map'' button in your Basic Info Window.";
			next;
			mes "[Izlude Guide]";
			mes "On your Mini-Map,";
			mes "click on the ''+'' and ''-''";
			mes "symbols to zoom in and";
			mes "our of your Mini-Map. We";
			mes "hope you enjoy your travels";
			mes "here in the city of Izlude.";
			break;
		case 4:
			mes "[Izlude Guide]";
			mes "Okay then, feel";
			mes "free to come to me";
			mes "if you ever feel lost";
			mes "around Izlude, alright?";
			close2;
			set .@loop1,0;
			break;
		}
	}
	cutin "prt_soldier",255;
	end;
}
