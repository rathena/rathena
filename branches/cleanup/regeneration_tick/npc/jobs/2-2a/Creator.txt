//===== rAthena Script ======================================= 
//= Advance Class jobchanger after kRO Normals.
//===== By: ==================================================
//= Nana
//= finished, optimized and tested by Lupus
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Advance Class jobchanger after kRO Normals.
//===== Additional Comments: ================================= 
//= 1.1 Made all into functions, additional checks, etc. [Lupus]
//= 1.2 Made numbers into constants. [Vicious]
//= 1.3 Now saves/restores all the quest skills [Lupus]
//============================================================ 

valkyrie,53,50,3	script	Biochemist#Valkyrie	122,{
	if (ADVJOB == 0 || Upper != 1) {
		set .@karma_d,rand(1,10);
		if (.@karma_d > 4) {
			mes "[Biochemist]";
			mes "Congratulations.";
			mes "Honor to the warriors!";
			close;
		}
		mes "[Biochemist]";
		mes "It's strange that";
		mes "someone like me is here.";
		mes "But even someone skilled";
		mes "in the ways of science";
		mes "can manage to be a hero.";
		next;
		mes "[Biochemist]";
		mes "In this instance,";
		mes "it's not necessarily";
		mes "the means I've used, but";
		mes "the ends for which I've";
		mes "fought that earned me";
		mes "a place in Valhalla...";
		close;
	}
	else if (ADVJOB == Job_Creator && Class == Job_Merchant_High && JobLevel > 39) {
		mes "[Biochemist]";
		mes "Yes...";
		mes "It's about time.";
		mes "We need more geniuses";
		mes "like you on Rune-Midgard.";
		next;
		mes "[Biochemist]";
		mes "Would you like to";
		mes "become a Biochemist?";
		next;
		if (select("No.:Yes.") == 1) {
			mes "[Biochemist]";
			mes "When you're ready,";
			mes "feel free to come back.";
			mes "Honor to the warriors!";
			close;
		}
		if (SkillPoint) {
			mes "[Biochemist]";
			mes "It is still possible for you to learn more skills. Please use";
			mes "all of your remaining Skill Points before returning to me.";
			close;
		}
		jobchange Job_Creator;
		set ADVJOB,0;
		mes "[Biochemist]";
		mes "Congratulations!";
		mes "As a Biochemist,";
		mes "I hope you use your";
		mes "vast knowledge for the";
		mes "right purposes.";
		close;
	}
	else {
		if (Class == Job_Creator && MISC_QUEST & 64 && getskilllv("AM_BIOETHICS") == 0) {
			mes "[Biochemist]";
			mes "Ah, have you come to";
			mes "retrieve the memories";
			mes "lost to you? Yes, you";
			mes "must be here for the";
			mes "secrets of life that";
			mes "were once yours...";
			next;
			if (select("Yes:No") == 1) {
				mes "[Biochemist]";
				mes "Close your eyes and";
				mes "put your mind at rest.";
				mes "We will return to your";
				mes "past to recollect the";
				mes "fragments of your lost";
				mes "memories.";
				next;
				mes "[Biochemist]";
				mes "When you open your eyes,";
				mes "you will clearly remember";
				mes "the secret of life. You will";
				mes "also remember the weight of";
				mes "responsibility in using these";
				mes "secrets for the right ends...";
				next;
				skill "AM_BIOETHICS",1,SKILL_PERM;
				mes "[Biochemist]";
				mes "Open your eyes...";
				mes "Now that you have";
				mes "remembered how to";
				mes "create artificial life, I only";
				mes "ask that you treat all of your";
				mes "creations with respect.";
				close;
			}
			mes "[Biochemist]";
			mes "If you wish to";
			mes "retrieve your lost";
			mes "memories, please";
			mes "come back to me.";
			mes "The secret to creating";
			mes "life is no trifling thing...";
			close;
		}
		mes "[Biochemist]";
		mes "Welcome";
		mes "to Valhalla,";
		mes "the Hall of Honor.";
		next;
		mes "[Biochemist]";
		mes "Please make";
		mes "yourself comfortable";
		mes "while you are here.";
		mes "Honor to the warriors!";
		close;
	}
}

