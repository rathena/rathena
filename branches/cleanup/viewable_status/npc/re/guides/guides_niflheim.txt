//===== rAthena Script ======================================= 
//= Niflheim Guide
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Guide for the city of Niflheim.
//===== Additional Comments: ================================= 
//= 1.0 First version, Renewal guide.
//= 1.1 Optimized. [Euphy]
//= 1.2 Navigation system update. [Euphy]
//============================================================ 

niflheim,201,187,3	script	Niflheim Guide#01nif	849,{
	mes "[Niflheim Guide]";
	mes "Welcome to.. ^8B4513Niflheim^000000...";
	mes "the Realm of the Dead...";
	mes "Do you need help navigating the realm...?";
	if (F_Navi() == 0) {
		next;
		mes "[Niflheim Guide]";
		mes "When you click on the.. ^B9062F[location name]^000000... you'll receive the most advanced ^B9062FNavigation^000000 services...!";
	}
	while (1) {
		next;
		switch(select("[ Tool Shop ]:[ Pub ]:[ Weapon Shop ]:[ Witch's Castle ]:Remove Marks from Mini-Map:Cancel")) {
		case 1:
			callsub L_Mark, F_Navi("Tool Shop","niflheim,218,196","^0000FF");
			viewpoint 1,218,196,0,0x0A82FF;
			break;
		case 2:
			callsub L_Mark, F_Navi("Pub","niflheim,189,208","^006400");
			viewpoint 1,189,208,1,0xAAFF00;
			break;
		case 3:
			callsub L_Mark, F_Navi("Weapon Shop","niflheim,218,170","^800080");
			viewpoint 1,218,170,2,0xDA70D6;
			break;
		case 4:
			callsub L_Mark, F_Navi("Witch's Castle","niflheim,255,195","^FF1493");
			viewpoint 1,255,195,3,0xFF1493;
			break;
		case 5:
			mes "[Niflheim Guide]";
			mes "Sure, I'll remove all marks from your mini-map...";
			mes "Is there anything else I can do for you...?";
			viewpoint 2,1,1,0,0xFFFFFF;
			viewpoint 2,1,1,1,0xFFFFFF;
			viewpoint 2,1,1,2,0xFFFFFF;
			viewpoint 2,1,1,3,0xFFFFFF;
			break;
		case 6:
			mes "[Niflheim Guide]";
			mes "......Bye~!..";
			close;
		}
	}
	end;
L_Mark:
	mes "[Niflheim Guide]";
	mes "Let me mark the location of the";
	mes getarg(0)+"^000000...";
	mes "on your mini-map.";
	mes "Would you like to check any other locations?";
	return;
}
