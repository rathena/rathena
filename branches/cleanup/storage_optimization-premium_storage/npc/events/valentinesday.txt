//===== rAthena Script =======================================
//= Valentine Event Script
//===== By: ==================================================
//= Muad_Dib (Prometheus Project)
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Valentine's Day Event
//===== Additional Comments: =================================
//= 07/06/05 : Added 1st Version. [Muad_Dib]
//= 08/28/05 : Ver. 1.01 Added a missing close; [Muad_Dib]
//= Converted to rAthena format by Dr.Evil
//= 1.1 Cleaning. [Euphy]
//============================================================

// Stephen - Valentine Event Chocolate seller
//============================================================
alberta,26,243,4	script	Stephen	58,{
	mes "[Stephen]";
	mes "Guess what I've got?";
	mes "A tasty treat not easily found in Rune-Midgard....";
	next;
	mes "[Stephen]";
	mes "Chocolate!";
	mes "That's right, don't you love chocolate.... I do.";
	mes "And you are in luck, because I'm selling them for only 5,000 zeny a piece!";
	next;
	if(select("I want some chocolate!:No thanks.")==2) {
		mes "[Stephen]";
		mes "You don't want any chocolate?";
		mes "I'm telling you! You'll regret it!";
		mes "You better get some now... you won't come across Chocolate like this ever again!";
		mes "Think it over and visit me again sometime.";
		close; }
	mes "[Stephen]";
	mes "Hah!";
	mes "I knew it!";
	mes "But I can't sell you more then 5 at once... but, if you really need more....";
	mes "you can come back again.";
	mes "So how many do you want?";
	next;
	input .@i;
	if (.@i <= 0) close;
	mes "[Stephen]";
	if (.@i > 5)  {
		mes "I'm sorry, but I can't give you that many.";
		close; }
	if (Zeny < .@i*5000) {
		mes "I'm sorry, but it seems you can't afford to buy these off me.";
		close; }
	set Zeny, Zeny - .@i*5000;
	getitem 558,.@i;
	mes "There you go!";
	mes "You can give that to someone as a gift, or enjoy it yourself!";
	mes "Mmm... sweet chocolate...";
	mes "Visit me anytime...!";
	close;
}

// Jainie -- Gives information about Valentine Event
//============================================================
alberta,29,243,4	script	Jainie	53,{
	mes "[Jainie]";
	mes "You know what? The chocolate that my boyfriend sells are from me!";
	mes "I made them by myself.";
	next;
	mes "[Jainie]";
	mes "You know ... In cetain countries, there's a tradition of presenting chocolates to a person that you love...";
	mes "They call it, ^3355FFValentine's Day^000000.";
	next;
	mes "[Jainie]";
	mes "So I gave him my delicious chocolate...";
	mes "And then he made me cook a lot more...";
	mes "And now he is selling them to everyone.";
	mes "I guess he really enjoyed it.";
	mes "But, I do feel good when people buy something I have made.";
	next;
	mes "[Jainie]";
	mes "It would be great if you bought some too...";
	mes "I will be making chocolates for a while so...";
	close;
}

// Carl Orleans -- Valentine Event Chocolate maker
//============================================================
prt_castle,54,34,4	script	Carl Orleans	47,{
	mes "[Carl Orleans]";
	mes "Yes?";
	next;
	if(select("I want some hand made chocolate...:I'm lost, sorry to bother you.")==2) {
		mes "[Carl Orleans]";
		mes "Oh... well, if you want me to make some of my special Hand Made Chocolate....";
		mes "You will need to give me at least ^0000FF 3 Chocolates^000000.";
		next;
		mes "[Carl Orleans]";
		mes "That's right, only ^0000FF 3 Chocolates^000000";
		mes "Bring them to me and you'll get what you came for.";
		next;
		mes "[Carl Orleans]";
		mes "See You.";
		close; }
	mes "[Carl Orleans]";
	mes "Well, I just might be able to fulfill your needs...";
	next;
	mes "[Carl Orleans]";
	if (countitem(558) < 3) {
		mes "I'm sorry, you don't have enough Chocolate Bars to do this.";
		close; }
	delitem 558,3;
	mes "You got 3 pieces of pure chocolate, I see.";
	mes "Give them to me...";
	next;
	mes "[Carl Orleans]";
	mes "Ok, now I will only create my special hand made chocolates if you promise to use it wisely.";
	next;
	mes "[Carl Orleans]";
	mes "....Hmmmmmm.....";
	mes "Well...";
	next;
	mes "[Carl Orleans]";
	getitem 559,1;
	mes "Here.";
	mes "I hope you give it to someone special, because its a special chocolate.";
	mes "As you know... only I can create this.";
	next;
	mes "[Carl Orleans]";
	mes "Enjoy.";
	close;
}