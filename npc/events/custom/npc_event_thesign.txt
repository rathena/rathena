//==============================================================================
// Temporary The Sign Script [Playtester]
//==============================================================================
// Version 1.0
//==============================================================================
// How it works:
// - to activate the fountain you need to have at least 1000000 Zeny
// - if you throw 250000 Zeny into the fountain the path to Geffenia 
//   will open for 30 seconds
// - the requirements will later be replaced by "Finishing The Sign Quest"
//==============================================================================
// Special thanks to Dr.Evil, Tharis, Blaze
//==============================================================================
geffen.gat,119,56,0	script	#gefeniagate	139,{
OnInit:
	disablenpc "gefeniawarp";
	end;
On_Start:
	misceffect 247;
	initnpctimer;
	end;
OnTimer10000:
	misceffect 247;
	donpcevent "Fountain::On_Effect";
	end;
OnTimer20000:
	misceffect 247;
	donpcevent "Fountain::On_Effect";
	end;
OnTimer30000:
	disablenpc "gefeniawarp";
	mapannounce "geffen.gat","The gate to Geffenia has closed.",0;
	stopnpctimer;
	end;
}

geffen.gat,119,56,0	script	gefeniawarp	45,1,1{
	set @warp,rand(4);
	if (@warp==0) warp "gefenia01.gat",58,169;
	if (@warp==1) warp "gefenia02.gat",116,115;
	if (@warp==2) warp "gefenia03.gat",130,206;
	if (@warp==3) warp "gefenia04.gat",133,88;
	end;
}

geffen.gat,119,48,0	script	Fountain	111,{
	mes "This is the";
	mes "Geffen Fountain.";
	if (Zeny >= 1000000) goto L_1;
	close;
L_1:
	mes "The Zeny in your pocket begins to glow.";
	mes "Do you want to throw ^FF0000250000 Zeny^000000 into the fountain?";
	next;
	menu "Yes",L_2,"No",-;
	close;
L_2:
	mes "When you threw the money into the fountain a bright light appears.";
	set Zeny, Zeny-250000;
	enablenpc "gefeniawarp";
	donpcevent "#gefeniagate::On_Start";
	misceffect 247;
	mapannounce "geffen.gat","With light, the gate to Geffenia opens.",0;
	close;
On_Effect:
	misceffect 247;
	end;
}
