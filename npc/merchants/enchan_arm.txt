//===== rAthena Script ======================================= 
//= Armor Enchanter
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Add a +1, +2, or +3 random stat to a specified armor.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//= 1.1 Corrected typo (Sit -> Suit) (bugreport:4586)
//= 1.2 Optimized. [Euphy]
//============================================================ 

prontera,165,60,2	script	Apprentice Craftsman	73,{
	mes "[Apprentice Craftsman]";
	if (Zeny > 399999) {
		mes "I've been studying ways to enhance an armor to maximize it's capability.";
		next;
		mes "[Apprentice Craftsman]";
		mes "Enchanting is an awesome skill that infuses a mysterious status power into the armor's hidden socket.";
		next;
		mes "[Apprentice Craftsman]";
		mes "However, you have to keep in mind that if there are two armors of the same kind in your possession, the Enchantment will be applied in the order they are placed in your inventory.";
		next;
		mes "[Apprentice Craftsman]";
		mes "In that case, the Enchantment may be applied to an item which you didn't mean to Enchant. So just bring ^5555ffONE Armor^000000 you want enchanted to be safe...";
		next;
		mes "[Apprentice Craftsman]";
		mes "I'm not responsible for what would happen if you have more than one of the same kind in your inventory.";
		next;
		switch(select("Non Slotted Armor.:Slotted Armor.:High Grade Armor.:Maybe next time.")) {
			case 1:
				setarray .@Items[0],2307,2309,2314,2316,2321,2325,2327,2328,2330,2332,2334,2335,2341,2344,2346,2348,2350,2337,2386,2394,2395,2396;
				set .@j,50;
				break;
			case 2:
				setarray .@Items[0],2311,2318,2319,2320,2308,2310,2315,2317,2322,2324,2326,2331,2333,2336,2342,2345,2347,2349,2351;
				set .@j,55;
				set .@k,1;
				break;
			case 3:
				setarray .@Items[0],2364,2365,2391,2374,2375,2376,2377,2378,2379,2380,2381,2382,2387,2388,2389,2390;
				set .@j,60;
				break;
			case 4:
				mes "[Apprentice Craftsman]";
				mes "Please come back when you have any interest in enchanting your armor.";
				close;
		}
		set .@menu$,"";
		for(set .@i,0; .@i<getarraysize(.@Items); set .@i,.@i+1)
			set .@menu$, .@menu$+getitemname(.@Items[.@i])+((.@k)?" [1]":"")+":";
		callsub S_EnchantArmor, .@Items[select(.@menu$)-1], .@j;
		end;
	}
	else {
		mes "I am in charge of Enchanting Armors. Simply put, I've been studying ways to power-up armor.";
		next;
		mes "[Apprentice Craftsman]";
		mes "If by any chance, you would want to enchant your armor, bring me 400,000 zeny and the armor you want to enchant and you are all set to go.";
		close;
	}

S_EnchantArmor:
	set .@itemid,getarg(0);
	set .@failrate,getarg(1);
	mes "[Apprentice Craftsman]";
	if ((countitem(.@itemid) > 0) && (countitem(.@itemid) < 2)) {
		mes "Socket enchant wil cost you 400,000 zeny. And there will be a random option enchanted. Of course, there is a chance of breaking your armor.";
		next;
		mes "[Apprentice Craftsman]";
		mes "First and most importantly.";
		mes "^ff5555Existing Refine Level of the Armor";
		mes "and Cards will be GONE.^000000";
		mes "Do you still want to try an Enchant?";
		next;
		if(select("Hmm... Let me think it over.:Go ahead.")==1) {
			mes "[Apprentice Craftsman]";
			mes "Well, I can't blame you. Safety first, eh?";
			mes "Now you have a nice day.";
			close;
		}
		mes "[Apprentice Craftsman]";
		mes "Quite of an adventurer huh? Well, shall we?";
		close2;
		specialeffect2 EF_MAPPILLAR;
		if (Zeny < 400000) {
			mes "[Apprentice Craftsman]";
			mes "Sorry, but you don't have enough zeny.";
			close;
		}
		progressbar "ffff00",7;
		set .@enc_ran,rand(1,.@failrate);
		set zeny,zeny-400000;
		delitem .@itemid,1;
		if (.@enc_ran == 1) set .@addpart,4702;
		else if (.@enc_ran == 2) set .@addpart,4712;
		else if (.@enc_ran == 3) set .@addpart,4722;
		else if (.@enc_ran == 4) set .@addpart,4732;
		else if (.@enc_ran == 5) set .@addpart,4742;
		else if (.@enc_ran == 6) set .@addpart,4752;
		else if ((.@enc_ran > 6) && (.@enc_ran < 9)) set .@addpart,4701;
		else if ((.@enc_ran > 8) && (.@enc_ran < 11)) set .@addpart,4711;
		else if ((.@enc_ran > 10) && (.@enc_ran < 13)) set .@addpart,4721;
		else if ((.@enc_ran > 12) && (.@enc_ran < 15)) set .@addpart,4731;
		else if ((.@enc_ran > 14) && (.@enc_ran < 17)) set .@addpart,4741;
		else if ((.@enc_ran > 16) && (.@enc_ran < 19)) set .@addpart,4751;
		else if ((.@enc_ran > 18) && (.@enc_ran < 22)) set .@addpart,4700;
		else if ((.@enc_ran > 21) && (.@enc_ran < 25)) set .@addpart,4710;
		else if ((.@enc_ran > 24) && (.@enc_ran < 28)) set .@addpart,4720;
		else if ((.@enc_ran > 27) && (.@enc_ran < 31)) set .@addpart,4730;
		else if ((.@enc_ran > 30) && (.@enc_ran < 34)) set .@addpart,4740;
		else if ((.@enc_ran > 33) && (.@enc_ran < 37)) set .@addpart,4750;
		else {
			specialeffect2 EF_PHARMACY_FAIL;
			mes "[Apprentice Craftsman]";
			mes "Well that's too bad.";
			mes "The requested equipment has failed to enchant.";
			close;
		}
		getitem2 .@itemid, 1, 1, 0, 0, 0, 0, 0, .@addpart;
	}
	else {
		mes "Hmm? There's nothing to be enchanted!";
		mes "Please come back with just ONE equipment to be enchanted.";
		close;
	}
}