//===== eAthena Script ======================================= 
//= Inn Npcs
//===== By: ================================================== 
//= Darkchild (1.1)
//= Playtester (1.2)
//===== Current Version: ===================================== 
//= 1.4
//===== Compatible With: ===================================== 
//= eAthena 1.0+
//===== Description: ========================================= 
//= Inn Npcs, Save and Heal
//=====================================================
//= variables:
//= arg0 - name of npc
//= arg1 - name of the inn
//= arg2 - map to save at
//= arg3, arg4 - x and y cord. to save at
//= @cost - cost of renting a room
//===== Additional Comments: ================================= 
//= 1.1 Blind Effect By Kobra_k88 (Taken from his old script)
//= I added it to all of them though [Darkchild]
//= 1.1a Minor bug fixes and optimizations. Switched from @variables 
//= to arguments.[kobra_k88]
//= 1.2 Rewrote inn script [Playtester]
//= 1.3 Added (finally) Rachel Inn Maid. Official warp
//=	  and save points[erKURITA]
//= 1.4 Added Hugel inn [erKurita]
//============================================================ 


//======================== Prontera ===================================
// West Side Inn -------------------
prt_in.gat,244,135,2	script	Inn Maid::Sammy	53,{

	callfunc "F_InnMaid","[Sammy]","West Inn","prt_in.gat",238,130;
	warp "prt_in.gat",237,102;
}
// East Side Inn ------------------
prt_in.gat,61,141,2	script	Inn Maid::Ahlma#01	53,{

	callfunc "F_InnMaid","[Ahlma]","East Inn","prt_in.gat",64,136;
	warp "prt_in.gat",49,173;
}

//======================== Alberta ====================================
// North --------------------------
alberta_in.gat,32,142,5	script	Inn Maid::Moira	53,{

	callfunc "F_InnMaid","[Moira]","North Inn","alberta_in.gat",26,138;
	warp "alberta_in.gat",18,188;
}
// South -----------------------------
alberta_in.gat,55,142,8	script	Inn Maid::Tina	53,{

	callfunc "F_InnMaid","[Tina]","South Inn","alberta_in.gat",60,140;
	warp "alberta_in.gat",68,188;
}

//====================== Geffen ======================================
geffen_in.gat,70,64,5	script	Inn Maid::Betty	53,{

	callfunc "F_InnMaid","[Betty]","Geffen Inn","geffen_in.gat",74,60;
	warp "geffen_in.gat",106,106;
}

//======================= Payon ======================================
payon_in01.gat,131,62,5	script	Inn Maid::SunHee	53,{

	callfunc "F_InnMaid","[Sun Hee]","Payon Inn","payon_in01.gat",132,56;
	warp "payon_in01.gat",140,15;
}

//======================== Morocc ====================================
// North East --------------------------
morocc_in.gat,147,138,5	script	Inn Maid::Suzie	53,{

	callfunc "F_InnMaid","[Suzie]","North East Inn","morocc_in.gat",142,140;
	warp "morocc_in.gat",174,144;
}
// South --------------------------------
morocc_in.gat,80,100,5	script	Inn Maid::Shala	53,{

	callfunc "F_InnMaid","[Shala]","South Inn","morocc_in.gat",78,95;
	warp "morocc_in.gat",74,128;
}

//======================== Rachel ====================================
ra_in01.gat,376,69,4	script	Inn Keeper	931,{

	callfunc "F_InnMaid","[Annie]","Rachel Inn","ra_in01.gat",375,58;
	warp "ra_in01.gat",384,128;
}

//======================== Hugel ====================================
hu_in01.gat,246,107,3	script	Inn Maid::Receptionist	53,{

	callfunc "F_InnMaid","[Receptionist]","Hugel Inn","hu_in01.gat",263,95;
	warp "hu_in01",267,5;
}

//======================= Inn Function ==============================
function	script	F_InnMaid	{
	mes getarg(0);
	mes "Welcome to '" + getarg(1) + "'.";
	mes "How may I help you?";
	next;
	menu "Save",Msave, "Take a Rest -> 5000 zeny",Mrent, "Cancel",Mend;

	Msave:
		mes getarg(0);
		mes "Your respawn point";
		mes "has been saved.";
		mes "Thank you,";
		mes "please come again.";
		savepoint getarg(2),getarg(3),getarg(4);
		close;
	Mrent:
		mes getarg(0);
		if(Zeny < 5000){
			mes "I'm sorry, but the service charge";
			mes "is 5,000 zeny. Please make sure";
			mes "that you have enough money to check";
			mes "in next time, okay?";
			close;
		}
		set Zeny,Zeny - 5000;
		percentheal 100,100;
		mes "Thank you.";
		mes "I hope you";
		mes "enjoy your rest~";
		close2;
		return;

	Mend:
		mes getarg(0);
		mes "I am waiting for a job to do.";
		close;
}
