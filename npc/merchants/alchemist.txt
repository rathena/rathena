//===== eAthena Script ======================================= 
//= Alchemist Shop
//===== By: ================================================== 
//= eAthena Team
//===== Current Version: ===================================== 
//= 1.4
//===== Compatible With: ===================================== 
//= eAthena 1.0+
//===== Description: ========================================= 
//= Selling Alchemist Materials and Manuals
//===== Additional Comments: ================================= 
//= 1.1 fixed Medicine Bowl issue, thanx 2 MasterOfMuppets
//= 1.2 Reddozen's fixes of typos. added optional Elemental
//= Potion Guide. [Lupus]
//= 1.3 Deleted Elemental Potions Guide due to original quest [Lupus]
//= 1.4 Alchemists can now purchase 2000 Medicine Bowls at once. [SinSloth]
//============================================================ 


alde_alche,24,188,3	script	Gever AI Sharp	740,{
	mes "[Gever AI Sharp]";
	mes "Welcome to the Alchemist Union.";
	mes "How can I assist you today?";
	next;
	menu "Purchase materials.",M_Material, "Purchase a production manual.",M_Manual, "Cancel Deal.", M_Bye;

	M_Material:
		mes "[Gever AI Sharp]";
		mes "What would you like?";
		next;
		menu "Medicine Bowl - 8z",-,"Cancel.",M_Bye;

			mes "[Gever AI Sharp]";
			mes "How many do you want?";	
			mes "Enter '0' if you want to quit.";
			next;
			input @num;
			if((@num < 1) || (@num > 2000)) goto M_Bye;
			set @price,@num * 8;
			mes "[Gever AI Sharp]";
			if(Zeny < @price) goto sL_NoZeny;
			if (checkweight(7134,@num) == 0) goto sL_OverW;
			set Zeny,Zeny-@price;
			getitem 7134,@num;//Items: Medicine_Bowl,
			goto L_Bye;
	M_Manual:
		mes "[Gever AI Sharp]";
		mes "What do you need?";
		mes "Manuals are generally 100,000 zeny.";
		mes "Except for a couple of special manuals.";
		next;
		menu 	"Potion Creation Guide",sM_Pot,	"Alcohol Creation Guide",sM_Alc, "Bottle Grenade Creation Guide",sM_Gren, 
			"Acid Bottle Creation Guide",sM_Acid,	"Plant Bottle Creation Guide",sM_Plant, "Marine Sphere Bottle Creation Guide",sM_Mar,
			"Glistening Coat Creation Guide",sM_Coat, "Condensed Potion Creation Guide",sM_Con, "Cancel Deal.",M_Bye;
		sM_Pot:
			set @itemid,7144;
			set @price,100000;
			set @itemname$,"Potion";
			goto L_Choice;
		sM_Alc:
			set @itemid,7127;
			set @price,100000;
			set @itemname$,"Alchohol";
			goto L_Choice;
		sM_Gren:
			set @itemid,7128;
			set @price,100000;
			set @itemname$,"Bottle Grenade";
			goto L_Choice;
		sM_Acid:
			set @itemid,7129;
			set @price,100000;
			set @itemname$,"Acid Bottle";
			goto L_Choice;
		sM_Plant:
			set @itemid,7130;
			set @price,100000;
			set @itemname$,"Plant Bottle";
			goto L_Choice;
		sM_Mar:
			set @itemid,7131;
			set @price,100000;
			set @itemname$,"Marine Sphere Bottle";
			goto L_Choice;
		sM_Coat:
			set @itemid,7132;
			set @price,100000;
			set @itemname$,"Glistening Coat";
			goto L_Choice;
		sM_Con:
			set @itemid,7133;
			set @price,240000;
			set @itemname$,"Condensed Potion";
			goto L_Choice;
//		sM_Ele:
//			set @itemid,7434;
//			set @price,240000;
//			set @itemname$,"Elemental Potion";
//			goto L_Choice;

	L_Choice:
		mes "[Gever AI Sharp]";
		mes "An " + @itemname$ + " Creation Guide??";
		mes "That will be "+@price/1000+",000 zeny.";
		next;
		menu "Purchase.",-,"Quit.",M_Bye;

			mes "[Gever AI Sharp]";
			if(Zeny < @price) goto sL_NoZeny;	
			if (checkweight(@itemid,1) == 0) goto sL_OverW;
			set Zeny,Zeny-@price;
			getitem @itemid,1;			
		L_Bye:
			mes "Thank you for buying!";
			mes "Come again.";
			close;

			sL_NoZeny:
				mes "You don't seem to have enough money.";
				close;
			sL_OverW:
				mes "Hmm.... it seems that you are overweight...";
				close;


	M_Bye:
		mes "[Gever AI Sharp]";
		mes "Then come again next time.";
		close;
}
