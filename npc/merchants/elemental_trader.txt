//===== rAthena Script ======================================= 
//= Elemental Stone Trader
//===== By: ================================================== 
//= rAthena Team & MasterOfMuppets
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Trade impure types of elemental stones for pure ones.
//===== Additional Comments: ================================= 
//= 1.0 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.1 Optimized. [Euphy]
//============================================================ 

ama_in02,61,27,7	script	Alchemist#ama	749,{
	mes "[Laspuchin Gregory]";
	mes "KeekeekeeKeheheh.";
	mes "This is amazing!";
	mes "The results are extraordinary!!";
	next;
	mes "[Laspuchin Gregory]";
	mes "Using my skills in this distant";
	mes "land was unexpected...";
	mes "Keheheh... The lord of palace";
	mes "was quite accomodating.";
	next;
	mes "[Laspuchin Gregory]";
	mes "Oops, I better be careful...";
	mes "If the guild finds out my";
	mes "location, stupid Myster will";
	mes "get mad at me. Kehehehkeh...";
	next;
	mes "[Laspuchin Gregory]";
	mes "What? Wanna say something?";
	mes "If you are here for tourism,";
	mes "enjoy your day off, then go back to your hometown.";
	mes "Keekeekee... Or else,";
	mes "I will let you taste my acid bottle...!";
	next;
	if (select("Please, excuse me:Do you need help?") == 1) {
		mes "[Laspuchin Gregory]";
		mes "Keheheh... Did you hear";
		mes "what I said? It would be";
		mes "better to forget...";
		mes "Keekeekeekeekee...";
		close;
	}
	mes "[Laspuchin Gregory]";
	mes "Help, eh?...";
	mes "Now that I think about it...";
	mes "I need some items right now...";
	mes "Keeheeheekeehee...";
	next;
	mes "[Laspuchin Gregory]";
	mes "Sir Laspuchin needs some";
	mes "enchant stones for an experiment.";
	mes "I will use it efficiently...";
	mes "Do you have them now?";
	next;
	if (select("Nope:I got some") == 1) {
		mes "[Laspuchin Gregory]";
		mes "Then, find 8 stones of one kind.";
		mes "If you bring 8 of one kind of";
		mes "enchant stones, I will change it";
		mes "to a better one...";
		next;
		mes "[Laspuchin Gregory]";
		mes "Then, find 8 stones of one kind.";
		mes "not the round gemstones but";
		mes "enchant stones.";
		mes "If you bring the wrong stones,";
		mes "I will throw a flame bottle!";
		next;
		mes "[Laspuchin Gregory]";
		mes "Bring me stones, slave~!!";
		mes "And keep your promise! Kehehehkehkeh...";
		close;
	}
	mes "[Laspuchin Gregory]";
	mes "Uh-huh, what did you bring?";
	next;
	setarray .@Items[1],995,997,994,996;
	set .@i, .@Items[select("Mystic Frozen:Great Nature:Flame Heart:Rough Wind")];
	mes "[Laspuchin Gregory]";
	if (countitem(.@i) > 7) {
		mes "I will take 8 of them and";
		mes "give you an enchant stone.";
		mes "How's that sound?";
		mes "Keheheh.....";
		next;
		mes "[Laspuchin Gregory]";
		mes "HeeHee, what do you want? Choose one!";
		next;
		set .@menu$, "";
		for(set .@j,1; .@j<5; set .@j,.@j+1) {
			if (.@Items[.@j] != .@i) set .@menu$, .@menu$+getitemname(.@Items[.@j]);
			set .@menu$, .@menu$+":";
		}
		set .@menu$, .@menu$+"Cancel the trade";
		set .@j, select(.@menu$);
		mes "[Laspuchin Gregory]";
		if (.@j == 5) {
			mes "Oh, well.";
			mes "Don't tell anyone about my location...";
			mes "Keheheh...after all, you did promise~";
			close;
		}
		else if (.@j == 4) {
			if (countitem(.@i) > 11) {
				delitem .@i,12;
				getitem 996,1;
				mes "Kehhehheh, You know something?";
				mes "I don't have many of those";
				mes "so I'm going to take ^0000FF12^000000 of yours, okay?";
				mes "Of course, it is okay.";
				mes "It is an honor to help Laspuchin!";
				close;
			}
			mes "Keheheh, You know something.";
			mes "I don't have many of these so";
			mes "I can't just get 8 of yours.";
			mes "If you want to change them to Rough Wind,";
			mes "bring me 4 more stones. 8 + 4 = 12...";
			mes "Requires ^0000FF12^000000 stones.";
			next;
			mes "[Laspuchin Gregory]";
			mes "Bring me stones, slave~!!";
			mes "And keep your promise! Kehheheheheh...";
			close;
		}
		else {
			delitem .@i,8;
			getitem .@Items[.@j],1;
			mes "Keheheh! You've chosen a good one!";
			mes "Use it well...";
			mes "Bring me other stones if you have them!";
			close;
		}
	} else if (countitem(.@i)) {
		mes "You can't help Laspuchin with just a couple";
		mes "of enchant stones... I said 8!";
		mes "Bring me just 8 stones!";
		mes "Keheheh.....";
		next;
		mes "[Laspuchin Gregory]";
		mes "Bring me stones, slave~!!";
		mes "And keep your promise! Keheheheheh...";
		close;
	}
	mes "Keheheh~ Check your pockets";
	mes "before you tell me";
	mes "how foolish you are.....";
	next;
	mes "[Laspuchin Gregory]";
	mes "Bring me stones! You said you are going to help me!";
	mes "Keep your promises! Kehehehkehkeh...";
	close;
}
