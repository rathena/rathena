//===== rAthena Script =======================================
//= Advanced Refiner
//===== Description: =========================================
//= [Official Conversion]
//= Refiner that uses Enriched ores to increase upgrade success.
//= - Dialog is only partly official to iRO.
//= - Uses the iRO position for this NPC.
//===== Changelog: ===========================================
//= 1.0 First Version. [L0ne_W0lf]
//= 1.1 Fixed a weird carriage return. o_o [L0ne_W0lf]
//= 1.2 Optimizing refine method [Zephyrus]
//= 1.3 Typo fixes [Yommy]
//= 1.4 Removed unnecessary dialogs [Zephyrus]
//= 1.4a Added 'disable_items' command. [Euphy]
//= 1.4b Fixed coordinates. [Euphy]
//= 1.5 Some official script updates. [Euphy]
//= 1.6 Added VIP features. [Euphy]
//= 1.7 Removed re-roll behavior. [Secret]
//============================================================

payon,157,146,6	script	Suhnbi#cash	85,{
	disable_items;
	mes "[Suhnbi]";
	mes "��Ҥ�� Armsmith";
	mes "��Ҩк͡������ ��Ң������ö�պǡ,";
	mes "���ظ �ش���� ��Тͧ��������";
	mes "��ҵ�ͧ��õպǡ����.";
	next;

	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for(set .@i,1; .@i<=10; set .@i,.@i+1) {
		if (getequipisequiped(.@indices[.@i])) {
			set .@menu$, .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			set .@equipped,1;
		}
		set .@menu$, .@menu$ + ":";
	}
	if (.@equipped == 0) {
		mes "[Suhnbi]";
		mes "������Դ��Ң�Ҩеպǡ���������������...";
		close;
	}
	set .@part, .@indices[ select(.@menu$) ];

	if (!getequipisequiped(.@part)) //custom check
		close;
	if (!getequipisenableref(.@part)) {
		mes "[Suhnbi]";
		mes "��Ҫ�ҧ�����. ����������ö�պǡ����͡.";
		close;
	}
	if (getequiprefinerycnt(.@part) >= 10) {
		mes "[Suhnbi]";
		mes "����... �ѹ�ʹ��������������. ������Դ��Ң�Ҩеպǡ�����.";
		close;
	}

	.@refineitemid = getequipid(.@part); // save id of the item
	.@refinerycnt = getequiprefinerycnt(.@part); //save refinery count
	.@price = getequiprefinecost(.@part, REFINE_COST_ENRICHED, REFINE_ZENY_COST);
	.@material = getequiprefinecost(.@part, REFINE_COST_ENRICHED, REFINE_MATERIAL_ID);

	// Make sure you have the necessary items and Zeny to refine your items
	// Determines chance of failure and verifies that you want to continue.
	callsub S_RefineValidate,getequipweaponlv(.@part),.@material,.@price,.@part,.@refineitemid,.@refinerycnt;

	mes "[Suhnbi]";
	mes "Clang! Clang! Clang!";
	if (getequippercentrefinery(.@part, REFINE_COST_ENRICHED) > rand(100)) {
		successrefitem .@part;
		next;
		emotion ET_BEST;
		mes "[Suhnbi]";
		mes "����! �ѹ��������.";
		mes "�����ҹҹ���Ƿ���������պǡ "+((getequipweaponlv(.@part))?"weapon":"armor")+". ��ҵ�ͧ�դ����آ�����������ѹ����觢��!";
		close;
	}
	failedrefitem .@part;
	next;
	emotion (!rand(5))?ET_MONEY:ET_HUK;
	mes "[Suhnbi]";
	mes "�ػ�����������!!!";
	next;
	mes "[Suhnbi]";
	mes "...";
	mes ".....";
	mes ".......����������~";
	mes "........������͡���з��ͧ���, ������������.";
	close;

S_RefineValidate:
	.@weapon_lvl = getarg(0);
	.@item_req = getarg(1);
	.@price = getarg(2);
	.@part = getarg(3);
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);

	// If the VIP system is enabled, the prices for non-VIP players are considerably higher.
	if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE)) {
		switch(.@weapon_lvl){
			case 0: set .@price, .@price * 10; break;
			case 1: set .@price, .@price * 40; break;
			case 2: set .@price, .@price * 50; break;
			case 3: set .@price, .@price * 2; break;
			case 4: set .@price, .@price * 2; break;
		}
	}

	mes "[Suhnbi]";
	if (.@weapon_lvl)
		mes "��ҵ�ͧ��õպǡ���ظ����� "+ .@weapon_lvl +" ����� ?";
	mes "㹡�õպǡ, ��ҵ�ͧ�� ^ff9999"+ getitemname(.@item_req) +"^000000 ��� "+ .@price +" zeny.";
	mes "�����ҡ�зӵ����� ?";
	next;
	if(select("Yes:No") == 1) {
		if (getequippercentrefinery(.@part,REFINE_COST_ENRICHED) < 100) {
			if (.@weapon_lvl) {
				mes "[Suhnbi]";
				mes "����!!";
				mes "���ظ���";
				mes "������͹�еպǡ...";
				mes "�����¤���...";
				mes "�ѹ��ᵡ����";
				mes "��ҵպǡ�ѹ�ա.";
				next;
				mes "��ж���ѹᵡ,";
				mes "��Ҩ���������ѹ�ա���!";
				mes "���촷���������Ϳ ^ff0000������^000000!";
				mes "^ff0000������Ѻ, �ͧ��������ᵡ�!^000000";
				mes "�����㨷��зӵ��������� ?";
				next;
				if(select("��:���") == 2) {
					mes "[Suhnbi]";
					mes "������.";
					mes "���ж�����ظᵡ�ҡ��õպǡ, ��Ҥ�����֡����, ���.";
					close;
				}
			} else {
				mes "[Suhnbi]";
				mes "��. ��. ���, ����դ�������, ��������§�պǡ.";
				mes "����������ѹ����§�ҡ, ��������������� ?";
				next;
				mes "�������ͧ��ͧ�ѹ���ᵡ, ��Ҩ���������ѹ�ա���.";
				mes "�����ҨС�������Ϳ ^ff0000�ѹ������^000000.";
				//mes "�ء���ҧ������. ���... �!";
				mes "����ѧ��ҡ�ӵ����� ?";
				next;
				if(select("��:���") == 2) {
					mes "[Suhnbi]";
					mes "�������. ��ҷ��������������.";
					mes "仵�«�.";
					close;
				}
			}
		}
		if (countitem(.@item_req) > 0 && Zeny > .@price) {
			delitem .@item_req,1;
			set Zeny, Zeny - .@price;

			// anti-hack
			if (callfunc("F_IsEquipIDHack", .@part, getarg(4)) ||
				callfunc("F_IsEquipRefineHack", .@part, getarg(5)) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3])) {
				mes "[Holink]";
				emotion ET_FRET;
				mes "���ѡ����...";
				mes "��ҤԴ��Ң�������?!";
				mes "����ͺ��Ѻ�ͧ�͹�����������! ����鹫�!";
				close;
			}

			return;
		}
		mes "[Suhnbi]";
		mes "����ͷ������������� ?";
		mes "�ѹ�������ҹѡ, ���ҵ�ͧ�͡�������������������Թ��������Ѻ�������� ���� ?";
		close;
	}
	mes "[Suhnbi]";
	mes "��Ҫ�������������������Ҩ�����㨡���...";
	close;
}
