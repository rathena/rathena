//===== rAthena Script =======================================
//= Hair Dresser
//===== By: ==================================================
//= Muad_Dib, Samuray22, Kisuka
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Allows you to change your hairstyle.
//===== Additional Comments: =================================
//= 1.2 Rescripted to the Aegis 10.3 Standards. [Samuray22]
//= 1.3 Updated to match AEGIS script. [Kisuka]
//= 1.4 Added Lighthalzen Hair Dresser. [L0ne_W0lf]
//= 1.5 Moving NPC Hair Dresser to Pre-Renewal [JohnnyPlayy]
//============================================================

// Roving Hair Dresser
// ============================================================
alberta,33,141,7	script	Roving Hair Dresser	87,{
	mes "[Rui Vishop]";
	mes "That Veronica...";
	mes "Hah! Best hair dresser my ass.";
	mes "She's not the best hair dresser...";
	mes "...";
	mes "I am!";
	next;
	mes "[Rui Vishop]";
	mes "I, Rui Vishop, the man to whom";
	mes "all scalps are canvases";
	mes "waiting to be transformed into";
	mes "works of magnificent art~!";
	next;
	switch(select("What are you?:Do my hair, please!:.....")) {
	case 1:
		mes "[Rui Vishop]";
		mes "Do you not know that I, Rui";
		mes "Vishop, maestro of the shears";
		mes "and sculptor of hair, am an";
		mes "artist far ahead of his time?!";
		mes "Well, I suppose an adventurer";
		mes "like yourself wouldn't know...";
		next;
		mes "[Rui Vishop]";
		mes "As a hair sculptor, I find joy";
		mes "in bestowing upon others the";
		mes "supreme favour of doing their";
		mes "hairstyle at a reasonable price.";
		next;
		mes "[Rui Vishop]";
		mes "Recently, however, I happened to";
		mes "overhear that some tyro has had";
		mes "the audacity to call herself a";
		mes "hair dresser.";
		next;
		mes "[Rui Vishop]";
		mes "So one day I went there,";
		mes "pretending to be a customer.";
		mes "I was apalled to see the boring,";
		mes "lifeless hairstyles that she was";
		mes "giving all of her clients...";
		next;
		mes "[Rui Vishop]";
		mes "It wasn't hard to notice that her";
		mes "skills, or lack thereof, are a";
		mes "joke. She brings shame to the";
		mes "great and honorable";
		mes "profession of hair dressing.";
		mes "A complete and utter disgrace!";
		next;
		mes "[Rui Vishop]";
		mes "But the worst part was...";
		mes "she forced her customers to";
		mes "choose a hairstyle before she";
		mes "styled their hair!";
		next;
		mes "[Rui Vishop]";
		mes "That's not how talented hair";
		mes "dressers do their job! She";
		mes "should know what hair style will";
		mes "fit a customer without ever";
		mes "asking them!";
		next;
		mes "[Rui Vishop]";
		mes "If by any chance you decide to";
		mes "do your hair, don't even think";
		mes "about giving her patronage.";
		mes "Instead, you may ask for my";
		mes "services. I assure you, I am";
		mes "faaaar better than her.";
		next;
		mes "[Rui Vishop]";
		mes "Do you understand? I mean, don't";
		mes "let her ruin your hair needlessly!";
		mes "You could get a Swordman to hack";
		mes "away at your hair if you want a";
		mes "hairstyle that horrible~!";
		close;
	case 2:
		if(BaseLevel < 60) {
			mes "[Rui Vishop]";
			mes "Hmm, I must say, your current";
			mes "style fits you best. Trust me, I know what I am saying.";
			close;
		} else if(Zeny < 199800) {
			mes "[Rui Vishop]";
			mes "Ah, I see that that you can";
			mes "recognize genius when it is";
			mes "right before you. In light";
			mes "of your good taste, I will";
			mes "only require money for my";
			mes "services.";
			next;
			mes "[Rui Vishop]";
			mes "Simply pay me the small";
			mes "fee of 199,800 zeny. You must";
			mes "know that I am doing you a";
			mes "huge favor by charging you";
			mes "such a small amount. My";
			mes "art is priceless, after all.";
			close;
		}
		mes "[Rui Vishop]";
		mes "Alright, I will be taking my";
		mes "199,800 zeny service charge now.";
		next;
		mes "[Rui Vishop]";
		mes "If you don't wish to do";
		mes "this right now, though I can't";
		mes "imagine why, you may ask that";
		mes "stupid hair dresser to";
		mes "do her clumsy work on you...";
		next;
		if(select("No, please do my hair.:Umm, I changed my mind.") ==1 ) {
			mes "[Rui Vishop]";
			mes "O~k~a~y!";
			mes "Now, let us begin~!";
			next;
			mes "[Rui Vishop]";
			mes "Wooooo~oooohhhh!! Toohhhhh~oooohhhh!!";
			next;
			mes "[Rui Vishop]";
			mes "Woooooo~aaaaaaahhhhh!!";
			next;
			mes "[Rui Vishop]";
			mes "Voila!";
			next;
			mes "[Rui Vishop]";
			mes "Oh, great~ it's awesome!";
			mes "Another Vishop masterpiece~";
			mes "Once more I've outdone myself.";
			mes "It's such a unique and talented";
			mes "style! Yes, I am the best! Wooohahahahahaha!";
			set Zeny, Zeny-199800;
			setlook 1,rand(1,19);
			setlook 6,rand(1,8);
			close;
		}
		mes "[Rui Vishop]";
		mes "Bah! Alright! It's your decision.";
		mes "But don't blame me later!";
		mes "One day you'll wake up, realize";
		mes "you're ugly and regret not";
		mes "having my genius shape every lock";
		mes "of hair on your head.";
		close;
	case 3:
		mes "[Rui Vishop]";
		mes "What? What a shame!";
		mes "Will you let that...that";
		mes "charlatan of a hair dresser ruin";
		mes "your hairstyle!? I'm sure the";
		mes "heavens are crying tears of";
		mes "pity at mankind's ignorance...";
		close;
	}
}


lhz_in02,91,155,5	script	Assistant Beautician#li	862,{
	mes "[Assistant Beautician]";
	mes "Wah?! Sweet Jiminy,";
	mes "you freaked me out!";
	mes "What are you doing?!";
	emotion ET_HUK;
	next;
	mes "[Assistant Beautician]";
	mes "Oh! Um, a customer!";
	mes "H-h-h-h-h-hello! Can";
	mes "I help you with anything?";
	next;
	switch(select("What do you do?:Please change my hairstyle.:Who is Prince Shammi?")) {
	case 1:
		mes "[Assistant Beautician]";
		mes "Oh! Me...? I'm";
		mes "just an assistant";
		mes "beautician, but I'm";
		mes "training hard everyday";
		mes "so that I can become";
		mes "a real professional!";
		next;
		mes "[Assistant Beautician]";
		mes "Yeah, I do all sorts of";
		mes "grunt work for the boss while";
		mes "I'm in training. Sometimes, he";
		mes "makes me work pretty hard.";
		mes "In fact, I better get back to work before he gets angry at me!";
		close;
	case 2:
		if (BaseLevel < 60) {
			mes "[Assistant Beautician]";
			mes "Me...? Oh no,";
			mes "no I can't! I mean,";
			mes "I'd love to but, I'm";
			mes "still in training and";
			mes "I can't take responsibility";
			mes "if I mess up on a little kid!";
			close;
		}
		else if ((Zeny < 250000)) {
			mes "[Assistant Beautician]";
			mes "Well... I'm just an";
			mes "assistant, but I have been";
			mes "studying hairstyling after";
			mes "work. If you want, just bring";
			mes "me 250,000 zeny and I'll try";
			mes "my best to change your hair~";
			next;
			mes "[Assistant Beautician]";
			mes "I'd appreciate it if you'd";
			mes "give me this chance! The";
			mes "boss doesn't think I'm ready";
			mes "for styling real people yet, so";
			mes "I haven't had much practice!";
			next;
			mes "[Assistant Beautician]";
			mes "I just know I could";
			mes "do a good job on your";
			mes "hair! Just... Just please";
			mes "understand if I mess up.";
			mes "It won't be too bad, I promise~";
			close;
		}
		else {
			mes "[Assistant Beautician]";
			mes "You're really going to";
			mes "give me a chance to practice?";
			mes "Oh, I love you so much! Okay,";
			mes "I'll need 250,000 zeny to make";
			mes "up for the material expenses.";
			mes "Is that okay with you?";
			next;
			switch(select("Of course~:On second thought...")) {
			case 1:
				mes "[Assistant Beautician]";
				mes "Great! Now, please";
				mes "choose a hairstyle";
				mes "from ''1'' to ''23.''";
				mes "Um, if you need to";
				mes "cancel, just enter ''0.''";
				next;
				input .@input;
				if (.@input == 0) {
					mes "[Assistant Beautician]";
					mes "Awwww...";
					mes "I guess you don't";
					mes "trust me after all...";
					close;
				}
				else if ((.@input < 1) || (.@input > 23)) {
					mes "[Assistant Beautician]";
					mes "Huh? I thought I asked";
					mes "you to enter a number from";
					mes "''1'' to ''23?'' What did I do";
					mes "wrong this time? Hmmm...";
					close;
				}
				else {
					mes "[Assistant Beautician]";
					mes "So this is the";
					mes "style you want me";
					mes "to try to do for you?";
					if (Sex == SEX_MALE) {
						if (.@input < 10)
							cutin "hair_m_0"+.@input+".BMP",4;
						else
							cutin "hair_m_"+.@input+".BMP",4;
					}
					else {
						if (.@input < 10)
							cutin "hair_f_0"+.@input+".BMP",4;
						else
							cutin "hair_f_"+.@input+".BMP",4;
					}
					next;
					switch(select("Yes, let's try it~:Cancel.")) {
					case 1:
						break;
					case 2:
						mes "[Assistant Beautician]";
						mes "Oooh, there must";
						mes "be some style that";
						mes "you like, right? Hmmm...";
						close3;
					}
				}
				mes "[Assistant Beautician]";
				mes "Great, you finally";
				mes "picked one! What, which";
				mes "one did you pick again?";
				mes "Ah, I found it, I found it!";
				mes "Haha! No problem here!";
				mes "Now it's time to style!";
				next;
				nude;
				mes "[Assistant Beautician]";
				mes "Bwwwwaaaahhhh!";
				next;
				mes "[Assistant Beautician]";
				mes "Yap! Pwwwaaattt!";
				next;
				mes "[Assistant Beautician]";
				mes "Waaaah!";
				mes "Oh crap!";
				mes "Wait, I can...";
				mes "I can fix this!";
				next;
				set .@style_r,rand(1,23);
				set .@color_r,rand(1,8);
				mes "[Assistant Beautician]";
				mes "^333333*Pant Pant Pant*^000000";
				next;
				set Zeny, Zeny-250000;
				setlook VAR_HEAD,.@style_r;
				setlook VAR_HEADPALETTE,.@color_r;
				mes "[Assistant Beautician]";
				mes "Bwahahaha! Success!";
				if (.@input == .@style_r) {
					mes "So... How do you like";
					mes "your new style? I love it!";
				}
				else {
					mes "Wha...? This isn't what";
					mes "you wanted? Uh oh... Um...";
					mes "Well, next time I know I can";
					mes "do a much better job! Right!";
				}
				next;
				mes "[Assistant Beautician]";
				mes "Oh, you're such a";
				mes "sweetheart for helping";
				mes "me! Thank you for using";
				mes "my service and come again~";
				emotion ET_CHUP;
				close3;
			case 2:
				mes "[Assistant Beautician]";
				mes "Huh? Oh no, you're";
				mes "quitting? Well, I guess";
				mes "I couldn't trust me to";
				mes "style my hair either...";
				mes "You're... You're right.";
				close;
			}
		}
	case 3:
		mes "[Assistant Beautician]";
		mes "Prince Shammi?";
		mes "He's only a genius when";
		mes "it comes to hairstyling!";
		mes "I'm just his apprentice, but";
		mes "maybe someday, I can be a";
		mes "force in the fashion world too!";
		close;
	}
}

//===== Old Changelog: =================================
//= 07/06/05 : Added 1st Version. [Muad_Dib]
//= Converted to rAthena format by Dr.Evil Fixed typos [Nexon]
//= 1.1 Removed Duplicates [Silent]
//============================================================
