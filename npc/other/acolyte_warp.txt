//===== rAthena Script =======================================
//= Acolyte Warpers
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Warpers to various towns.
//===== Additional Comments: =================================
//= 1.0 First version. [Euphy]
//=     Unsure if this is on kRO, so disabled for now.
//= 1.1 Added iRO VIP features. [Euphy]
//=     The function is located in re/quests/eden/eden_vip.txt.
//============================================================

// Warpers :: acolyte_warp
//============================================================
prontera,98,121,4	script	Acolyte#Prtclear	90,{
	mes "[Keiki]";
	mes "Hello there, adventurer.";
	mes "I've been studying magic from all over Rune-Midgarts to upgrade what I believe to be one of the greatest skills available to the acolyte class.";
	next;

	if (is_function("VIP_iRO_Acolyte"))
		callfunc "VIP_iRO_Acolyte","[Keiki]","Juno";

	mes "[Keiki]";
	mes "I am the one and only Acolyte that has attained the Level 10 Warp Portal skill!";
	emotion ET_THINK, playerattached();
	next;
	mes "[Keiki]";
	mes "That's right! And...";
	mes "I promise you that I don't forget locations that I have already memorized.";
	mes "One day I will level up my skills to warp to wherever I please~";
	next;
	mes "[Keiki]";
	mes "I am willing to warp you to the many locations that I have memorized for a small fee.";
	mes "Would you like to use this service?";
	next;
	switch(select("Yes:No")) {
	case 1:
		mes "[Keiki]";
		mes "Where would you like to go to?";
		mes "I wish you goodluck on your journey.";
		next;

		setarray .@towns$[0], "Izlude", "Geffen", "Payon", "Morocc", "Alberta", "Al De Baran", "Comodo", "Umbala", "Juno";
		setarray .@cost[0],        600,     1200,    1200,     1200,      1800,          2200,     2200,     2200,   1800;

		set .@size, getarraysize(.@towns$);
		for(set .@i,0; .@i<.@size; set .@i,.@i+1)
			set .@menu$, .@menu$+.@towns$[.@i]+" -> "+.@cost[.@i]+"z:";
		set .@i, select(.@menu$+"Cancel")-1;
		if (.@i == .@size)
			close;
		if (Zeny < .@cost[.@i]) {
			mes "[Keiki]";
			mes "I'm sorry, but you don't have";
			mes "enough zeny for the Teleport";
			mes "Service. The fee to teleport";
			mes "to "+.@towns$[.@i]+" is "+.@cost[.@i]+" zeny.";
			close;
		}
		specialeffect2 EF_READYPORTAL;
		specialeffect2 EF_TELEPORTATION;
		specialeffect2 EF_PORTAL;
		next;
		set Zeny, Zeny - .@cost[.@i];
		switch(.@i) {
			case 0: if (checkre(0)) warp "izlude",128,98; else warp "izlude",91,105; break;
			case 1: warp "geffen",120,39; break;
			case 2: warp "payon",161,58; break;
			case 3: warp "morocc",156,46; break;
			case 4: warp "alberta",117,56; break;
			case 5: warp "aldebaran",168,112; break;
			case 6: warp "comodo",209,143; break;
			case 7: warp "umbala",100,154; break;
			case 8: warp "yuno",158,125; break;
		}
		close;
	case 2:
		close;
	}
}

yuno,142,184,6	script	Acolyte#Junoclear	90,{
	mes "[Isalei]";
	mes "Hello, adventurer.";
	mes "My companion Keiki and I have discovered a way to increase our warp portal abilities.";
	next;

	if (is_function("VIP_iRO_Acolyte"))
		callfunc "VIP_iRO_Acolyte","[Isalei]","Prontera";

	mes "[Isalei]";
	mes "Though I have not mastered up to the level that she has, I have been able to attain Level 5.";
	emotion ET_THINK, playerattached();
	next;
	mes "[Isalei]";
	mes "Maybe one day I can level up my skills enough so that I can use Warp portal to more saved locations.";
	next;
	mes "[Isalei]";
	mes "I am willing to warp you to the many locations that I have memorized for a small fee.";
	mes "Would you like to use this service?";
	next;
	switch(select("Yes:No")) {
	case 1:
		mes "[Isalei]";
		mes "Where would you like to go to?";
		mes "I wish you goodluck on your journey.";
		next;

		setarray .@towns$[0], "Einbroch", "Lighthalzen", "Hugel", "Rachel", "Prontera";
		setarray .@cost[0],         2200,          2200,    2200,     2200,       1800;

		set .@size, getarraysize(.@towns$);
		for(set .@i,0; .@i<.@size; set .@i,.@i+1)
			set .@menu$, .@menu$+.@towns$[.@i]+" -> "+.@cost[.@i]+"z:";
		set .@i, select(.@menu$+"Cancel")-1;
		if (.@i == .@size)
			close;
		if (Zeny < .@cost[.@i]) {
			mes "[Isalei]";
			mes "I'm sorry, but you don't have";
			mes "enough zeny for the Teleport";
			mes "Service. The fee to teleport";
			mes "to "+.@towns$[.@i]+" is "+.@cost[.@i]+" zeny.";
			close;
		}
		specialeffect2 EF_READYPORTAL;
		specialeffect2 EF_TELEPORTATION;
		specialeffect2 EF_PORTAL;
		next;
		set Zeny, Zeny - .@cost[.@i];
		switch(.@i) {
			case 0: warp "einbroch",67,195; break;
			case 1: warp "lighthalzen",159,90; break;
			case 2: warp "hugel",98,150; break;
			case 3: warp "rachel",119,135; break;
			case 4: warp "prontera",116,72; break;
		}
		close;
	case 2:
		close;
	}
}
