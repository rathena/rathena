//===== eAthena Script =======================================
//= War of Emperium Guild Treasure Room Functions
//===== By: ==================================================
//= holyAngelX (1.0) Akaru and ho|yAnge|X (1.1)
//===== Current Version: =====================================
//= 1.9
//===== Compatible With: =====================================
//= eAthena 1+; RO Episode 4+
//===== Description: =========================================
//= F_GldTreas spawns treasure chests used by the guild master.
//= F_GldTreasSw allows the player to get out of the treasure room.
//= F_TreasProtect removes non-guild master players from treasure room.
//==============================================
//= Break down of arguments used in the F_GldTreas:
//=   arg(0): name of guild castle
//=   arg(1): name of script that called the function
//=   arg(2): the box number amount
//=   arg(3): temp variable (count)
//=   arg(4): temp variable (box/monster id#)
//=   arg(5): box/monster id#
//=   arg(6): x1 coordinate for areamonster call
//=   arg(7): y1 coordinate for areamonster call
//=   arg(8): x2 coordinate for areamonster call
//=   arg(9): y1 coordinate for areamonster call
//=   
//= Break down of arguments used in the F_GldTreasSw:
//=   arg(0): name of guild castle.
//=   arg(1): x1 coordinate for warp back to guild castle
//=   arg(2): y1 coordinate for warp back to guild castle
//
//= Break down of arguments used in the F_TreasProtect:
//=   arg(0): name of guild castle.
//=   arg(1): name of warp-to city 
//=   arg(2): x1 coordinate for warp-to city
//=   arg(3): y1 coordinate for warp-to city
//===== Additional Comments: =================================
//= 1.2 Treasure room Spawn, and Treasure room Switch scripts now use these functions.[kobra_k88]
//= 1.2a Function now returns to script that called it.  Removed TreasureSpawn2.
//= Changed back to using specific global variables for number of boxes and the box id. [kobra_k88]
//= 1.2b Added a check to allow un broken treasure chests to respawn after map server restart.[kobra_k88]
//= 1.3 Fixed treasure boxes spawn. (Unrolled one loop a bit) [Lupus]
//= 1.4 New number of Treasure Boxes per castle: 25 at 100 Economic pts [Lupus]
//=	So you get your first chest only when your Economic Pts >= 4
//= 1.5 Fixed treasure number 'round exploit' [Lupus]
//= 1.6 to Aegis X.2 formula 4..24 Treasure Chests [Lupus]
//= 1.7 Box Count fix by Zoc. Now it spawns 1st/2nd Treasure Chest 50%/50% [Lupus]
//= 1.8 Official Treasure Spawn in Novice Castles. Had to add +1 in odd/even formula to leave correct ID of NG Treasure Box 8) [Lupus]
//= 1.9 Added F_TreasProtect to remove players who are not guild master from treasure room. [L0ne_W0lf]
//============================================================


//================================================
// Treasure Spawning Function
//================================================
function	script	F_GldTreas	{
	if(getarg(10) == 1) goto L_SPAWN;
	setcastledata getarg(0),4,0;
	setcastledata getarg(0),5,0;
	killmonster getarg(0),"Treasure_"+getarg(1)+"::OnDied";
	if(GetCastleData(getarg(0),2) > 100 || GetCastleData(getarg(0),1) == 0) return;
	//sets the counter variable = to the box number amount
	if(getarg(0) == "nguild_prt" || getarg(0) == "nguild_alde" || getarg(0) == "nguild_gef" || getarg(0) == "nguild_pay" ) {
		//Novice Castles can't have more than 1 Treasure Chest
		set getarg(2),1;
	}else{
		set getarg(2),GetCastleData(getarg(0),2)/5+4;
	}
	if (getarg(2) <= 0) return;
	set getarg(3), getarg(2);	//sets the counter variable = to the box number amount

L_SPAWN:
	set getarg(4), getarg(5)+((getarg(3)+1) & 1);	//sets the box id variable = to the box id
	areamonster getarg(0),getarg(6),getarg(7),getarg(8),getarg(9),"Treasure Chest",getarg(4),1,"Treasure_"+getarg(1)+"::OnDied";
	set getarg(3), getarg(3) - 1;
	if(getarg(3) > 0) goto L_SPAWN;
	return;
}

//==============================================================
//  Treasure Room Switch 
//===============================================================
function	script	F_GldTreasSw	{
	mes " ";
	mes "There is little switch over here";
	mes "Would you like to pull the switch down?";
	next;
	menu "Yes",M_1,"No",-;
	close;
	
	M_1:
		warp getarg(0),getarg(1),getarg(2);
		return;
}

//==============================================================
//  Treasure Room Protrection
//===============================================================
function	script	F_TreasProtect	{
	set @GID, GetCastleData(getarg(0),1);
	if (strcharinfo(0) != getguildmaster(@GID)) warp getarg(1),getarg(2),getarg(3);
}
