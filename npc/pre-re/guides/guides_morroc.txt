//===== rAthena Script =======================================
//= Morocc Guides
//===== By: ==================================================
//= kobra_k88; L0ne_W0lf
//===== Current Version: =====================================
//= 1.4a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Guides for the City of Morocc
//===== Additional Comments: =================================
//= 1.1 Now using duplicate command.
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.3 Removed Duplicates [Silent]
//= 1.4 rescripted to official 10.3 standards. [L0ne_W0lf]
//= 1.4a Fixed some errors with the loop[Samuray22]
//	-Thanks to $ephiroth.
//============================================================

morocc,153,286,6	script	Guide#moc::MocGuide	707,{
	cutin "moc_soldier",2;
	mes "[Morocc Guide]";
	mes "Welcome to Morocc,";
	mes "the frontier town of the";
	mes "Rune-Midgarts Kingdom.";
	mes "Please ask me for help if";
	mes "you're having any trouble";
	mes "finding anything in town.";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("City Guide:Remove Marks from Mini-Map:Notice:Cancel")) {
		case 1:
			mes "[Morocc Guide]";
			mes "Please select";
			mes "a location from";
			mes "the following menu.";
			if (.@compass_check == 0) {
				mes "Would you like me";
				mes "to mark locations";
				mes "on your Mini-Map?";
				next;
				if (select("Yes.:No.") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
				
				switch(select("^FF0000Thief Guild^000000:Weapon Shop:Inn:Pub:Mercenary Guild:Forge:Cancel")) {
				case 1:
					mes "[Morocc Guide]";
					mes "The Thief Guild is";
					mes "in charge of all Job";
					mes "Changes to the Thief";
					mes "Class. From what I hear,";
					mes "you can find them inside";
					mes "the Pyramids nearby...";
					if (.@compass_check)
						viewpoint 1,24,297,2,0xFF0000;
					break;
				case 2:
					mes "[Morocc Guide]";
					mes "The Weapon Shop";
					mes "is in the southeast";
					mes "end of Morocc.";
					if (.@compass_check)
						viewpoint 1,253,56,3,0xFF00FF;
					break;
				case 3:
					mes "[Morocc Guide]";
					mes "There are Inns";
					mes "where you can rest";
					mes "at the southeast and";
					mes "northeast ends of Morocc.";
					if (.@compass_check) {
						viewpoint 1,197,66,4,0xFF00FF;
						viewpoint 1,273,269,5,0xFF00FF;
					}
					break;
				case 4:
					mes "[Morocc Guide]";
					mes "You can find the";
					mes "Pub in northeast Morocc.";
					if (.@compass_check)
						viewpoint 1,52,259,6,0xFF00FF;
					break;
				case 5:
					mes "[Morocc Guide]";
					mes "The Mercenary";
					mes "Guild is located";
					mes "in East Morocc.";
					if (.@compass_check)
						viewpoint 1,284,171,7,0x00FF00;
					break;
				case 6:
					mes "[Morocc Guide]";
					mes "The Forge is";
					mes "located just";
					mes "southwest from";
					mes "the center of Morocc.";
					if (.@compass_check)
						viewpoint 1,47,47,7,0xFF00FF;
					break;
				case 7:
					mes "[Morocc Guide]";
					mes "Please ask me to ''Remove";
					mes "Marks from Mini-Map'' if you";
					mes "no longer wish to have the";
					mes "location marks displayed";
					mes "on your Mini-Map.";
					set .@loop2,0;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,2,0x00FF00;
			viewpoint 2,237,41,3,0x0000FF;
			viewpoint 2,46,345,4,0x00FF00;
			viewpoint 2,175,220,5,0xFF0000;
			viewpoint 2,175,220,6,0xFF0000;
			viewpoint 2,175,220,7,0xFF0000;
			set .@compass_check,0;
			break;
		case 3:
			mes "[Morocc Guide]";
			mes "Advances in sorcery and";
			mes "technology have allowed";
			mes "us to update our information";
			mes "system, enabling up to mark";
			mes "locations on your Mini-Map";
			mes "for easier navigation.";
			next;
			mes "[Morocc Guide]";
			mes "Your Mini-Map is located";
			mes "in the upper right corner";
			mes "of the screen. If you can't";
			mes "see it, press the Ctrl + Tab";
			mes "keys or click the ''Map'' button in your Basic Info Window.";
			next;
			mes "[Morocc Guide]";
			mes "On your Mini-Map,";
			mes "click on the ''+'' and ''-''";
			mes "symbols to zoom in and";
			mes "our of your Mini-Map. We";
			mes "hope you enjoy your travels";
			mes "here in the city of Morocc.";
			break;
		case 4:
			mes "[Morocc Guide]";
			mes "Alright then,";
			mes "try to stay out of";
			mes "too much trouble";
			mes "out there, adventurer.";
			close2;
			set .@loop1,0;
		}
	}
	cutin "moc_soldier",255;
	end;
}

morocc,54,97,0	duplicate(MocGuide)	Guide#2moc	707
