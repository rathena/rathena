//===== rAthena Script =======================================
//= Prontera Guides
//===== By: ==================================================
//= kobra_k88; L0ne_W0lf
//===== Current Version: =====================================
//= 1.6a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Guides for the City of Prontera
//===== Additional Comments: =================================
//= 1.1 Now using duplicate command.
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.3 Updated the guide according to how it is on iRO, thanks [MasterOfMuppets]
//=     to Muad_Dib
//= 1.4 Fitted the many lines into 1 or 2, and corrected some typos [erKURITA]
//= 1.5 Removed Duplicates [Silent]
//= 1.6 Rescripted to official 10.3 standards. [L0ne_W0lf]
//= 1.6a Fixed some errors with the loop. [Samuray22]
//	-Thanks to $ephiroth.
//= 1.6b Fixed a Problem with Unnecesary RGB Values. [Samuray22]
//	-Thanks to Brainstorm.
//============================================================

prontera,154,187,4	script	Guide#prt::PrtGuide	105,{
	cutin "prt_soldier",2;
	mes "[Prontera Guide]";
	mes "Welcome to Prontera,";
	mes "the beautiful capital of the";
	mes "Rune-Midgarts Kingdom. If";
	mes "you have questions or need help finding something in the city, don't hesitate to ask.";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("City Guide.:Remove Marks from Mini-Map:Notice:Cancel")) {
		case 1:
			mes "[Prontera Guide]";
			mes "Please select";
			mes "a location from";
			mes "the following menu.";
			if (.@compass_check == 0) {
				mes "Would you like me";
				mes "to mark locations";
				mes "on your Mini-Map?";
				next;
				if (select("Yes:No") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while (.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;

				switch(select("Swordman Association:^0000FFSanctuary^000000:Prontera Chivalry:Weapon Shop:Tool Shop:Inn:Trading Post:Pub:Library:Job Agency:Prontera Castle:City Hall:Cancel")) {
				case 1:
					mes "[Prontera Guide]";
					mes "The Swordman Association,";
					mes "which handles Job Changes";
					mes "to the Swordman class, has";
					mes "moved to Izlude. This facility";
					mes "is just an empty building now.";
					if (.@compass_check)
						viewpoint 1,237,41,4,0x00FF00;
					break;
				case 2:
					mes "[Prontera Guide]";
					mes "The Prontera Sanctuary";
					mes "handles Job Changes to";
					mes "the Acolyte class, and can";
					mes "be found in the northeast";
					mes "corner of Prontera.";
					if (.@compass_check)
						viewpoint 1,236,316,5,0xFF0000;
					break;
				case 3:
					mes "[Prontera Guide]";
					mes "The Prontera Chivralry,";
					mes "which is responsible for";
					mes "the safety of our capital, is";
					mes "in Prontera's northwest corner.";
					if (.@compass_check)
						viewpoint 1,46,345,6,0x00FF00;
					break;
				case 4:
					mes "[Prontera Guide]";
					mes "The Weapon Shop";
					mes "is located northeast";
					mes "of the central fountain.";
					if (.@compass_check)
						viewpoint 1,175,220,7,0xFF00FF;
					break;
				case 5:
					mes "[Prontera Guide]";
					mes "The Tool Shop";
					mes "is located northwest";
					mes "of the central fountain.";
					if (.@compass_check)
						viewpoint 1,134,221,8,0xFF00FF;
					break;
				case 6:
					mes "[Prontera Guide]";
					mes "The Inns in Prontera are";
					mes "located both to the east";
					mes "and west of Prontera's";
					mes "central fountain area.";
					if (.@compass_check) {
						viewpoint 1,204,191,9,0xFF00FF;
						viewpoint 1,107,192,10,0xFF00FF;
					}
					break;
				case 7:
					mes "[Prontera Guide]";
					mes "The Trading Post";
					mes "can be found southeast";
					mes "from the central fountain.";
					if (.@compass_check)
						viewpoint 1,179,184,11,0x00FF00;
					break;
				case 8:
					mes "[Prontera Guide]";
					mes "The Pub is located";
					mes "southeast of the fountain,";
					mes "behind the Trading Post.";
					if (.@compass_check)
						viewpoint 1,208,154,12,0x00FF00;
					break;
				case 9:
					mes "[Prontera Guide]";
					mes "If you head north from";
					mes "the central fountain, you'll";
					mes "find an empty area in which";
					mes "both branches of the Prontera";
					mes "Library can be accessed if you";
					mes "head towards the east or west.";
					if (.@compass_check) {
						viewpoint 1,120,267,13,0x00FF00;
						viewpoint 1,192,267,14,0x00FF00;
					}
					break;
				case 10:
					mes "[Prontera Guide]";
					mes "The Job Agency is";
					mes "just southwest of the";
					mes "central fountain area.";
					if (.@compass_check)
						viewpoint 1,133,183,15,0x00FF00;
					break;
				case 11:
					mes "[Prontera Guide]";
					mes "The Prontera Castle is";
					mes "located at the northern";
					mes "sector of this city. You can";
					mes "go to the fields that are north";
					mes "of Prontera by going through";
					mes "the castle's rear exit.";
					if (.@compass_check)
						viewpoint 1,156,360,16,0x00FF00;
					break;
				case 12:
					mes "[Prontera Guide]";
					mes "The City Hall";
					mes "is located in the";
					mes "southwest corner";
					mes "in our city of Prontera.";
					if (.@compass_check)
						viewpoint 1,75,91,17,0x01FF01;
					break;
				case 13:
					mes "[Prontera Guide]";
					mes "Please ask me to ''Remove";
					mes "Marks from Mini-Map'' if you";
					mes "no longer wish to have the";
					mes "location marks displayed";
					mes "on your Mini-Map.";
					set .@loop2,0;
					break;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,4,0x00FF00;
			viewpoint 2,237,41,5,0x0000FF;
			viewpoint 2,46,345,6,0x00FF00;
			viewpoint 2,175,220,7,0xFF0000;
			viewpoint 2,134,221,8,0xFF0000;
			viewpoint 2,204,191,9,0xFF0000;
			viewpoint 2,107,192,10,0xFF0000;
			viewpoint 2,179,184,11,0x00FF00;
			viewpoint 2,208,154,12,0x00FF00;
			viewpoint 2,120,267,13,0x00FF00;
			viewpoint 2,192,267,14,0x00FF00;
			viewpoint 2,133,183,15,0x00FF00;
			viewpoint 2,156,360,16,0x00FF00;
			viewpoint 2,75,91,17,0x00FF00;
			set .@compass_check,0;
			break;
		case 3:
			mes "[Prontera Guide]";
			mes "Advances in sorcery and";
			mes "technology have allowed";
			mes "us to update our information";
			mes "system, enabling up to mark";
			mes "locations on your Mini-Map";
			mes "for easier navigation.";
			next;
			mes "[Prontera Guide]";
			mes "Your Mini-Map is located";
			mes "in the upper right corner";
			mes "of the screen. If you can't";
			mes "see it, press the Ctrl + Tab";
			mes "keys or click the ''Map'' button in your Basic Info Window.";
			next;
			mes "[Prontera Guide]";
			mes "On your Mini-Map,";
			mes "click on the ''+'' and ''-''";
			mes "symbols to zoom in and";
			mes "our of your Mini-Map. We";
			mes "hope you enjoy your travels";
			mes "here in the city of Prontera.";
			break;
		case 4:
			mes "[Prontera Guide]";
			mes "Well, adventurer...";
			mes "I hope your journeys";
			mes "through Rune-Midgard";
			mes "are both fun and safe.";
			close2;
			set .@loop1,0;
			break;
		}
	}
	cutin "prt_soldier",255;
	end;
}

prontera,282,208,2	duplicate(PrtGuide)	East Gate-Guide	105

prontera,29,200,6	duplicate(PrtGuide)	West Gate-Guide	105

prontera,160,29,0	duplicate(PrtGuide)	South Gate-Guide	105

prontera,151,330,4	duplicate(PrtGuide)	North Gate-Guide	105
