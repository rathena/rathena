//===== rAthena Script =======================================
//= Manuk Merchant
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= [Aegis Conversion]
//= Manuk coin redemption NPC.
//===== Additional Comments: =================================
//= 1.0 First version 
//= 1.1 Fixed checkweight. [Gepard]
//= 1.2 Optimized. [Euphy]
//============================================================

man_in01,286,16,1	script	Merchant of Manuk	454,{
	mes "[Merchant of Manuk]";
	if ((isequipped(2782) == 1) && (ep13_2_rhea == 100)) {
		if (!checkweight(1201,1)) {
			mes "It looks like you're carrying too many things.";
			mes "Why not put some of your items in storage and come back?";
			close;
		}
		mes "Hello.";
		mes "What are you looking for?";
		next;
		setarray .@Items[2],12342,12343,12348;
		set .@i, select("View item description:Manuk's Opportunity:Manuk's Courage:Manuk's Faith:Cancel");
		mes "[Merchant of Manuk]";
		if (.@i == 1) {
			mes "Here are the item descriptions.";
			close2;
			ReadBook 11019,1;
			end;
		}
		else if (.@i < 5) {
			mes "I can sell you 3 "+getitemname(.@Items[.@i])+" for^3131FF 1 coin^000000.";
			next;
			switch(select("Purchase:Do not purchase")) {
			case 1:
				if (countitem(6080)) {
					mes "[Merchant of Manuk]";
					mes "Thank you for coming.";
					delitem 6080,1;
					getitem .@Items[.@i],3;
					close;
				}
				else {
					mes "[Merchant of Manuk]";
					mes "You don't have enough coins.";
					close;
				}
			case 2:
				mes "[Merchant of Manuk]";
				break;
			}
		}
		mes "Come again if you change your mind.";
		close;
	}
	else {
		mes "Rtt od d";
		mes "Qwo hd is d irr";
		close;
	}
}