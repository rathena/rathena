//===== eAthena Script ======================================= 
// BattleGround System - KvM 1-59
//===== By: ================================================== 
//= ???, L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= eAthena 1.0
//===== Description: ========================================= 
//= [AEGIS Conversion]
//= Kreiger Von Midgard Battleground for levels 1 to 59
//= - Winning Team: 1 point
//= - Losing Team: 0 points
//===== Additional Comments: ================================= 
//= 1.0 First Version.
//= 1.1 Updated using official Aegis files. [L0ne_W0lf]
//============================================================

// Waiting Room NPCs
//============================================================
bat_room,197,226,5	script	KVM Waiting Room#a2::KvM02R_Guillaume	418,{
	end;

OnInit:
	waitingroom "Battle Station 5 Players",5,"KvM02_BG::OnGuillaumeJoin",1;
	end;
	
OnEnterBG:
	set $@KvM02BG_id1, waitingroom2bg("bat_c02",52,129,"KvM02_BG::OnGuillaumeQuit","KvM02_BG::OnGuillaumeDie");
	end;
}

bat_room,197,220,0	warp	#kvm601	2,2,bat_room,154,150

bat_room,197,205,3	script	KVM Waiting Room#b2::KvM02R_Croix	414,{
	end;

OnInit:
	waitingroom "Battle Station 5 Players",5,"KvM02_BG::OnCroixJoin",1;
	end;

OnEnterBG:
	set $@KvM02BG_id2, waitingroom2bg("bat_c02",147,55,"KvM02_BG::OnCroixQuit","KvM02_BG::OnCroixDie");
	end;
}

bat_room,197,211,0	warp	#kvm602	2,2,bat_room,154,150

// Starting Line
//============================================================
-	script	KVM02::CellEffect2	-1,{
	end;

OnKvM02One: 
	misceffect EF_CHANGECOLD;
	end;

OnKvM02Two: 
	misceffect EF_CHANGEPOISON;
	end;
}

bat_c02,54,124,3	duplicate(CellEffect2)	#RedcellA1-2	111
bat_c02,55,124,3	duplicate(CellEffect2)	#RedcellA2-2	111
bat_c02,56,124,3	duplicate(CellEffect2)	#RedcellA3-2	111
bat_c02,57,124,3	duplicate(CellEffect2)	#RedcellA4-2	111
bat_c02,57,125,3	duplicate(CellEffect2)	#RedcellA5-2	111
bat_c02,57,126,3	duplicate(CellEffect2)	#RedcellA6-2	111
bat_c02,57,127,3	duplicate(CellEffect2)	#RedcellA7-2	111

bat_c02,145,59,3	duplicate(CellEffect2)	#RedcellB1-2	111
bat_c02,144,59,3	duplicate(CellEffect2)	#RedcellB2-2	111
bat_c02,143,59,3	duplicate(CellEffect2)	#RedcellB3-2	111
bat_c02,142,59,3	duplicate(CellEffect2)	#RedcellB4-2	111
bat_c02,142,56,3	duplicate(CellEffect2)	#RedcellB5-2	111
bat_c02,142,57,3	duplicate(CellEffect2)	#RedcellB6-2	111
bat_c02,142,58,3	duplicate(CellEffect2)	#RedcellB7-2	111

bat_c02,54,128,3	script	#A_camp_start02	111,4,4,{
	end;

OnInit:
	disablenpc "#A_camp_start02";
	end;

OnEnable:
	enablenpc "#A_camp_start02";
	end;

OnDisable:
	disablenpc "#A_camp_start03";
	end;

OnTouch:
	set Bat_Team,1;
	setquest 6025;
	end;
}

bat_c02,146,56,3	script	#B_camp_start02	111,4,4,{
	end;

OnInit:
	disablenpc "#B_camp_start02";
	end;

OnEnable:
	enablenpc "#B_camp_start02";
	end;

OnDisable:
	disablenpc "#B_camp_start02";
	end;

OnTouch:
	set Bat_Team,2;
	setquest 6025;
	end;
}

// Battleground Engine
//============================================================
-	script	KvM02_BG	-1,{
	end;

OnInit:
	setwall "bat_c02",54,122,6,7,0,"batc02wall_a";
	setwall "bat_c02",55,122,5,7,0,"batc02wall_b";
	setwall "bat_c02",140,56,6,7,0,"batc02wall_c";
	setwall "bat_c02",140,57,5,7,0,"batc02wall_d";
	disablenpc "VintenarKvM02a";
	disablenpc "VintenarKvM02b";
	end;

OnGuillaumeJoin:
OnCroixJoin:
	donpcevent "KvM02_BG::OnReadyCheck";
	end;

OnGuillaumeQuit:
	//set BG_Delay_Tick, gettimetick(2) + 1200;
OnGuillaumeDie:
	if( $@KvM02BG == 2 )
	{
		set .Guillaume_Count, .Guillaume_Count - 1;
		set .Croix_Score, .Croix_Score + 1;
		bg_updatescore "bat_c02",.Guillaume_Score,.Croix_Score;
		if( .Guillaume_Count < 1 ) donpcevent "KvM02_BG::OnCroixWin";
	}
	end;

OnCroixQuit:
	//set BG_Delay_Tick, gettimetick(2) + 1200;
OnCroixDie:
	if( $@KvM02BG == 2 )
	{
		set .Croix_Count, .Croix_Count - 1;
		set .Guillaume_Score, .Guillaume_Score + 1;
		bg_updatescore "bat_c02",.Guillaume_Score,.Croix_Score;
		if( .Croix_Count < 1 ) donpcevent "KvM02_BG::OnGuillaumeWin";
	}
	end;

OnReadyCheck:
	if( $@KvM02BG )
		end;
	set .@Guillaume, getwaitingroomstate(0,"KvM02R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"KvM02R_Croix");

	if( .@Guillaume < 5 || .@Croix < 5 )
		end;

	set $@KvM02BG, 1; // Starting
	donpcevent "KvM02R_Croix::OnEnterBG";
	donpcevent "KvM02R_Guillaume::OnEnterBG";
	donpcevent "KvM02_BG::OnStart";
	end;

OnStart:
	disablenpc "VintenarKvM02a";
	disablenpc "VintenarKvM02b";
	set $@KvM02BG_Victory, 0;
	// Warp Teams
	bg_warp $@KvM02BG_id1,"bat_c02",53,128;
	bg_warp $@KvM02BG_id2,"bat_c02",146,55;
	// ScoreBoard
	set .Guillaume_Score, 0;
	set .Croix_Score, 0;
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c02", "In 1 minute, KVM will start.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c02", "The maximum time for a KVM battle is 5 minutes.",bc_map,"0x00ff00";
	end;

OnTimer6000:
	mapannounce "bat_c02", "Please prepare for the KVM battle.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "You can buff your people.",bc_map,"0x00ff00";
	donpcevent "#A_camp_start02::OnEnable";
	donpcevent "#B_camp_start02::OnEnable";
	end;

OnTimer13000:
	donpcevent "#A_camp_start02::OnDisable";
	donpcevent "#B_camp_start02::OnDisable";
	end;

OnTimer30000:
	mapannounce "bat_c02", "30 seconds remaining to start KVM battle.",bc_map,"0x00ff00";
	end;

OnTimer45000:
	mapannounce "bat_c02", "15 seconds remaining to start KVM battle.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01One";
	end;

OnTimer50000:
	mapannounce "bat_c02", "10 seconds remaining to start KVM battle.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01Two";
	end;

OnTimer55000:
	mapannounce "bat_c02", "5 seconds remaining to start KVM battle.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01One";
	end;

OnTimer59000:
	mapannounce "bat_c02", "KVM is now commencing.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01Two";
	end;

OnTimer61000:
	// Team Members
	set .Guillaume_Count, bg_get_data($@KvM02BG_id1, 0);
	set .Croix_Count, bg_get_data($@KvM02BG_id2, 0);
	if( .Guillaume_Count < 5 || .Croix_Count < 5 )
	{
		set $@KvM02BG_Victory, 3;
		set $@KvM02BG, 3;
		mapannounce "bat_c02","There are not enough players to start the battle",1,0x808080;
		stopnpctimer;
		donpcevent "KvM02_BG::OnStop";
		end;
	}
	set $@KvM02BG, 2; // Playing
	areapercentheal "bat_c02",50,123,58,131,100,100;
	bg_warp $@KvM02BG_id1,"bat_c02",62,119;
	areapercentheal "bat_c02",141,52,149,60,100,100;
	bg_warp $@KvM02BG_id2,"bat_c02",137,64;
	end;

OnTimer300000:
	mapannounce "bat_c02", "KVM will be ending in 1 minute.",bc_map,"0x00ff00";
	end;

OnTimer330000:
	mapannounce "bat_c02", "KVM will be ending in 30 seconds.",bc_map,"0x00ff00";
	end;

OnTimer345000:
	mapannounce "bat_c02", "KVM will be ending in 15 seconds.",bc_map,"0x00ff00";
	end;

OnTimer350000:
	mapannounce "bat_c02", "KVM will be ending in 10 seconds.",bc_map,"0x00ff00";
	end;

OnTimer355000:
	mapannounce "bat_c02", "KVM will be ending in 5 seconds.",bc_map,"0x00ff00";
	end;

OnTimer360000:
	mapannounce "bat_c02", "KVM has ended.",bc_map,"0x00ff00";
	if( .Croix_Count > .Guillaume_Count )
		donpcevent "KvM02_BG::OnCroixWin";
	else if( .Croix_Count < .Guillaume_Count )
		donpcevent "KvM02_BG::OnGuillaumeWin";
	else
	{ // Draw Game
		set $@KvM02BG, 3;
		set $@KvM02BG_Victory, 3;
		mapannounce "bat_c02", "Number of Guillaume survivors "+.Guillaume_Count+".",bc_map,"0x00ff00";
		mapannounce "bat_c02", "Number of Croix survivors "+.Croix_Count+".",bc_map,"0x00ff00";
		mapannounce "bat_c02", "Regardless of the outcome of the two camps.",bc_map,"0x00ff00";
		donpcevent "KvM02_BG::OnStop";
	}
	end;

OnGuillaumeWin:
	set $@KvM02BG, 3;
	set $@KvM02BG_Victory, 1;
	mapannounce "bat_c02", "Guillaume camp victory.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Congratulations to all Guillaume members.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "All personnel move to the starting zone.",bc_map,"0x00ff00";
	donpcevent "KvM02_BG::OnStop";
	end;

OnCroixWin:
	set $@KvM02BG, 3;
	set $@KvM02BG_Victory, 2;
	mapannounce "bat_c02", "Croix camp victory.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Congratulations to all Croix members.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "All personnel move to the starting zone.",bc_map,"0x00ff00";
	donpcevent "KvM02_BG::OnStop";
	end;

OnStop:
	stopnpctimer;
	enablenpc "VintenarKvM02a";
	enablenpc "VintenarKvM02b";
	// Warp Teams
	bg_warp $@KvM02BG_id1,"bat_c02",53,128;
	bg_warp $@KvM02BG_id2,"bat_c02",146,55;
	donpcevent "KvM02_BG_Out::OnBegin";
	end;

OnReset:
	set .Croix_Count, 0;
	set .Guillaume_Count, 0;
	set .Croix_Score, 0;
	set .Guillaume_Score, 0;
	set $@KvM02BG_Victory, 0;
	if( $@KvM02BG_id1 ) { bg_destroy $@KvM02BG_id1; set $@KvM02BG_id1, 0; }
	if( $@KvM02BG_id2 ) { bg_destroy $@KvM02BG_id2; set $@KvM02BG_id2, 0; }
	disablenpc "KVM Officer#KVM02A";
	disablenpc "KVM Officer#KVM02B";
	mapwarp "bat_c02","bat_room",154,150;
	maprespawnguildid "bat_c02",0,3; // Just in case someone else
	bg_updatescore "bat_c02",0,0;
	set $@KvM02BG, 0;
	donpcevent "KvM02_BG::OnReadyCheck"; // Maybe a game is ready to start
	end;
}

-	script	KvM02_BG_Out	-1,{
	end;

OnBegin:
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c02", "Please apply with the Officer to acquire KVM points.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c02", "The Officer will grant you the points for 30 seconds.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "In 30 seconds, the Officer will be sent away.",bc_map,"0x00ff00";
	end;

OnTimer5000:
	mapannounce "bat_c02", "If you do not claim your points now, you will not be able to.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Please make sure to collect before the time expires.",bc_map,"0x00ff00";
	end;

OnTimer60000:
	stopnpctimer;
	donpcevent "KvM02_BG::OnReset";
	end;
}

// Battleground rewards
//============================================================
bat_c02,51,130,5	script	KVM Officer#KVM02A	419,{
	if( $@KvM01BG_Victory )
	{
		if( $@KvM02BG_Victory == Bat_Team )
		{ // Victory
			set kvm_point,kvm_point + 1;
			mes "[Swandery]";
			mes "Blessed Guillaume!!";
			mes "Let's enjoy our glorious victory!";
			mes "" + strcharinfo(0) + ", its a sign reflecting victory";
			close2;
		}
		else
		{ // 
			mes "[Swandery]";
			mes "You lost, but you're dedicated to this battle.";
			close2;
		}
		bg_leave;
		set Bat_Team,0;
		warp "bat_room",154,150;
		end;
	}
	end;
}

bat_c02,148,53,1	script	KVM Officer#KVM02B	415,{
	if( $@KvM01BG_Victory )
	{
		if( $@KvM02BG_Victory == Bat_Team )
		{ // Victory
			set kvm_point,kvm_point + 1;
			mes "[Swandery]";
			mes "Blessed Croax!!";
			mes "Let's enjoy our glorious victory!";
			mes "" + strcharinfo(0) + ", its a sign reflecting victory";
			mes "Just take this defeat a lesson, and later you would definitely learn.";
			close2;
		}
		else
		{ // 
			mes "[Swandery]";
			mes "Oh, " + strcharinfo(0) + ". Don't be sad.";
			mes "Even though we didn't win, we did our best.";
			mes "Please don't forget this battle. We can win the next.";
			close2;
		}
		bg_leave;
		set Bat_Team,0;
		warp "bat_room",154,150;
		end;
	}
	end;
}

// MapFlags
//============================================================

bat_c02	mapflag	battleground	2
bat_c02	mapflag	nomemo
bat_c02	mapflag	nosave	SavePoint
bat_c02	mapflag	noteleport
bat_c02	mapflag	nowarp
bat_c02	mapflag	nowarpto
bat_c02	mapflag	noreturn
bat_c02	mapflag	nobranch
bat_c02	mapflag	nopenalty
