//===== rAthena Script =======================================
//= BattleGround System - KvM 80-99
//===== Description: =========================================
//= [Official Conversion]
//= Kreiger Von Midgard Battleground for levels 80 to 99
//= - Winning Team: 5 points
//= - Losing Team: 1 point
//===== Changelogs: ==========================================
//= 1.0 First Version. [L0ne_W0lf]
//= 1.1 Updated using official Aegis files. [L0ne_W0lf]
//= 1.2 Upated some announces and dialogs from iRO.
//=     Changed how the scoreboard works slightly.
//=     Removed the areapercentheals, and minor things.
//= 1.3 Added battle_config checks to allow this script to use the new queue interface or the previous method. [Aleos]
//============================================================

// Waiting Room NPCs
//============================================================
bat_room,169,226,5	script	KVM Waiting Room#a::KvM01R_Guillaume	418,{
	end;
OnInit:
	waitingroom "Battle Station 5 Players",6,"KvM01_BG_Out::OnGuillaumeJoin",1;
	end;
OnEnterBG:
	set $@KvM01BG_id1, waitingroom2bg("bat_c01",52,129,"KvM01_BG::OnGuillaumeQuit","KvM01_BG::OnGuillaumeDie");
	end;
}

bat_room,169,205,3	script	KVM Waiting Room#b::KvM01R_Croix	414,{
	end;

OnInit:
	waitingroom "Battle Station 5 Players",6,"KvM01_BG_Out::OnCroixJoin",1;
	end;

OnEnterBG:
	set $@KvM01BG_id2, waitingroom2bg("bat_c01",147,55,"KvM01_BG::OnCroixQuit","KvM01_BG::OnCroixDie");
	end;
}

bat_room,169,220,0	warp2	#kvm801	2,2,bat_room,154,150
bat_room,169,211,0	warp2	#kvm802	2,2,bat_room,154,150

// Starting Line
//============================================================
-	script	KVM01::CellEffect	-1,{
	end;
OnKvM01One:
	specialeffect EF_CHANGECOLD;
	end;
OnKvM01Two:
	specialeffect EF_CHANGEPOISON;
	end;
}

bat_c01,54,124,3	duplicate(CellEffect)	#RedcellA1-1	139
bat_c01,55,124,3	duplicate(CellEffect)	#RedcellA2-1	139
bat_c01,56,124,3	duplicate(CellEffect)	#RedcellA3-1	139
bat_c01,57,124,3	duplicate(CellEffect)	#RedcellA4-1	139
bat_c01,57,125,3	duplicate(CellEffect)	#RedcellA5-1	139
bat_c01,57,126,3	duplicate(CellEffect)	#RedcellA6-1	139
bat_c01,57,127,3	duplicate(CellEffect)	#RedcellA7-1	139

bat_c01,145,59,3	duplicate(CellEffect)	#RedcellB1-1	139
bat_c01,144,59,3	duplicate(CellEffect)	#RedcellB2-1	139
bat_c01,143,59,3	duplicate(CellEffect)	#RedcellB3-1	139
bat_c01,142,59,3	duplicate(CellEffect)	#RedcellB4-1	139
bat_c01,142,56,3	duplicate(CellEffect)	#RedcellB5-1	139
bat_c01,142,57,3	duplicate(CellEffect)	#RedcellB6-1	139
bat_c01,142,58,3	duplicate(CellEffect)	#RedcellB7-1	139

bat_c01,54,128,3	script	#A_camp_start01	139,4,4,{
	end;
OnInit:
	disablenpc "#A_camp_start01";
	end;
OnEnable:
	enablenpc "#A_camp_start01";
	end;
OnDisable:
	disablenpc "#A_camp_start01";
	end;
OnTouch:
	set Bat_Team,1;
	if (!getbattleflag("feature.bgqueue"))
		setquest 6025;
	end;
}

bat_c01,146,56,3	script	#B_camp_start01	139,4,4,{
	end;
OnInit:
	disablenpc "#B_camp_start01";
	end;
OnEnable:
	enablenpc "#B_camp_start01";
	end;
OnDisable:
	disablenpc "#B_camp_start01";
	end;
OnTouch:
	set Bat_Team,2;
	if (!getbattleflag("feature.bgqueue"))
		setquest 6025;
	end;
}

// Battleground Engine
//============================================================
-	script	KvM01_BG	-1,{
	end;

OnInit:
	if (getbattleflag("feature.bgqueue"))
		bg_unbook "bat_c01";
	mapwarp "bat_c01","bat_room",154,150;
	setwall "bat_c01",54,122,6,7,0,"batc01wall_a";
	setwall "bat_c01",55,122,5,7,0,"batc01wall_b";
	setwall "bat_c01",140,56,6,7,0,"batc01wall_c";
	setwall "bat_c01",140,57,5,7,0,"batc01wall_d";
	disablenpc "KVM Officer#KVM01A";
	disablenpc "KVM Officer#KVM01B";
	end;

OnGuillaumeQuit:
	if (getbattleflag("feature.bgqueue"))
		bg_desert;
//	else
//		set BG_Delay_Tick, gettimetick(2) + 1200;
OnGuillaumeDie:
	if( $@KvM01BG == 2 )
	{
		set .Guillaume_Count, .Guillaume_Count - 1;
		bg_updatescore "bat_c01",.Guillaume_Count,.Croix_Count;
		if( .Guillaume_Count < 1 ) donpcevent "KvM01_BG::OnCroixWin";
		else {
			mapannounce "bat_c01", "จำนวน Guillaumes คือ "+.Guillaume_Count+".",bc_map,"0x00ff00";
			mapannounce "bat_c01", "จำนวน Croixes คือ "+.Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	end;

OnCroixQuit:
	if (getbattleflag("feature.bgqueue"))
		bg_desert;
//	else
//		set BG_Delay_Tick, gettimetick(2) + 1200;
OnCroixDie:
	if( $@KvM01BG == 2 )
	{
		set .Croix_Count, .Croix_Count - 1;
		bg_updatescore "bat_c01",.Guillaume_Count,.Croix_Count;
		if( .Croix_Count < 1 ) donpcevent "KvM01_BG::OnGuillaumeWin";
		else {
			mapannounce "bat_c01", "จำนวน Guillaumes คือ "+.Guillaume_Count+".",bc_map,"0x00ff00";
			mapannounce "bat_c01", "จำนวน Croixes คือ "+.Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	end;

OnGuillaumeActive:
	warp "bat_c01",61,120;
	end;

OnCroixActive:
	warp "bat_c01",138,63;
	end;

OnStart:
	disablenpc "KVM Officer#KVM01A";
	disablenpc "KVM Officer#KVM01B";
	set $@KvM01BG_Victory, 0;
	// Warp Teams
	bg_warp $@KvM01BG_id1,"bat_c01",53,128;
	bg_warp $@KvM01BG_id2,"bat_c01",146,55;
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c01", "ภายใน 1 นาที KVM จะเริ่มต้น",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c01", "เวลาสูงสุดสำหรับการต่อสู้ KVM คือ 5 นาที",bc_map,"0x00ff00";
	end;

OnTimer6000:
	mapannounce "bat_c01", "โปรดเตรียมความพร้อม สำหรับการต่อสู้ KVM",bc_map,"0x00ff00";
	mapannounce "bat_c01", "ท่านสามารถเพิ่มความสามารถพิเศษให้คนของท่านได้",bc_map,"0x00ff00";
	donpcevent "#A_camp_start01::OnEnable";
	donpcevent "#B_camp_start01::OnEnable";
	end;

OnTimer13000:
	donpcevent "#A_camp_start01::OnDisable";
	donpcevent "#B_camp_start01::OnDisable";
	end;

OnTimer30000:
	mapannounce "bat_c01", "อีก 30 วินาที การต่อสู้ KVM จะเริ่มต้น",bc_map,"0x00ff00";
	end;

OnTimer45000:
	mapannounce "bat_c01", "อีก 15 วินาที การต่อสู้ KVM จะเริ่มต้น",bc_map,"0x00ff00";
	donpcevent "::OnKvM01One";
	end;

OnTimer50000:
	mapannounce "bat_c01", "อีก 10 วินาที การต่อสู้ KVM จะเริ่มต้น",bc_map,"0x00ff00";
	donpcevent "::OnKvM01Two";
	end;

OnTimer55000:
	mapannounce "bat_c01", "อีก 5 วินาที การต่อสู้ KVM จะเริ่มต้น",bc_map,"0x00ff00";
	donpcevent "::OnKvM01One";
	end;

OnTimer59000:
	mapannounce "bat_c01", "KVM ได้เริ่มขึ้นแล้ว",bc_map,"0x00ff00";
	donpcevent "::OnKvM01Two";
	end;

OnTimer61000:
	// Team Members
	set .Guillaume_Count, bg_get_data($@KvM01BG_id1, 0);
	set .Croix_Count, bg_get_data($@KvM01BG_id2, 0);

	if (!getbattleflag("feature.bgqueue")) {
		if (.Guillaume_Count < 5 || .Croix_Count < 5) {
			set $@KvM01BG_Victory, 3;
			set $@KvM01BG, 3;
			mapannounce "bat_c01","มีผู้เล่นไม่เพียงพอ ที่จะเริ่มการต่อสู้",1,0x696969;
			stopnpctimer;
			donpcevent "KvM01_BG::OnStop";
			end;
		}
	}
	bg_updatescore "bat_c01",.Guillaume_Count,.Croix_Count;
	set $@KvM01BG, 2; // Playing
	bg_warp $@KvM01BG_id1,"bat_c01",61,120;
	bg_warp $@KvM01BG_id2,"bat_c01",138,63;
	end;

OnTimer300000:
	mapannounce "bat_c01", "อีก 1 นาที การต่อสู้ KVM จะสิ้นสุด",bc_map,"0x00ff00";
	end;

OnTimer330000:
	mapannounce "bat_c01", "อีก 30 นาที การต่อสู้ KVM จะสิ้นสุด",bc_map,"0x00ff00";
	end;

OnTimer345000:
	mapannounce "bat_c01", "อีก 15 นาที การต่อสู้ KVM จะสิ้นสุด",bc_map,"0x00ff00";
	end;

OnTimer350000:
	mapannounce "bat_c01", "อีก 10 นาที การต่อสู้ KVM จะสิ้นสุด",bc_map,"0x00ff00";
	end;

OnTimer355000:
	mapannounce "bat_c01", "อีก 5 นาที การต่อสู้ KVM จะสิ้นสุด",bc_map,"0x00ff00";
	end;

OnTimer360000:
	mapannounce "bat_c01", "การต่อสู้ KVM จบลงแล้ว",bc_map,"0x00ff00";
	if( .Croix_Count > .Guillaume_Count )
		donpcevent "KvM01_BG::OnCroixWin";
	else if( .Croix_Count < .Guillaume_Count )
		donpcevent "KvM01_BG::OnGuillaumeWin";
	else
	{ // Draw Game
		set $@KvM01BG, 3;
		set $@KvM01BG_Victory, 3;
		mapannounce "bat_c01", "จำนวน Guillaumes คือ "+.Guillaume_Count+".",bc_map,"0x00ff00";
		mapannounce "bat_c01", "จำนวน Croixes คือ "+.Croix_Count+".",bc_map,"0x00ff00";
		mapannounce "bat_c01", "การต่อสู้ครั้งนี้จบลงด้วยผลเสมอ",bc_map,"0x00ff00";
		donpcevent "KvM01_BG::OnStop";
	}
	end;

OnGuillaumeWin:
	set $@KvM01BG, 3;
	set $@KvM01BG_Victory, 1;
	mapannounce "bat_c01", "Guillaume ชนะ!",bc_map,"0x00ff00";
	mapannounce "bat_c01", "ขอแสดงความยินดีกับสมาชิก Guillaume",bc_map,"0x00ff00";
	mapannounce "bat_c01", "ทุกคนจะถูกย้ายไปยังจุดเริ่มต้น",bc_map,"0x00ff00";
	donpcevent "KvM01_BG::OnStop";
	end;

OnCroixWin:
	set $@KvM01BG, 3;
	set $@KvM01BG_Victory, 2;
	mapannounce "bat_c01", "Croix ชนะ!",bc_map,"0x00ff00";
	mapannounce "bat_c01", "ขอแสดงความยินดีกับสมาชิก Croix",bc_map,"0x00ff00";
	mapannounce "bat_c01", "ทุกคนจะถูกย้ายไปยังจุดเริ่มต้น",bc_map,"0x00ff00";
	donpcevent "KvM01_BG::OnStop";
	end;

OnStop:
	stopnpctimer;
	enablenpc "KVM Officer#KVM01A";
	enablenpc "KVM Officer#KVM01B";
	// Warp Teams
	bg_warp $@KvM01BG_id1,"bat_c01",53,128;
	bg_warp $@KvM01BG_id2,"bat_c01",146,55;
	donpcevent "KvM01_BG_Out::OnBegin";
	if (getbattleflag("feature.bgqueue"))
		bg_reserve "bat_c01", true;
	end;
}

-	script	KvM01_BG_Out	-1,{
	end;

OnBegin:
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c01", "โปรดลงทะเบียนกับเจ้าหน้าที่ เพื่อรับคะแนน KVM",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c01", "เจ้าหน้าที่ จะให้คะแนนกับท่านเป็นเวลา 30 วินาที",bc_map,"0x00ff00";
	mapannounce "bat_c01", "ใน 30 วินาที เจ้าหน้าที่จะถูกส่งออกไป",bc_map,"0x00ff00";
	end;

OnTimer5000:
	mapannounce "bat_c01", "หากท่านไม่ได้คุยกับเจ้าหน้าที่ ท่านจะไม่สามารถได้รับคะแนนได้",bc_map,"0x00ff00";
	mapannounce "bat_c01", "โปรดระวัง",bc_map,"0x00ff00";
	end;

OnTimer55000:
	mapannounce "bat_c01", "ท่านจะถูกส่งกลับ",bc_map,"0x00ff00";
	end;

OnTimer60000:
	stopnpctimer;
	if (getbattleflag("feature.bgqueue"))
		bg_reserve "bat_c01", true;
	mapwarp "bat_c01","bat_room",154,150;
	set getvariableofnpc(.Croix_Count,"KvM01_BG"), 0;
	set getvariableofnpc(.Guillaume_Count,"KvM01_BG"), 0;
	set $@KvM01BG_Victory, 0;
	if( $@KvM01BG_id1 ) { bg_destroy $@KvM01BG_id1; set $@KvM01BG_id1, 0; }
	if( $@KvM01BG_id2 ) { bg_destroy $@KvM01BG_id2; set $@KvM01BG_id2, 0; }
	if (getbattleflag("feature.bgqueue"))
		bg_unbook "bat_c01";
	disablenpc "KVM Officer#KVM01A";
	disablenpc "KVM Officer#KVM01B";
	set $@KvM01BG, 0;
	end;

OnGuillaumeJoin:
OnCroixJoin:
	if( $@KvM01BG )
		end;
	set .@Guillaume, getwaitingroomstate(0,"KvM01R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"KvM01R_Croix");

	if( .@Guillaume < 5 || .@Croix < 5 )
		end;

	set $@KvM01BG, 1; // Starting
	donpcevent "KvM01R_Croix::OnEnterBG";
	donpcevent "KvM01R_Guillaume::OnEnterBG";
	donpcevent "KvM01_BG::OnStart";
	end;
}

// Battleground rewards
//============================================================
bat_c01,51,130,5	script	KVM Officer#KVM01A	419,{
	if( $@KvM01BG_Victory ) {
		mes "[KVM Officer]";
		if( $@KvM01BG_Victory == Bat_Team ) {
			mes "เกมที่ดี";
			mes "ขอให้เกียรติของ KVM อยู่กับท่าน";
			mes "ท่านได้รับคะแนนแห่งชัยชนะ: 5";
			close2;
			set kvm_point,kvm_point + 5;
		}
		else {
			mes "ขออภัย";
			mes "ข้าขอให้ท่านโชคดีในครั้งต่อไป";
			mes "ท่านได้รับคะแนนความพ่ายแพ้: 1";
			close2;
			set kvm_point,kvm_point + 1;
		}
		bg_leave;
		set Bat_Team,0;
		if (!getbattleflag("feature.bgqueue"))
			warp "bat_room",154,150;
	}
	end;
}
bat_c01,148,53,1	duplicate(KVM Officer#KVM01A)	KVM Officer#KVM01B	415

// BG Queue makes these scripts useless
-	script	BGQueueInit#kvm01	-1,{
	end;

OnInit:
	if (getbattleflag("feature.bgqueue")) {
		unloadnpc "KvM01R_Guillaume";
		unloadnpc "KvM01R_Croix";
		unloadnpc "#kvm801";
		unloadnpc "#kvm802";
	}
	end;
}
