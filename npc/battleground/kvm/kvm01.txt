//===== eAthena Script ======================================= 
// BattleGround System - KvM 80-99
//===== By: ================================================== 
//= ???, L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= eAthena 1.0
//===== Description: ========================================= 
//= [AEGIS Conversion]
//= Kreiger Von Midgard Battleground for levels 80 to 99
//= - Winning Team: 5 points
//= - Losing Team: 1 point
//===== Additional Comments: ================================= 
//= 1.0 First Version.
//= 1.1 Updated using official Aegis files. [L0ne_W0lf]
//============================================================

// Waiting Room NPCs
//============================================================
bat_room,169,226,5	script	KVM Waiting Room#a::KvM01R_Guillaume	418,{
	end;

OnInit:
	waitingroom "Battle Station 5 Players",5,"KvM01_BG::OnGuillaumeJoin",1;
	end;
	
OnEnterBG:
	set $@KvM01BG_id1, waitingroom2bg("bat_c01",52,129,"KvM01_BG::OnGuillaumeQuit","KvM01_BG::OnGuillaumeDie");
	end;
}

bat_room,169,220,0	warp	#kvm801	2,2,bat_room,154,150

bat_room,169,205,3	script	KVM Waiting Room#b::KvM01R_Croix	414,{
	end;

OnInit:
	waitingroom "Battle Station 5 Players",5,"KvM01_BG::OnCroixJoin",1;
	end;

OnEnterBG:
	set $@KvM01BG_id2, waitingroom2bg("bat_c01",147,55,"KvM01_BG::OnCroixQuit","KvM01_BG::OnCroixDie");
	end;
}

bat_room,169,211,0	warp	#kvm802	2,2,bat_room,154,150

// Starting Line
//============================================================
-	script	KVM01::CellEffect	-1,{
	end;

OnKvM01One: 
	misceffect EF_CHANGECOLD;
	end;

OnKvM01Two: 
	misceffect EF_CHANGEPOISON;
	end;
}

bat_c01,54,124,3	duplicate(CellEffect)	#RedcellA1-1	111
bat_c01,55,124,3	duplicate(CellEffect)	#RedcellA2-1	111
bat_c01,56,124,3	duplicate(CellEffect)	#RedcellA3-1	111
bat_c01,57,124,3	duplicate(CellEffect)	#RedcellA4-1	111
bat_c01,57,125,3	duplicate(CellEffect)	#RedcellA5-1	111
bat_c01,57,126,3	duplicate(CellEffect)	#RedcellA6-1	111
bat_c01,57,127,3	duplicate(CellEffect)	#RedcellA7-1	111

bat_c01,145,59,3	duplicate(CellEffect)	#RedcellB1-1	111
bat_c01,144,59,3	duplicate(CellEffect)	#RedcellB2-1	111
bat_c01,143,59,3	duplicate(CellEffect)	#RedcellB3-1	111
bat_c01,142,59,3	duplicate(CellEffect)	#RedcellB4-1	111
bat_c01,142,56,3	duplicate(CellEffect)	#RedcellB5-1	111
bat_c01,142,57,3	duplicate(CellEffect)	#RedcellB6-1	111
bat_c01,142,58,3	duplicate(CellEffect)	#RedcellB7-1	111

bat_c01,54,128,3	script	#A_camp_start01	111,4,4,{
	end;

OnInit:
	disablenpc "#A_camp_start01";
	end;

OnEnable:
	enablenpc "#A_camp_start01";
	end;

OnDisable:
	disablenpc "#A_camp_start01";
	end;

OnTouch:
	set Bat_Team,1;
	setquest 6025;
	end;
}

bat_c01,146,56,3	script	#B_camp_start01	111,4,4,{
	end;

OnInit:
	disablenpc "#B_camp_start01";
	end;

OnEnable:
	enablenpc "#B_camp_start01";
	end;

OnDisable:
	disablenpc "#B_camp_start01";
	end;

OnTouch:
	set Bat_Team,2;
	setquest 6025;
	end;
}

// Battleground Engine
//============================================================
-	script	KvM01_BG	-1,{
	end;

OnInit:
	setwall "bat_c01",54,122,6,7,0,"batc01wall_a";
	setwall "bat_c01",55,122,5,7,0,"batc01wall_b";
	setwall "bat_c01",140,56,6,7,0,"batc01wall_c";
	setwall "bat_c01",140,57,5,7,0,"batc01wall_d";
	disablenpc "KVM Officer#KVM01A";
	disablenpc "KVM Officer#KVM01B";
	end;

OnGuillaumeJoin:
OnCroixJoin:
	donpcevent "KvM01_BG::OnReadyCheck";
	end;

OnGuillaumeQuit:
	//set BG_Delay_Tick, gettimetick(2) + 1200;
OnGuillaumeDie:
	if( $@KvM01BG == 2 )
	{
		set .Guillaume_Count, .Guillaume_Count - 1;
		set .Croix_Score, .Croix_Score + 1;
		bg_updatescore "bat_c01",.Guillaume_Score,.Croix_Score;
		if( .Guillaume_Count < 1 ) donpcevent "KvM01_BG::OnCroixWin";
	}
	end;

OnCroixQuit:
	//set BG_Delay_Tick, gettimetick(2) + 1200;
OnCroixDie:
	if( $@KvM01BG == 2 )
	{
		set .Croix_Count, .Croix_Count - 1;
		set .Guillaume_Score, .Guillaume_Score + 1;
		bg_updatescore "bat_c01",.Guillaume_Score,.Croix_Score;
		if( .Croix_Count < 1 ) donpcevent "KvM01_BG::OnGuillaumeWin";
	}
	end;

OnReadyCheck:
	if( $@KvM01BG )
		end;
	set .@Guillaume, getwaitingroomstate(0,"KvM01R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"KvM01R_Croix");

	if( .@Guillaume < 5 || .@Croix < 5 )
		end;

	set $@KvM01BG, 1; // Starting
	donpcevent "KvM01R_Croix::OnEnterBG";
	donpcevent "KvM01R_Guillaume::OnEnterBG";
	donpcevent "KvM01_BG::OnStart";
	end;

OnStart:
	disablenpc "KVM Officer#KVM01A";
	disablenpc "KVM Officer#KVM01B";
	set $@KvM01BG_Victory, 0;
	// Warp Teams
	bg_warp $@KvM01BG_id1,"bat_c01",53,128;
	bg_warp $@KvM01BG_id2,"bat_c01",146,55;
	// ScoreBoard
	set .Guillaume_Score, 0;
	set .Croix_Score, 0;
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c01", "In 1 minute, KVM will start.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c01", "The maximum time for a KVM battle is 5 minutes.",bc_map,"0x00ff00";
	end;

OnTimer6000:
	mapannounce "bat_c01", "Please prepare for the KVM battle.",bc_map,"0x00ff00";
	mapannounce "bat_c01", "You can buff your people.",bc_map,"0x00ff00";
	donpcevent "#A_camp_start01::OnEnable";
	donpcevent "#B_camp_start01::OnEnable";
	end;

OnTimer13000:
	donpcevent "#A_camp_start01::OnDisable";
	donpcevent "#B_camp_start01::OnDisable";
	end;

OnTimer30000:
	mapannounce "bat_c01", "30 seconds remaining to start KVM battle.",bc_map,"0x00ff00";
	end;

OnTimer45000:
	mapannounce "bat_c01", "15 seconds remaining to start KVM battle.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01One";
	end;

OnTimer50000:
	mapannounce "bat_c01", "10 seconds remaining to start KVM battle.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01Two";
	end;

OnTimer55000:
	mapannounce "bat_c01", "5 seconds remaining to start KVM battle.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01One";
	end;

OnTimer59000:
	mapannounce "bat_c01", "KVM is now commencing.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01Two";
	end;

OnTimer61000:
	// Team Members
	set .Guillaume_Count, bg_get_data($@KvM01BG_id1, 0);
	set .Croix_Count, bg_get_data($@KvM01BG_id2, 0);
	if( .Guillaume_Count < 5 || .Croix_Count < 5 )
	{
		set $@KvM01BG_Victory, 3;
		set $@KvM01BG, 3;
		mapannounce "bat_c01","There are not enough players to start the battle",1,0x696969;
		stopnpctimer;
		donpcevent "KvM01_BG::OnStop";
		end;
	}
	set $@KvM01BG, 2; // Playing
	areapercentheal "bat_c01",50,123,58,131,100,100;
	bg_warp $@KvM01BG_id1,"bat_c01",61,120;
	areapercentheal "bat_c01",141,52,149,60,100,100;
	bg_warp $@KvM01BG_id2,"bat_c01",138,63;
	end;

OnTimer300000:
	mapannounce "bat_c01", "KVM will be ending in 1 minute.",bc_map,"0x00ff00";
	end;

OnTimer330000:
	mapannounce "bat_c01", "KVM will be ending in 30 seconds.",bc_map,"0x00ff00";
	end;

OnTimer345000:
	mapannounce "bat_c01", "KVM will be ending in 15 seconds.",bc_map,"0x00ff00";
	end;

OnTimer350000:
	mapannounce "bat_c01", "KVM will be ending in 10 seconds.",bc_map,"0x00ff00";
	end;

OnTimer355000:
	mapannounce "bat_c01", "KVM will be ending in 5 seconds.",bc_map,"0x00ff00";
	end;

OnTimer360000:
	mapannounce "bat_c01", "KVM has ended.",bc_map,"0x00ff00";
	if( .Croix_Count > .Guillaume_Count )
		donpcevent "KvM01_BG::OnCroixWin";
	else if( .Croix_Count < .Guillaume_Count )
		donpcevent "KvM01_BG::OnGuillaumeWin";
	else
	{ // Draw Game
		set $@KvM01BG, 3;
		set $@KvM01BG_Victory, 3;
		mapannounce "bat_c01", "Number of Guillaume survivors "+.Guillaume_Count+".",bc_map,"0x00ff00";
		mapannounce "bat_c01", "Number of Croix survivors "+.Croix_Count+".",bc_map,"0x00ff00";
		mapannounce "bat_c01", "Regardless of the outcome of the two camps.",bc_map,"0x00ff00";
		donpcevent "KvM01_BG::OnStop";
	}
	end;

OnGuillaumeWin:
	set $@KvM01BG, 3;
	set $@KvM01BG_Victory, 1;
	mapannounce "bat_c01", "Guillaume camp victory.",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Congratulations to all Guillaume members.",bc_map,"0x00ff00";
	mapannounce "bat_c01", "All personnel move to the starting zone.",bc_map,"0x00ff00";
	donpcevent "KvM01_BG::OnStop";
	end;

OnCroixWin:
	set $@KvM01BG, 3;
	set $@KvM01BG_Victory, 2;
	mapannounce "bat_c01", "Croix camp victory.",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Congratulations to all Croix members.",bc_map,"0x00ff00";
	mapannounce "bat_c01", "All personnel move to the starting zone.",bc_map,"0x00ff00";
	donpcevent "KvM01_BG::OnStop";
	end;

OnStop:
	stopnpctimer;
	enablenpc "KVM Officer#KVM01A";
	enablenpc "KVM Officer#KVM01B";
	// Warp Teams
	bg_warp $@KvM01BG_id1,"bat_c01",53,128;
	bg_warp $@KvM01BG_id2,"bat_c01",146,55;
	donpcevent "KvM01_BG_Out::OnBegin";
	end;

OnReset:
	set .Croix_Count, 0;
	set .Guillaume_Count, 0;
	set .Croix_Score, 0;
	set .Guillaume_Score, 0;
	set $@KvM01BG_Victory, 0;
	if( $@KvM01BG_id1 ) { bg_destroy $@KvM01BG_id1; set $@KvM01BG_id1, 0; }
	if( $@KvM01BG_id2 ) { bg_destroy $@KvM01BG_id2; set $@KvM01BG_id2, 0; }
	disablenpc "KVM Officer#KVM01A";
	disablenpc "KVM Officer#KVM01B";
	mapwarp "bat_c01","bat_room",154,150;
	maprespawnguildid "bat_c01",0,3; // Just in case someone else
	bg_updatescore "bat_c01",0,0;
	set $@KvM01BG, 0;
	donpcevent "KvM01_BG::OnReadyCheck"; // Maybe a game is ready to start
	end;
}

-	script	KvM01_BG_Out	-1,{
	end;

OnBegin:
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c01", "Please apply with the Officer to acquire KVM points.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c01", "The Officer will grant you the points for 30 seconds.",bc_map,"0x00ff00";
	mapannounce "bat_c01", "In 30 seconds, the Officer will be sent away.",bc_map,"0x00ff00";
	end;

OnTimer5000:
	mapannounce "bat_c01", "If you do not claim your points now, you will not be able to.",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Please make sure to collect before the time expires.",bc_map,"0x00ff00";
	end;

OnTimer60000:
	stopnpctimer;
	donpcevent "KvM01_BG::OnReset";
	end;
}

// Battleground rewards
//============================================================
bat_c01,51,130,5	script	KVM Officer#KVM01A	419,{
	if( $@KvM01BG_Victory )
	{
		if( $@KvM01BG_Victory == Bat_Team )
		{ // Victory
			set kvm_point,kvm_point + 5;
			mes "[Swandery]";
			mes "Blessed Guillaume!!";
			mes "Let's enjoy our glorious victory!";
			mes "" + strcharinfo(0) + ", its a sign reflecting victory";
			close2;
		}
		else
		{ // 
			set kvm_point,kvm_point + 1;
			mes "[Swandery]";
			mes "You lost, but you're dedicated to this battle.";
			mes "This is a reward for your great dedication by Guillaume Marollo!";
			mes "Just take this defeat a lesson, and later you would definitely learn.";
			close2;
		}
		bg_leave;
		warp "bat_room",154,150;
		end;
	}
	end;
}

bat_c01,148,53,1	script	KVM Officer#KVM01B	415,{
	if( $@KvM01BG_Victory )
	{
		if( $@KvM01BG_Victory == Bat_Team )
		{ // Victory
			set kvm_point,kvm_point + 5;
			mes "[Swandery]";
			mes "Blessed Croax!!";
			mes "Let's enjoy our glorious victory!";
			mes "" + strcharinfo(0) + ", its a sign reflecting victory";
			close2;
		}
		else
		{ // 
			set kvm_point,kvm_point + 1;
			mes "[Swandery]";
			mes "Oh, " + strcharinfo(0) + ". Don't be sad.";
			mes "Even though we didn't win, we did our best.";
			mes "This is a Royal gift from Croix, and please don't forget this battle. We can win the next.";
			close2;
		}
		bg_leave;
		warp "bat_room",154,150;
		end;
	}
	end;
}

// MapFlags
//============================================================

bat_c01	mapflag	battleground	2
bat_c01	mapflag	nomemo
bat_c01	mapflag	nosave	SavePoint
bat_c01	mapflag	noteleport
bat_c01	mapflag	nowarp
bat_c01	mapflag	nowarpto
bat_c01	mapflag	noreturn
bat_c01	mapflag	nobranch
bat_c01	mapflag	nopenalty
