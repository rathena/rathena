//===== rAthena Script =======================================
//= Reset NPC
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= Resets skills, stats, or both.
//===== Additional Comments: =================================
//= 1.0 First Version
//= 1.1 Optimized for the greater good. [Kisuka]
//= 1.2 Cleaning [Euphy]
//============================================================

prontera,150,193,4	script	Reset Girl	124,{

	set .@ResetStat,5000;	// Zeny for stat reset
	set .@ResetSkill,5000;	// Zeny for skill reset
	set .@ResetBoth,9000;	// Zeny for resetting both together

	mes "[Reset Girl]";
	mes "I am the Reset Girl.";
	mes "Reset Stats: "+ .@ResetStat +"z";
	mes "Reset Skills: "+ .@ResetSkill +"z";
	mes "Reset Both: "+ .@ResetBoth +"z";
	mes "Please select the service you want:";
	next;
	switch(select("^FF3355Reset Skills:Reset Stats:Reset Both^000000:Cancel")) {
	case 1:
		mes "[Reset Girl]";
		if (Zeny < .@ResetSkill) {
			mes "Sorry, you don't have enough Zeny.";
			close; }
		set Zeny, Zeny-.@ResetSkill;
		ResetSkill;
		mes "There you go!";
		close;
	case 2:
		mes "[Reset Girl]";
		if (Zeny < .@ResetStat) {
			mes "Sorry, you don't have enough Zeny.";
			close; }
		set Zeny, Zeny-.@ResetStat;
		ResetStatus;
		mes "There you go!";
		close;
	case 3:
		mes "[Reset Girl]";
		if (Zeny < .@ResetBoth) {
			mes "Sorry, you don't have enough Zeny.";
			close; }
		set Zeny, Zeny-.@ResetBoth;
		ResetSkill;
		ResetStatus;
		mes "There you go!";
		close;
	case 4:
		close;
	}
}
