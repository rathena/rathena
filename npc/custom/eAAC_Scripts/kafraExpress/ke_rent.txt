//===== rAthena Script ======================================= 
//= Kafra Express - Rental Module
//===== By: ================================================== 
//= Skotlex 
//===== Current Version: ===================================== 
//= 1.8
//===== Compatible With: ===================================== 
//= rAthena SVN R3424+
//===== Description: ========================================= 
//= Part of the Kafra Express Script Package.
//= Rents PecoPecos, Falcons, Carts 
//===== Additional Comments: ================================= 
//= See config.txt for configuration.
//============================================================ 

-	script	keInit_rent	-1,{
OnInit:	//Load Config
	donpcevent "keConfig::OnLoadRent";
	end;
}

function	script	F_keRent	{
	set @cartCost,callfunc("F_keCost",$@kert_cartCost,100);
	if(@kert_cartOnly) {
		set @kmenu, 2;
	} else {
		set @falconCost,callfunc("F_keCost",$@kert_falconCost,100);
		set @pecoCost,callfunc("F_keCost",$@kert_pecoCost,100);
		set @kmenu, select (
			"- Cancel",
			"- Rent a Cart ("+@cartCost+"z)",
			"- Rent a Falcon ("+@falconCost+"z)",
			"- Rent a PecoPeco ("+@pecoCost+"z)"
		);
	}
	switch (@kmenu) {
	case 2: //Cart
		if (getskilllv("MC_PUSHCART")==0) {
			callfunc "F_keIntro", -1, "Sorry, only those with the skill 'Pushcart' may rent a Cart.";
		} else 
		if (checkcart()) {
			callfunc "F_keIntro", -1, "You are already equipped.";
		} else
		if (!(callfunc("F_keCharge",$@kert_cartCost,100,1))) {
			callfunc "F_keIntro", e_an, "Sorry, but you don't have enough Zeny.";
		} else {
			setcart;
			emotion e_ok;
		}
		break;
	case 3: //Falcon
		if (getskilllv("HT_FALCON")==0) {
			callfunc "F_keIntro", -1, "Sorry, only those with the skill 'Falcon Taming' may rent a Falcon.";
		} else
		if (checkfalcon()) {
			callfunc "F_keIntro", -1, "You are already equipped.";
		} else
		if (!(callfunc("F_keCharge",$@kert_falconCost,100,1))) {
			callfunc "F_keIntro", e_an, "Sorry, but you don't have enough Zeny.";
		} else {
			setfalcon;
			emotion e_ok;
		}
		break;
	case 4: //pecopeco
		if (getskilllv("KN_RIDING")==0) {
			callfunc "F_keIntro", -1, "Sorry, only those with the skill 'PecoPeco Riding' may rent a PecoPeco.";
		} else
		if (checkriding()) {
			callfunc "F_keIntro", -1, "You are already equipped.";
		} else
		if (!(callfunc("F_keCharge",$@kert_pecoCost,100,1))) {
			callfunc "F_keIntro", e_an, "Sorry, but you don't have enough Zeny.";
		} else {
			setriding;
			emotion e_ok;
		}
		break;
	}
	return;
}
