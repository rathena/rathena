//===== rAthena Script =======================================
//= Healer
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Basic healer script.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.1 Aligned coordinates with @go.
//============================================================

-	script	Healer	-1,{

	.@Price = 0;	// Zeny required for heal
	.@Buffs = 1;	// Also buff players? (1: yes / 0: no)
	.@Delay = 0;	// Heal delay, in seconds

	if (@HD > gettimetick(2))
		end;
	if (.@Price) {
		message strcharinfo(0), "Healing costs " + callfunc("F_InsertComma",.@Price) + " Zeny.";
		if (Zeny < .@Price)
			end;
		if (select("^0055FFHeal^000000:^777777Cancel^000000") == 2)
			end;
		Zeny -= .@Price;
	}
	
	sc_end SC_STONE;
	sc_end SC_SLOWDOWN;
	sc_end SC_FREEZE;
	sc_end SC_SLEEP;
	sc_end SC_CURSE;
	sc_end SC_SILENCE;
	sc_end SC_CONFUSION;
	sc_end SC_BLIND;
	sc_end SC_BLEEDING;
	sc_end SC_DECREASEAGI;
	sc_end SC_POISON;
	sc_end SC_HALLUCINATION;
	sc_end SC_STRIPWEAPON;
	sc_end SC_STRIPARMOR;
	sc_end SC_STRIPHELM;
	sc_end SC_STRIPSHIELD;
	sc_end SC_CHANGEUNDEAD;
	sc_end SC_ORCISH;
	sc_end SC_BERSERK;
	sc_end SC_SKE;
	sc_end SC_SWOO;
	sc_end SC_SKA;
	specialeffect2 EF_HEAL2;
	percentheal 100,100;
	
	if (.@Buffs) {
		specialeffect2 EF_INCAGILITY;
		sc_start SC_INCREASEAGI,3600000,10;

		if (BaseLevel >= 10) {
			specialeffect2 EF_BLESSING;
			sc_start SC_BLESSING,3600000,10;
		}
		
		if (BaseLevel >= 20) {
			specialeffect2 EF_IMPOSITIO;
			sc_start SC_IMPOSITIO,3600000,5;
		}
		
		if (BaseLevel >= 60) {
			specialeffect2 EF_ASSUMPTIO;
			sc_start SC_ASSUMPTIO,3600000,5;

			specialeffect2 EF_MAGNIFICAT;
      		sc_start SC_MAGNIFICAT,3600000,5;
		}

		if (BaseLevel >= 100) {
			specialeffect2 AM_CP_HELM;
			sc_start SC_CP_HELM,3600000,5;
			specialeffect2 AM_CP_ARMOR;
			sc_start SC_CP_ARMOR,3600000,5;
			specialeffect2 AM_CP_SHIELD;
			sc_start SC_CP_SHIELD,3600000,5;
			specialeffect2 AM_CP_WEAPON;
			sc_start SC_CP_WEAPON,3600000,5;
		}
	}
	if (.@Delay)
		@HD = gettimetick(2) + .@Delay;
	end;
}


// Duplicates
//============================================================
alberta,25,240,6	duplicate(Healer)	Healer#alb	864
aldebaran,135,118,6	duplicate(Healer)	Healer#alde	864
amatsu,200,79,4	duplicate(Healer)	Healer#ama	864
ayothaya,207,169,6	duplicate(Healer)	Healer#ayo	864
comodo,184,158,6	duplicate(Healer)	Healer#com	864
einbech,57,36,6	duplicate(Healer)	Healer#einbe	864
einbroch,57,202,6	duplicate(Healer)	Healer#einbr	864
geffen,115,72,6	duplicate(Healer)	Healer#gef	864
gonryun,156,122,6	duplicate(Healer)	Healer#gon	864
hugel,89,150,6	duplicate(Healer)	Healer#hug	864
izlude,121,150,6	duplicate(Healer)	Healer#izl	864	//Pre-RE: (125,118)
jawaii,250,139,4	duplicate(Healer)	Healer#jaw	864
lighthalzen,152,100,6	duplicate(Healer)	Healer#lhz	864
louyang,226,103,4	duplicate(Healer)	Healer#lou	864
manuk,272,144,6	duplicate(Healer)	Healer#man	864
mid_camp,203,289,6	duplicate(Healer)	Healer#mid	864
moc_ruins,72,164,4	duplicate(Healer)	Healer#moc	864
morocc,153,97,6	duplicate(Healer)	Healer#mor	864
moscovia,220,191,4	duplicate(Healer)	Healer#mos	864
niflheim,212,182,5	duplicate(Healer)	Healer#nif	864
payon,179,106,4	duplicate(Healer)	Healer#pay	864
prontera,162,193,4	duplicate(Healer)	Healer#prt	864
rachel,125,116,6	duplicate(Healer)	Healer#rac	864
splendide,201,153,4	duplicate(Healer)	Healer#spl	864
thor_camp,249,74,4	duplicate(Healer)	Healer#thor	864
umbala,105,148,3	duplicate(Healer)	Healer#umb	864
veins,217,121,4	duplicate(Healer)	Healer#ve	864
xmas,143,136,4	duplicate(Healer)	Healer#xmas	864
yuno,164,45,4	duplicate(Healer)	Healer#yuno	864

// Duplicates (Renewal)
//============================================================
brasilis,194,221,6	duplicate(Healer)	Healer#bra	864
dewata,195,187,4	duplicate(Healer)	Healer#dew	864
dicastes01,201,194,4	duplicate(Healer)	Healer#dic	864
ecl_in01,45,60,4	duplicate(Healer)	Healer#ecl	864
malangdo,132,114,6	duplicate(Healer)	Healer#mal	864
malaya,227,204,6	duplicate(Healer)	Healer#ma	864
mora,55,152,4	duplicate(Healer)	Healer#mora	864
wolfvill,133,136,4	duplicate(Healer)	Healer#wolfvill	864