//===== rAthena Script =======================================
//= Card Removal NPC
//===== By: ==================================================
//= TyrNemesis^
//===== Current Version: =====================================
//= 1.2a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Removes cards from equipped items.
//===== Additional Comments: =================================
//= 1.0 First version. [TyrNemesis^]
//= 1.2 Optimized and fixed getequipname menu. [Kisuka]
//= 1.2a Added 'disable_items' command. [Euphy]
//============================================================


//= Translated & Modified by Start (Discord ID: Start#1356)


prontera,163,171,3	script	ถอดการ์ด	10307,{
	disable_items;
	mes "ถอดการ์ดฟรี";
	mes "ถอดได้เลย ไม่ต้องกลัวอะไรหายทั้งนั้น";
	next;
	setarray .@indices[1],EQI_HEAD_TOP,EQI_ARMOR,EQI_HAND_L,EQI_HAND_R,EQI_GARMENT,EQI_SHOES,EQI_ACC_L,EQI_ACC_R,EQI_HEAD_MID,EQI_HEAD_LOW,EQI_SHADOW_WEAPON,EQI_SHADOW_ARMOR,EQI_SHADOW_SHIELD,EQI_COSTUME_GARMENT,EQI_SHADOW_SHOES,EQI_SHADOW_ACC_L,EQI_SHADOW_ACC_R,EQI_COSTUME_HEAD_TOP,EQI_COSTUME_HEAD_MID,EQI_COSTUME_HEAD_LOW;
	for(.@i = 1; .@i < getarraysize(.@indices); .@i++){
		if(getequipisequiped(.@indices[.@i])){
			.@menu$ = .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			.@equipped = 1;
		}
		.@menu$ = .@menu$ + ":";
	}
	
	if(!.@equipped){
		mes "เกิดข้อผิดพลาด";
		close;
	}
	
	.@part = .@indices[select(.@menu$)];
	.@equipId = getequipid(.@part);
	
	if(!getequipisequiped(.@part)){
		mes "เกิดข้อผิดพลาด";
		close;
	}
	
	//Store refine
	.@refine_count = getequiprefinerycnt(.@part);
	
	//Store card
	if(getequipcardid(.@part,0) > 0)
	.@card_slot_1 = getequipcardid(.@part,0);
	if(getequipcardid(.@part,1) > 0)
	.@card_slot_2 = getequipcardid(.@part,1);
	if(getequipcardid(.@part,2) > 0)
	.@card_slot_3 = getequipcardid(.@part,2);
	if(getequipcardid(.@part,3) > 0)
	.@card_slot_4 = getequipcardid(.@part,3);

	//Store random option
	if(getequiprandomoption(.@part,0,ROA_ID) > 0){
		.@random_option_id_slot_1 = getequiprandomoption(.@part,0,ROA_ID);
		.@random_option_val_slot_1 = getequiprandomoption(.@part,0,ROA_VALUE);
	}	
	if(getequiprandomoption(.@part,1,ROA_ID) > 0){
		.@random_option_id_slot_2 = getequiprandomoption(.@part,1,ROA_ID);
		.@random_option_val_slot_2 = getequiprandomoption(.@part,1,ROA_VALUE);
	}	
	if(getequiprandomoption(.@part,2,ROA_ID) > 0){
		.@random_option_id_slot_3 = getequiprandomoption(.@part,2,ROA_ID);
		.@random_option_val_slot_3 = getequiprandomoption(.@part,2,ROA_VALUE);
	}
	if(getequiprandomoption(.@part,3,ROA_ID) > 0){
		.@random_option_id_slot_4 = getequiprandomoption(.@part,3,ROA_ID);
		.@random_option_val_slot_4 = getequiprandomoption(.@part,3,ROA_VALUE);
	}
	if(getequiprandomoption(.@part,4,ROA_ID) > 0){
		.@random_option_id_slot_5 = getequiprandomoption(.@part,4,ROA_ID);
		.@random_option_val_slot_5 = getequiprandomoption(.@part,4,ROA_VALUE);
	}

	//Delete equipment
	delequip .@part;
	
	setarray .@optID[0],.@random_option_id_slot_1,.@random_option_id_slot_2,.@random_option_id_slot_3,.@random_option_id_slot_4,.@random_option_id_slot_5;
	setarray .@optVal[0],.@random_option_val_slot_1,.@random_option_val_slot_2,.@random_option_val_slot_3,.@random_option_val_slot_4,.@random_option_val_slot_5;
	setarray .@optParam[0],0,0,0,0,0;
	
	//Give card
	if(.@card_slot_1>0)
	getitem .@card_slot_1,1;
	if(.@card_slot_2>0)
	getitem .@card_slot_2,1;
	if(.@card_slot_3>0)
	getitem .@card_slot_3,1;
	if(.@card_slot_4>0)
	getitem .@card_slot_4,1;

	//Give item without card
	if(.@random_option_id_slot_1 <= 0 && .@random_option_id_slot_2 <= 0 && .@random_option_id_slot_3 <= 0 && .@random_option_id_slot_4 <= 0 && .@random_option_id_slot_5 <= 0)
	getitem2 .@equipId,1,1,.@refine_count,0,0,0,0,0;
	else
	getitem3 .@equipId,1,1,.@refine_count,0,0,0,0,0,.@optID,.@optVal,.@optParam;

	mes "เรียบร้อย";
	specialeffect2 218;
	end;
	
OnInit:
	waitingroom "ถอดการ์ด",0;
	end;
}
