//===== rAthena Script =======================================
//= Platinum Skills
//===== By: ==================================================
//= Keichii, DarkChild
//===== Current Version: =====================================
//= 2.8
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Single NPC that assigns quests skills for all classes
//===== Additional Comments: =================================
//= 2.0 Added advanced classes by ShadowLady.
//= 2.1 Added baby clases by Midas
//= 2.2 Simplified Job Checks [Silentdragon]
//= 2.3 Thehell? Cleaned up script removed the nastiness. Also added rebirth skills. [Spre]
//= 2.4 Added Advanced skills. [Spre]
//= 2.5 Added dialog for if the user is none of the listed jobs. [Kisuka]
//= 2.6 Fixed 2nd advanced classes not being able to get their advanced skills. [Ancyker]
//= 2.7 Rewrote to give all correct skills to all proper classes. [Paradox924X]
//= 2.8 Clustered conditionals replaced with switch to include all Upper types. [Euphy]
//============================================================

prontera,128,200,6	script	Platinum Skill NPC	94,{
	mes "[Platinum Skill NPC]";
	mes "I can give you the special skills available to your job.";
	mes "Would you like these skills now?";
	next;
	if (select("Yes Please:No") == 2) {
		mes "[Platinum Skill NPC]";
		mes "Have a nice day... >.>";
		close;
	}
	skill "NV_FIRSTAID",1,0;
	switch (BaseClass) {
	case Job_Novice:
		if (Class != Job_SuperNovice)
			skill "NV_TRICKDEAD",1,0;
		break;
	case Job_Swordman:
		skill "SM_MOVINGRECOVERY",1,0;
		skill "SM_FATALBLOW",1,0;
		skill "SM_AUTOBERSERK",1,0;
		break;
	case Job_Mage:
		skill "MG_ENERGYCOAT",1,0;
		break;
	case Job_Archer:
		skill "AC_MAKINGARROW",1,0;
		skill "AC_CHARGEARROW",1,0;
		break;
	case Job_Acolyte:
		skill "AL_HOLYLIGHT",1,0;
		break;
	case Job_Merchant:
		skill "MC_CARTREVOLUTION",1,0;
		skill "MC_CHANGECART",1,0;
		skill "MC_LOUD",1,0;
		break;
	case Job_Thief:
		skill "TF_SPRINKLESAND",1,0;
		skill "TF_BACKSLIDING",1,0;
		skill "TF_PICKSTONE",1,0;
		skill "TF_THROWSTONE",1,0;
		break;
	default:
		break;
	}
	switch (BaseJob) {
	case Job_Knight:
		skill "KN_CHARGEATK",1,0;
		break;
	case Job_Priest:
		skill "PR_REDEMPTIO",1,0;
		break;
	case Job_Wizard:
		skill "WZ_SIGHTBLASTER",1,0;
		break;
	case Job_Blacksmith:
		skill "BS_UNFAIRLYTRICK",1,0;
		skill "BS_GREED",1,0;
		break;
	case Job_Hunter:
		skill "HT_PHANTASMIC",1,0;
		break;
	case Job_Assassin:
		skill "AS_SONICACCEL",1,0;
		skill "AS_VENOMKNIFE",1,0;
		break;
	case Job_Crusader:
		skill "CR_SHRINK",1,0;
		break;
	case Job_Monk:
		skill "MO_KITRANSLATION",1,0;
		skill "MO_BALKYOUNG",1,0;
		break;
	case Job_Sage:
		skill "SA_CREATECON",1,0;
		skill "SA_ELEMENTWATER",1,0;
		skill "SA_ELEMENTGROUND",1,0;
		skill "SA_ELEMENTFIRE",1,0;
		skill "SA_ELEMENTWIND",1,0;
		break;
	case Job_Rogue:
		skill "RG_CLOSECONFINE",1,0;
		break;
	case Job_Alchemist:
		skill "AM_BIOETHICS",1,0;
		break;
	case Job_Bard:
		skill "BA_PANGVOICE",1,0;
		break;
	case Job_Dancer:
		skill "DC_WINKCHARM",1,0;
		break;
	default:
		break;
	}
	mes "[Platinum Skill NPC]";
	mes "There you go!";
	close;
}
