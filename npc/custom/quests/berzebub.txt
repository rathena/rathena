//===== rAthena Script =======================================
//= Berzebub Card Quest
//===== By: ==================================================
//= jabs
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Quest to get the Berzebub Card
//===== Additional Comments: =================================
//= 1.1 Fixed ingredients. Was too exploitable [Lupus]
//= 1.2 Optimized. [Euphy]
//============================================================

prontera,165,178,8	script	Old Woman	103,{

	setarray .@Items[0],714,25,1061,100,1095,200,618,10,1551,2,1614,1,727,20;
	set .@n$,"[Old Woman]";

	mes .@n$;
	if(BerzQuest == 1) {
		mes "Hey, I remember you! I already told you that you may only complete this quest once.";
		close; }
	mes "Greeting, lad. I found a really nifty item while I was picking mushrooms near Glast Heim.";
	next;
	if(select("Keep Talking:Leave")==2) goto M_Leave;

	mes .@n$;
	mes "I have never seen an item like this before. I am told it can be placed inside any accessory with a slot available.";
	next;
	mes .@n$;
	mes "If you're interested in this item, I'll tell you what you need for me to make you one of your own. I can only give one of these to each player, so once you finish this quest, you may not do it again.";
	next;
	if(select("Tell me more about it:Nah, I don't care about it")==2) goto M_Leave;

	mes .@n$;
	mes "I believe the item is called a ^FF0000Berzebub Card^000000. It makes you cast spells really fast!";
	next;
	mes .@n$;
	mes "I need all of the following items:";
	for(set .@i,0; .@i<getarraysize(.@Items); set .@i,.@i+2) {
		mes " ~ "+.@Items[.@i+1]+"x "+getitemname(.@Items[.@i]);
		if (countitem(.@Items[.@i]) < .@Items[.@i+1]) set .@nr,1; }
	next;
	if(select("I have all of that!:I'll get those ASAP")==2) goto M_Leave;

	mes .@n$;
	if (.@nr) {
		mes "You lie! Get out of here and get those items, you sorry excuse for a rock star.";
		close; }
	for(set .@i,0; .@i<getarraysize(.@Items); set .@i,.@i+2)
		delitem .@Items[.@i], .@Items[.@i+1];
	getitem 4145,1;
	set BerzQuest, 1;
	mes "Congratulations! You have all the items. Here is your ^FF0000Berzebub Card^000000, as promised.";
	emotion ET_CONGRATULATION;
	close;

M_Leave:
	mes .@n$;
	mes "Such a great item I have right here...";
	close;
}
