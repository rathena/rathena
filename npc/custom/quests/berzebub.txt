//===== rAthena Script =======================================
//= Berzebub Card Quest
//===== By: ==================================================
//= jabs
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= Quest to get the Berzebub Card
//===== Additional Comments: =================================
//= 1.1 Fixed ingredients. Was too exploitable [Lupus]
//============================================================

prontera,165,178,8	script	Old Woman	103,{
	mes "[Old Woman]";
	if(BerzQuest == 1) goto L_Already;
	mes "Greeting lad. I found a really nifty item while I was picking mushrooms near Glast Heim.";
	next;
	menu "Keep talking",-, "Leave", M_Leave;
	
	mes "[Old Woman]";
	mes "I have never seen an item like this before.  I am told it can be placed inside am accessory that has a slot avaiable in it.";
	next;
	mes "[Old Woman]";
	mes "If you're interested in this item, tell me and I'll tell you what you need for me to make you one of your own.  I can only give one of these to each player, so once you finish this quest once, you may not do it again.";
	next;
	menu "Tell me more about it",-, "Nah, I don't care about it", M_Leave;
	
	mes "[Old Woman]";
	mes "I believe the item is called a ^FF0000Berzebub Card^000000.  It can make any spell caster cast spells really fast!";
	next;
	mes "[Old Woman]";
	mes "I need all of the following items:";
	mes "^0080FF25^000000 Emperiums";
	mes "^0080FF100^000000 Witched Starsands";
	mes "^0080FF200^000000 Needles of Alarm";
	mes "^0080FF10^000000 Worn Out Scrolls";
	mes "^0080FF2^000000 Biblies";
	mes "^0080FF1^000000 Wand of the Occult";
	mes "^0080FF20^000000 Opals";
	next;
	menu "I have all that!",-, "I'll get those ASAP", M_Leave;
	
	mes "[Old Woman]";
	mes "Anyone can say they have the items, but do they really have them?  Let's take a look here...";
	next;
	mes "[Old Woman]";
	if(countitem(714) < 25 || countitem(1061) < 100 || countitem(1095) < 200 || countitem(618) < 10 || countitem(1551) < 2 || countitem(1614) < 1 || countitem(727) < 20) goto L_noItems;
	delitem 714, 25;
	delitem 1061, 100;
	delitem 1095, 200;
	delitem 618, 10;
	delitem 1551, 2;
	delitem 1614, 1;
	delitem 727, 20;
	getitem 4145, 1;
	set BerzQuest, 1;
	mes "Well congratulations!  You have all the items.  Here is your ^FF0000Berzebub Card^000000, just as I promised.";
	emotion e_grat;
	close;

M_Leave:
	mes "[Old Woman]";
	mes "Such a great item I have right here...";
	close;
L_noItems:
	mes "I knew you were lying!  Get out of here and get those items you sorry excuse for a rock star.";
	close;
L_Already:
	mes "Hey I remember you!  I already told you that you may only complete this quest once.";
	close;
}
