//===== rAthena Script =======================================
//= Friday Memorial Dungeon
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Friday Memorial Dungeon
//===== Changelogs: ==========================================
//= 1.0 Initial release [Everade]
//============================================================

gef_tower,57,170,3	script	Marry Jay	4_F_JOB_BLACKSMITH,{
	if(BaseLevel < 130){
		mes "[Marry Jay]";
		mes "Your base level is too low, come back when you hit level 130";
		close;
	}
	mes "^ff0000If you do any monster processing, such as monster taming within the dungeon, it won't count towards your progress. Please take note.^000000";
	next;
	.@day = gettime(DT_DAYOFWEEK);
	//.@day = FRIDAY; // for testing
	if(.@day != FRIDAY) {
		mes "[Marry Jay]";
		mes "Oh, a curious onlooker? It's Marry Jay, the person in charge of Fridays among the days of the week.";
		next;
		select("When is the opening time?");
		mes "[Marry Jay]";
		mes "The dungeon will be open during:";
		mes "^0000FFFriday 00:00 ~ Friday 23:59^000000";
		next;
		mes "[Marry Jay]";
		mes "I hope to see you when Friday arrives~";
		close;
	}
	.@md_name$ = "Friday Dungeon";
		switch(checkquest(12379,PLAYTIME)) {
		case -1: break;
		case 0:
		case 1:
			mes "[Marry Jay]";
			mes "Sorry, but the entry period to the "+.@md_name$+" has expired.";
			mes "Take a rest while waiting for the dungeon to be available again.";
			close;
		case 2:
			erasequest 12379;
			mes "[Marry Jay]";
			mes "The cooldown has expired.";
			mes "You may re-enter the Memorial Dungeon.";
			close;
		}
		mes "[Marry Jay]";
		mes "You came just in time.";
		mes "The memorial dungeon is just ready to be opened.";
		mes "Would you like to enter now?";
		next;
		if (is_party_leader() == true) {
			switch(select("Prepare Friday Memorial:Enter Friday Memorial:Cancel")) {
				case 1:
					instance_create(.@md_name$);
					mes "[Marry Jay]";
					mes "The Memorial Dungeon has been created.";
					mes "You may now enter.";
					close;
				case 2:
					switch( instance_enter(.@md_name$) ) {
						case IE_NOMEMBER:
							end;
						case IE_NOINSTANCE:
							mes "There is no Memorial Dungeon registered.";
							close;
						case IE_OTHER:
							mes "An unknown error occurred.";
							close;
						case IE_OK:
							mes "[Marry Jay]";
							mes "Off you go!";
							mapannounce "gef_tower", "" + strcharinfo(0) + " of the party, " + getpartyname( getcharid(1) ) + ", is entering the " + .@md_name$ + ".", bc_map, 0xFF99;
							if(isbegin_quest(12379)<1) setquest 12379;
					}
					end;
				case 3:
					mes "[Marry Jay]";
					mes "You have not prepared?";
					mes "Can't really help it I guess";
					close;
			}
		} else {
			switch(select("Enter Friday Memorial:Cancel")) {
				case 1:
					switch( instance_enter(.@md_name$) ) {
						case IE_NOMEMBER:
							mes "Only party members can enter this Memorial dungeon.";
							close;
						case IE_NOINSTANCE:
							mes "The Friday Memorial Dungeon does not exist.";
							mes "Your party leader has not yet created the Memorial dungeon.";
							close;
						case IE_OTHER:
							mes "An unknown error occurred.";
							close;
						case IE_OK:
							mes "[Marry Jay]";
							mes "Off you go!";
							mapannounce "gef_tower", "" + strcharinfo(0) + " of the party, " + getpartyname( getcharid(1) ) + ", is entering the " + .@md_name$ + ".", bc_map, 0xFF99;
							if(isbegin_quest(12379)<1) setquest 12379;
							end;
					}
					break;
				case 2:
					mes "[Marry Jay]";
					mes "You have not prepared?";
					mes "Can't really help it I guess";
					close;
			}
		}
	end;
}

//Instance
1@md_gef,185,81	monster	Shining Plant	1083,1,1800000,900000

1@md_gef,110,129,6	script	Marry Jay#0_1	4_F_JOB_BLACKSMITH,{

	if (is_party_leader() == true) {
		mes "[Marry Jay]";
		mes "Before we start setting up the environment here, we'll give you the chance to choose an appropriate difficulty level.";
		mes "I could make my own choice, but each person handles it differently based on their abilities.";
		next;
		switch(select("Cancel:LV130 ~ 199:LV200+ ")) {
			case 1:
				close;
			//MinLevel, Lich Lord, Nightmare, Jakk, Ghoul, Drainliar
			case 2:
				setarray 'mob,130,3658,3660,3662,3664,3666; break;
			case 3:
				if(BaseLevel < 200){
				mes "[Marry Jay]";
					mes "You can't choose this difficulty, because your base level is below 200.";
					close;
				}
				'hard = 1;
				setarray 'mob,200,3659,3661,3663,3665,3667; break;
		}
		if(BaseLevel < 'mob[0]){
			mes "[Marry Jay]";
			mes "Your base level is too low to select this, you need to be level "+'mob[0]+" or above";
			close;
		}
		close2;
		npctalk "Now, this place is about to become a hunting ground. I'll get out and close the front door.";
		sleep 4000;
		npctalk "Good Luck~";
		sleep 3000;
		if ('step == 0) {
			'step = 1;
			monster 'map$,0,0,"Nightmare",'mob[2],20,instance_npcname(strnpcinfo(0))+"::OnNightmareDead";
			monster 'map$,0,0,"Jakk",'mob[3],30,instance_npcname(strnpcinfo(0))+"::OnJakkDead";
			monster 'map$,0,0,"Ghoul",'mob[4],30,instance_npcname(strnpcinfo(0))+"::OnGhoulDead";
			monster 'map$,0,0,"Drainliar",'mob[5],20,instance_npcname(strnpcinfo(0))+"::OnDrainliarDead";
			hideonnpc instance_npcname(strnpcinfo(0));
			enablenpc instance_npcname("The Stranger#cadaver");
			enablenpc instance_npcname("Bizzare Sculpture#boss");
		}
	}
	end;

OnNightmareDead:
	callsub S_ChkTreasure;
	sleep 10000;
	monster 'map$,0,0,"Nightmare",'mob[2],1,instance_npcname(strnpcinfo(0))+"::OnNightmareDead";
	end;
OnJakkDead:
	callsub S_ChkTreasure;
	sleep 10000;
	monster 'map$,0,0,"Jakk",'mob[3],1,instance_npcname(strnpcinfo(0))+"::OnJakkDead";
	end;
OnGhoulDead:
	callsub S_ChkTreasure;
	sleep 10000;
	monster 'map$,0,0,"Ghoul",'mob[4],1,instance_npcname(strnpcinfo(0))+"::OnGhoulDead";
	end;
OnDrainliarDead:
	callsub S_ChkTreasure;
	sleep 10000;
	monster 'map$,0,0,"Drainliar",'mob[5],1,instance_npcname(strnpcinfo(0))+"::OnDrainliarDead";
	end;

// Treasure Chest Spawn
S_ChkTreasure:
	'ct++;
	if ('ct == 100) {
		'rand = rand(1,4);
		'chest1 = 'rand;	//remember in case someone kills 100 enemies within 3 minutes
		enablenpc instance_npcname("#fd_box"+'chest1);
		instance_announce -1, "A treasure chest appeared somewhere in the dungeon. It'll disappear after a while, so let's find it.",bc_map,"0xff5500";
		sleep 180000;	//3min before chest disappears
		disablenpc instance_npcname("#fd_box"+'chest1);
	}
	if ('ct == 200) {
		'rand++;
		if ('rand >4) 'rand = 1;
		'chest2 = 'rand;	//remember in case someone kills 100 enemies within 3 minutes
		enablenpc instance_npcname("#fd_box"+'chest2);
		instance_announce -1, "A treasure chest appeared somewhere in the dungeon. It'll disappear after a while, so let's find it.",bc_map,"0xff5500";
		sleep 180000;	//3min before chest disappears
		disablenpc instance_npcname("#fd_box"+'chest2);
	}
	if ('ct == 300) {
		'rand++;
		if ('rand >4) 'rand = 1;
		'chest3 = 'rand;	//remember in case someone kills 100 enemies within 3 minutes
		enablenpc instance_npcname("#fd_box"+'chest3);
		instance_announce -1, "A treasure chest appeared somewhere in the dungeon. It'll disappear after a while, so let's find it.",bc_map,"0xff5500";
		sleep 180000;	//3min before chest disappears
		disablenpc instance_npcname("#fd_box"+'chest3);
	}
	if ('ct == 400) {
		'rand++;
		if ('rand >4) 'rand = 1;
		'chest4 = 'rand;	//remember in case someone kills 100 enemies within 3 minutes
		enablenpc instance_npcname("#fd_box"+'chest4);
		instance_announce -1, "A treasure chest appeared somewhere in the dungeon. It'll disappear after a while, so let's find it.",bc_map,"0xff5500";
		sleep 180000;	//3min before chest disappears
		disablenpc instance_npcname("#fd_box"+'chest4);
	}
	return;

OnInstanceInit:
	'ct = 0;
	'freestones = 0;
	'boss_actv = 0;
	'map$ = instance_mapname(strnpcinfo(4));
	'step = 0;
	end;
}


1@md_gef,110,129,6	script	Mary Juj#0_2	4_F_JOB_BLACKSMITH,{
	npctalk "Hello";
	end;
OnInstanceInit:
	disablenpc();
	end;
}

1@md_gef,183,222,5	script	The Stranger#cadaver	4_TOWER_11,{
	mes "It is a stranger's corpse.";
	mes "Perhaps he died while wandering in this place on the search for treasure.";
	next;
	switch(select("Calm down:Investigate the body")) {
	case 1:
		mes "You respect the dead and decided to leave the body alone.";
		close;
	case 2:
		mes "Looking at a hastily scribbled note in his pocket, perhaps the adventurer's name was Walter.";
		next;
		mes "[Adventurer's Log]";
		mes "My name is Walter. For the adventurers who will visit this place in the future, I'm leaving this so that you don't make the same mistake again.";
		next;
		mes "[Adventurer's Log]";
		mes "On the first day, I curiously put my hand on the slab of a strange Sculpture nearby.";
		mes "...and I awokened something that I should not have.";
		next;
		mes "[Adventurer's Log]";
		mes "Right now the revived ancient king is wandering in front of me.";
		mes "Perhaps he has no memories of his previous life.";
		mes "One thing is certain, he will kill me.";
		next;
		mes "[Adventurer's Log]";
		mes "To the adventurers who will visit this place in the future, I hope you do not make the same mistake as me.";
		mes "If you do see a suspicious slab, do not show any interest and pass by it.";
		next;
		mes "[Adventurer's Log]";
		mes "Again, never touch the slab. Whatever temptation you may have.";
		if('freestones==0) {
			next;
			mes "Several craft stones fall from his body. They were probably collected while exploring this place.";
			close2;
			if('freestones==0) {
				if ( 'hard == 1 )
					.@num = rand(7,9);
				else
					.@num = rand(5,7);
					for ( .@i = 0; .@i < .@num; .@i++ )
						makeitem 25235,1, 'map$, rand(181,183), rand(221,224);
				'freestones=1;
			}
		} else
			close;
	}
	end;

OnInstanceInit:
	disablenpc();
	end;
}

1@md_gef,199,73,0	script	Bizzare Sculpture#boss	CLEAR_NPC,{
	if('ct < 100) {
		mes "It is a strange sculpture with a lot of dust piled up. I don't see anything suspicious.";
		close;
	}
	if('boss_actv >= 1) {
		mes "Craft Stones have been melted by a powerful force into the sockets.";
		mes "Seems like the sockets can't be used for a while.";
		close;
	}
	mes "At some point, the accumulated dust scatters, and 10 hollow holes are visible on the slab under the sculpture.";
	next;
	mes "Each of the 10 hollow sockets seem to perfectly fit a craft stone.";
	next;
	switch(select("Ignore:Try inserting craft stones")) {
		case 1:
			close;
		case 2:
			if(countitem(25235) >= 10) {
				mes "When you insert a craft stone into a hole, it emits a bright light and melts.";
				next;
				delitem 25235,10;
				specialeffect EF_ANGELUS;
				.@rand = rand(1,10);
				if(.@rand==10) {
					mes "Something seems to have changed in the air around me.";
					instance_announce -1, "Lord of the Dead: Who Awakened Me? I will meet you in person and ask you why.",bc_map,"0xFFFF00";
					if ('boss_actv == 0) {
						monster 'map$,210,110,"Lich King",'mob[1],1;
						'boss_actv = 1;
					}
				} else {
					mes "But nothing else happened.";
					close2;
					if ('boss_actv == 0) {
						disablenpc();	// Disable NPC for 5 minutes when MVP didn't spawn.
						sleep 300000;
						enablenpc();
					}
					end;
				}
				close;
			}
			mes "Craft Stones have been melted by a powerful force into the sockets.";
			mes "Seems like the sockets can't be used for a while.";
			break;
	}
	close;

OnInstanceInit:
	disablenpc();
	end;
}


// Treasure Chest Reward
1@md_gef,212,212,3	script	#fd_box1	4_TREASURE_BOX,{
	specialeffect EF_COIN;
	disablenpc();
	initnpctimer;
	end;

OnTimer500:
	'chestname$ = strnpcinfo(2);
	if ( 'hard == 1 )
		.@num = rand(7,9);
	else
		.@num = rand(5,7);
	if ( 'chestname$ == "fd_box1" ) {
		for ( .@i = 0; .@i < .@num; .@i++ )
			makeitem 25235,1, 'map$, rand(210,214), rand(210,214);
	}
	if ( 'chestname$ == "fd_box2" ) {
		for ( .@i = 0; .@i < .@num; .@i++ )
			makeitem 25235,1, 'map$, rand(188,192), rand(54,58);
	}
	if ( 'chestname$ == "fd_box3" ) {
		for ( .@i = 0; .@i < .@num; .@i++ )
			makeitem 25235,1, 'map$, rand(47,51), rand(55,59);
	}
	if ( 'chestname$ == "fd_box4" ) {
		for ( .@i = 0; .@i < .@num; .@i++ )
			makeitem 25235,1, 'map$, rand(42,46), rand(209,213);
	}
	stopnpctimer;
	end;

OnInstanceInit:
	disablenpc();
	end;
}

1@md_gef,190,56,3	duplicate(#fd_box1)	#fd_box2	4_TREASURE_BOX
1@md_gef,49,57,3	duplicate(#fd_box1)	#fd_box3	4_TREASURE_BOX
1@md_gef,44,211,3	duplicate(#fd_box1)	#fd_box4	4_TREASURE_BOX


//Merchant
gef_tower,57,167,3	script	Amateur Collector#pa0829	1_F_01,{
	disable_items;
	function equip_menu;
	function enchant;

	if (checkweight(1201,1) == 0) {
		mes "You have too many kinds of objects. Clear your inventory.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "Can't continue because you have too many heavy objects. Reduce your total weight by clearing your inventory.";
		close;
	}
	mes "[Amateur Collector]";
	mes "If you bring me a <ITEM>"+getitemname(25235)+"<INFO>25235</INFO></ITEM>, I can enchant a wonderful effect on your accessory!";
	next;
	switch(select("What can you do with the Crafting Stone?:Accessory Enchantment:Reset Enchant")) {
		case 1:
			mes "[Amateur Collector]";
			mes "I'm a trade expensive stones from the dungeon in exchange for my craftsmanship. I'm still a beginner, but my skills are passable.";
			mes "Please take care of me.";
			next;
			switch(select("I've heard this before.:I'm listening.")) {
				case 1:
					mes "[Amateur Collector]";
					mes "Is that so?";
					close;
				case 2:
					mes "[Amateur Collector]";
					mes "By using the Craft Stone, I can enchant one of your accessories with abilities.";
					next;
					mes "[Amateur Collector]";
					mes "I need 10 x <ITEM>"+getitemname(25235)+"<INFO>25235</INFO></ITEM> and 100,000 Zeny.";
					mes "There is no failure in the enchantment process.";
					next;
					mes "[Amateur Collector]";
					mes "However ^0000FF if the accessory has a random option, the random option will be removed after enchantment.^000000";
					next;
					select("Anything else?");
					mes "[Amateur Collector]";
					mes "Cards slotted to the accessory will remain after the enchantement.";
					next;
					mes "[Amateur Collector]";
					mes "A reset is possible, but I can't say that I'm experienced.";
					mes "My success rate is:";
					mes "Royal Ring: 80%";
					mes "Other Accessory: 20%";
					next;
					mes "[Amateur Collector]";
					mes "^0000FFIf the accessory has a random option, the random option will be removed after initialization.^000000";
					close;
			}
		case 2:
			.@slot = equip_menu;
			.@equip_id = getequipid(.@slot);
			.@equip_refine = getequiprefinerycnt(.@slot);
			setarray .@equip_card[0], getequipcardid(.@slot,0), getequipcardid(.@slot,1), getequipcardid(.@slot,2), getequipcardid(.@slot,3);

			if(.@equip_id==0) {
				mes "[Amateur Collector]";
				mes "Please equip your accessory.";
				close;
			}
			if(compare(.list$,","+.@equip_id+",")==0) {
				mes "[Amateur Collector]";
				mes "I cannot enchant this <ITEM>"+getitemname(.@equip_id)+"<INFO>"+.@equip_id+"</INFO></ITEM> accessory.";
				close;
			}
			//Royal Ring Enchantment
			if(.@equip_id==28483) {
				if(.@equip_card[2] != 0) {
					mes "[Amateur Collector]";
					mes "The <ITEM>"+getitemname(.@equip_id)+"<INFO>"+.@equip_id+"</INFO></ITEM> already has maximum enchantment.";
					mes "I cannot work on this any further.";
					close;
				}
				.@pos = .@equip_card[3]!=0?2:3;
				mes "[Amateur Collector]";
				mes "Please select an enchantment type~";
				next;
				switch(select("Physical:Magical:Ranged")) {
					case 1:
						if(.@pos==2)
							.@equip_card[2] = enchant(2);
						else
							.@equip_card[3] = enchant(3);
						break;
					case 2:
						if(.@pos==2)
							.@equip_card[2] = enchant(4);
						else
							.@equip_card[3] = enchant(5);
						break;
					case 3:
						if(.@pos==2)
							.@equip_card[2] = enchant(6);
						else
							.@equip_card[3] = enchant(7);
						break;
				}
				if(.@pos==2) {
					mes "[Amateur Collector]";
					mes "Are you sure to enhance the <ITEM>"+getitemname(.@equip_id)+"<INFO>"+.@equip_id+"</INFO></ITEM>?";
					mes "^0000FFThere is a no chance of failure^000000.";
					next;
					if(select("Yes, please proceed:Cancel")==2) close;
				}
				if(countitem(25235)<10) {
					mes "[Amateur Collector]";
					mes "Please bring me 10 x <ITEM>"+getitemname(25235)+"<INFO>25235</INFO></ITEM>";
					close;
				}
				if(Zeny < 100000) {
					mes "[Amateur Collector]";
					mes "I need 100,000 zeny!";
					close;
				}
				delitem 25235,10; //Catalyst
				Zeny -= 100000;
				delequip .@slot;
				specialeffect2 EF_REPAIRWEAPON;
				mes "[Amateur Collector]";
				mes "The <ITEM>"+getitemname(.@equip_id)+"<INFO>"+.@equip_id+"</INFO></ITEM> has been enchanted!!";
				getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];
			//Normal Accessory Enchantment
			} else {
				if(.@equip_card[3] != 0) {
					mes "[Amateur Collector]";
					mes "The <ITEM>"+getitemname(.@equip_id)+"<INFO>"+.@equip_id+"</INFO></ITEM> already has an enchant.";
					mes "I cannot work on this any further.";
					close;
				}
				mes "[Amateur Collector]";
				mes "Are you sure to enhance the <ITEM>"+getitemname(.@equip_id)+"<INFO>"+.@equip_id+"</INFO></ITEM>?";
				mes "^0000FFThere is a no chance of failure^000000.";
				next;
				if(select("Yes, please proceed:Cancel")==2) close;
				if(countitem(25235)<10) {
					mes "[Amateur Collector]";
					mes "Please bring me 10 x <ITEM>"+getitemname(25235)+"<INFO>25235</INFO></ITEM>";
					close;
				}
				if(Zeny < 100000) {
					mes "[Amateur Collector]";
					mes "I need 100,000 zeny!";
					close;
				}
				delitem 25235,10; //Catalyst
				Zeny -= 100000;
				delequip .@slot;
				.@enchant = enchant(1);
				specialeffect2 EF_REPAIRWEAPON;
				mes "[Amateur Collector]";
				mes "The <ITEM>"+getitemname(.@equip_id)+"<INFO>"+.@equip_id+"</INFO></ITEM> has been enhanted with <ITEM>"+getitemname(.@enchant)+"<INFO>"+.@enchant+"</INFO></ITEM>.";
				getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@enchant;
			}
			close;
		case 3:
			.@slot = equip_menu;
			.@equip_id = getequipid(.@slot);
			.@equip_refine = getequiprefinerycnt(.@slot);
			setarray .@equip_card[0], getequipcardid(.@slot,0), getequipcardid(.@slot,1), getequipcardid(.@slot,2), getequipcardid(.@slot,3);

			if(.@equip_id==0) {
				mes "[Amateur Collector]";
				mes "Please equip your accessory.";
				close;
			}
			if(compare(.list$,","+.@equip_id+",")==0) {
				mes "[Amateur Collector]";
				mes "I cannot reset this <ITEM>"+getitemname(.@equip_id)+"<INFO>"+.@equip_id+"</INFO></ITEM> accessory.";
				close;
			}
			if(.@equip_id==28483) {
				.@pos = 2;
				.@success = .success_royal;
			} else {
				.@pos = 3;
				.@success = .success_normal;
			}
			if(.@equip_card[.@pos] == 0) {
				mes "[Amateur Collector]";
				mes "The <ITEM>"+getitemname(.@equip_id)+"<INFO>"+.@equip_id+"</INFO></ITEM> is not at its maximum enchantment.";
				mes "I cannot reset this accessory.";
				close;
			}
			mes "[Amateur Collector]";
			mes "All enchanted effects will be removed from the <ITEM>"+getitemname(.@equip_id)+"<INFO>"+.@equip_id+"</INFO></ITEM>?";
			mes "^FF0000There is a "+.@success+"% chance of success. The accessory will be destroyed if the reset fails!^000000";
			next;
			if(select("I'll be back later:Please proceed")==1) close;
			if(countitem(25235)<10) {
				mes "[Amateur Collector]";
				mes "Please bring me 10 x <ITEM>"+getitemname(25235)+"<INFO>25235</INFO></ITEM>";
				close;
			}
			if(Zeny < 100000) {
				mes "[Amateur Collector]";
				mes "I need 100,000 zeny!";
				close;
			}
			delitem 25235,10; //Catalyst
			Zeny -= 100000;
			delequip .@slot;
			if(rand(100)>.@success) {
				specialeffect2 EF_SUI_EXPLOSION;
				mes "[Amateur Collector]";
				mes "Darn it...";
				mes "I have failed and the accessory has been destroyed. I'm so sorry.";
				close;
			}
			specialeffect2 EF_REPAIRWEAPON;
			mes "[Amateur Collector]";
			mes "I have succeeded.";
			mes "The <ITEM>"+getitemname(.@equip_id)+"<INFO>"+.@equip_id+"</INFO></ITEM> has been initialized.";
			getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],0,0;
			close;
	}

	function	equip_menu {
		.@l$ = getitemname(getequipid(EQI_ACC_L));
		.@r$ = getitemname(getequipid(EQI_ACC_R));
		.@acc_l$ = .@l$=="null"?"":"Left Acc - "+.@l$;
		.@acc_r$ = .@r$=="null"?"":"Right Acc - "+.@r$;
		switch(select(.@acc_l$,.@acc_r$)) {
			case 1: .@slot = EQI_ACC_L; break;
			case 2: .@slot = EQI_ACC_R; break;
		}
		return .@slot;
	}

	function	enchant  {
		.@type = getarg(0);

		switch(.@type) {
		case 1: //Normal Enchant
			.@i = rand(920);
				 if (.@i < 30) .@enchant = 4701; //STR+2
			else if (.@i < 60) .@enchant = 4702; //STR+3
			else if (.@i < 90) .@enchant = 4703; //STR+4
			else if (.@i < 120) .@enchant = 4711; //INT+2
			else if (.@i < 150) .@enchant = 4712; //INT+3
			else if (.@i < 180) .@enchant = 4713; //INT+4
			else if (.@i < 210) .@enchant = 4721; //DEX+2
			else if (.@i < 240) .@enchant = 4722; //DEX+3
			else if (.@i < 270) .@enchant = 4723; //DEX+4
			else if (.@i < 300) .@enchant = 4731; //AGI+2
			else if (.@i < 330) .@enchant = 4732; //AGI+3
			else if (.@i < 360) .@enchant = 4733; //AGI+4
			else if (.@i < 390) .@enchant = 4741; //VIT+2
			else if (.@i < 420) .@enchant = 4742; //VIT+3
			else if (.@i < 450) .@enchant = 4743; //VIT+4
			else if (.@i < 480) .@enchant = 4796; //HP+200
			else if (.@i < 510) .@enchant = 4798; //HP+400
			else if (.@i < 540) .@enchant = 4792; //DEF+6
			else if (.@i < 570) .@enchant = 4794; //DEF+12
			else if (.@i < 600) .@enchant = 4800; //SP+50
			else if (.@i < 630) .@enchant = 4801; //SP+100
			else if (.@i < 660) .@enchant = 4786; //MDEF+2
			else if (.@i < 690) .@enchant = 4787; //MDEF+4
			else if (.@i < 710) .@enchant = 4861; //MHP+1%
			else if (.@i < 730) .@enchant = 4862; //MHP+2%
			else if (.@i < 750) .@enchant = 4929; //MSP+1%
			else if (.@i < 770) .@enchant = 4882; //ATK+1%
			else if (.@i < 790) .@enchant = 4883; //MATK+1%
			else if (.@i < 800) .@enchant = 4817; //Sharp2
			else if (.@i < 810) .@enchant = 4816; //Sharp3
			else if (.@i < 815) .@enchant = 4843; //Sharp4
			else if (.@i < 825) .@enchant = 29047; //Fatal
			else if (.@i < 835) .@enchant = 4863; //Fatal1Lv
			else if (.@i < 840) .@enchant = 4864; //Fatal2Lv
			else if (.@i < 850) .@enchant = 4832; //Expert Archer1
			else if (.@i < 860) .@enchant = 4813; //Spell3
			else if (.@i < 870) .@enchant = 4812; //Spell4
			else if (.@i < 875) .@enchant = 4826; //Spell5
			else if (.@i < 885) .@enchant = 4805; //Archbishop1Lv
			else if (.@i < 895) .@enchant = 4850; //Heal Amount2
			else if (.@i < 905) .@enchant = 4809; //Fighting Spirit3
			else if (.@i < 915) .@enchant = 4808; //Fighting Spirit4
			else .@enchant = 4820; //Fighting Spirit5
			break;
		case 2: //Royal Physical Slot 3
			.@i = rand(225);
				 if (.@i < 30) .@enchant = 4809; //Fighting Spirit3
			else if (.@i < 60) .@enchant = 4808; //Fighting Spirit4
			else if (.@i < 70) .@enchant = 4820; //Fighting Spirit5
			else if (.@i < 80) .@enchant = 4821; //Fighting Spirit6
			else if (.@i < 85) .@enchant = 4822; //Fighting Spirit7
			else if (.@i < 115) .@enchant = 4818; //Sharp1
			else if (.@i < 145) .@enchant = 4817; //Sharp2
			else if (.@i < 155) .@enchant = 4816; //Sharp3
			else if (.@i < 165) .@enchant = 4843; //Sharp4
			else if (.@i < 170) .@enchant = 4844; //Sharp5
			else if (.@i < 200) .@enchant = 4819; //Atk1
			else if (.@i < 210) .@enchant = 4766; //ATK+2%
			else if (.@i < 215) .@enchant = 4767; //ATK+3%
			else if (.@i < 220) .@enchant = 4807; //Atk Speed1
			else .@enchant = 4842; //Atk Speed2
			break;
		case 3: //Royal Physical Slot 4
			.@i = rand(375);
				 if (.@i < 50)  .@enchant = 4702; //STR+3
			else if (.@i < 100) .@enchant = 4703; //STR+4
			else if (.@i < 110) .@enchant = 4704; //STR+5
			else if (.@i < 120) .@enchant = 4705; //STR+6
			else if (.@i < 125) .@enchant = 4706; //STR+7
			else if (.@i < 175) .@enchant = 4732; //AGI+3
			else if (.@i < 225) .@enchant = 4733; //AGI+4
			else if (.@i < 235) .@enchant = 4734; //AGI+5
			else if (.@i < 245) .@enchant = 4735; //AGI+6
			else if (.@i < 250) .@enchant = 4736; //AGI+7
			else if (.@i < 300) .@enchant = 4742; //VIT+3
			else if (.@i < 350) .@enchant = 4743; //VIT+4
			else if (.@i < 360) .@enchant = 4744; //VIT+5
			else if (.@i < 370) .@enchant = 4745; //VIT+6
			else .@enchant = 4746; //VIT+7
			break;
		case 4: //Royal Magical Slot 3
			.@i = rand(230);
				 if (.@i < 30) .@enchant = 4813; //Spell3
			else if (.@i < 60) .@enchant = 4812; //Spell4
			else if (.@i < 70) .@enchant = 4826; //Spell5
			else if (.@i < 80) .@enchant = 4827; //Spell6
			else if (.@i < 85) .@enchant = 4828; //Spell7
			else if (.@i < 115) .@enchant = 4929; //MSP+1%
			else if (.@i < 145) .@enchant = 4805; //Archbishop1Lv
			else if (.@i < 155) .@enchant = 4850; //Archbishop2Lv
			else if (.@i < 165) .@enchant = 4851; //Archbishop3Lv
			else if (.@i < 170) .@enchant = 4852; //Archbishop4Lv
			else if (.@i < 200) .@enchant = 4883; //MATK + 1%
			else if (.@i < 210) .@enchant = 4896; //MATK + 2%
			else if (.@i < 215) .@enchant = 4897; //MATK + 3%
			else if (.@i < 225) .@enchant = 4760; //MATK 1Lv
			else .@enchant = 4761; //MATK 2Lv
			break;
		case 5: //Royal Magical Slot 4
			.@i = rand(375);
				 if (.@i < 50)  .@enchant = 4712; //INT+3
			else if (.@i < 100) .@enchant = 4713; //INT+4
			else if (.@i < 110) .@enchant = 4714; //INT+5
			else if (.@i < 120) .@enchant = 4715; //INT+6
			else if (.@i < 125) .@enchant = 4716; //INT+7
			else if (.@i < 175) .@enchant = 4722; //DEX+3
			else if (.@i < 225) .@enchant = 4723; //DEX+4
			else if (.@i < 235) .@enchant = 4724; //DEX+5
			else if (.@i < 245) .@enchant = 4725; //DEX+6
			else if (.@i < 250) .@enchant = 4726; //DEX+7
			else if (.@i < 300) .@enchant = 4742; //VIT+3
			else if (.@i < 350) .@enchant = 4743; //VIT+4
			else if (.@i < 360) .@enchant = 4744; //VIT+5
			else if (.@i < 370) .@enchant = 4745; //VIT+6
			else .@enchant = 4746; //VIT+7
			break;
		case 6: //Royal Ranged Slot 3
			.@i = rand(225);
				 if (.@i < 30) .@enchant = 29047; //Fatal
			else if (.@i < 60) .@enchant = 4863; //Fatal1Lv
			else if (.@i < 70) .@enchant = 4864; //Fatal2Lv
			else if (.@i < 80) .@enchant = 4865; //Fatal3Lv
			else if (.@i < 85) .@enchant = 4866; //Fatal4Lv
			else if (.@i < 115) .@enchant = 4818; //Sharp1
			else if (.@i < 145) .@enchant = 4817; //Sharp2
			else if (.@i < 155) .@enchant = 4816; //Sharp3
			else if (.@i < 165) .@enchant = 4843; //Sharp4
			else if (.@i < 170) .@enchant = 4844; //Sharp5
			else if (.@i < 200) .@enchant = 4832; //Expert Archer1
			else if (.@i < 210) .@enchant = 4833; //Expert Archer2
			else if (.@i < 215) .@enchant = 4834; //Expert Archer3
			else if (.@i < 220) .@enchant = 4807; //Atk Speed1
			else .@enchant = 4842; //Atk Speed2
			break;
		case 7: //Royal Ranged Slot 4
			.@i = rand(375);
				 if (.@i < 50)  .@enchant = 4732; //AGI+3
			else if (.@i < 100) .@enchant = 4733; //AGI+4
			else if (.@i < 110) .@enchant = 4734; //AGI+5
			else if (.@i < 120) .@enchant = 4735; //AGI+6
			else if (.@i < 125) .@enchant = 4736; //AGI+7
			else if (.@i < 175) .@enchant = 4722; //DEX+3
			else if (.@i < 225) .@enchant = 4723; //DEX+4
			else if (.@i < 235) .@enchant = 4724; //DEX+5
			else if (.@i < 245) .@enchant = 4725; //DEX+6
			else if (.@i < 250) .@enchant = 4726; //DEX+7
			else if (.@i < 300) .@enchant = 4752; //LUK+3
			else if (.@i < 350) .@enchant = 4753; //LUK+4
			else if (.@i < 360) .@enchant = 4754; //LUK+5
			else if (.@i < 370) .@enchant = 4755; //LUK+6
			else .@enchant = 4756; //LUK+7
			break;
		}
		return .@enchant;
	}

OnInit:
	.success_royal = 80;
	.success_normal = 20;
	.list$ = ",28483,2601,2602,2603,2605,2607,2608,2609,2610,2611,2612,2613,2614,2615,2616,2617,2618,2619,2620,2621,2622,2623,2625,2626,2627,2628,2636,2637,2638,2639,2640,2641,2645,2648,2649,2650,2651,2652,2653,2654,2655,2656,2658,2663,2664,2665,2666,2667,2671,2680,2692,2700,2701,2702,2703,2712,2714,2715,2716,2718,2719,2720,2721,2722,2723,2724,2725,2726,2727,2728,2729,2730,2731,2732,2736,2737,2743,2744,2745,2746,2747,2748,2749,2766,2767,2768,2769,2770,2771,2772,2773,2774,2777,2778,2779,2780,2781,2783,2784,2787,2788,2789,2790,2794,2795,2800,2801,2802,2803,2804,2805,2807,2808,2809,2810,2826,2827,2843,2853,2854,2881,2956,";
	end;
}
