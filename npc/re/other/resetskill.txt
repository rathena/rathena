//===== rAthena Script =======================================
//= Hypnotist
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Stat and skill resetter for expanded first class under
//= base level 50 and normal first class.
//===== Additional Comments: =================================
//= 1.0 First version, Renewal script.
//= 1.1 Added Izlude duplicates. [Euphy]
//============================================================

// Main NPC :: resetskill_novice
//============================================================
izlude,127,175,3	script	Hypnotist#novice	124,{
	mes "[Hypnotist]";
	mes "Greetings, adventurer.";
	mes "I'm a member of the Hypnotist";
	mes "Academy sent here to Izlude";
	mes "to provide Skill Reset services";
	mes "to certain First Class characters for a really good price: free!";
	next;
	mes "[Hypnotist]";
	mes "Although I offer unlimited";
	mes "stat/skill resets for now, I have";
	mes "two conditions that must be";
	mes "fulfilled. First, you must be";
	mes "lower than ^FF0000Base Level 50^000000.";
	next;
	mes "[Hypnotist]";
	mes "Second, you must be a";
	mes "^FF0000Swordman, Acolyte, Mage,";
	mes "Thief, Archer, Merchant,";
	mes "Taekwon Boy, or Taekwon Girl,";
	mes "Gunslinger and Ninja^000000";
	mes "Job character to qualify.";
	mes "Now, do you have any questions?";
	next;
	switch(select("^FF0000Stat/Skill Reset?:Reset stats:Reset skills^000000:Cancel")) {
	case 1:
		mes "[Hypnotist]";
		mes "Stat/Skill Resets allow adventuers";
		mes "to redistribute their Skill";
		mes "Points if they are unhappy";
		mes "with their current skills.";
		next;
		mes "[Hypnotist]";
		mes "Before proceeding with";
		mes "a Stat/Skill Reset, you must";
		mes "reduce all of the weight";
		mes "of all carried items on your";
		mes "character to 0. You can put";
		mes "extra items in Kafra Storage.";
		next;
		mes "[Hypnotist]";
		mes "Ah, it's also important";
		mes "to remove your ^FF0000Pushcart^000000";
		mes "if you have one equipped.";
		mes "Otherwise, hypnosis won't";
		mes "work, or will backfire...";
		close;
	case 2:
		set .@str$,"Stat";
	case 3:
		if (.@str$ == "")
			set .@str$,"Skill";
		mes "[Hypnotist]";
		mes "Are you sure that you";
		mes "want to proceed with";
		mes "my ^FF0000"+.@str$+" Reset^000000 service?";
		next;
		if(select("Yes:Cancel") == 1)
			break;
	case 4:
		mes "[Hypnotist]";
		mes "Thank you, and good";
		mes "luck on your adventures.";
		mes "Please travel in safety~";
		close;
	}
	if ((Class >= Job_Swordman && Class <= Job_Thief) || Class == Job_Taekwon || Class == Job_Gunslinger || Class == Job_Ninja) {
		if (checkcart()) {
			mes "[Hypnotist]";
			mes "Oh! Please remove your";
			mes "Pushcart before proceeding";
			mes "with the "+.@str$+" Reset service.";
			mes "Thanks for cooperating~";
			close;
		}
		if (Weight != 0) {
			mes "[Hypnotist]";
			mes "If you're here for my "+.@str$;
			mes "Reset service, please";
			mes "remember that you can't";
			mes "reset your "+.@str$+"s until the";
			mes "^FF0000weight of your carried items in";
			mes "your Inventory is reduced to 0^000000.";
			next;
			mes "[Hypnotist]";
			mes "Why don't you place your";
			mes "things into the Kafra Storage";
			mes "for now? That way, you can";
			mes "safely keep all of your goods.";
			close;
		}
		if (BaseLevel >= 50 && (Class == Job_Taekwon || Class == Job_Gunslinger || Class == Job_Ninja)) {
			mes "[Hypnotist]";
			mes "I'm sorry, but characters";
			mes "with Base Levels higher";
			mes "than 50 are ineligible for the";
			mes .@str$+" Reset service I provide.";
			close;
		}
		mes "[Hypnotist]";
		mes "Thank you for using";
		mes "my "+.@str$+" Redistribution";
		mes "services. Oh, and best";
		mes "of luck to you on your";
		mes "travels, adventurer.";
		if (.@str$ == "Stat")
			resetstatus;
		else
			resetskill;
		close;
	} else {
		mes "[Hypnotist]";
		mes "I'm sorry, but your";
		mes "Job Class doesn't qualify";
		mes "for the "+.@str$+" Reset service";
		mes "that I provide. I can only";
		mes "offer "+.@str$+" Resets to the";
		mes "following Jobs...";
		next;
		mes "[Hypnotist]";
		mes "^FF0000Swordman, Acolyte,";
		mes "Mage, Thief, Archer,";
		mes "Merchant, Taekwon";
		mes "Boy, Taekwon Girl,";
		mes "Gunslinder and Ninja^000000.";
		close;
	}
}
izlude_a,127,175,3	duplicate(Hypnotist#novice)	Hypnotist#novice_a	124
izlude_b,127,175,3	duplicate(Hypnotist#novice)	Hypnotist#novice_b	124
izlude_c,127,175,3	duplicate(Hypnotist#novice)	Hypnotist#novice_c	124
izlude_d,127,175,3	duplicate(Hypnotist#novice)	Hypnotist#novice_d	124
