//===== rAthena Script ======================================= 
//= Al De Baran Guides
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Guides for the city of Al De Baran.
//===== Additional Comments: ================================= 
//= 1.0 First version, Renewal guide.
//= 1.1 Navigation system update. [Euphy]
//=     Optimized.
//============================================================ 

aldebaran,139,63,4	script	Guide#01aldebaran::GuideAldearan	105,{
	cutin "prt_soldier",2;
	mes "[Al De Baran Guide]";
	mes "Welcome to ^8B4513Al De Baran^000000,";
	mes "the Border City.";
	mes "Do you need help navigating the city?";
	F_Navi("[Al De Baran Guide]");
	next;
	set .@str$,"Would you like to check any other locations?";
	while (1) {
		switch(select("[ Main Facilities ]:[ Merchants & Helpers ]:Remove Marks from Mini-Map:Cancel")) {
		case 1:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ Kafra Headquarters ]:[ Alchemist Guild ]:[ Clock Tower ]:[ Inn & Tool Shop ]:[ Repair & Buy Equipment ]:[ Tavern ]:Previous Menu")) {
				case 1:
					callsub L_Mark, F_Navi("Kafra Headquarters","aldebaran,61,229","^B9062F");
					mes .@str$;
					viewpoint 1,61,229,0,0xFF0000;
					next;
					break;
				case 2:
					callsub L_Mark, F_Navi("Alchemist Guild","aldebaran,60,60","^0000FF");
					mes .@str$;
					viewpoint 1,60,60,1,0x0A82FF;
					next;
					break;
				case 3:
					callsub L_Mark, F_Navi("Clock Tower","aldebaran,139,133","^FF5400");
					mes .@str$;
					viewpoint 1,139,133,2,0xFFB400;
					next;
					break;
				case 4:
					mes "[Al De Baran Guide]";
					mes "Let me mark the location of the";
					mes F_Navi("Inn & Tool Shop","aldebaran,197,70","^006400")+" building";
					mes "on your mini-map.";
					mes .@str$;
					viewpoint 1,197,70,3,0xAAFF00;
					next;
					break;
				case 5:
					mes "[Al De Baran Guide]";
					mes "If you'd like to ";
					mes F_Navi("Buy or Repair Equipment","aldebaran,197,70","^7F3300")+",";
					mes "then this is the place you'll want to visit.";
					mes .@str$;
					viewpoint 1,72,197,4,0xD2691E;
					next;
					break;
				case 6:
					callsub L_Mark, F_Navi("Tavern","aldebaran,231,106","^800080");
					viewpoint 1,231,106,5,0xDA70D6;
					next;
					break;
				case 7:
					set .@loop,0;
					break;
				}
			}
			break;
		case 2:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ Eden Teleport Officer ]:[ Kafra Employee ]:[ Mr. Claus ]:[ Chef Assistant ]:Previous Menu")) {
				case 1:
					callsub L_Mark, F_Navi("Eden Teleport Officer","aldebaran,133,119","^B9062F");
					mes .@str$;
					viewpoint 1,133,119,6,0xFF0000;
					next;
					break;
				case 2:
					callsub L_Mark, "^0000FFKafra Employee^000000";
					viewpoint 1,143,119,7,0x0A82FF;
					next;
					break;
				case 3:
					mes "[Al De Baran Guide]";
					mes "Mr. Claus can teleport";
					mes "you to "+F_Navi("Lutie","aldebaran,168,168","^006400")+".";
					mes "Let me mark his location";
					mes "on your mini-map.";
					mes .@str$;
					viewpoint 1,168,168,8,0xAAFF00;
					next;
					break;
				case 4:
					callsub L_Mark, F_Navi("Chef Assistant","aldebaran,165,107","^7F3300");
					mes .@str$;
					viewpoint 1,165,107,9,0xD2691E;
					next;
					break;
				case 5:
					set .@loop,0;
					break;
				}
			}
			break;
		case 3:
			mes "[Al De Baran Guide]";
			mes "Sure, I'll remove all marks from your mini-map.";
			mes "Is there anything else I can do for you?";
			viewpoint 2,1,1,0,0xFFFF00;
			viewpoint 2,1,1,1,0x000000;
			viewpoint 2,1,1,2,0xFF0000;
			viewpoint 2,1,1,3,0xFFFF00;
			viewpoint 2,1,1,4,0xFFFF00;
			viewpoint 2,1,1,5,0xFFFF00;
			viewpoint 2,1,1,6,0xFFFF00;
			viewpoint 2,1,1,7,0xFFFF00;
			viewpoint 2,1,1,8,0xFFFF00;
			viewpoint 2,1,1,9,0xFFFF00;
			next;
			break;
		case 4:
			mes "[Al De Baran Guide]";
			mes "Alright then,";
			mes "enjoy your stay!";
			close2;
			cutin "prt_soldier",255;
			end;
		}
	}
	end;
L_Mark:
	mes "[Al De Baran Guide]";
	mes "Let me mark the location of the";
	mes getarg(0);
	mes "on your mini-map.";
	return;
}
aldebaran,243,143,2	duplicate(GuideAldearan)	Guide#02aldebaran	105
aldebaran,135,243,4	duplicate(GuideAldearan)	Guide#03aldebaran	105
aldebaran,36,135,6	duplicate(GuideAldearan)	Guide#04aldebaran	105
