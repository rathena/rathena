//===== rAthena Script ======================================= 
//= Renters
//===== By: ================================================== 
//= rAthena Dev Team
//===== Current Version: ===================================== 
//= 2.2
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Renewal-specific breeder NPCs.
//===== Additional Comments: ================================= 
//= 2.1 Moved some renters to a separate renewal file. [Daegaladh]
//= 2.2 Updated to match the official scripts. [Euphy]
//============================================================ 

// Dragon Breeder :: dragon
//============================================================
job3_rune01,88,62,5	script	Dragon Breeder	105,{
	mes "[Dragon Breeder]";
	if (Class == Job_Rune_Knight || Class == Job_Rune_Knight_T || Class == Job_Baby_Rune) {
		mes "Welcome. Would you like to rent a Dragon?";
		next;
		switch(select("Yes:No")) {
		case 1:
			//if (!getskilllv("RK_DRAGONTRAINING")) {
			if (!getskilllv("KN_RIDING")) {
				mes "[Dragon Breeder]";
				mes "Please learn how to ride a Dragon first.";
				close;
			}
			else if (checkdragon()) {
				mes "[Dragon Breeder]";
				mes "You already have a Dragon.";
				close;
			}
			else if (ismounting()) {
				mes "[Dragon Breeder]";
				mes "Please remove your cash mount.";
				close;
			}
			setdragon;
			close;
		case 2:
			mes "[Dragon Breeder]";
			mes "I see. Then have a great day.";
			close;
		}
	}
	mes "What are you doing here?";
	mes "Only Rune Knights can rent a Dragon.";
	close;
}

// Dragon/Gryphon Master :: dragon
//============================================================
prontera,130,213,5	script	Riding Creature Master	105,{
	mes "[Riding Creature Master]";
	if (Class == Job_Rune_Knight || Class == Job_Rune_Knight_T || Class == Job_Baby_Rune) {
		mes "Welcome. Would you like to rent a Dragon?";
		next;
		switch(select("Yes:No")) {
		case 1:
			//if (!getskilllv("RK_DRAGONTRAINING")) {
			if (!getskilllv("KN_RIDING")) {
				mes "[Riding Creature Master]";
				mes "Please learn how to ride a Dragon first.";
				close;
			}
			else if (checkdragon()) {
				mes "[Riding Creature Master]";
				mes "You already have a Dragon.";
				close;
			}
			else if (ismounting()) {
				mes "[Riding Creature Master]";
				mes "Please remove your cash mount.";
				close;
			}
			setdragon;
			close;
		case 2:
			mes "[Riding Creature Master]";
			mes "I see. Then have a great day.";
			close;
		}
	}
	if (Class == Job_Royal_Guard || Class == Job_Royal_Guard_T || Class == Job_Baby_Guard) {
		mes "Welcome. Would you like to rent a Gryphon?";
		next;
		switch(select("Yes:No")) {
		case 1:
			if (!getskilllv("KN_RIDING")) {
				mes "[Riding Creature Master]";
				mes "Please learn how to ride a Gryphon first.";
				close;
			}
			else if (checkriding()) {
				mes "[Riding Creature Master]";
				mes "You already have a Gryphon.";
				close;
			}
			else if (ismounting()) {
				mes "[Riding Creature Master]";
				mes "Please remove your cash mount.";
				close;
			}
			setriding;
			close;
		case 2:
			mes "[Riding Creature Master]";
			mes "I see. Then have a great day.";
			close;
		}
	}
	mes "I'm here to provide Rune Knights and Royal Guards with riding creatures.";
	close;
}
geffen,100,55,3	duplicate(Riding Creature Master)	Riding Creature Master#2	105
payon,166,102,5	duplicate(Riding Creature Master)	Riding Creature Master#3	105
aldebaran,133,109,5	duplicate(Riding Creature Master)	Riding Creature Master#4	105
yuno,171,187,3	duplicate(Riding Creature Master)	Riding Creature Master#5	105
rachel,106,130,5	duplicate(Riding Creature Master)	Riding Creature Master#6	105

// Peco removing NPC :: dragon
//============================================================
prontera,125,208,5	script	Peco Peco Remover	105,{
	mes "[Soldier]";
	mes "If you're unable to dismount from a Peco Peco";
	mes "for some unknown reason,";
	mes "please feel free to use my services.";
	if (checkriding()) {
		next;
		mes "[Soldier]";
		mes "You're riding a Peco Peco.";
		mes "Would you like to dismount?";
		next;
		switch(select("Yes:No")) {
		case 1:
			setriding 0;
			mes "[Soldier]";
			mes "Say, how does it feel to";
			mes "step on the ground on your own";
			mes "feet again?";
			close;
		case 2:
			mes "[Soldier]";
			mes "I see. Please feel free to ask me";
			mes "if you change your mind.";
			close;
		}
	}
	close;
}

// Mado Gear Renter :: madogear
//============================================================
-	script	::mgm	-1,{
	mes "[Mado Gear Armorer]";
	if (Class == Job_Mechanic || Class == Job_Mechanic_T || Class == Job_Baby_Mechanic) {
		mes "Welcome, Mechanic.";
		mes "Would you like to rent a Pushcart or";
		mes "ride a Mado Gear?";
		next;
		switch(select("Rent a Pushcart:Ride a Mado Gear:Buy Emergency Mado Gear:Upgrade Cooling Device:Cancel")) {
		case 1:
			if (checkcart()) {
				mes "[Mado Gear Armorer]";
				mes "I'm sorry, but you already";
				mes "have a Pushcart.";
				close;
			}
			setcart;
			mes "[Mado Gear Armorer]";
			mes "There you go!";
			close;
		case 2:
			if (checkmadogear()) {
				mes "[Mado Gear Armorer]";
				mes "I'm sorry, but you're already";
				mes "riding a Mado Gear.";
				close;
			}
			else if (!getskilllv("NC_MADOLICENCE")) {
				mes "[Mado Gear Armorer]";
				mes "Please learn the skill to get the Mado Gear License first.";
				close;
			}
			else if(ismounting()) {
				mes "[Mado Gear Armorer]";
				mes "Please remove your cash mount.";
				close;
			}
			setmadogear;
			mes "[Mado Gear Armorer]";
			mes "Have fun, and please come again!";
			close;
		case 3:
			mes "[Mado Gear Armorer]";
			mes "Emergency Mado Gear is really useful for emergency situations and it is sold at 1,000,000 Zeny.";
			next;
			if (select("Purchase:Cancel") == 2) {
				mes "[Mado Gear Armorer]";
				mes "I see. Please feel free to ask me";
				mes "if you change your mind.";
				close;
			}
			if (countitem(23277) > 0) {
				mes "[Mado Gear Armorer]";
				mes "I'm sorry, but you already have an Emergency Mado Gear.";
				close;
			}
			if (Zeny < 1000000) {
				mes "[Mado Gear Armorer]";
				mes "I'm sorry, but you don't have enough Zeny to purchase the Emergency Mado Gear.";
				close;
			}
			Zeny -= 1000000;
			getitem 23277,1; //Mado_Box
			mes "[Mado Gear Armorer]";
			mes "There you go!";
			close;
		case 4:
			mes "[Mado Gear Armorer]";
			mes "Which device do you want to upgrade?";
			next;
			if (select("Cooling Device:High Quality Cooler") == 1) {
				mes "[Mado Gear Armorer]";
				mes "Upgrading Cooling Device to High Quality Cooler needs 1 Cooling Device and 2,000,000 Zeny.";
				next;
				.@itemid = 2804;
				.@cost = 2000000;
			} else {
				mes "[Mado Gear Armorer]";
				mes "Upgrading High Quality Cooler to Special Cooler needs 1 High Quality Cooler and 4,000,000 Zeny.";
				next;
				.@itemid = 2809;
				.@cost = 4000000;
			}
			if (select("Upgrade:Cancel") == 2) {
				mes "[Mado Gear Armorer]";
				mes "I see. Please feel free to ask me";
				mes "if you change your mind.";
				close;
			}
			if (!countitem(.@itemid)) {
				mes "[Mado Gear Armorer]";
				mes "I'm sorry, but you don't have the " + getitemname(.@itemid) + ".";
				close;
			}
			if (Zeny < .@cost) {
				mes "[Mado Gear Armorer]";
				mes "I'm sorry, but you don't have enough Zeny to upgrade the device.";
				close;
			}
			Zeny -= .@cost;
			delitem .@itemid,1;
			getitem (.@itemid == 2804 ? 2809 : 2810),1; //High_Quality_Cooler,Special_Cooler
			mes "[Mado Gear Armorer]";
			mes "Here you are! Your very own " + getitemname(.@itemid) + ".";
			close;
		case 5:
			close;
		}
	}
	mes "How may I help you?";
	mes "Mado Gears are only available for Mechanics.";
	close;
}
prontera,163,178,3	duplicate(mgm)	Mado Gear Armorer#prt	105
geffen,103,55,5	duplicate(mgm)	Mado Gear Armorer#gef	105
payon,166,106,5	duplicate(mgm)	Mado Gear Armorer#pay	105
aldebaran,133,112,5	duplicate(mgm)	Mado Gear Armorer#alde	105
yuno,167,187,3	duplicate(mgm)	Mado Gear Armorer#yuno	105
rachel,106,134,5	duplicate(mgm)	Mado Gear Armorer#ra	105
dicastes01,187,207,3	duplicate(mgm)	Mado Gear Armorer#dic	105
manuk,273,212,5	duplicate(mgm)	Mado Gear Armorer#man	105
splendide,180,174,5	duplicate(mgm)	Mado Gear Armorer#spl	105
mid_camp,242,243,3	duplicate(mgm)	Mado Gear Armorer#mid	105
