//===== rAthena Script ======================================= 
//= 3rd Item Seller
//===== By: ================================================== 
//= Masao, Mercurial
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Sells some 3rd Job related items.
//===== Additional Comments: ================================= 
//= 1.0 First Version.
//= 1.1 Optimized Poison Herb Salesman NPC
//= 1.2 Optimized and standardized. [Euphy]
//============================================================ 

job3_guil01,79,96,3	script	Poison Herb Salesman::PHS	877,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 20000) {
		mes "- Wait a minute !! -";
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please try again -";
		mes "- after you lose some weight. -";
		close;
	}
	mes "[Poison Herb Salesman]";
	mes "I am the professional";
	mes "poison herb seller.";
	mes "Which one do you want?";
	mes "They cost 4,000 zeny each!";
	set .@i, select("Nerium:Rantana:Makulata:Seratum:Scopolia:Amoena:Poison Kit")-1;
	next;
	if (.@i < 6) {
		mes "[Poison Herb Salesman]";
		mes "How many do you want? You can";
		mes "only buy a maximum of 2000 ea";
		mes "and if you want to cancel, enter '0'.";
		next;
		input .@amount;
		mes "[Poison Herb Salesman]";
		if (.@amount == 0) {
			mes "You've cancelled the trade.";
			close;
		}
		if (.@amount > 2000) {
			mes "Please check the maximum";
			mes "amount and then try again.";
			close;
		}
		if (Zeny < .@amount*4000) {
			mes "Please check the money you have.";
			mes "It doesn't seem like you have enough money.";
			close;
		}
		set Zeny, Zeny-(.@amount*4000);
		getitem 7932+.@i,.@amount;
		mes "Thank you. See you~!";
		close;
	} else {
		mes "[Poison Herb Salesman]";
		mes "You can only buy";
		mes "1 Poison Kit and it costs 5,000 zeny.";
		mes "Will you buy it?";
		next;
		if(select("Yes, I will.:No, I won't.") == 2) {
			mes "[Poison Herb Salesman]";
			mes "Well, I see. See you~!";
			close;
		}
		mes "[Poison Herb Salesman]";
		if (Zeny < 5000) {
			mes "Please check the money you have.";
			mes "It doesn't seem like you have enough money.";
			close;
		}
		set Zeny, Zeny-5000;
		getitem 7931,1;
		mes "[Poison Herb Salesman]";
		mes "Thank you. See you~!";
		close;
	}
}
morocc,190,96,4	duplicate(PHS)	Poison Herb Salesman#moc	877
lhz_in02,16,205,4	duplicate(PHS)	Poison Herb Salesman#lhz	877

job3_rune01,90,62,3	script	Rune Salesman::runesale	853,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 20000) {
		mes "- Wait a minute !! -";
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please try again -";
		mes "- after you lose some weight. -";
		close;
	}
	mes "[Rune Salesman]";
	mes "Hey, do you need Runes?!";
	mes "I sell all kinds of Rune Stones!";
	mes "What kind of Rune do you want?";
	next;
	set .@i, select("Buy high-quality Rune Stones.:Buy ordinary Rune Stones.");
	mes "[Rune Salesman]";
	if (.@i == 1) {
		mes "High-quality Runes!";
		mes "They cost 2,500 zeny each!";
		setarray .@rune[0],12734,2500;
	} else {
		mes "Ordinary Rune Stones?";
		mes "They cost 1,000 zeny each!";
		setarray .@rune[0],12737,1000;
	}
	mes "Tell me how many you want to buy";
	mes "and remember, you can only";
	mes "buy a maximum amount of 2000 ea!";
	next;
	input .@amount;
	mes "[Rune Salesman]";
	if (.@amount == 0) {
		mes "You're not buying? Go away!";
		close;
	}
	if (.@amount > 2000) {
		mes "Please check the maximum";
		mes "amount and then come back to me again!";
		close;
	}
	if (Zeny < .@amount*.@rune[1]) {
		mes "Money! Money!";
		mes "You are short of money!";
		mes "Check the amount of money you have!!";
		close;
	}
	if (!checkweight(.@rune[0],.@amount)) {
		mes "You're not able to carry it, so why are you trying to buy it?!";
		close;
	}
	set Zeny, Zeny-(.@amount*.@rune[1]);
	getitem .@rune[0],.@amount;
	mes "You can buy more again.";
	mes "So, see you later.";
	close;
}
prontera,168,228,3	duplicate(runesale)	Rune Salesman#1	853

job3_guil01,91,93,3	script	Rare Herb Collector	49,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 20000) {
		mes "- Wait a minute !! -";
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please try again -";
		mes "- after you lose some weight. -";
		close;
	}
	if (Class == Job_Guillotine_Cross || Class == Job_Guillotine_Cross_T || Class == Job_Baby_Cross) {
		mes "[Rare Poison Herb Collector]";
		mes "I wander around the world and collect rare poison herbs. Recently, I am dealing in the herb called Izidor. If you are interested, you can buy them.";
		next;
		set .@i, select("How can I buy them?:Exchange it with Animal Blood:Exchange it with a Bitter Herb:Exchange it with a Deadly Noxious Herb:Exchange it with a Frozen Rose:Exchange it with Ment:Exchange it with Hinalle")-1;
		if (.@i == 0) {
			mes "[Rare Poison Herb Collector]";
			mes "You can buy any of those items for 10,000 zeny: Animal Blood, Bitter Herb, Deadly Noxious Herb, Frozen Rose, Ment or Hinalle. ";
			next;
			mes "[Rare Poison Herb Collector]";
			mes "Why I ask for money? That is just to pay a little respect to a collector like me. Ha ha ha...";
			close;
		} else {
			setarray .@exchange[1],702,621,631,749,605,703;
			mes "[Rare Poison Herb Collector]";
			if (!countitem(.@exchange[.@i]) || Zeny < 10000) {
				mes "Hey, try again after you've prepared all the requirements for the exchange.";
				close;
			}
			mes "Thank you. I've received your payment.";
			delitem .@exchange[.@i],1;
			set Zeny, Zeny-10000;
			getitem 709,1;
			close;
		}
	}
	mes "[Rare Poison Herb Collector]";
	mes "I wander around the world and collect rare poison herbs. But I don't feel like selling my herbs to a person like you... ha ha ha...";
	close;
}

gef_tower,105,172,5	script	Point Salesman#Sorcerer::pss	700,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 20000) {
		mes "- Wait a minute !! -";
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please try again -";
		mes "- after you lose some weight. -";
		close;
	}
	mes "[Point Salesman]";
	mes "Hello. I'm selling a catalyst called ^FF0000Points^000000 for Sorcerers. What would you like?";
	next;
	set .@i, select("Scarlet Points - 200z:Lime Green Points - 200z:Indigo Points - 200z:Yellow Wish Points - 200z:Cancel")-1;
	mes "[Point Salesman]";
	if (.@i == 4) {
		mes "You can't find the stuff you need?";
		close;
	}
	setarray .@itemid[0],6360,6363,6361,6362;
	setarray .@color$[0],"Scarlet","Lime Green","Indigo","Yellow Wish";
	mes "You have chosen "+.@color$[.@i]+" Points.";
	mes "How many do you want?";
	mes "If you want to cancel, enter 0.";
	next;
	input .@amount;
	mes "[Point Salesman]";
	if (.@amount == 0) {
		mes "You've cancelled the trade.";
		close;
	}
	if (Zeny < .@amount*200) {
		mes "You don't seem to have enough money.";
		close;
	}
	if (!checkweight(.@itemid[.@i],.@amount)) {
		mes "You don't have enough space in your inventory to buy this amount.";
		close;
	}
	set Zeny, Zeny-(.@amount*200);
	getitem .@itemid[.@i],.@amount;
	mes "Thank you very much. See you~!";
	close;
}
comodo,241,103,6	duplicate(pss)	Point Salesman#1	700
alberta,105,52,7	duplicate(pss)	Point Salesman#2	700
veins,202,128,6	duplicate(pss)	Point Salesman#3	700