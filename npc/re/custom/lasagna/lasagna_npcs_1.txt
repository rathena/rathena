//===== rAthena Script =======================================
//= Lasagna NPCs.
//===== Description: =========================================
//--- Lasagna NPCs from iRO playing conversion.
// Notes:
//- lasagna_quest[0] <-> 1st part variable
//- lasagna_quest[1] <-> 2nd part variable until Tartufi
//- lasagna_quest[2] <-> Kazu Quest (lvl 110-140 req)
//-- Some npctalk may be inaccurate (text of npc)
//-- npctalk are supposed to only be visible by the character attached
//-- hideonnpc / hideoffnpc are supposed to only be visible by character attached until character leave npc range
//-- script untested
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

function	script	close3	{
	close2;
	cutin "",255;
	end;
}

lasa_fild01,47,297,0	script	#doint_evt01	HIDDEN_WARP_NPC,1,1,{
	end;
OnTouch:
	if (Class == Job_Summoner && isbegin_quest(7711) == 0) {
		sleep2 100;
		npctalk "Hey, newbie! Get over here!", "Vigilante Ajegna#doram0", bc_self;
		npctalk "Come on over here! Click me, quick!", "Vigilante Ajegna#doram0", bc_self;
		setquest 7711;// Beginning
	}
	end;
}

lasa_fild01,49,304,3	script	Vigilante Ajegna#doram0	4_DR_SOLDIER,{
	if (Class != Job_Summoner) {
		mes "[Vigilante Ajegna]";
		mes "Huh? I doubt you're from Far-Star.";
		mes "I get it! You're from the New Continent, aren't you?";
		next;
		mes "[Vigilante Ajegna]";
		mes "If you're planning to cross that ridge, I wouldn't recommend it.";
		mes "Why don't you just return to your village from here?";
		close;
	}
	switch( isbegin_quest(7712) ) {
	case 0:
		mes "[Vigilante Ajegna]";
		mes "Oh, there you go!";
		mes "You can talk to others in the same way you just spoke to me.";
		next;
		mes "!- Notice -!";
		mes "^4d4dffBasically, you talk by left-clicking the mouse button while hovering the cursor over an NPC.";
		mes "Left-click the mouse button to talk with an NPC, move about and attack a target.^000000";
		cutin "tutorial01.bmp",4;
		next;
		cutin "",255;
		mes "[Vigilante Ajegna]";
		mes "Good work crossing over the mountain, newbie.";
		mes "Just like yourself, there are a bunch of people who are crossing this mountain range these days to reach the ^4d4dffNew Continent^000000.";
		next;
		mes "[Vigilante Ajegna]";
		mes "Honestly, I was surprised, too.";
		mes "I never imagined even in my wildest dreams that there would be any other continent other than ^4d4dffMidgard^000000 or ^4d4dffFar-Star,^000000 which we live in.";
		next;
		mes "[Vigilante Ajegna]";
		mes "That explains the slew of Dorams, who simply wander about, in ^4d4dffLasagna^000000, which was nothing but a fishing village in the border area.";
		next;
		mes "[Vigilante Ajegna]";
		mes "What's worse is that the Dorams who come from outside of Lasagna would often cause problems. This is really causing a headache for the Vigilante Corp.";
		mes "A Vigilante's job is to assist the Dorams who are in the New Continent expedition, in addition to maintaining public order, so please help.";
		next;
		mes "[Vigilante Ajegna]";
		mes "Do you see the Doram over there? He's ^4d4dffAglio^000000. He'll let you in on some useful stuff. Go meet the guy.";
		if (isbegin_quest(7711) == 0)
			setquest 7711;
		completequest 7711;// Beginning
		setquest 7712;// Where am I? Who am I??
		close;
	case 1:
		mes "[Vigilante Ajegna]";
		mes "I should welcome the new Dorams coming in here.";
		mes "^4d4dffAglio^000000 will let you in on more details. He's right over there.";
		next;
		mes "[Vigilante Ajegna]";
		mes "Aglio is one of the very, I mean, very good Doram in the Farfalle Vigilante Corp, so watch and learn!";
		close;
	case 2:
		mes "[Vigilante Ajegna]";
		mes "If you have any questions, don't hesitate to ask him.";
		mes "Don't be a stranger even when you're out on the New Continent!";
		next;
		mes "[Vigilante Ajegna]";
		mes "The Lasagna Port is over that way!";
		mes "Don't tell me you're here because you didn't know that.";
		close;
	}
OnInit:
	questinfo 7712, QTYPE_QUEST, 1;
	end;
}

lasa_fild01,53,322,3	script	Applicant Paul#doramt13	4_M_MERCAT1,4,4,{
	if (Class != Job_Summoner) {
		mes "[Paul]";
		mes "~Shrieks~ You startled me!";
		mes "A human? Wow, what an odd-looking one!";
		close;
	}
	switch( isbegin_quest(7723) ) {
	case 0:
		mes "[Paul]";
		mes "So, you're that adventurer Doram, eh?";
		mes "Hey, what do you think?";
		mes "You agree with me about damage, right? STR comes first!";
		next;
		mes "[Paul]";
		mes "You need to be on the offense to win the battle.";
		mes "^4d4dffThe quickest way to boost ATK is to boost STR^000000!";
		next;
		mes "[MacCarnie]";
		mes "...Reckless attacks are of no use!";
		mes "No attack will work if you miss the target!";
		next;
		mes "[MacCarnie]";
		mes "So, ^4d4dffyou need to boost DEX first to increase Accuracy^000000.";
		mes "It doesn't matter how strong you are. You need to hit the target first!";
		next;
		mes "[MacCarnie]";
		mes "I refuse to use martial arts willy-nilly as you do!";
		mes "^4d4dffI'm using the summoning skill by boosting INT for magical attacks!^000000";
		next;
		mes "[Paul]";
		mes "Pfff! What's wrong with using the summoning skills to complement the physical skills?";
		mes "Are you implying that I haven't got a clue?";
		next;
		mes "[MacCarnie]";
		mes "...That's not what I meant! This cat's brains must be full of fat!";
		next;
		mes "[Vigilante Gnocchi]";
		mes "...Stop fighting, you two!";
		mes "Hey, there. Newcomer.";
		mes "You can ask me if there is something you'd like to know.";
		npctalk "You can ask me if there is something you'd like to know.", "Vigilante Gnocchi#doramt13", bc_self;
		setquest 7723;// The potential within
		close;
	case 1:
		mes "[Paul]";
		mes "He might tell you What's good about being a Vigilante, so go ahead and ask him.";
		mes "He'll prove that I was right, after all.";
		close;
	case 2:
		mes "[Paul]";
		mes "So, is this all about boosting your abilities based on your competencies?";
		next;
		mes "[Paul]";
		mes "Come to think of it, there is a ^4d4dffhypnotist^000000 in the village.";
		mes "^4d4dffYou can reset skills and statuses through a hypnotist^000000. Just so you know that you can.";
		close;
	}
OnTouch:
	if (Class == Job_Summoner && isbegin_quest(7723) == 0) {
		npctalk "You, dimwit! Nothing beats damage! You should boost your STR first!", "", bc_self;
		sleep2 2000;
		npctalk "You have no idea! DEX comes first if you want your attack to be successful!!", "Applicant MacCarnie#doramt13", bc_self;
		sleep2 1000;
		emotion 36;
		emotion 54,0,"Applicant MacCarnie#doramt13";
	}
	end;
OnInit:
	if (strnpcinfo(0) == "Applicant Paul#doramt13")
		questinfo 7723, QTYPE_QUEST, 1;
	end;
}
lasa_fild01,50,321,6	duplicate(Applicant Paul#doramt13)	Applicant MacCarnie#doramt13	4_M_MERCAT1

lasa_fild01,54,325,3	script	Vigilante Gnocchi#doramt13	4_DR_SOLDIER,{
	if (Class != Job_Summoner) {
		mes "[Vigilante Gnocchi]";
		mes "Ahem! This is like a gateway to the Far-Star Continent.";
		mes "No more humans are allowed!";
		close;
	}
	switch( isbegin_quest(7723) ) {
	case 0:
		npctalk "Do you happen to know why they are making so much noise?", "", bc_self;
		end;
	case 1:
		mes "[Vigilante Gnocchi]";
		mes "So they were having an animated discussion over status.";
		next;
		break;
	case 2:
		mes "[Vigilante Gnocchi]";
		mes "Do you need more explanation on the status?";
		next;
		if (select( "Yes.", "No." ) == 2) {
			mes "[Vigilante Gnocchi]";
			mes "Up to level 49, the village hypnotist will reset your stats and skills.";
			mes "This will let you practice and experiment which will help find the path that works for you.";
			close;
		}
		break;
	}
	mes "[Vigilante Gnocchi]";
	mes "Press the ^4d4dffAlt+Q^000000 keys to view your physical stats.";
	mes "Calibration is also available depending on your equipment.";
	next;
	mes "[Vigilante Gnocchi]";
	mes "Let me explain the statuses in detail.";
	mes "^4d4dffSTR means strength^000000. It ^4d4dffboosts physical attacks^000000.";
	next;
	mes "[Vigilante Gnocchi]";
	mes "^4d4dffVIT is vitality which indicates health^000000. This is what ^4d4dffboosts your overall HP^000000.";
	mes "^4d4dffAGI means agility^000000. It affects ^4d4dffevasion and attack speed^000000.";
	next;
	mes "[Vigilante Gnocchi]";
	mes "^4d4dffINT refers to intelligence and MATK means magical attack^000000. If your skills are centered on magic, you must boost INT first and foremost.";
	next;
	mes "[Vigilante Gnocchi]";
	mes "^4d4dffDEX is related with dexterity and accuracy^000000.";
	mes "You can both quicken casting speed and boost the accuracy of your attacks.";
	next;
	mes "[Vigilante Gnocchi]";
	mes "^4d4dffLUK means luck; the more luck you have, the higher your critical hit rate (CRI) becomes^000000.";
	mes "It also means you can dodge an enemy's attack more easily.";
	next;
	mes "[Vigilante Gnocchi]";
	mes "If you want to boost your physical attacks, focus on STR. If what you want is magical attacks, go for INT. Got it?";
	next;
	mes "[Vigilante Gnocchi]";
	mes "DEX is highly crucial in the early stages because it is what boosts Accuracy.";
	mes "This can be a difficult concept to get, so you should try boosting this and that to find your own way.";
	switch( isbegin_quest(7723) ) {
	case 0:
		close;
	case 1:
		completequest 7723;// The potential within
		getexp 50,50;
		next;
		mes "[Vigilante Gnocchi]";
		mes "I wish you godspeed!";
		close;
	case 2:
		close;
	}
OnInit:
	questinfo 7723, QTYPE_QUEST, 1;// todo : display when set, removed when completed
	setquestinfo_req 7723,7723,2;
	end;
}

lasa_fild01,60,322,3	script	Vigilante Aglio#doram02	4_DR_AGLIO,{
	if (Class != Job_Summoner) {
		mes "[Vigilante Aglio]";
		mes "Oh! You must be the famous strangers from the New Continent, eh?";
		mes "Out-of-towner Dorams abound in this place, I definitely don't want any more of them.";
		next;
		mes "[Vigilante Aglio]";
		mes "wouldn't they be surprised to see you guys?";
		mes "Moreover, it's dangerous to cross the mountain ridge.";
		mes "This is for your own good. Just go back to where you come from.";
		close;
	}
	if (lasagna_quest[0] == 0) {
		mes "[Vigilante Aglio]";
		mes "Oh, hello?";
		mes "I'm Aglio. I'm going to be your attendant from this point on.";
		mes "Consider it a privilege.";
		mes "Because I'm one of the ace members of the Farfalle Vigilante Corp.";
		next;
		mes "[Vigilante Aglio]";
		mes "I'm sure I can be of help for just about anything.";
		mes "First off, let's see what you've got.";
		next;
		mes "[Vigilante Aglio]";
		mes "Hang on. Let me check if you are properly equipped first.";
		mes "You don't want to fight with bare hands, now do you?";
		mes "^4d4dffYou should equip your weapons.^000000";
		next;
		cutin "tutorial03.BMP",4;
		mes "!- Notice -!";
		mes "^4d4dffYou can use or equip an item by double-clicking it.";
		mes "Open or close the item window with the shortcut key [Alt+E].";
		next;
		mes "!- Notice -!";
		mes "^4d4dffCheck out your equipped items easily by pressing the shortcut key [Alt+Q].";
		mes "Simply drag the appropriate items to the area you'd like to equip.^000000";
		next;
		cutin "",255;
		mes "[Vigilante Aglio]";
		mes "Now that we've checked your weapons, why don't we take a look at a target to hunt?";
		mes "Do you see those ^4d4dffEggring^000000 wandering around over there?";
		next;
		mes "[Vigilante Aglio]";
		mes "They may look dumb but they can be a real pain in the butt. They will pick up and eat anything they come across.";
		mes "They are a dime a dozen around here, so they're a great target for the novice Dorams to test their worth on.";
		next;
		mes "[Vigilante Aglio]";
		mes "Go ahead and get rid of some ^4d4dffEggring^000000, will you? ^4d4dffThree^000000 should do for now.";
		if (isbegin_quest(7712) == 0)
			setquest 7712;// Where am I? Who am I??
		completequest 7712;
		setquest 7713;// First Battle!
		lasagna_quest[0] = 1;
		next;
		cutin "tutorial02.BMP",4;
		mes "^4d4dff!- Notice -!^000000";
		mes "You have accepted a quest from an NPC.";
		mes "^4d4dffCheck out the quest details by pressing the shortcut key [Alt+U],^000000 or through the Quest icon found in the menu tray.^000000";
		close3;
	}
	if (lasagna_quest[0] == 1 || lasagna_quest[0] == 2 || lasagna_quest[0] == 3) {
		if (lasagna_quest[0] == 1) {
			if (checkquest(7713,HUNTING) < 2) {
				mes "[Vigilante Aglio]";
				mes "Alright, to warm up, go and eliminate ^4d4dffthree Eggrings^000000.";
				next;
				mes "[Vigilante Aglio]";
				mes "What do they look like? You'll see just by glancing at them right away. You can't miss them!";
				mes "They abound around here. Go get them, tiger!";
				next;
				cutin "tutorial02.BMP",4;
				mes "^4d4dff!- Notice -!^000000";
				mes "^4d4dffCheck out the quest details by pressing the shortcut key [Alt+U],^000000 or through the Quest icon found in the menu tray.^000000";
				close;
			}
			else {
				mes "[Vigilante Aglio]";
				mes "Whoa, you're good!";
				mes "I should've seen that in you because crossing the mountains is never easy.";
				next;
				mes "[Vigilante Aglio]";
				mes "I believe you can do very well, even in a new location.";
				mes "Here, take this.";
				next;
				mes "[Vigilante Aglio]";
				mes "I brought some snacks for you.";
				mes "It's cool apple juice!";
				completequest 7713;// First Battle!
				getexp 70,70;
				setquest 7714;// Refreshing Apple Juice
				lasagna_quest[0] = 2;
				getitem 531,1;// Apple Juice
				next;
			}
		}
		if (lasagna_quest[0] == 2) {
			if (countitem(531) > 0) {
				mes "[Vigilante Aglio]";
				mes "Click the 'Item' icon in the basic information window found in the upper left-hand side to open the item window.";
				next;
				mes "[Vigilante Aglio]";
				mes "The item window comes with four tabs.";
				mes "Items are automatically placed into the Item, Gear and Etc tabs, depending on their type.";
				next;
				mes "[Vigilante Aglio]";
				mes "To use or equip an item, ^4d4dffdouble-click that item^000000, or";
				mes "^4d4dffdrag it to the Equipment window^000000 to use.";
				next;
				cutin "tutorial03.BMP",4;
				mes "!- Notice -!";
				mes "^4d4dffYou can use or equip an item by double-clicking it.";
				mes "Open or close the item window with the shortcut key [Alt+E].";
				next;
				mes "!- Notice -!";
				mes "^4d4dffCheck out your equipped items easily by pressing the shortcut key [Alt+Q].";
				mes "Or, simply drag the appropriate items to the area you'd like to equip.^000000";
				next;
				cutin "",255;
				mes "[Vigilante Aglio]";
				mes "If you want to ^4d4dffsee a detailed description for each item, right-click that item.^000000";
				mes "This shows the detailed description of the selected item.";
				next;
				cutin "tutorial04.BMP",4;
				mes "!- Notice -!";
				mes "^4d4dffRight-click an item to view its detailed description.^000000";
				next;
				mes "!- Notice -!";
				mes "^4d4dffIf a player wishes, they can use the social options such as Booking, Party, or Guild. Here, they join a party or request transactions.^000000";
				next;
				cutin "",255;
				mes "[Vigilante Aglio]";
				mes "Well, then. ^4d4dffGo ahead and drink the apple juice that I gave you.^000000";
				close;
			}
			mes "[Vigilante Aglio]";
			mes "How do you like it? It's cool, sweet, and just good, right?";
			mes "I would've given you more if I had any, but...no.";
			next;
			mes "[Vigilante Aglio]";
			mes "Oh, right! I'll give you this, too.";
			mes "It'll restore your stamina if you take it when you're tired.";
			next;
			mes "[Vigilante Aglio]";
			mes "Also, I'll give you a new staff.";
			mes "It should be better than that sprout you brought with you.";
			mes "Now you know how to equip an item because I already explained it to you, don't you?";
			completequest 7714;// Refreshing Apple Juice
			getitem 569,30;// Novice Potion
			getitem 1687,1;// Beginner Foxtail Staff [1]
			getexp 70,70;
			lasagna_quest[0] = 3;
		}
		next;
		hideoffnpc "Kid#doram03t";// only for player attached
		mes "[Vigilante Aglio]";
		mes "I'll direct you to the village, now.";
		npctalk "....Aglioooooo!!", "Kid#doram03t", bc_self;
		next;
		npctalk "Oh, my days! You startled me!", "", bc_self;
		mes "[Vigilante Aglio]";
		mes "What's up?";
		mes "Oh, you're the youngest kitten of the tool merchant!";
		mes "it's dangerous around here. What brought you all this way?";
		next;
		mes "[The Youngest Kitten of the Tool Merchant]";
		mes "Oh, actually...I lost it!";
		mes "There was a box!";
		mes "I was supposed to give it to Haru.";
		mes "I was just trying to hang out for a while, but I fell down!";
		next;
		mes "[The Youngest Kitten of the Tool Merchant]";
		mes "Please find it, Aglio!";
		mes "Or, I'm going to be in trouble!";
		next;
		mes "[Vigilante Aglio]";
		mes "So, you were running an errand to deliver a box to Healer Haru.";
		mes "But you fell down while hanging out for a while and lost the box. Is that it?";
		next;
		mes "[The Youngest Child of the Tool Merchant]";
		mes "Yes, sir!";
		mes "You're so good at summing it up!";
		next;
		mes "[Vigilante Aglio]";
		mes "Alright. You just return to your village.";
		mes "I'll see what I can do. You return home and wait.";
		next;
		mes "[Vigilante Aglio]";
		mes "One more thing. Just drop that habit of acting like you're naive in front of the strangers!";
		next;
		mes "[The Youngest Kitten of the Tool Merchant]";
		mes "Come on...I'm the cutest, youngest kitten from Lasagna!";
		mes "Anyway, I hope you help me by finding that box. Please.";
		next;
		npctalk "You know that I love you, don't you?", "Kid#doram03t", bc_self;
		sleep2 1000;
		hideonnpc "Kid#doram03t";	// only for player attached
		npctalk "Just go, already!", "", bc_self;
		mes "[Vigilante Aglio]";
		mes ".....";
		mes "........~Sighs deeply~...";
		next;
		mes "[Vigilante Aglio]";
		mes "Well, there's no choice, then.";
		mes "" + strcharinfo(0) + "";
		mes "I think you can step up and handle it this time.";
		next;
		mes "[Vigilante Aglio]";
		mes "it's a relief to have a competent Doram with me.";
		mes "My guess is that an ^4d4dffEggring must've eaten that box.^000000";
		next;
		mes "[Vigilante Aglio]";
		mes "Keep hunting Eggrings until you find that box...";
		mes "Let's go!";
		setquest 7715;// Midding Box
		lasagna_quest[0] = 4;
		next;
		mes "!- Notice -!";
		mes "^4d4dffYou accepted a new quest from Vigilante Aglio.";
		mes "Check out the details of the quest in the Quest Information Window.";
		next;
		mes "!- Notice -!";
		mes "Press the ^4d4dffAlt + U^000000 shortcut key to open the Quest Information Window.";
		close;
	}
	if (lasagna_quest[0] == 4) {
		if (countitem(1081) < 1) {// Midding Box
			mes "[Vigilante Aglio]";
			mes "You must remember Eggrings, you've hunted them before, haven't you?";
			mes "They are easily spotted around here.";
			next;
			mes "[Vigilante Aglio]";
			mes "I have no idea which Eggring swallowed the box, but, you'll find it once you start hunting them.";
			next;
			mes "[Vigilante Aglio]";
			mes "Well, then. I'm counting on you.";
			close;
		}
		mes "[Vigilante Aglio]";
		mes "Ohh...! This is the box!";
		mes "You can bring this when you visit the village!";
		next;
		mes "[Vigilante Aglio]";
		mes "^4d4dffWhen you arrive in the village, find the tool shop first and deliver this box.^000000";
		next;
		mes "[Vigilante Aglio]";
		mes "I'll let you know how to reach the village.";
		mes "Follow the arrows that just appeared on the ground.";
		erasequest 7715;// Midding Box
		setquest 7716;// To the Village!
		getexp 100,0;
		lasagna_quest[0] = 5;
		next;
		mes "!- Notice -!";
		mes "^4d4dffSometimes NPCs will help you find your destination through the Navigation tool.";
		mes "Links can be created if there is a need for it. If you see the [Target], click it for pathfinding.^000000";
		cutin "navi01.bmp",4;
		next;
		mes "!- Notice -!";
		mes "^4d4dffCall up the Navigation panel by clicking the appropriate menu icon.";
		mes "Search for the target, set the goal, and click Pathfinding.";
		mes "Arrows will be generated pointing toward the location of the target.^000000";
		cutin "navi02.bmp",4;
		navigateto "lasagna",168,124;
		close;
	}
	if (lasagna_quest[0] == 5) {
		mes "[Vigilante Aglio]";
		mes "What's in front of you is Lasagna, the harbor village.";
		mes "You should bring that box to the tool shop first, right?";
		next;
		mes "[Vigilante Aglio]";
		mes "I'll let you know how to reach the village.";
		mes "Follow the arrows that just appeared on the ground.";
		next;
		mes "[Vigilante Aglio]";
		mes "I'll be right behind you!";
		mes "Don't worry about me. Just keep going.";
		navigateto "lasagna",168,124;
		close;
	}
	mes "[Vigilante Aglio]";
	mes "Haven't you left for Midgard yet?";
	mes "Well, problems do occur at the harbor from time to time, but I thought it wouldn't be an issue for you.";
	close;
// OnInit:
	// questinfo 7712, QTYPE_QUEST, 1;// inaccurate
	// end;
}

lasa_fild01,62,321,3	script	Kid#doram03t	4_DR_KID_01,{
	end;
OnInit:
	hideonnpc "Kid#doram03t";
	end;
}

lasagna,168,125,3	script	Kid#doram04t	4_DR_KID_01,{
	if (lasagna_quest[0] == 5) {
		if (countitem(1081) > 0) {
			mes "[Kitten]";
			mes "This is it!!";
			next;
			mes "The kitten hoisted the box given to him up high, screaming out in joy";
			next;
			mes "[Tool Merchant]";
			mes "Wait. This box looks familiar...";
			mes "Hey! Isn't this the box that I told you to deliver to Haru?";
			next;
			mes "[Kitten]";
			mes "~Gulps~";
			mes "I'm leaving! Right now!";
			mes "Thank you for finding the box!";
			delitem 1081, countitem(1081);// Delivery Box
			completequest 7716;// To the Village!
			getexp 70,70;
			hideonnpc "Kid#doram04t";// only for player attached
			lasagna_quest[0] = 6;
			next;
			npctalk "...Wow, that was fast.", "Tool Merchant#doram05t", bc_self;
			mes "[Tool Merchant]";
			mes "Anyway, thank you for finding the box.";
			mes "If you have the time, sit down and talk with me.";
			close;
		}
	}
	if (lasagna_quest[0] < 6) {
		mes "[Kitten]";
		mes "Why is he taking so long...?";
		mes "I'm worried.";
		next;
		select("Is there anything that troubles you?");
		mes "[Kitten]";
		mes "No, it's nothing.";
		mes "I'm not one of the Dorams who would randomly give up on their errands!";
		next;
		mes "The little Doram starts whining, secretly being conscious of the adult Doram next to him.";
		close;
	}
	mes "[Kitten]";
	mes "I was told you're headed to the New Continent.";
	mes "I want to go there myself, too!";
	close2;
	npctalk "You're too young! Just join the Vigilante Corp.", "Tool Merchant#doram05t", bc_self;
	end;
// OnInit:
	// questinfo 7716, 
	// end;
}

lasagna,165,125,5	script	Tool Merchant#doram05t	4_DR_M_01,{
	if (lasagna_quest[0] == 5) {
		if (countitem(1081) > 0) {
			mes "[Kitten]";
			mes "This is it!!";
			next;
			mes "The kitten hoisted the box given to him up high, screaming out in joy";
			next;
			mes "[Tool Merchant]";
			mes "Wait. This box looks familiar...";
			mes "Hey! Isn't this the box that I told you to deliver to Haru?";
			next;
			mes "[Kitten]";
			mes "~Gulps~";
			mes "I'm leaving! Right now!";
			mes "Thank you for finding the box!";
			delitem 1081, countitem(1081);// Delivery Box
			completequest 7716;// To the Village!
			getexp 70,70;
			hideonnpc "Kid#doram04t";// only for player attached
			lasagna_quest[0] = 6;
			next;
			npctalk "...Wow, that was fast.", "", bc_self;
			mes "[Tool Merchant]";
			mes "Anyway, thank you for finding the box.";
			mes "If you have the time, sit down and talk with me.";
			close;
		}
	}
	if (lasagna_quest[0] == 6) {
		mes "[Tool Merchant]";
		mes "You must be here to apply for the expedition to the New Continent.";
		mes "It looks like my son caused some trouble for you.";
		mes "Thank you for your help.";
		next;
		mes "[Tool Merchant]";
		mes "This is not much, but please take it. It's a token of my appreciation.";
		mes "I also gave you ^4d4dffa new set of clothes^000000 so that you can change out of your dirty ones.";
		next;
		mes "[Tool Merchant]";
		mes "You can find other tool merchants like me in each village.";
		mes "You can buy and sell the items through those merchants.";
		next;
		mes "[Tool Merchant]";
		mes "Ask ^4d4dffHealer Haru about the Combination Kit^000000 and she'll help you use it effectively.";
		mes "Just take this with you.";
		setquest 7717;// Merchant's Kindness
		getitem 569,50;// Novice Potion
		getitem 12323,10;// Novice Fly Wing
		getitem 12324,10;// Novice Butterfly Wing
		getitem 7059,10;// Free Ticket for Kafra Storage
		getitem 12849,5;// Combination Kit
		getitem 15150,1;// White Shirt [1]
		getexp 50,0;
		lasagna_quest[0] = 7;
		next;
		mes "[Tool Merchant]";
		mes "If you want to dispose of any items, you should see me.";
		mes "Do you understand?";
		next;
		mes "[Tool Merchant]";
		mes "^4d4dffHealer Haru's house^000000 is straight ^4d4dffto the east^000000 from here.";
		next;
		mes "[Tool Merchant]";
		mes "Plants and grasses are packed in front of her house because she's busy testing medicinal herbs.";
		mes "^4d4dffIt's the house where lots of flowerpots are aligned.^000000 You can't miss it.";
		close;
	}
	if (lasagna_quest[0] == 7) {
		mes "[Tool Merchant]";
		mes "Haru is a very smart girl, so every sick person goes to her place.";
		next;
		mes "[Tool Merchant]";
		mes "You're about to go on a journey and she's going to be a lot of help.";
		mes "Be sure to meet her.";
		next;
		mes "[Tool Merchant]";
		mes "^4d4dffHealer Haru's house^000000 is straight ^4d4dffto the east^000000 from here.";
		next;
		mes "[Tool Merchant]";
		mes "Plants and grasses are packed in front of her house because she's busy testing medicinal herbs.";
		mes "^4d4dffIt's the house where lots of flowerpots are aligned.^000000 You can't miss it.";
		next;
		mes "[Tool Merchant]";
		mes "Is there anything you need?";
	}
	else {
		mes "[Tool Merchant]";
		mes "Welcome.";
	}
	next;
	if (select( "Buy an item.", "Sell your belongings." ) == 1) {
		mes "[Tool Merchant]";
		mes "Do you need anything? I have basic potions, traps, magnifiers...You name it!";
		close2;
		callshop "tool_shop_lasagna",1;
		end;
	}
	mes "[Tool Merchant]";
	mes "Good thinking! I knew you would. you'd better sell away items that you no longer need!";
	close2;
	callshop "tool_shop_lasagna",2;
	end;
}
-	shop	tool_shop_lasagna	-1,1750:-1,611:-1,501:-1,502:-1,503:-1,504:-1,645:-1,656:-1,713:-1,601:-1,602:-1,1065:-1

lasagna,224,126,5	script	Healer Haru#doram07t	4_DR_M_02,{
	if (lasagna_quest[0] == 7) {
		mes "[Healer Haru]";
		mes "I see a lot of out-of-towners these days.";
		mes "What do you need?";
		.@menu$[2] = "Show her the Combination Kit.";
	}
	else {
		mes "[Healer Haru]";
		mes "What's up?";
		setarray .@menu$[0], "Somebody, help me!", "About First Aid", "About Medical Botany";
	}
	next;
	switch( select( .@menu$[0], .@menu$[1], .@menu$[2] ) ) {
	case 1:
		if (Class == Job_Summoner) {
			mes "[Healer Haru]";
			mes "Oh, no...";
			mes "It's going to be fine.";
			npcskill "AL_HEAL",10,130,115;// 1748 hp
			percentheal 100,100;
			close;
		}
		mes "[Healer Haru]";
		mes "Oh, dear...";
		mes "Does it hurt a lot?";
		mes "You need 5,000 Zeny to heal it all. Do you want me to do it?";
		next;
		if (select( "Say yes.", "It's too expensive." ) == 2) {
			mes "[Healer Haru]";
			mes "Take your time and get some rest.";
			close;
		}
		if (Zeny >= 5000)// intended ?
			Zeny = Zeny - 5000;
		mes "[Healer Haru]";
		mes "Thank you for using our service.";
		npcskill "AL_HEAL",10,130,115;// 1748 hp
		percentheal 100,100;
		close;
	case 2:
		if (getskilllv("NV_FIRSTAID") > 0) {
			mes "[Healer Haru]";
			mes "You already know what First Aid is...";
			mes "Please. I don't have all day.";
			close;
		}
		mes "[Healer Haru]";
		mes "Oh, you'd like to learn about First Aid! How admirable of you!";
		mes "First Aid is a skill converting your SP into HP in an emergency.";
		next;
		mes "[Healer Haru]";
		mes "The healing amount can be minimal...";
		mes "But you can at least avoid getting stunned by a critical hit.";
		specialeffect EF_BEGINSPELL6;
		specialeffect EF_SPELLBREAKER;
		next;
		mes "[Healer Haru]";
		mes "Click the ^4d4dffSkill^000000 button to open the Skill Window.";
		mes "FYI, the shortcut key is ^4d4dffAlt+S^000000.";
		getexp 0,100;
		setquest 7722;// EMT
		completequest 7722;// EMT
		skill "NV_FIRSTAID",1,SKILL_PERM;
		next;
		mes "[Healer Haru]";
		mes "Well, then. I'll let you in on something juicy while I'm at it.";
		mes "Press the ^4d4dffF12 key^000000, will you?";
		mes "Surprise! It brings up the shortcut key window where nine small slots are arranged in a row.";
		next;
		mes "[Healer Haru]";
		mes "Drag the corner area in the right-hand side of the shortcut key window to view other pages.";
		next;
		mes "[Healer Haru]";
		mes "It's easy to use. ^4d4dffEach slot corresponds to the keys from F1 through F9.^000000";
		mes "Drag your skill or item to use in the slot and press the corresponding key to activate it instantly.";
		next;
		cutin "tutorial05.BMP",4;
		mes "!- Notice -!";
		mes "^4d4dffTo place an item or skill icon to use, drag it to the shortcut key slot.^000000";
		next;
		mes "!- Notice -!";
		mes "^4d4dffTo use or equip an item or equipment registered in the slot, press the key that corresponds to each slot from F1 to F9.^000000";
		next;
		mes "!- Notice -!";
		mes "^4d4dffIn the shortcut key settings, you can select the corresponding shortcut keys as you desire.^000000";
		next;
		mes "!- Notice -!";
		mes "^4d4dffPlace an item and/or skill that you often use for quick usage.^000000";
		cutin "",255;
		next;
		mes "[Healer Haru]";
		mes "Well, now. Open the Skill Window and place the First Aid skill in a shortcut key slot to use.";
		mes "It's going to be a lot easier to use it.";
		close;
	case 3:
		if (isbegin_quest(7718) == 0 || lasagna_quest[0] == 7) {
			mes "[Healer Haru]";
			mes "Oh, so you're here to learn the potion combination?";
			mes "Then, you need a combination scroll first.";
			mes "Above all, the most important out of all the combination items should be a potion.";
			next;
			mes "[Healer Haru]";
			mes "Have you ever seen the grass growing in the field while you're traveling?";
			mes "^4d4dffRed Grass, Blue Grass, Green Grass, White Grass...^000000etc...";
			next;
			mes "[Healer Haru]";
			mes "You can obtain herbs from these grasses.";
			mes "Each herb is a good source to be processed into various kinds of medicine.";
			next;
			mes "[Healer Haru]";
			mes "Red Herbs are running out.";
			mes "I think you should go pick some now.";
			next;
			mes "[Healer Haru]";
			mes "The herbs are growing in the entrance of the village. Did you see them?";
			mes "^4d4dffGo and collect 1 Red Herb in the entrance of the village.^000000 This should be a good practice.";
			erasequest 7717;
			if (isbegin_quest(7718) == 0)
				setquest 7718;// I'm used to this
			if (lasagna_quest[0] == 7)
				lasagna_quest[0] = 8;
			next;
			mes "[Healer Haru]";
			mes "Come on. Hurry!";
			close2;
			if (lasagna_quest[0] == 7)
				npctalk "Hey, there. Newbie Doram. Talk to me.", "Hypnotist#doram08t", bc_self;
			end;
		}
		if (isbegin_quest(7718) == 1) {
			if (countitem(507) > 0) {
				mes "[Healer Haru]";
				mes "You've got them, perfect.";
				mes "This ^4d4dffherb is effective even when eaten as it is, but, it is far more effective when it is made into a potion.^000000";
				next;
				mes "[Healer Haru]";
				mes "Effects vary for the herbs, depending on their colors.";
				mes "^4d4dffGreen Herbs are known for detoxification.^000000";
				mes "^4d4dff Blue Herbs restore SP^000000.";
				next;
				mes "[Healer Haru]";
				mes "Anything else?";
				mes "Well, now. Your HP is being restored as the Red Herb does it's work.";
				mes "Press juice out of it and put them in a bottle...";
				next;
				mes "[Healer Haru]";
				mes "This is what we call a potion that you can take anytime, anywhere!";
				mes "This is for you on this special encounter.";
				next;
				mes "[Healer Haru]";
				mes "Prepare the materials written in the combination scroll.";
				mes "Then, ^4d4dffuse the Combination Kit or cooking utensils^000000 to make an item you want.";
				next;
				mes "[Healer Haru]";
				mes "You can also make various other types of tools, other than potions, with the materials you pick up, so use them well.";
				next;
				mes "[Healer Haru]";
				mes "I gave you Red Herbs and apples.";
				mes "So give it a try and craft something with the Combination Kit.";
				next;
				mes "[Healer Haru]";
				mes "Here's the directions. To read the combination scroll,";
				mes "^4d4dffRight-click the scroll,^000000 and then, click the ^4d4dffbook-shaped icon^000000 found in the description window.";
				if (isbegin_quest(7717) > 0)
					erasequest 7717;
				if (isbegin_quest(7718) == 0)
					setquest 7718;// I'm used to this
				completequest 7718;
				getexp 200,200;
				getitem 569,30;// Novice Potion
				getitem 11058,1;// Novice Combination Book
				getitem 12849,10;// Combination Kit
				getitem 507,1;// Red Herb
				getitem 512,1;// Apple
				npctalk "Oh! Here you are!", "Vigilante Aglio#doram09", bc_self;
				hideoffnpc "Vigilante Aglio#doram09";// only for player attached
				lasagna_quest[0] = 9;
				close;
			}
			else {
				mes "[Healer Haru]";
				mes "There is a place near the entrance of the village where herbs grow.";
				mes "^4d4dffBeat the Red Grass and get 1 Red Herb^000000.";
				next;
				mes "[Healer Haru]";
				mes "You should know what the herbs look like.";
				close;
			}
		}
		mes "[Healer Haru]";
		mes "I've given you all the details about the basics of medicinal botany.";
		mes "By the way, there is ^4d4dffShining Grass^000000 in this world.";
		next;
		mes "[Healer Haru]";
		mes "You have no idea what kind of herb the Shining Grass will produce.";
		mes "I was lucky enough to obtain Igdrasil's Fruit once.";
		next;
		mes "[Healer Haru]";
		mes "Rumor has it that, among humans, ^4d4dffAlchemist^000000, ^4d4dffCreator^000000, or the upper-level ^4d4dffGeneric^000000";
		mes "are highly specialized in crafting potions.";
		next;
		mes "[Healer Haru]";
		mes "If you happen to meet a human in the Midgard Continent, why don't you ask them?";
		close;
	}

OnInit:
	questinfo 7722, QTYPE_QUEST, 1;
	end;
}

lasagna,230,127,3	script	Vigilante Aglio#doram09	4_DR_AGLIO,{
	mes "[Vigilante Aglio]";
	mes "I was looking for you at the tool shop and they said you headed here.";
	next;
	mes "[Vigilante Aglio]";
	mes "First off, we should meet up with our leader to make decisions about the schedule.";
	mes "We'd better head to the square.";
	next;
	mes "[Vigilante Aglio]";
	mes "I think you can find another Vigilante like myself in the square...";
	navigateto "lasagna",170,152;
	close;
OnInit:
	hideonnpc "Vigilante Aglio#doram09";// no sprite
	end;
}


lasa_fild01,132,374,3	script	Sloth#doram	4_DR_KID_01,3,3,{
	switch( isbegin_quest(7719) ) {
	case 0:
		mes "[Sloth]";
		mes "Hmm...You look energetic.";
		mes "You see, I have an upset stomach.";
		next;
		mes "[Sloth]";
		mes "The weather is sunny and warm, but...";
		mes "I think I'm having a very bad day...";
		next;
		mes "[Sloth]";
		mes "I should pluck all the Red Grass in this herb field...";
		next;
		mes "[Sloth]";
		mes "But I'm pleasantly full in this fine sunny weather...";
		mes "And there's a good breeze blowing...";
		next;
		mes "[Sloth]";
		mes "I don't think I can ever drag myself up.";
		mes "But I'm supposed to pluck the Red Grass...";
		mes "If you do it I'll give you anchovies, okay?";
		next;
		switch( select( "I'll pluck the Red Grass for you.", "Move your butt, you lazy sloth!" ) ) {
		case 1:
			mes "[Sloth]";
			mes "^4d4dffPluck 3 Red Grass and then collect 2 Red Herbs^000000 around here!";
			next;
			mes "[Sloth]";
			mes "This may be of great help to you!";
			mes "Well, then. I'm counting on you.";
			next;
			mes "[Sloth]";
			mes "I'll rest here to make my upset stomach feel better.";
			setquest 7719;// Time to Digest
			close;
		case 2:
			mes "[Sloth]";
			mes "Meeow....";
			mes "....Meeowww....";
			close;
		}
	case 1:
		if (checkquest(7719,HUNTING) != 2) {
			mes "[Sloth]";
			mes "What I want isn't too much.";
			mes "Just ^4d4dffpluck the Red Grass and collect 2 Red Herbs^000000 around here. That's all.";
			next;
			mes "[Sloth]";
			mes "They grow too fast and keep growing no matter how many times I pluck them. I've had it...";
			close;
		}
		if (countitem(507) < 2) {
			mes "[Sloth]";
			mes "What I want isn't too much.";
			mes "Just ^4d4dffpluck the Red Grass and collect 2 Red Herbs^000000 around here. That's all.";
			close;
		}
		mes "[Sloth]";
		mes "Oh...This aroma!";
		mes "You plucked the Red Herbs just right.";
		next;
		mes "[Sloth]";
		mes "I'll munch on them like twigs and digest them just like that. Meow!";
		next;
		mes "[Sloth]";
		mes "I'll give you the left-over herbs as a reward.";
		mes "Chew them up and ^4d4dffyour HP will be restored.^000000";
		next;
		mes "[Sloth]";
		mes "If you ask Haru, she'll tell you how to turn them into a potion with more effects.";
		next;
		mes "[Sloth]";
		mes "Anyway, I'm the cat who knows how to return the favor.";
		mes "This is for you.";
		mes "Stay out of trouble, now.";
		completequest 7719;
		delitem 507,1;// Red Herb
		getitem 11601,20;// Delicious Anchovy
		getitem 12323,10;// Novice Fly Wing
		getitem 12324,10;// Novice Butterfly Wing
		getexp 4,20;
		close;
	case 2:
		mes "[Sloth]";
		mes "I'm feeling... so tired...";
		close;
	}
OnTouch:
	if (isbegin_quest(7719) == 2)
		npctalk "~Growling~", "", bc_self;
	else
		npctalk "Hey, there! Get over here for a minute!", "", bc_self;
	end;
OnInit:
	questinfo 7719, QTYPE_QUEST, 1;
	end;
}

lasa_fild01,103,344,3	script	Well-known Troublemaker	4_DR_KID_01,3,3,{
	if (Class != Job_Summoner) {
		mes "[Well-known Troublemaker]";
		mes "What brought you all the way up here, human?";
		mes "Be gone! Be gone, meeow!";
		next;
		mes "[Well-known Troublemaker]";
		mes "This is like a gateway. It cannot and should not be tainted by humans!";
		close;
	}
	switch( isbegin_quest(7721) ) {
	case 0:
		mes "[Well-known Kittie]";
		mes "Hey, buddy!";
		mes "Nyo-hoho! Nyo-hoho! You look so hectic. Where are you going?";
		mes "To me, you look like a newbie adventurer who has just crossed the mountain ridge, meeow.";
		next;
		mes "[Well-known Kittie]";
		mes "Aren't you too weak to handle the Dorams in Lasagna?";
		next;
		mes "[Well-known Kittie]";
		mes "Do you even know how to use skills?";
		mes "To me, you look like a greenhorn who's simply acting out.";
		next;
		select( "I'm a better fighter than you are!", "Whoa, is that so?" );
		mes "[Well-known Kittie]";
		mes "Nyo-ho! Nyo-ho! How dare you disrespect me?";
		mes "How about making a bet with me?";
		next;
		mes "[Well-known Kittie]";
		mes "I'll give you 10 full minutes while you hunt ^4d4dff3 Leaf Lunatics and 2 Grass Fabres^000000 around here!";
		next;
		mes "[Well-known Kittie]";
		mes "If you show me what you're made of, I'll get out of your way.";
		next;
		switch( select( "Accept.", "Decline." ) ) {
		case 1:
			mes "[Well-known Kittie]";
			mes "You've got 10 minutes. No more, no less!";
			mes "^4d4dff3 Leaf Lunatics and 2 Grass Fabres!^000000";
			setquest 7721;// Cat showoff contest
			close;
		case 2:
			mes "[Well-known Kittie]";
			mes "Are you running away?!";
			mes "No adventurer with a faint heart ever survived on the New Continent!";
			close;
		}
	case 1:
		.@quest_hunting = checkquest(7721,HUNTING);
		.@quest_playtime = checkquest(7721,PLAYTIME);
		if (.@quest_playtime < 2 && .@quest_hunting < 2) {
			mes "[Well-known Kittie]";
			mes "Eliminate ^4d4dff3 Leaf Lunatics and 2 Grass Fabres^000000 swarming around here within ^4d4dff10 minutes^000000!";
			close;
		}
		if (.@quest_playtime == 2 && .@quest_hunting < 2) {
			mes "[Well-known Kittie]";
			mes "Nyo-hoho!! Nyo-hoho!!";
			mes "Was it too tough for you to handle?";
			mes "What do you think? Want to give it another try?";
			next;
			mes "[Well-known Kittie]";
			mes "Was it too hard for you?";
			mes "Are you using your ^4d4dffskills^000000 well while hunting?";
			next;
			mes "[Well-known Kittie]";
			mes "You can ^4d4dffbring up the Skill Tree by pressing the [Alt+S] shortcut key or clicking the corresponding skill icon.^000000";
			next;
			mes "[Well-known Kittie]";
			mes "Setup the skill you'd like to use so that you can use it while in battle.";
			next;
			mes "[Well-known Kittie]";
			mes "^4d4dffSkills that are activated manually ^000000 can be ^4d4dffplaced on the quickbar^000000 for fast use.";
			next;
			mes "[Well-known Kittie]";
			mes "...Now you know how to use skills.";
			mes "So, what are you going to do?";
			next;
			switch( select( "Try again.", "I'll quit right now!" ) ) {
			case 1:
				mes "[Well-known Kittie]";
				mes "You have moxie and I like that!";
				mes "Alright, then. Give it another go!";
				erasequest 7721;
				setquest 7721;// Cat showoff contest
				close;
			case 2:
				mes "[Well-known Kittie]";
				mes "How can you hope to survive the New Continent with that weak mindset?";
				mes "Nyo-hoho! Nyo-hoho! I'm the strongest Doram in this domain!";
				close;
			}
		}
		if (.@quest_playtime < 2 && .@quest_hunting == 2) {
			mes "[Well-known Kittie]";
			mes "Hmm...You are good.";
			mes "I feel challenged whenever I stumble upon a competent Doram such as you.";
		}
		else {
			mes "[Well-known Kittie]";
			mes "Time is already up! Nyo-hoho!!";
			mes "So that again proves that I'm the best in this domain!";
		}
		next;
		mes "[Well-known Kittie]";
		mes "You finished the job on a high note so I'd like to help you in return.";
		next;
		mes "[Well-known Kittie]";
		mes "By the way, do you know what a ^4d4dffSkill Tree^000000 is?";
		mes "Did I teach you about that?";
		mes "Do you remember anything about it?";
		next;
		mes "[Well-known Kittie]";
		mes "You can ^4d4dffbring up the Skill Tree by pressing the [Alt+S] shortcut key or clicking the corresponding skill icon.^000000";
		next;
		mes "[Well-known Kittie]";
		mes "Setup the skill you'd like to use so that you can use it while in battle.";
		next;
		mes "[Well-known Kittie]";
		mes "^4d4dffSkills that are activated manually ^000000 can be ^4d4dffplaced on the quickbar^000000 for fast use.";
		next;
		mes "[Well-known Kittie]";
		mes "...Now you know how to use skills.";
		mes "You should thank me. Nyo-hoho!";
		completequest 7721;// Cat showoff contest
		getexp 120,120;
		doevent "Vigilante Aglio#doram12::OnEvent";
		close;
	case 2:
		mes "[Well-known Kittie]";
		mes "Do you need to be enlightened again?";
		next;
		mes "[Well-known Kittie]";
		mes "There's nothing special about life in this world.";
		mes "You'd better make yourself familiar with the skill tree. It is accessed when you click the Skill icon.";
		next;
		mes "[Well-known Kittie]";
		mes "Battle Dorams are specialized in summoning.";
		mes "Focus on using the summoning skills so that you can win battles.";
		close;
	}

OnTouch:
	if (Class == Job_Summoner && isbegin_quest(7721) == 0)
		npctalk "Meeow?? Is this another out-of-towner Doram that I haven't met before?", "", bc_self;
	end;
}

lasa_fild01,102,342,7	script	Vigilante Aglio#doram12	4_DR_AGLIO,{
	end;
OnEvent:
	hideoffnpc "Vigilante Aglio#doram12";// only for player attached
	sleep2 1000;
	npctalk "You! You're picking a fight with another innocent passerby?", "", bc_self;
	sleep2 2000;
	npctalk "Picking a fight? This is what we call training!", "Well-known Troublemaker", bc_self;
	sleep2 2000;
	npctalk "Shut your mouth! You're joining the Vigilante Corp the moment you return to the village, got it?", "", bc_self;
	sleep2 2000;
	npctalk "Well, uh...I'm...uh...I'm free-spirited! That's all!", "Well-known Troublemaker", bc_self;
	sleep2 2000;
	npctalk "Enough. " + strcharinfo(0) + " the next destination is the tool shop in the village. I'll let you know where it is, again.", "", bc_self;
	sleep2 2000;
	npctalk "I'll see you again in the village!", "", bc_self;
	navigateto "lasagna",168,124;
	sleep2 2000;
	npctalk "You're just full of it.", "Well-known Troublemaker", bc_self;
	hideonnpc "Vigilante Aglio#doram12";// only for player attached
	end;
OnInit:
	hideonnpc "Vigilante Aglio#doram12";// only for player attached
	end;
}

-	shop	Cat Paw Shrimp Merchant#shop::doram_shrimp	-1,515:-1,567:-1,11602:-1

izlude,117,165,3	script	Cat Paw Shrimp Merchant#iz	4_M_MERCAT1,{
	npctalk "Here are the shrimps, carrots and catnip!", "", bc_self;
	switch( select( "Buy", "Sell", "Cancel" ) ) {
	case 1:
		callshop "doram_shrimp",1;
		end;
	case 2:
		callshop "doram_shrimp",2;
		end;
	case 3:
		npctalk "Come back again soon!", "", bc_self;
		end;
	}
}

alberta,111,159,3	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#alb	4_M_MERCAT1
aldeba_in,89,56,3	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#ald	4_M_MERCAT1
amatsu,102,122,5	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#ama	4_M_MERCAT1
// ayo_in01,15,177
brasilis,249,257,3	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#bra	4_M_MERCAT1
comodo,103,136,3	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#com	4_M_MERCAT1
dewata,193,169,5	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#dew	4_M_MERCAT1
einbroch,164,316,3	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#ein	4_M_MERCAT1
ein_in01,194,22,3	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#ein1	4_M_MERCAT1
dic_in01,242,119,5	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#dic	4_M_MERCAT1
ecl_in01,72,86,3	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#ecl	4_M_MERCAT1
geffen_in,75,140,3	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#gef	4_M_MERCAT1
gonryun,147,88,5	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#gon	4_M_MERCAT1
hugel,76,165,5	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#hug	4_M_MERCAT1
izlude_a,117,165,3	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#iz_a	4_M_MERCAT1
izlude_b,117,165,3	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#iz_b	4_M_MERCAT1
izlude_c,117,165,3	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#iz_c	4_M_MERCAT1
izlude_d,117,165,3	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#iz_d	4_M_MERCAT1
lasagna,166,117,6	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#las	4_M_MERCAT1
lhz_in02,29,219,5	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#lhz	4_M_MERCAT1
lou_in02,241,168,5	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#lou	4_M_MERCAT1
malaya,286,175,3	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#mal	4_M_MERCAT1
mora,110,118,3	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#mora	4_M_MERCAT1
morocc,169,105,3	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#moro	4_M_MERCAT1
mosk_in,30,246,3	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#mosk	4_M_MERCAT1
payon,157,96,5	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#pay	4_M_MERCAT1
prontera,137,223,5	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#prt	4_M_MERCAT1
ra_in01,243,272,5	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#ra	4_M_MERCAT1
um_in,104,118,3	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#um	4_M_MERCAT1
ve_in,263,303,3	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#ve	4_M_MERCAT1
xmas_in,32,38,5	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#xma	4_M_MERCAT1
yuno,205,116,5	duplicate(Cat Paw Shrimp Merchant#iz)	Cat Paw Shrimp Merchant#yu	4_M_MERCAT1

izlude,195,213,5	script	Con-Chliina Crewman#doi	4_M_MERCAT1,{
	mes "[Con-Chliina Crewman]";
	mes "From the continent of Midgard to Far-Star!";
	next;
	mes "[Con-Chliina Crewman]";
	if (Class == Job_Summoner)
		.@cost = 200;
	else
		.@cost = 2000;
	mes "Only ^0000cd" + .@cost + " Zenies^000000 to the port city Lasagna.";
	next;
	if (select( "To Lasagna - " + .@cost + "z", "Cancel" ) == 2) {
		mes "[Con-Chliina Crewman]";
		mes "I see.";
		close;
	}
	if (Zeny < .@cost) {
		mes "[Con-Chliina Crewman]";
		mes "Not enough? How about checking how much he has?";
		close;
	}
	Zeny = Zeny - .@cost;
	warp "lasagna",206,297;
	end;
}
malangdo,218,81,5	duplicate(Con-Chliina Crewman#doi)	Con-Chliina Crewman#dom	4_M_MERCAT1

izlude,138,183,3	script	Vigilante Penne#doect01	4_DR_SOLDIER,{
	if (Class == Job_Summoner) {
		mes "[Vigilante Penne]";
		mes "Hiya! Isn't it delightful to see the Farfalle Vigilante even here?";
		next;
		mes "[Vigilante Penne]";
		mes "By the way, how's your settlement in this land coming along?";
		mes "Have you been to ^4d4dffProntera^00000 or ^4d4dffPayon^000000 near here?";
		next;
		mes "[Vigilante Penne]";
		mes "There is a Paradise Group you might want to visit.";
		mes "It doesn't hurt to go visit there.";
		close;
	}
	mes "[Vigilante Penne]";
	mes "Humans always intrigue me with their peculiar look.";
	mes "Like a big, hairless cat!";
	next;
	mes "[Vigilante Penne]";
	mes "Me? Let me explain. I'm the proud member of the Farfalle Vigilante from the great Far-Star Continent.";
	next;
	switch( select( "Far-Star Continent?", "Farfalle Vigilante?", "Are you from the Cat Paw Commerce Group?" ) ) {
	case 1:
		mes "[Vigilante Penne]";
		mes "There are more continents other than Midgard.";
		mes "The land we crossed from is called Far-Star.";
		next;
		mes "[Vigilante Penne]";
		mes "Well, we're no different because we thought there was only the Far-Star Continent under the sun.";
		mes "The world sure is wide...";
		close;
	case 2:
		mes "[Vigilante Penne]";
		mes "So, do you see this ribbon that goes perfectly well with this cool uniform?";
		mes "(Show the below-neck area.)";
		next;
		mes "[Vigilante Penne]";
		mes "This is the symbol of our Farfalle Vigilante.";
		mes "We are the legendary Vigilante Corp that protects Lasagna!";
		next;
		mes "[Vigilante Penne]";
		mes "You have no idea how cool our leader Peperoncino can be...";
		mes "We do all kinds of chores from handling the urchins at the port to maintaining the security of the village.";
		next;
		mes "[Vigilante Penne]";
		mes "Moreover, you can come to this...Mid...Midgur...Midgar...";
		mes "Anyway! You're allowed to come here only when the Farfalle Vigilante accepts.";
		next;
		mes "This proud cat is getting really talkative...";
		close2;
		npctalk "Huh? Are you just passing by?";// todo
		end;
	case 3:
		mes "[Vigilante Penne]";
		mes "No, That's not it!!";
		mes "The Cat Paw Commerce Group is led by Captain Tomei-Tomas!";
		next;
		mes "[Vigilante Penne]";
		mes "I'm with the Farfalle Vigilante commanded by Leader Peperoncino!";
		next;
		mes "[Vigilante Penne]";
		mes "Oh, my days! So you haven't even been to Lasagna, have you?";
		mes "Our great Captain Gamberri ventured here in person!";
		next;
		mes "[Vigilante Penne]";
		mes "We are gracious enough to give humans like you a chance to go to the land of Lasagna.";
		mes "Before long, this Midgur...Midgarr...?? This land will soon be controlled by us, the Dorams!";
		next;
		mes "[Vigilante Penne]";
		mes "You'd better earn our favor. If you do, we might give you a job to clean anchovies in Lasagna!";
		close;
	}
}

lasagna,209,301,3	script	Mustachi#dorcon01	4_DR_KID_01,{
	mes "[Mustachi]";
	mes "We trim your beards. We comb your tails. Be sure to groom yourselves before setting out to the new world.";
	mes "There's a hair designer in downtown, so make sure you drop by.";
	close;
}

lasagna,232,181,5	script	Linguina#dorcon01	4_CAT_ADV1,{
	mes "[Linguina]";
	mes "This is a pub but also an adventurers' club where adventurers stop by. They come from all over the place and share their stories as well as find companions for adventures.";
	next;
	mes "[Linguina]";
	mes "These days, many people come to seek information before heading off to the new world.";
	next;
	mes "[Linguina]";
	mes "About ten years ago, it was a conciliating place to rehab hoodlum dorams and recruit them as vigilantes. Before that, rough sailors came to relax after a hard day's work.";
	next;
	mes "[Linguina]";
	mes "Chief Peperoncino and Captain Gamberry first met here as well. The three became sworn brothers and became legends after their endeavors proved to be successful.";
	next;
	mes "[Linguina]";
	mes "So people who wish to succeed or adventurers who wish to become just like them come here a lot.";
	close;
}

lasagna,227,187,1	script	Vongolo#dorcon01	4_CAT_SAILOR1,{
	mes "[Vongolo]";
	mes "Did you decide what you'll have?";
	next;
	mes "[Vongolo]";
	mes "Everyone orders kaluna milk here because of the oven resolution by Peperoncino, Gamberry, and Thomas. But Pub-Oven's real specialty menu is the fish cake.";
	next;
	mes "[Vongolo]";
	mes "Fish cake is a white fish fillet mashed with boiled beans that is shaped and fried. It's really delicious but no one asks for it.";
	next;
	mes "[Vongolo]";
	mes "So we don't even have the ingredients prepared these days. When there's an order once in a while, we run to the fish store to buy a cod and start cooking then.";
	next;
	mes "[Vongolo]";
	mes "Oh, It's really good...";
	close;
}

lasagna,220,172,6	script	Master of Property#do_h	4_M_BABYCAT,{
	.@r = rand(0,4);
	if (.@r == 0)
		npctalk "Earth element defeats Wind element.", "", bc_self;
	else if (.@r == 1)
		npctalk "Water element is superior to Fire element.", "", bc_self;
	else if (.@r == 2)
		npctalk "Wind element outperforms Water element.", "", bc_self;
	else if (.@r == 3)
		npctalk "Fire element beats Earth element.", "", bc_self;
	else
		npctalk "Holy element and Dark element are in stark contrast to each other.", "", bc_self;
	mes "[Property Cat]";
	mes "Every life in this world comes with a property.";
	mes "There are numerous kinds of elements from neutral to four major natural elements, alongside the holy and dark elements.";
	next;
	mes "[Property Cat]";
	mes "Most representative elements are the natural ones.";
	mes "Put simply, these are the elements of Water, Wind, Earth, and Fire.";
	mes "Besides, there are elements of Undead, Holiness and Darkness.";
	next;
	mes "[Property Cat]";
	mes "Each element is intertwined with each other. So be smart and use the opposing elements.";
	next;
	mes "[Property Cat]";
	mes "That way, you can get the upper hand of a battle.";
	mes "Let me give you a brief example on their correlation.";
	next;
	mes "[Property Cat]";
	mes "Fire element is highly resistant to Earth element.";
	mes "Meanwhile, Earth element definitely outperforms Wind element.";
	next;
	mes "[Property Cat]";
	mes "Wind element beats Water element.";
	mes "Yet, Water element defeats Fire element.";
	next;
	mes "[Property Cat]";
	mes "Holy element and Dark element directly oppose each other, and you should keep that in mind.";
	next;
	mes "[Property Cat]";
	mes "Other than that, there are also Neutral and Undead elements. You'll soon know what they are about.";
	mes "You will have to see it for yourself.";
	close;
}

lasagna,208,188,3	script	Cat Paw Service#lasagna	4_M_MERCAT1,4,4,{
	if (Class == Job_Summoner && isbegin_quest(7720) == 0) {
		mes "[Cat Paw Commerce Group]";
		mes "Hey, little fellow!";
		mes "Are you headed to the Midgard Continent?";
		mes "Then, you'd better listen to me!";
		next;
		mes "[Cat Paw Commerce Group]";
		mes "Like our Cat Paw Commerce Group, there are other groups who provide the ^4d4dffLocation sharing service^000000 and the ^4d4dffWarehouse service^000000.";
		next;
		mes "[Cat Paw Commerce Group]";
		mes "They are: ^4d4dffKafra^000000 and ^4d4dffZonda^000000.";
		mes "Of course, don't forget about the Cat Paw Commerce Group!";
		next;
		mes "[Cat Paw Commerce Group]";
		mes "^4d4dffLocation sharing^000000 is to save the location for you to return to.";
		mes "If you fall down due to fatigue during an adventure, you should return to the village. That's what you can do with this service.";
		next;
		mes "[Cat Paw Commerce Group]";
		mes "Use the ^4d4dffButterfly Wings^000000 item to fly straight to that location.";
		next;
		mes "[Cat Paw Commerce Group]";
		mes "Most of the locations shared are the safe village or city.";
		next;
		mes "[Cat Paw Commerce Group]";
		mes "The ^4d4dffWarehouse service^000000 is also similar.";
		mes "You pay a little service fee to deposit your items safely.";
		next;
		mes "[Cat Paw Commerce Group]";
		mes "You can't just carry around every item with you, can you?";
		mes "This is where the warehouse comes in handy.";
		next;
		mes "[Cat Paw Commerce Group]";
		mes "You can deposit and take out your item in the warehouse anytime, anywhere if you pay the service fee!";
		mes "Now do you get it?";
		setquest 7720;// Service Manual
		completequest 7720;// Service Manual
		getitem 12323,10;// Novice Fly Wing
		getitem 12324,10;// Novice Butterfly Wing
		getexp 62,62;
		next;
		mes "[Cat Paw Commerce Group]";
		mes "Oh, do you want me to explain it again?";
		next;
		if (select( "No.", "Please." ) == 2) {
			mes "[Cat Paw Commerce Group]";
			mes "Like our Cat Paw Commerce Group, there are other groups who provide the ^4d4dffLocation sharing service^000000 and the ^4d4dffWarehouse service^000000.";
			next;
			mes "[Cat Paw Commerce Group]";
			mes "They are: ^4d4dffKafra^000000 and ^4d4dffZonda^000000.";
			mes "Of course, don't forget about the Cat Paw Commerce Group!";
			next;
			mes "[Cat Paw Commerce Group]";
			mes "^4d4dffLocation sharing^000000 is to save the location for you to return to.";
			mes "If you fall down due to fatigue during an adventure, you should return to the village. That's what you can do with this service.";
			next;
			mes "[Cat Paw Commerce Group]";
			mes "Use the ^4d4dffButterfly Wings^000000 item to fly straight to that location.";
			next;
			mes "[Cat Paw Commerce Group]";
			mes "Most of the locations shared are the safe village or city.";
			next;
			mes "[Cat Paw Commerce Group]";
			mes "The ^4d4dffWarehouse service^000000 is also similar.";
			mes "You pay a little service fee to deposit your items safely.";
			next;
			mes "[Cat Paw Commerce Group]";
			mes "You can't just carry around every item with you, can you?";
			mes "This is where the warehouse comes in handy.";
			next;
			mes "[Cat Paw Commerce Group]";
			mes "You can deposit and take out your item in the warehouse anytime, anywhere if you pay the service fee!";
			mes "Now do you get it?";
			close;
		}
		mes "[Cat Paw Commerce Group]";
		mes "Then, what do you want me to help you with?";
	}
	else {
		mes "[Cat Paw Commerce Group]";
		mes "Come on! Welcome!";
		mes "What do you want me to help you?";
	}
	next;
	switch( select( "Location sharing service", "Warehouse service", "Quit." ) ) {
	case 1:
		mes "[Cat Paw Commerce Group]";
		mes "Saved, meow!";
		savepoint "lasagna",193,183,1,1;
		close;
	case 2:
		if (getskilllv("SU_BASIC_SKILL") < 1) {
			mes "[Cat Paw Commerce Group]";
			mes "I'm sorry, but...";
			mes "You should learn some basic skills first if you want to use the warehouse.";
			mes "Go and learn the skills until you reach the level to use the warehouse.";
			mes "I believe it should be ^4d4dffbasic level 6^000000 for humans.";
			close;
		}
		if (countitem(7059) > 0)// Free Ticket for Kafra Storage
			delitem 7059,1;
		else {
			if (Zeny < 50) {
				mes "[Cat Paw Commerce Group]";
				mes "If you don't have cheese, I'll gladly take Zeny.";
				mes "The Warehouse Service costs 50 Zeny.";
				close;
			}
			Zeny = Zeny - 50;
		}
		// todo kafra points
		mes "[Cat Paw Commerce Group]";
		mes "I'll get the warehouse open.";
		mes "Come back again!";
		close2;
		openstorage();
		end;
	case 3:
		mes "[Cat Paw Commerce Group]";
		mes "Come again then.";
		close;
	}
OnTouch:
	if (Class == Job_Summoner && isbegin_quest(7720) != 2)
		npctalk "Meow! Hey, a new friend! I've got a story to tell you. You don't want to miss this!", "", bc_self;
	end;
OnInit:
	questinfo 7720, QTYPE_QUEST, 1;
	end;
}

lasagna,222,123,5	script	Hypnotist#doram08t	4_CAT_MERMASTER,{
	if (Class != Job_Summoner) {
		mes "[Hypnotist]";
		mes "Oh! So you're that famous human everyone is talking about!";
		mes "But why is your hair uneven?";
		next;
		mes "[Hypnotist]";
		mes "Have you come down with a skin disease or something?";
		mes "This healer next to me is really good. You should ask her to take a look at it!";
		next;
		mes "[Hypnotist]";
		mes "You're really big and that type of poor hair just doesn't go well with you...";
		mes "It must've been a lot of work.";
		close;
	}
	mes "[Hypnotist]";
	mes "I'm casting hypnosis on the young lads for their future.";
	next;
	if (BaseLevel > 49) {
		mes "[Hypnotist]";
		mes "You must have been over with wandering by now.";
		close;
	}
	mes "[Hypnotist]";
	mes "If you don't like a skill you just learned, I can reset it for you.";
	next;
	mes "[Hypnotist]";
	mes "^4d4dffUp to level 49, your skills and status can be reset anytime you want.^000000";
	next;
	.@s = select( "Reset the status.", "Reset the skill." );
	if (Weight > 0) {
		mes "[Hypnotist]";
		mes "Your items can be lost, so be sure to make yourself weigh ^FF00000^000000.";
		next;
		mes "[Hypnotist]";
		mes "The Cat Paw Commerce Group is responsible for the warehouse, so leave your items to them.";
		close;
	}
	if (.@s == 1) {
		resetstatus();
		mes "[Hypnotist]";
		mes "Your status has been reset successfully!";
		next;
		mes "[Hypnotist]";
		mes "I don't do it for the experienced. So you should figure out your own status well beforehand.";
		close;
	}
	resetskill();
	mes "[Hypnotist]";
	mes "Your skill has been reset successfully!";
	next;
	mes "[Hypnotist]";
	mes "I reset skills and statuses without charge only when your level stays at 49 or below.";
	mes "Before that, I advise you to practice with many skills in diverse ways, to help you decide what you like.";
	close;
}

lasagna,171,105,5	script	Emergency Patient#dorco	4_M_DIEMAN,5,5,{
	emotion 26;
	npctalk "S-someone help me... Dorams, they're so cute I'm going to die...", "", bc_self;
	end;
OnTouch:
	emotion 26;
	end;
}

lasagna,200,278,5	script	Recruiter#dorcon01	4_CAT_ADV2,{
	mes "[Recruiter]";
	mes "Go for a chance to win a cruise trip and annual salary of one hundred million with a price of three anchovies! Chances are in favor of those who don't doubt!";
	next;
	mes "[Gomae]";
	mes "Vigilantes! Over here! there's someone here tricking people to get on board!";
	next;
	mes "[Recruiter]";
	mes "Blah! No luck here!";
	close;
}

lasagna,202,276,5	script	Gomae#dorcon01	4_DR_F_01,{
	npctalk "I heard that there are illegal anchovy boats tricking people to get on board these days. It's a scary world. The vigilantes have their eyes wide open to catch them.", "", bc_self;
	end;
}

lasagna,211,186,5	script	Boarding Attendant#dorc	4_CAT_ADV1,5,5,{
	if (lasagna_quest[1] < 12) {
		mes "[Boarding Attendant]";
		mes "We wish you a pleasant day on Con-Chliina!";
		close;
	}
	if (lasagna_quest[1] == 12) {
		mes "[Boarding Attendant]";
		mes "Hello, adventurer!";
		next;
		mes "[Boarding Attendant]";
		mes "I assist those who finished their training from Farfalle Vigilantes with boarding on Con-Chliina.";
		next;
		mes "[Boarding Attendant]";
		mes "Those heading to the new world should follow the red yarn and go to the ^0000cddock^000000.";
		next;
		mes "[Boarding Attendant]";
		mes "Please check to see if you have the boarding pass with you before boarding!";
		navigateto "lasagna",205,325;
		close;
	}
	end;
OnTouch:
	if (isbegin_quest(11445) == 2 && isbegin_quest(14545) == 0)
		npctalk "Please don't miss the announcement about the details of Con-Chliina!", "", bc_self;
	end;
OnInit:
	questinfo 14545, QTYPE_QUEST2, 1;
	setquestinfo_req 14545,11445,2;
	end;
}

lasa_fild01,356,374,7	script	Cow#dorcon01	4_BULL,{
	npctalk "Moo -", "", bc_self;
	end;
}
lasa_fild01,352,376,5	script	Cow#dorcon02	4_WHITE_COW,{
	npctalk "Moo - Moo -", "", bc_self;
	end;
}
lasa_fild01,361,372,3	script	Cow#dorcon03	4_BULL,{
	npctalk "Bba -", "", bc_self;
	end;
}

lasagna,217,192,5	script	Kittie#do_hint01	4_CAT_SAILOR1,{
	npctalk "F12! F12!", "", bc_self;
	mes "[Kittie]";
	mes "^4d4dffPress the F12 key and it will bring up the shortcut key slots.^000000";
	next;
	cutin "tutorial05.BMP",4;
	mes "!- Notice -!";
	mes "^4d4dffTo prepare an item or skill icon to use, drag it to the shortcut key slot.";
	next;
	mes "!- Notice -!";
	mes "^4d4dffTo use or equip an item or equipment registered in the slot, press the key that corresponds to each slot from F1 to F9.";
	next;
	mes "!- Notice -!";
	mes "^4d4dffIn the shortcut key settings, you can select the corresponding shortcut key as you desire.";
	next;
	mes "!- Notice -!";
	mes "^4d4dffPrepare an item and/or skill that you often use for faster use.";
	cutin "",255;
	close;
}

lasagna,134,113,3	script	Hair Designer#dr	4_DR_F_02,{
	mes "[Lonza]";
	mes "If you're one of the trendsetting Dorams, watch what Lonza can do with her great scissors!";
	next;
	if (Class != Job_Summoner) {
		mes "[Lonza]";
		mes "If you're one of the trendsetting Dorams, watch what Lonza can do with her great scissors!";
		next;
		mes "[Lonza]";
		mes "Oh? I'm sorry, but my great scissors are not for anyone who demands it.";
		mes "They're allowed only for the Doram.";
		close;
	}
	switch( select( "Change hairstyle.", "Dye hair.", "Cancel" ) ) {
	case 1:
		mes "[Lonza]";
		mes "I've lined up the attractive hairstyles that never go out of style. Will you take a look?";
		next;
		while( true ) {
			.@i++;
			cutin "hair_dr_m_0" + (.@i%6) + ".bmp",4;
			mes "[Lonza]";
			mes "This is the " + (.@i%6) + "th hairdo. How do you like it?";
			next;
			switch( select( "Browse other styles.", "I'll take this.", "End of dialogue." ) ) {
			case 1:
				continue;
			case 2:
				mes "[Lonza]";
				mes "So you picked the " + (.@i%6) + "th hairstyle. I like your choice.";
				next;
				mes "[Lonza]";
				mes "But you need some preparations before you change your hairdo.";
				next;
				mes "[Lonza]";
				mes "3 Counteragent,";
				mes "3 Mixture,";
				mes "100 Braided Hair,";
				mes "100 Short Braids,";
				mes "100 Long Hair,";
				mes "100 Golden Hair,";
				mes "100 Glossy Hair,";
				next;
				mes "[Lonza]";
				mes "and 99,800 Zeny!";
				next;
				if (select( "It's all here.", "I'll get them all." ) == 2) {
					mes "[Lonza]";
					mes "Good! Keep it up! Run!";
					close;
				}
				if (countitem(501) < 1) {// todo
					mes "[Lonza]";
					mes "I can help you only when you bring me the materials. Will you check on your belongings again?";
					close;
				}
				// todo
				end;
			case 3:
				mes "[Lonza]";
				mes "Goodbye then. Don't make me and my great scissors wait too long for you to return.";
				close;
			}
		}
	case 2:
		mes "[Lonza]";
		mes "Trendsetters know the right color for their hair, too!";
		next;
		mes "[Lonza]";
		mes "Bring me the dye to have your hair dyed and 1,000 Zeny and I'll dye your hair so beautifully that even fashion stars would go home crying.";
		next;
		mes "^FF0000[!!Caution!!]^000000";
		mes "^FF0000The Dorams inherently have the shortest hair. Even if you proceed with the dialogue, therefore, nothing will happen.^000000";
		next;
		.@s = select( "Yellow", "Purple", "Orange", "Green" );
		if (countitem(501) < 1) {// todo
			mes "[Lonza]";
			mes "How can I dye your hair without the dye?";
			close;
		}
		// todo
		end;
	case 3:
		mes "[Lonza]";
		mes "It feels as if you want someone to take care of your whole day. don't you have one of those days?";
		mes "Come looking for Lonza anytime you want to be freshened up for a cooler look!";
		close;
	}
}

lasagna,260,134,3	script	Rasmus#dorcon01	4_CAT_SAILOR3,{
	npctalk "Red yarn will take you to the place you want to go, and the yellow road will take you home. don't forget. If you get lost, follow the red yarn.", "", bc_self;
	end;
}

lasagna,244,231,3	script	Crewman of Anchovy Fish	4_CAT_SAILOR1,{
	npctalk "Follow this yellow cheese road and you'll reach my hometown. I want to go back there one day.", "", bc_self;
	end;
}

lasagna,190,239,5	script	Youngster meow#dorcon01	4_CAT_ADV1,{
	npctalk "I'm the boss of this playground-", "", bc_self;
	end;
}

lasagna,180,230,7	script	Youngster meow#dorcon02	4_CAT_ADV2,{
	npctalk "Whew, you little kids... You guys are too young for me.", "", bc_self;
	end;
}

lasagna,192,245,7	script	Youngster meow#dorcon03	4_CAT_REST,{
	npctalk "There are one ten two ten three ten yarn balls... So many!", "", bc_self;
	end;
}

lasagna,184,234,5	script	Youngster meow#dorcon04	4_M_BABYCAT,{
	npctalk "Anyone who wants to ride the seesaw- come here-", "", bc_self;
	end;
}

lasagna,186,231,3	script	Youngster meow#dorcon05	4_DR_KID_01,{
	emotion 28;
	npctalk "Why Isn't this rolling- Make it roll-", "", bc_self;
	end;
}


lasagna,226,196,5	script	Aram#dorcon01	4_DR_F_02,{
	npctalk "Is this the famous Pub-Oven?", "Tos#dorcon01", bc_self;
	sleep2 2000;
	npctalk "Stop looking around like a hillbilly!", "", bc_self;
	sleep2 2000;
	npctalk "Wow! Look at this! Would this be the place where all three of them sat down?", "Porto#dorcon01", bc_self;
	sleep2 2000;
	npctalk "Geesh! You're just screaming that you're a hillbilly!", "", bc_self;
	sleep2 2000;
	npctalk "Since we're here, Let's make an oven resolution!", "Tos#dorcon01", bc_self;
	sleep2 2000;
	npctalk "That's why we went through the trouble to come here! Just like Peperoncino, Thomas, and Gamberry, we'll unite and become successful!", "Porto#dorcon01", bc_self;
	sleep2 2000;
	npctalk "Boss! Three glasses of kaluna milk shaken hard, please! And egg yolk on top!", "Tos#dorcon01", bc_self;
	sleep2 2000;
	npctalk "Oh, this is embarrassing! How many times do I have to tell you that everyone here has it stirred?!", "", bc_self;
	sleep2 2000;
	npctalk "Stirred or shaken, who cares as long as It's tasty?", "Porto#dorcon01", bc_self;
	sleep2 1000;
	npctalk "don't let people notice that you're from out of town!", "", bc_self;
	sleep2 2000;
	npctalk "Who cares if it shows?! Apparently, there Aren't too many Lasagna natives here.", "Tos#dorcon01", bc_self;
	sleep2 2000;
	npctalk "Yeah, Let's just enjoy ourselves since we're here. I'll try not to let it show too much.", "Porto#dorcon01", bc_self;
	sleep2 2000;
	npctalk "Yeah, yeah. I won't show it.", "Tos#dorcon01", bc_self;
	sleep2 2000;
	hideoffnpc "Employee#dorcon01";// only for player attached
	npctalk "Three glasses of kaluna milk. Here you go.", "Employee#dorcon01", bc_self;
	sleep2 2000;
	hideonnpc "Employee#dorcon01";// only for player attached
	npctalk "Finally!", "Porto#dorcon01", bc_self;
	sleep2 2000;
	npctalk "Let's all make a toast. Hold the glasses high!", "Tos#dorcon01", bc_self;
	sleep2 2000;
	npctalk "We weren't born on the same day, but Let's all die on the same day!", "", bc_self;
	sleep2 2000;
	npctalk "Why mention death?", "Tos#dorcon01", bc_self;
	sleep2 2000;
	npctalk "Then one for all! All for one!", "Porto#dorcon01", bc_self;
	sleep2 1000;
	npctalk "......", "", bc_self;
	sleep2 2000;
	npctalk "Cheesy...", "", bc_self;
	end;
}

lasagna,228,193,5	script	Porto#dorcon01	4_DR_M_01,{
	npctalk "Since I've come all the way here, I should try the kaluna milk!", "", bc_self;
	sleep2 2000;
	npctalk "Don't be so loud! Everyone is looking!", "Aram#dorcon01", bc_self;
	sleep2 2000;
	npctalk "You know, you're the one who's louder.", "", bc_self;
	sleep2 2000;
	npctalk "Right, right.", "Tos#dorcon01", bc_self;
	end;
}

lasagna,229,197,5	script	Tos#dorcon01	4_DR_M_02,{
	npctalk "Befitting to its fame, this place is packed with people.", "", bc_self;
	sleep2 2000;
	npctalk "Stop looking around like a hillbilly!", "Aram#dorcon01", bc_self;
	end;
}

lasagna,226,199,5	script	Employee#dorcon01	4_CAT_SAILOR3,{ end; }

lasagna,230,192,3	script	Perky Doram#do_hint03	4_CAT_ADV2,{
	npctalk "Always right-click when it comes to information! Right-click a human, item and cat to know more about them!", "", bc_self;
	end;
}

lasagna,235,193,3	script	Customer#dorcon02	4_DR_TORTEL,{
	npctalk "Always right-click when it comes to information! Right-click a human, item and cat to know more about them!", "", bc_self;
	sleep2 1000;
	emotion 3;
	npctalk "Ha- ppi- ness-", "", bc_self;
	sleep2 2000;
	emotion 45;
	npctalk "We're full, so there's only one thing to do. Sleep.", "", bc_self;
	end;
}

lasagna,220,190,5	script	Customer#dorcon01	4_CAT_ADV1,{
	npctalk "Have you ever seen the crazy seagull nearby the south bridge over there? It was hopping like crazy. What's going on?", "", bc_self;
	end;
}

lasagna,220,194,3	script	Serious Doram#do_hint08	4_M_MERCAT1,{
	npctalk "Humans say their weapons become stronger through refinement. What's refinement anyway?", "", bc_self;
	end;
}


lasagna,221,175,5	script	Smart Doram#do_hint06	4_CAT_SAILOR2,{
	npctalk "DEX (Dexterity) is about accuracy and casting speed.", "", bc_self;
	end;
}

lasagna,219,178,3	script	Kittie#do_hint02	4_CAT_SAILOR3,{
	npctalk "Double-click a consumable item to use.", "", bc_self;
	end;
}

// Intended ?
/*
lasagna,208,179,3	script	Kittie the Eraser#do_hi	4_M_MERCAT1,{
	npctalk "I'm deleting all the rows from 7711 through 7720.", "", bc_self;
	lasagna_quest[0] = 0;
	for ( .@quest_id = 7711; .@quest_id < 7721; .@quest_id++ ) {
		if (isbegin_quest(.@quest_id) > 0)
			erasequest .@quest_id;
	}
	end;
}
*/

lasagna,225,174,3	script	Doram On The Journey#do	4_CAT_3COLOR,{
	npctalk "You can't recover naturally if the items you carry exceed 50% of your max weight amount.", "", bc_self;
	end;
}

lasagna,233,174,5	script	Boozer#dorcon01	4_CAT_SAILOR5,{
	npctalk "Kaluna milk should be stirred. With many wanders flocking around here lately, there's been an increase in those who shake it. But That's how kids drink it, Isn't?", "", bc_self;
	end;
}

lasagna,236,173,3	script	Boozer#dorcon02	4_CAT_SAILOR5,{
	npctalk "Kaluna milk should be shaken. You have to shake it to taste the delicate flavor produced when adequately fermented kaluna is mixed with milk.", "", bc_self;
	end;
}

lasagna,236,179,5	script	Meow goon#dorcon01	4_DR_M_01,{
	mes "[Meow goon]";
	mes "If you run into a problem, ask them for help. You know, the twins in the vigilante corps. Aglio... Ali... Ol...";
	next;
	mes "[Meow goon]";
	mes "Right! They're Aglio and Oligo!";
	npctalk "Aglio and Oligo!", "", bc_self;
	next;
	emotion 4,0,"Goon meow#dorcon01";
	mes "[Goon meow]";
	mes "Hey, That's not it... No...";
	next;
	mes "[Meow goon]";
	mes "No?";
	next;
	mes "[Goon meow]";
	mes "They're Aglio and Olio. Not Aglio and Oligo. You made them sound like a sugar.";
	npctalk "They're Aglio and Olio.", "Goon meow#dorcon01", bc_self;
	next;
	mes "[Meow goon]";
	mes "Whichever.";
	close;
}

lasagna,235,178,5	script	Goon meow#dorcon01	4_DR_M_02,{
	mes "[Goon meow]";
	mes "It's dangerous to put yourself out into the world. Take these tri-colored anchovies.";
	npctalk "It's dangerous to put yourself out into the world.", "", bc_self;
	next;
	mes "[Meow goon]";
	mes "Everyone keep away! I'm dangerous. Krahh! How long can the tri-colored anchovies hold off?!";
	npctalk "Everyone keep away! I'm dangerous.", "Meow goon#dorcon01", bc_self;
	next;
	mes "[Goon meow]";
	mes "I'm... finished... You go ahead...";
	next;
	mes "[Meow goon]";
	mes "Kah... The poltergeist that had been sleeping inside my bag is raging!";
	next;
	emotion 21;
	emotion 21,0,"Meow goon#dorcon01";
	mes "[Goon meow]";
	mes "Good! This should be enough to keep me from getting snubbed in the new world.";
	npctalk "We're thoroughly prepared all right!", "Meow goon#dorcon01";
	close;
}

lasagna,232,187,6	script	Fettuccina#dorcon01	4_CAT_SAILOR2,{
	mes "[Fettuccina]";
	mes "I've worked here for a while, but I'm still the youngest so I do all the dishes.";
	next;
	mes "[Fettuccina]";
	mes "A glass before sailing out, a glass on land after a long voyage, a glass for everyone who came from out of town to make an oven resolution, a glass for every regular customers, a glass for everyone who drinks because others drink it..";
	next;
	mes "[Fettuccina]";
	mes "So basically, It's beyond your imagination how many glasses I have to wash each day. Not only that, I have to listen to vigilance corps and merchants' low-ranking dorams whine, and counsel rookie adventurers.";
	next;
	mes "[Fettuccina]";
	mes "Really, 24 hours are too short. We should hire one more person!";
	close;
}

lasagna,131,245,5	script	The diligent second son	4_CAT_3COLOR,{
	mes "[Yota Chara]";
	mes "Father, mother. Please come back home safely.";
	mes "(He's making something, mumbling.)";
	next;
	if (select( "Craft Amulet", "About the Family" ) == 2) {
		mes "[Yota Chara]";
		mes "...My parents keep going out into the dangerous areas. I'm still a child to help them, so what I do is to make these amulets for them.";
		next;
		emotion 19;
		mes "[Yota Chara]";
		mes "What if some bad news reach us even if I try hard making these amulets for their safety?";
		next;
		mes "[Yota Chara]";
		mes "I get startled each time some mail arrives...";
		next;
		emotion 28,0,"The skittish fourth#wea";
		mes "[Dandi Chara]";
		mes "Whatever happened to our parents? ~Bursting into crying~";
		next;
		emotion 21,0,"The brave third#weapon";
		mes "[Jogi Chara]";
		mes "Well, then. I'll be your dad!!";
		next;
		emotion 28,0,"The skittish fourth#wea";
		emotion 19;
		mes "[Dandi Chara]";
		mes "~Keeps crying uncontrollably~ No way!! Mom!! Dad!!";
		next;
		mes "[Goma Chara]";
		emotion 6,0,"The sensitive-minded ela";
		emotion 19;
		mes "Both will be totally fine and return to us soon. So quit it! Did you really have to make him cry, huh?!";
		next;
		emotion 9;
		mes "- Yota became disheartened and started making another amulet, quietly. -";
		close;
	}
	mes "[Yota Chara]";
	mes "...Do you have someone you're close to and whom you wish for their wellness?";
	mes "If you want an amulet, you can exchange it with the Doram tokens. I have a bunch of amulets with me.";
	next;
	.@s = select( "Low-grade Horse Mackerel Amulet", "Low-grade Leaf Amulet", "Low-grade Rabbit Amulet", "Shining Twig Charm", "Fresh Tuna Amulet", "Plump Earthworm Amulet" );
	mes "[Yota Chara]";
	switch(.@s) {
	case 1:
		mes "<ITEM>Low-grade Horse Mackerel Amulet <INFO>28413</INFO></ITEM> is for my father who is far out to the sea.";
		break;
	case 2:
		mes "<ITEM>Low-grade Leaf Amulet <INFO>28416</INFO></ITEM> is for my mother who should be wandering about the deep side of the forest.";
		break;
	case 3:
		mes "<ITEM>Low-grade Rabbit Amulet <INFO>28419</INFO></ITEM> is for my brothers' health and wellness.";
		break;
	case 4:
		mes "<ITEM>Shining Twig Charm <INFO>28422</INFO></ITEM> is to wish for my mother to return home safely from anywhere in the world.";
		break;
	case 5:
		mes "<ITEM>Fresh Tuna Amulet <INFO>28423</INFO></ITEM> is to wish for my father to return home quick with his ship full of tunas.";
		break;
	case 6:
		mes "<ITEM>Plump Earthworm Amulet <INFO>28424</INFO></ITEM> is to wish for easy fishing so that my family won't be starved.";
		break;
	}
	if (.@s < 4) {
		.@cost = 10;
		.@item_id = 28410 + .@s * 3;
	}
	else {
		.@cost = 30;
		.@item_id = 28418 + .@s;
	}
	mes "Will you exchange it for " + .@cost + " <ITEM>Doram Token<INFO>25142</INFO></ITEM>?";
	next;
	if (select( "Exchange", "Cancel" ) == 2) {
		mes "[Yota Chara]";
		mes "Did...did I misspeak, or what?";
		mes "I'm not an expert but I did my best.";
		close;
	}
	if (countitem(25142) < .@cost) {
		mes "[Yota Chara]";
		mes "I'm sorry. My sister will get pretty mad if I don't sell it for the proper money. Will you come back with 10 Doram Tokens?";
		close;
	}
	mes "[Yota Chara]";
	mes "Thanks you. I hope my amulet will be of good use to you...";
	delitem 25142, .@cost;
	getitem .@item_id, 1;
	close;
}

lasagna,131,250,5	script	The brave third#weapon	4_DR_M_02,{
	mes "[Jogi Chara]";
	mes "~Whirring~";
	mes "Here is Jogi, the bomber from Lasagna!";
	mes "Drop your weapons and surrender!";
	next;
	if (select( "Act as if you surrender.", "Ignore him." ) == 2) {
		mes "[Jogi Chara]";
		mes "~Makes a whiffing sound~";
		mes "~Thumping sound~";
		mes "Huh? Where do you think you're going?";
		close;
	}
	mes "[Jogi Chara]";
	mes "Wahaha! Another victory for me! What a rewarding day!";
	mes "What should we do next?";
	next;
	if (select( "Exchange tokens for a weapon.", "About the Family" ) == 2) {
		emotion 21;
		mes "[Jogi Chara]";
		mes "My sister told me my parents are warriors! They left for a huge adventure!!";
		next;
		emotion 21;
		mes "[Jogi Chara]";
		mes "I'll join the Militia and become great like my parents!";
		mes "I'm still too young to join the Militia, but, I'm positive that I can be the ace of the band once I join it because I'm committed to training myself.";
		next;
		emotion 32;
		mes "[Jogi Chara]";
		mes "My family members are very lazy. Unless I train them based on my strong belief, they can never become warriors.";
		mes "Most notoriously, my elder and younger brother can be a real pain in my neck!";
		next;
		emotion 18;
		mes "[Jogi Chara]";
		mes "Dandi! Today's training is to <put 10 earthworms on the head!> Get over here!";
		next;
		emotion 28,0,"The skittish fourth#wea";
		mes "[Dandi Chara]";
		mes "~Bursting into crying~ Goma Chara!!";
		close;
	}
	emotion 52;
	mes "[Jogi Chara]";
	mes "Whoa, a new weapon! New power!";
	mes "What are you going to give me? Do you have a lot of weapons? Can I search into them? Will you exchange it for <ITEM>Doram Token <INFO>25142</INFO></ITEM>?";
	next;
	if (select( "Search", "Cancel" ) == 1) {
		setarray .@item_data[0],
			1690, 1,	// Marvelous Foxtail Staff [3]
			1691, 2,	// Wondrous Foxtail Staff [2]
			1692, 3,	// Magic Foxtail Staff [2]
			1693, 4,	// Magic Yellow Foxtail Staff [1]
			1694, 1,	// Foxtail Replica [3]
			1695, 2,	// Fine Foxtail Replica [2]
			1696, 3,	// Elaborate Foxtail Replica [2]
			1697, 4;	// Elaborate Yellow Foxtail Replica [1]
		.@total_item = getarraysize(.@item_data);

		while(true) {
			for ( .@i = 0; .@i < .@total_item; .@i += 2 ) {
				if ((countitem(.@item_data[.@i]) - isequippedcnt(.@item_data[.@i])) > 0)
					break;
			}
			if (.@i == .@total_item) {
				mes "[Jogi Chara]";
				mes "That's it?";
				next;
				if (select( "Search again.", "Stop." ) == 2)
					break;
			}
			else {
				mes "[Jogi Chara]";
				mes "Whoa! This is ^3131FF" + getitemname(.@item_data[.@i]) + "^000000!";
				mes "Exchange it for " + .@item_data[.@i+1] + " <ITEM>Doram Token<INFO>25142</INFO></ITEM>! Please? Will you do it, please?";
				next;
				if (select( "Exchange", "Quit exchanging" ) == 2) {
					mes "[Jogi Chara]";
					mes "~Giggles~ We should see each other more often!";
					close;
				}
				mes "[Jogi Chara]";
				mes "Yay!! Thanks a mil!";
				mes "Can I look for something else?";
				delitem .@item_data[.@i],1;
				getitem 25142,.@item_data[.@i+1];// Doram token
				next;
			}
		}
	}
	mes "[Jogi Chara]";
	mes "~Scoffs~ Alright, then.";
	close;
}

lasagna,140,250,3	script	The skittish fourth#wea	4_DR_F_01,{
	emotion 23;
	mes "[Dandi Chara]";
	mes "~Shrieks~ Hi...Hiya...";
	mes "You really startled me...I thought it was Jogi.";
	next;
	if (select( "Exchange tokens for armor.", "About the Family" ) == 2) {
		mes "[Dandi Chara]";
		mes "She gets angry easily but makes new clothes for me, saying I deserve pretty ones.";
		mes "I like her...";
		next;
		emotion 28;
		mes "[Dandi Chara]";
		mes "My brothers can be pretty mean. So I try my best to obey what Dandi tells me to.";
		mes "Still, Yota would never show his smiles, while Jogi would bring me hideous stuff like an earthworm, saying I should be tougher. ~Makes a sobbing sound~";
		next;
		emotion 28;
		mes "[Dandi Chara]";
		mes "What's more, Jogi keeps wandering near the forest, saying He's going for the Militia. Mom said it's dangerous...";
		next;
		emotion 28;
		mes "[Dandi Chara]";
		mes "What if a monster takes my brother away? That monster's gotta hurt a lot...";
		next;
		emotion 0;
		mes "[Dandi Chara]";
		mes "Do you happen to have any piece of armor to spare for my brother? Would you give it to me? I'll pay for it...";
		close;
	}
	mes "[Dandi Chara]";
	mes "Do you mind if I search you to see the items you have with you?";
	mes "If I find an item suitable for Jogi, I'll pay <INFO>25142</INFO><ITEM>Doram Token </ITEM>....";
	next;
	if (select( "Search", "Cancel" ) == 1) {	// todo complete
		setarray .@item_data[0],
			28380, 2,	// Fresh Grass Necklace [1]
			28381, 2,	// Cute Grass Necklace [1]
			28382, 2,	// Charming Grass Necklace [1]
			15156, 4,	// Elegant Doram Suit [1]
			20790, 4,	// Elegant Doram Manteau [1]
			22085, 4;	// Elegant Doram Shoes [1]
		.@total_item = getarraysize(.@item_data);

		while(true) {
			for ( .@i = 0; .@i < .@total_item; .@i += 2 ) {
				if ((countitem(.@item_data[.@i]) - isequippedcnt(.@item_data[.@i])) > 0)
					break;
			}
			if (.@i == .@total_item) {
				mes "[Dandi Chara]";
				mes "...I think That's just it. Should I look over it again, or stop right here?";
				next;
				if (select( "Search again.", "Stop." ) == 2)
					break;
			}
			else {
				mes "[Dandi Chara]";
				mes "Whoa, this must be ^3131FF" + getitemname(.@item_data[.@i]) + "^000000!!";
				mes "Don't you want to exchange this with " + .@item_data[.@i+1] + " <ITEM>Doram Token<INFO>25142</INFO></ITEM>?";
				next;
				if (select( "Exchange", "Quit exchanging" ) == 2) {
					mes "[Dandi Chara]";
					mes "I know. I searched into your inventory too much. Sorry...";
					close;
				}
				mes "[Dandi Chara]";
				mes "Thank you.";
				mes "Can I look for something else?";
				delitem .@item_data[.@i],1;
				getitem 25142,.@item_data[.@i+1];// Doram token
				next;
			}
		}
	}
	mes "[Dandi Chara]";
	mes "Alright...Thank you for your...help, I guess...";
	close;
}

lasagna,140,245,3	script	The sensitive-minded el	4_CAT_SAILOR3,{
	mes "[Goma Chara]";
	mes "I'm Goma Chara, the eldest daughter of the Charas. These brats are who quicken my ageing process...";
	mes "If you happen to have the amulet that my brother Yota made, I can polish it for you.";
	next;
	if (select( "Upgrade Amulet", "About the Family" ) == 2) {
		mes "[Goma Chara]";
		mes "My father was a born seaman so went out into the sea a long time ago, saying he'll catch the world's largest tuna.";
		next;
		mes "[Goma Chara]";
		mes "My mother can be a collector so she would go into the deepmost side of the forest, looking for the odd fruits. She would come and go even before I know it.";
		next;
		mes "[Goma Chara]";
		mes "So taking care of these bratty brothers has become my responsibility! I wonder why I was born the eldest! I've almost had it!";
		next;
		emotion 6;
		mes "[Goma Chara]";
		mes "Yota! Tidy up your place after making amulets! Pick up the scales of horse mackerels there. Oh, my days...";
		next;
		emotion 6;
		mes "[Goma Chara]";
		mes "Jogi, Dandi is about to burst into crying. Please behave yourself, will you?";
		next;
		emotion 7;
		mes "[Goma Chara]";
		mes "~Signs deeply~ My head is spinning!";
		mes "I'm sorry but our chit-chat ends here. Time flies when you're busy taking care of little brothers.";
		close;
	}
	mes "[Goma Chara]";
	mes "Yota often makes quality amulets, but he still needs more polishing.";
	mes "If he pays attention to details, his amulets will be even greater. But he just doesn't care.";
	next;
	.@s = (select( "Polish the Horse Mackerel Amulet.", "Polish the Leaf Amulet.", "Polish the Rabbit Amulet." ) - 1) * 4;

	setarray .@item_data[0],
		28413, 30,	// Basic Saurel Charm [1]
		28414, 90,	// Fair Saurel Charm [1]
		28416, 30,	// Basic Leaf Charm [1]
		28417, 90,	// Fair Leaf Charm [1]
		28419, 30,	// Basic Bunny Charm [1]
		28420, 90;	// Fair Bunny Charm [1]

	if ((countitem(.@item_data[.@s]) - isequippedcnt(.@item_data[.@s]))  > 0)
		.@index = .@s;
	else if ((countitem(.@item_data[.@s+2]) - isequippedcnt(.@item_data[.@s+2])) > 0)
		.@index = .@s + 2;
	else {
		mes "[Goma Chara]";
		mes "Show me your amulet, will you? I can't seem to find it in your inventory.";
		close;
	}
	mes "[Goma Chara]";
	mes "Let me. Hmm... " + getitemname(.@item_data[.@index]) + ", it is.";
	mes "...I knew this was going to happen again. Worse, this one must require a lot more time to polish!";
	next;
	mes "[Goma Chara]";
	mes "Hey, if you give me " + .@item_data[.@index+1] + "<ITEM>Doram Token<INFO>25142</INFO></ITEM>, I'll fix this for you.";
	mes "<ITEM>This should produce the <INFO>" + (.@item_data[.@index]+1) + "</INFO></ITEM> function, I guess. How do you like it?";
	next;
	if (select( "Upgrade", "Cancel" ) == 2) {
		mes "[Goma Chara]";
		mes "As you please.";
		close;
	}
	mes "[Goma Chara]";
	mes "The inside of this amulet seems pretty tough.";
	mes "But my dexterity is tougher. Tada! Put my brother's amulet to good use.";
	delitem 25142, .@item_data[.@index+1];// Doram token
	delitem .@item_data[.@index],1;
	getitem (.@item_data[.@index] + 1),1;
	close;
}
