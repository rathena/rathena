//===== rAthena Script =======================================
//= Quests EXP
//===== Description: =========================================
//= [Walkthrough Conversion]
//--- NPC quests related to new dungeons.
//===== Changelog: ===========================================
//= 1.0 Initial release of Glast Heim Abyss quests. [OptimusM]
//= 1.1 Corrections here and there. [Capuche]
//= 1.2 Initial release of Abyss Dungeon 4 quests. [Capuche]
//============================================================

glast_01,191,273,3	script	Magician Nillem#glabyss01	4_M_JOB_WIZARD,{
	if (BaseLevel < 175) {
		mes "[Nillem]";
		mes "What are you doing here?";
		mes "That's right, I'm waiting for a strong adventurer to satisfy my curiosity!";
		next;
		mes "[Nillem]";
		mes "If you reach level 175, you will be my destined opponent-ยกร";
		close;
	}
	mes "[Nillem]";
	mes "I think I've made a great discovery, so why don't you come with me?";
	next;
	switch( select( "Let's go!", "Please explain it.", "What I found", "Refuse." )) {
	case 1:
		mes "[Nillem]";
		mes "This is my bae!";
		mes "You're gonna jump into the abyss with me! I can't help But L-O-V-E you.";
		close2;
		warp "gl_cas01_",200,26;
		end;
	case 2:
		mes "[Nillem]";
		mes "As you know, I'm a good court wizard, right?";
		mes "A genius wizard who can do this and that on dimension!";
		next;
		mes "[Nillem]";
		mes "There was a report of strange magical power near central castle of Glast Heim.";
		mes "I came here to investigate and found it.";
		next;
		mes "[Nillem]";
		mes "A week crack of dimension!";
		mes "You know, this genius wizard is good at connecting the crack of dimensions.";
		next;
		mes "[Nillem]";
		mes "So I forced the connection and went in....";
		mes "A piece of time that has stopped in the flow.... Hmmm... It's so hard to explain..";
		next;
		mes "[Nillem]";
		mes "Anyway, the flood of creatures trapped for a long time came to me like waves!";
		next;
		mes "[Nillem]";
		mes "I almost D - i - e - d there.";
		mes "So I'm looking for adventurers to join me on this expedition.";
		close;
	case 3:
		mes "[Nillem]";
		mes "Right, there's no mission here, but curiosity follows discovery!";
		mes "You noticed before I told you there were a few things I wanted to do with you, My - Darling -.";
		next;
		switch( select( "An Empty Thing", "A Thing with High Freezing Point", "A Thing Like A Mirror", "Well, I'm okay." )) {
		case 1:
			mes "[Nillem]";
			mes "Beyond the cracks of this dimension, there are Empty Things..";
			mes "^0000FFContaminated Raydric^000000 and ^0000FFContaminated Raydric Archer^000000...";
			mes "Contamination";
			next;
			mes "[Nillem]";
			mes "^0000FF20 Contaminated Raydrics^000000,";
			mes "^0000FF20 Contaminated Raydric Archers^000000!! Defeat them and come back. Then this genius wizard will study it.";
			mes "What do you think, bae?";
			next;
			if (select( "That's not hard.", "I'm busy." ) == 2) {
				mes "[Nillem]";
				mes "If you're busy, you can't help me.";
				mes "But honey, please help me when You - Have - Spare - Time - !";
				close;
			}
			switch( checkquest(8542,PLAYTIME) ) {
			case -1:
				break;
			case 0:
			case 1:
				mes "[Nillem]";
				mes "I asked you too much today, Sweetie.";
				mes "Instead, you're sharing another new discovery tomorrow. O - K - A - Y -?";
				close;
			case 2:
				mes "^4d4dff - You can take this mission again. Speak to Mage Nillem once again to accept it. - ^000000";
				erasequest 8542;
				break;
			}
			switch( checkquest(8541,HUNTING) ) {
			case -1:
				mes "[Nillem]";
				mes "That's my baby!";
				mes "It's hard to see the impact there because of the strange magic.";
				mes "If you finish the quest, let's get out of there first And - Then Check - !";
				setquest 8541;
				close;
			case 0:
			case 1:
				mes "[Nillem]";
				mes "^0000FF20 Contaminated Raydrics^000000,";
				mes "^0000FF20 Contaminated Raydric Archers^000000.";
				mes "Woo-hoo, aren't you excited about the effects of empty things?";
				close;
			case 2:
				mes "[Nillem]";
				mes "Ha~ It's so obvious out here. Could you please show me your... weapon and... armor? Hmm...";
				mes "Hmmm!!";
				mes "Haha, what a discovery of the century!";
				next;
				select( "What do you get?" );
				mes "[Nillem]";
				mes "It's hard to explain to you, but in short, You - Did - it - !!";
				mes "But this isn't enough, so please do it again tomorrow.";
				erasequest 8541;
				setquest 8542;
				getexp 8142552,5674080;
				close;
			}
			end;
		case 2:
			mes "[Nillem]";
			mes "There are things that have a high freezing point beyond the crack of this dimension.";
			mes "^0000FFFrozen Gargoyle^000000 and ^0000FFPrison Breaker^000000...";
			next;
			mes "[Nillem]";
			mes "^0000FF5 Frozen Gargoyles^000000,";
			mes "^0000FF5 Prison Breakers^000000!! Defeat them and come back. Then this genius wizard will study it.";
			mes "What do you think, bae?";
			next;
			if (select( "That's not hard.", "I'm busy." ) == 2) {
				mes "[Nillem]";
				mes "If you're busy, you can't help me.";
				mes "But honey, please help me when You - Have - Spare - Time - !";
				close;
			}
			switch( checkquest(8545,PLAYTIME) ) {
			case -1:
				break;
			case 0:
			case 1:
				mes "[Nillem]";
				mes "I asked you too much today, Sweetie.";
				mes "Instead, you're sharing another new discovery tomorrow. O - K - A - Y -?";
				close;
			case 2:
				mes "^4d4dff - You can take this mission again. Speak to Mage Nillem once again to accept it. - ^000000";
				erasequest 8545;
				break;
			}
			switch( checkquest(8544,HUNTING) ) {
			case -1:
				mes "[Nillem]";
				mes "That's my baby!";
				mes "It's hard to see the impact there because of the strange magic.";
				mes "If you finish the quest, let's get out of there first And - Then Check - !";
				setquest 8544;
				close;
			case 0:
			case 1:
				mes "[Nillem]";
				mes "^0000FF5 Frozen Gargoyles^000000,";
				mes "^0000FF5 Prison Breakers^000000!!";
				mes "Woo-hoo, aren't you excited about Things with High Freezing Point?";
				close;
			case 2:
				mes "[Nillem]";
				mes "Ha~ It's so obvious out here. Could you please show me your... weapon and... armor? Hmm...";
				mes "Hmmm!!";
				mes "Haha, what a discovery of the century!";
				next;
				select( "What do you get?" );
				mes "[Nillem]";
				mes "It's hard to explain to you, but in short, You - Did - it - !!";
				mes "But this isn't enough, so please do it again tomorrow.";
				erasequest 8544;
				setquest 8545;
				getexp 2036754,1425726;
				close;
			}
			end;
		case 3:
			mes "[Nillem]";
			mes "Beyond the crack of this dimension, there are mirrors of each other.";
			mes "^0000FFIce Ghost^000000 and ^0000FFFlame Ghost^000000...";
			mes "It looks completely different, but the crack of dimensions is very unknown.";
			next;
			mes "[Nillem]";
			mes "^0000FF10 Ice Ghosts^000000,";
			mes "^0000FF10 Flame Ghosts^000000!! Defeat them and come back. Then this genius wizard will study it.";
			mes "What do you think, bae?";
			next;
			if (select( "That's not hard.", "I'm busy." ) == 2) {
				mes "[Nillem]";
				mes "If you're busy, you can't help me.";
				mes "But honey, please help me when You - Have - Spare - Time - !";
				close;
			}
			switch( checkquest(8548,PLAYTIME) ) {
			case -1:
				break;
			case 0:
			case 1:
				mes "[Nillem]";
				mes "I asked you too much today, Sweetie.";
				mes "Instead, you're sharing another new discovery tomorrow. O - K - A - Y -?";
				close;
			case 2:
				mes "^4d4dff - You can take this mission again. Speak to Mage Nillem once again to accept it. - ^000000";
				erasequest 8548;
				break;
			}
			switch( checkquest(8547,HUNTING) ) {
			case -1:
				mes "[Nillem]";
				mes "That's my baby!";
				mes "It's hard to see the impact there because of the strange magic.";
				mes "If you finish the quest, let's get out of there first And - Then Check - !";
				setquest 8547;
				close;
			case 0:
			case 1:
				mes "[Nillem]";
				mes "^0000FF10 Ice Ghosts^000000,";
				mes "^0000FF10 Flame Ghosts^000000!!";
				mes "Woo-hoo, aren't you excited about Things Like A Mirror?";
				close;
			case 2:
				mes "[Nillem]";
				mes "Ha~ It's so obvious out here. Could you please show me your... weapon and... armor? Hmm...";
				mes "Hmmm!!";
				mes "Haha, what a discovery of the century!";
				next;
				select( "What do you get?" );
				mes "[Nillem]";
				mes "It's hard to explain to you, but in short, You - Did - it - !!";
				mes "But this isn't enough, so please do it again tomorrow.";
				erasequest 8547;
				setquest 8548;
				getexp 4138800,2897160;
				close;
			}
			end;
		case 4:
			mes "[Nillem]";
			mes "There, there. Sweet heart, you are right.";
			mes "If you're tired, take a rest.";
			mes "Do - Not - Push - yourself --- !";
			close;
		}
		end;
	case 4:
		mes "[Nillem]";
		mes "Oh, my sweetheart, you're so mean today.";
		mes "Nillem is saaaad. *Tears*";
		close;
	}
	end;

OnInit:
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8541,HUNTING) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8544,HUNTING) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8547,HUNTING) == 2" );

	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8542,PLAYTIME) == -1 && checkquest(8541,HUNTING) == -1" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8545,PLAYTIME) == -1 && checkquest(8544,HUNTING) == -1" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8548,PLAYTIME) == -1 && checkquest(8547,HUNTING) == -1" );

	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8542,PLAYTIME) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8545,PLAYTIME) == 2" );
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "checkquest(8548,PLAYTIME) == 2" );
	end;
}

gl_cas01_,205,36,5	script	Magician Nillem#glabyss02	4_M_JOB_WIZARD,{
	mes "[Nillem]";
	mes "Woo-hoo! I'm so excited whenever I come here!";
	mes "I love this bleakness!";
	next;
	if (select( "Let's get out of here!", "I'd like to look around." ) == 2) {
		mes "[Nillem]";
		mes "See you!";
		close;
	}
	mes "[Nillem]";
	mes "Sure!";
	mes "Let's - Go - Out - Side - !";
	close2;
	warp "glast_01",191,270;
	end;
}


// abyss_04
abyss_03,108,97,3	script	Traveler's Monument#abyss	1_JOURNEY_STONE_F,{
	mes "[Traveler's Monument]";
	mes "The Traveler's Monument stands still.";
	close;
}

hugel,102,148,3	script	Ethnographer Marigold#exdq0	4_F_HUWOMAN,{
	if (checkquest(7893,HUNTING) == 2) {
		mes "[Marigold]";
		mes "Purple Ferus... Hmm, that's right. We still need more investigation. Should we do it this time?";
		erasequest 7893;
		setquest 7894;
		getexp 15823920,11076720;
		next;
	}
	if (checkquest(7895,HUNTING) == 2) {
		mes "[Marigold]";
		mes "Treasure Mimics carry those things. Oh, you can take all their loot. All I need to know is the type.";
		erasequest 7895;
		setquest 7896;
		getexp 7979360,5585560;
		next;
	}
	if (checkquest(7897,HUNTING) == 2) {
		mes "[Marigold]";
		mes "How did you feel against the Black Acidus and the Silver Acidus? Interesting!";
		erasequest 7897;
		setquest 7898;
		getexp 32090640,22463360;
		next;
	}
	if (checkquest(7899,HUNTING) == 2) {
		mes "[Marigold]";
		mes "Thank you for dealing with Bone Ferus and Bone Acidus. I haven't been able to explain why this is happening.";
		erasequest 7899;
		setquest 7900;
		getexp 48548160,33983640;
		next;
	}
	mes "[Marigold]";
	mes "Why do dragons stack up so much gold?";
	next;
	mes "[Marigold]";
	mes "I want to investigate the new dragons found deep in the Abyss Cave, can you help me?";
	next;
	switch( select( "A unique color of Ferus", "New Mimic Investigation", "Acidus Attribute Research", "Going against Providence", "What are you studying?" ) ) {
	case 1:
		switch( checkquest(7894,PLAYTIME) ) {
		case -1:
			break;
		case 0:
		case 1:
			mes "[Marigold]";
			mes "We are still organizing the data you collected the other day.";
			mes "There is still time until the next investigation, so how about taking a break?";
			close;
		case 2:
			erasequest 7894;
			break;
		}
		switch( checkquest(7893,HUNTING) ) {
		case -1:
			mes "[Marigold]";
			mes "An unusual Purple Ferus was found on the 4th floor of the Abyss Cave.";
			mes "Ferus color usually follows it's natural attributes, isn't it amazing that it's purple?";
			mes "So I want to know more about this Purple Ferus.";
			next;
			if (select( "Accept the request", "Reject" ) == 2) {
				mes "[Marigold]";
				mes "The world is large and full of people.";
				mes "It sounds like there are many adventurers with great skills.";
				close;
			}
			mes "[Marigold]";
			mes "The thing to investigate this time is the ^4d4dffPurple Ferus^000000 found on the ^4d4dff4th floor of the Abyss Cave^000000!";
			mes "Show me the loot you've earned while dealing with it in moderation.";
			setquest 7893;
			close;
		case 0:
		case 1:
			mes "[Marigold]";
			mes "Purple Ferus is said to appear on the 4th floor of the Abyss Cave.";
			close;
		case 2:
			end;
		}
		end;
	case 2:
		switch( checkquest(7896,PLAYTIME) ) {
		case -1:
			break;
		case 0:
		case 1:
			mes "[Marigold]";
			mes "We are still organizing the data you collected the other day.";
			mes "There is still time until the next investigation, so how about taking a break?";
			close;
		case 2:
			erasequest 7896;
			break;
		}
		switch( checkquest(7895,HUNTING) ) {
		case -1:
			mes "[Marigold]";
			mes "A new Mimic has been discovered on the 4th floor of the Abyss Cave.";
			mes "I wonder what kind of loot this guy called 'Treasure Mimic' will give you.";
			next;
			if (select( "Accept the request", "Reject" ) == 2) {
				mes "[Marigold]";
				mes "The world is large and full of people.";
				mes "It sounds like there are many adventurers with great skills.";
				close;
			}
			mes "[Marigold]";
			mes "The thing to investigate this time is the ^4d4dffTreasure Mimic^000000 found on the ^4d4dff4th floor of the Abyss Cave^000000!";
			mes "Show me the loot you've earned while dealing with it in moderation.";
			setquest 7895;
			close;
		case 0:
		case 1:
			mes "[Marigold]";
			mes "The Treasure Mimic is said to be found on the 4th floor of the Abyss Cave.";
			close;
		case 2:
			end;
		}
		end;
	case 3:
		switch( checkquest(7898,PLAYTIME) ) {
		case -1:
			break;
		case 0:
		case 1:
			mes "[Marigold]";
			mes "We are still organizing the data you collected the other day.";
			mes "There is still time until the next investigation, so how about taking a break?";
			close;
		case 2:
			erasequest 7898;
			break;
		}
		switch( checkquest(7897,HUNTING) ) {
		case -1:
			mes "[Marigold]";
			mes "The color of an Acidus gives a rough estimate of it's natural properties.";
			mes "But I don't know about Black Acidus and Silver Acidus. I'm looking for someone to help me collect data while dealing with them.";
			next;
			if (select( "Accept the request", "Reject" ) == 2) {
				mes "[Marigold]";
				mes "The world is large and full of people.";
				mes "It sounds like there are many adventurers with great skills.";
				close;
			}
			mes "[Marigold]";
			mes "This time, we are going to investigate ^4d4dffSilver Acidus and Black Acidus^000000 found on the ^4d4dff4th floor of the Abyss Cave^000000!";
			mes "Tell me what attributes they have when you deal with them directly.";
			setquest 7897;
			close;
		case 0:
		case 1:
			mes "[Marigold]";
			mes "Black Acidus and Silver Acidus live on the 4th floor of the Abyss Cave.";
			close;
		case 2:
			end;
		}
		end;
	case 4:
		switch( checkquest(7900,PLAYTIME) ) {
		case -1:
			break;
		case 0:
		case 1:
			mes "[Marigold]";
			mes "We are still organizing the data you collected the other day.";
			mes "There is still time until the next investigation, so how about taking a break?";
			close;
		case 2:
			erasequest 7900;
			break;
		}
		switch( checkquest(7899,HUNTING) ) {
		case -1:
			mes "[Marigold]";
			mes "What kind of force acts on the 4th floor of the Abyss Cave?";
			mes "Ferus and Acidus, who are already dead and have only bones, move as if they were alive.";
			mes "It's against providence. I don't think it should be left alone.";
			next;
			if (select( "Accept the request", "Reject" ) == 2) {
				mes "[Marigold]";
				mes "The world is large and full of people.";
				mes "It sounds like there are many adventurers with great skills.";
				close;
			}
			mes "[Marigold]";
			mes "Please defeat the ^4d4dffBone Ferus and Bone Acidus^000000 who became monsters ^4d4dffin the 4th floor of the Abyss Cave^000000!";
			mes "I'll collect data to find out why this happens.";
			setquest 7899;
			close;
		case 0:
		case 1:
			mes "[Marigold]";
			mes "Bone Ferus and Bone Acidus are said to be found on the 4th floor of the Abyss Cave.";
			close;
		case 2:
			end;
		}
		end;
	case 5:
		mes "[Marigold]";
		mes "I'm researching nearby phenomena, monsters, and religious relics and objects.";
		mes "There is one other person besides me, it's Cinnamon.";
		next;
		mes "[Marigold]";
		mes "He was researching the ancient temple of Odin and went to the island where the temple of Odin is located.";
		mes "Even though it is possible to recruit skilled adventurers even here in the square.";
		next;
		mes "[Marigold]";
		mes "I've heard interesting news about the Dragon's Cave in Abyss Lake to the west and am investigating.";
		next;
		mes "[Marigold]";
		mes "The level we knew as the lowest level, where gold coins were piled up, was not really the lowest floor.";
		mes "What was the space under the high pile of gold coins?";
		next;
		mes "[Marigold]";
		mes "The gold coins pierced the ceiling and went upstairs.";
		mes "So now we are collecting data on the ecology down there.";
		mes "It's a dangerous place, so it's taking time.";
		close;
	}
	end;

OnInit:
	// completed
	questinfo( QTYPE_QUEST2, QMARK_YELLOW, "checkquest(7893,HUNTING) == 2" );
	questinfo( QTYPE_QUEST2, QMARK_YELLOW, "checkquest(7895,HUNTING) == 2" );
	questinfo( QTYPE_QUEST2, QMARK_YELLOW, "checkquest(7897,HUNTING) == 2" );
	questinfo( QTYPE_QUEST2, QMARK_YELLOW, "checkquest(7899,HUNTING) == 2" );

	// timeout
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(7894,PLAYTIME) == 2" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(7896,PLAYTIME) == 2" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(7898,PLAYTIME) == 2" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(7900,PLAYTIME) == 2" );

	// not started
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "isbegin_quest(7893) == 0 && checkquest(7894,PLAYTIME) == -1" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "isbegin_quest(7895) == 0 && checkquest(7896,PLAYTIME) == -1" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "isbegin_quest(7897) == 0 && checkquest(7898,PLAYTIME) == -1" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "isbegin_quest(7899) == 0 && checkquest(7900,PLAYTIME) == -1" );
	end;
}
