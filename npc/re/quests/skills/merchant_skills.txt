//===== rAthena Script =======================================
//= Merchant Skills Quests
//===== Description: =========================================
//= [Official Conversion]
//- NPC for skills Pushcart Decoration.
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

alberta,47,37,5	script	Cart Remodel Expert	4_M_REPAIR,{
	.@max_skill_lvl = 1;
	if (cart_remodel == 0) {
		mes "[Jason]";
		mes "The original pushcart is beautiful!";
		next;
		mes "[Jason]";
		mes "The solid grey color pushcart.";
		next;
		mes "[Jason]";
		mes "I can remodel it into new cute designs.";
		next;
		switch( select( "What is Pushcart Decoration?", "Learn Pushcart Decoration", "End Dialog" ) ) {
		case 1:
			mes "[Jason]";
			mes "I would like to completely remodel the poor pushcart.";
			next;
			mes "[Jason]";
			mes "A little remodel isn't enough, don't you agree?";
			next;
			mes "[Jason]";
			mes "Therefore I have invented some new designs.";
			next;
			mes "[Jason]";
			mes "It is cute and refreshing!";
			close;
		case 2:
			if (BaseClass != Job_Merchant) {
				mes "[Jason]";
				mes "Why do you want learn Pushcart Decoration? Don't disturb me!";
				close;
			}
			if (countitem(6606) == 0) {
				mes "[Jason]";
				mes "It is difficult to learn the Pushcart Decoration.";
				next;
				mes "[Jason]";
				mes "I will teach you if you bring the ^3131FFCute Cart Remodel Coupon^000000 to me.";
				close;
			}
			mes "[Jason]";
			mes "You have brought the Cute Cart Remodel Coupon. Are you sure you want to learn Pushcart Decoration?";
			next;
			if (select("YES", "NO") == 2) {
				mes "[Jason]";
				mes "Come again after you have changed your mind!";
				close;
			}
			mes "[Jason]";
			mes "Alright! Follow me.";
			next;
			mes "[Jason]";
			mes "This design";
			mes "needs to catch the pushcart...";
			next;
			mes "[Jason]";
			mes "This design";
			mes "needs to catch the pushcart.";
			mes "Then add this....";
			mes "...";
			next;
			mes "[Jason]";
			mes "You are a genius! You have learned it!";
			next;
			mes "[Jason]";
			mes "Now is your turn to do it.";
			mes "Press Alt+S to change your pushcart!";
			next;
			mes "[Jason]";
			mes "This is the new cute design pushcart. You are one of our family now...!";
			delitem 6606,1;	// P_Cart_C
			cart_remodel = 1;
			skill "MC_CARTDECORATE",1,SKILL_PERM;
			close;
		case 3:
			close;
		}
	}
	mes "[Jason]";
	mes "Have you decorated the pushcart?";
	next;
	switch( select( "Is there more to learn?", "I forgot how to decorate my pushcart" ) ) {
	case 1:
		if (cart_remodel == .@max_skill_lvl) {
			mes "[Jason]";
			mes "No. I am still exploring new designs. I will teach you once I have learned it.";
			close;
		}
		mes "[Jason]";
		mes "I have something to teach, do you want to learn?";
		next;
		mes "[Jason]";
		mes "I need a ^3131FFCute Cart Remodel Coupon^000000.";
		next;
		if (select("I want to learn", "No") == 2) {
			mes "[Jason]";
			mes "You can come back anytime.";
			close;
		}
		if (countitem(6606) < 0) {
			mes "[Jason]";
			mes "You don't have any Cute Cart Remodel Coupon?";
			close;
		}
		mes "[Jason]";
		mes "Ok, let's begin!";
		next;
		mes "[Jason]";
		mes "Doing it like this is better.";
		mes "Do that like this as well...";
		next;
		mes "[Jason]";
		mes "....";
		mes "Let's do it!";
		next;
		mes "[Jason]";
		mes "I have taught you the skill, you can try it now.";
		delitem 6606,1;	// P_Cart_C
		cart_remodel++;
		skill "MC_CARTDECORATE", cart_remodel ,SKILL_PERM;
		close;
	case 2:
		mes "[Jason]";
		mes "Oh? Let me see.";
		next;
		if (BaseClass != Job_Merchant) {
			mes "[Jason]";
			mes "Please come again after you can use the pushcart!";
			close;
		}
		mes "[Jason]";
		mes "Do you not remember what I have taught you?";
		mes "I have taught you this method.";
		next;
		mes "[Jason]";
		mes "You have forgotten?";
		mes "Let's recall now!";
		next;
		mes "[Jason]";
		mes "Think carefully, you will remember it again.";
		skill "MC_CARTDECORATE",1,SKILL_PERM;
		close;
	}
}
