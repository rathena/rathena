//===== rAthena Script =======================================
//= Clock Tower Unknown Basement
//===== Description: =========================================
//= [Walkthrough Conversion]
//--- NPCs quests for Clock Tower Unknown Basement
//===== Changelog: ===========================================
//= 1.0 First version. [Atemo]
//============================================================

c_tower1,243,202,4	script	Question Box#D240	4_NONMYSTCASE,{
	if (BaseLevel < 240) {
		mes "I don't think this is a box like this, but... it's a gorgeously wrapped box. Could it be a new Mimic?";
		next;
		mes "I want to touch it, but I don't think I'm strong enough to take responsibility for my actions yet. Let's promise later.";
		close;
	}
	if (isbegin_quest(5971) == 0)
		mes "I don't think this is a box like this, but... it's a gorgeously wrapped box. Could it be a new Mimic?";
	else
		mes "This is the box where the demon sleeps. Shall we wake him up?";
	next;
	if (select( "Touch it", "Leave it" ) == 2)
		end;
	emotion ET_GOODBOY;
	setpcblock PCBLOCK_NPC, true;
	sleep2 500;
	specialeffect EF_PURPLEBODY;
	sleep2 500;
	setpcblock PCBLOCK_NPC, false;
	cloakonnpcself();
	cloakoffnpcself( "Demon in the Box#D240" );
	npctalk "Demon in the Box: Who awakens this body?", "Demon in the Box#D240", bc_self;
	end;
}

c_tower1,243,202,4	script	Demon in the Box#D240	VAVAYAGA,{
	if (BaseLevel < 240)
		end;
	if (checkweight(1201,3) == 0) {
		mes "- You cannot proceed because you have too many items. -";
		close;
	}
	if (isbegin_quest(5971) == 0) {
		mes "The guy who jumped out of the box glanced at me and started talking to himself as if it were natural.";
		next;
		mes "[Demon in the box]";
		mes "Huh?! What is this? Where is this? This is...";
		next;
		mes "[Demon in the box]";
		mes "Hmm.. This is.. Clock Tower? Is it? What matters is not where I was!";
		next;
		mes "[Demon in the box]";
		mes "Hu~um~";
		next;
		mes "[Demon in the box]";
		mes "Oh! A musty smell.. I can smell the place where I was from that spring. I have to go back.";
		next;
		emotion ET_THINK;
		mes "[Demon in the box]";
		mes ".. .. ..";
		next;
		emotion ET_HUK;
		mes "[Demon in the box]";
		mes "Ughhhh?! What? Why? Why can't I go in?";
		while(1) {
			next;
			mes "Looks like you're in trouble. Shall we join in?";
			next;
			if (select( "Leave it as it is", "Help" ) == 2)
				break;
			mes "[Demon in the box]";
			mes "No! It can't be like this. Judging from this strong smell, that's definitely where I came from!";
			next;
			mes "[Demon in the box]";
			mes ".. .. ..";
			next;
			emotion ET_OTL;
			mes "[Demon in the box]";
			mes "Ouch! Why! Why not!";
		}
		emotion ET_HUK;
		mes "[Demon in the box]";
		mes "Keuk?! What? What? Do you see me?";
		next;
		mes "[" + strcharinfo(0) + "]";
		mes "Looks good. By the way, I'm in trouble. Can I help you?";
		next;
		mes "The demon in the box looks very suspicious.";
		next;
		mes "[Demon in the box]";
		mes "Okay... Mama said to be suspicious of all beings who try to do favors for no reason...";
		next;
		mes "[" + strcharinfo(0) + "]";
		mes "That makes sense... you're well educated. But isn't that a bit odd for what the demon says?";
		next;
		mes "[Demon in the box]";
		mes "Tch! You're treating a great demon like a kid in the middle of the day!";
		next;
		mes "[Demon in the box]";
		mes "I'll give you a taste of the demon's curse!!";
		next;
		emotion ET_QUESTION;
		mes "[Demon in the box]";
		mes "?";
		next;
		emotion ET_QUESTION, playerattached();
		mes "[" + strcharinfo(0) + "]";
		mes "?";
		next;
		npctalk "Growl-..", "", bc_self;
		mes "[" + strcharinfo(0) + "]";
		mes "Are you hungry? Excuse me, what does the demon eat?";
		next;
		mes "[Demon in the box]";
		mes "Keugh.. I'm embarrassed. If I wasn't hungry.. I'm sorry, but I'm a generous demon, should I get some help?";
		next;
		mes "[Demon in the box]";
		mes "Whoops, don't be alarmed, this demon eats the dregs of a fresh soul";
		next;
		mes "[" + strcharinfo(0) + "]";
		mes "I see. The remnants of a fresh soul... can I just get them from the place you came from?";
		next;
		mes "[Demon in the box]";
		mes "Contrary to what it looks like, you're a quick-witted guy. When I recover my strength, I'll generously give you a taste of the weak curse.";
		next;
		mes "[" + strcharinfo(0) + "]";
		mes "Yes, yes! You do that, and even the demon knows Give and Take, right?";
		next;
		mes "[Demon in the box]";
		mes "Give and Take? Everything.. of course I know!";
		next;
		mes "[" + strcharinfo(0) + "]";
		mes "You don't seem to know? No, it's a clever demon! Of course you understand. Then, would you like to ask me appropriately?";
		next;
		mes "[Demon in the box]";
		mes "Okay. Give and Take. I'm a smart demon, so of course I know!";
		next;
		setquest 5971;
		completequest 5971;
		emotion ET_FRET;
		mes "[Demon in the box]";
		mes "You wretch! Talk to me again and take that request from me!";
		close;
	}

	setpcblock PCBLOCK_NPC, true;

	.@playtime_5961 = checkquest(5961,PLAYTIME);
	.@playtime_5965 = checkquest(5965,PLAYTIME);
	.@playtime_5967 = checkquest(5967,PLAYTIME);
	.@playtime_5968 = checkquest(5968,PLAYTIME);

	if (.@playtime_5961 == 2) {
		erasequest 5961;
		.@playtime_5961 = -1;
		sleep2 300;
		npctalk "Demon: Feeder! It's time to eat!", "Demon in the Box#D240", bc_self;
	}
	if (.@playtime_5965 == 2) {
		erasequest 5965;
		.@playtime_5965 = -1;
		npctalk "Demon: Come to think of it, it's time for a special meal.", "Demon in the Box#D240", bc_self;
	}
	if (.@playtime_5967 == 2) {
		erasequest 5967;
		.@playtime_5967 = -1;
		sleep2 300;
		npctalk "Demon: Meals and snacks are important.", "Demon in the Box#D240", bc_self;
	}
	if (.@playtime_5968 == 2) {
		erasequest 5968;
		.@playtime_5968 = -1;
		sleep2 300;
		npctalk "Demon: I'll be in trouble if I forget my pet Mimic's meal!", "Demon in the Box#D240", bc_self;
	}

	if (checkquest(5962,HUNTING) == 2) {
		erasequest 5962;
		setquest 5961;
		getitem 1000680,1;	// ClockTower_Lead
		getexp 38409074,25606020;
		npctalk "Demon: Is this for today's meal? I'll be sincere and eat it.", "", bc_self;
	}
	if (checkquest(5963,HUNTING) == 2) {
		erasequest 5963;
		setquest 5961;
		getitem 1000680,3;	// ClockTower_Lead
		getexp 136177626,90784980;
		npctalk "Demon: Is this for today's meal? I'll be sincere and eat it.", "", bc_self;
	}
	if (checkquest(5964,HUNTING) == 2) {
		erasequest 5964;
		setquest 5961;
		getitem 1000680,10;	// ClockTower_Lead
		getexp 698346800,465564000;
		npctalk "Demon: Is this for today's meal? I'll be sincere and eat it.", "", bc_self;
	}
	if (checkquest(5966,HUNTING) == 2) {
		erasequest 5966;
		setquest 5965;
		getitem 1000681,3;	// ClockTower_Gear
		getexp 2182333750,1454887500;
		npctalk "Demon: Ooh Meal + Special Meal? Someone who knows something.", "", bc_self;
	}
	if (isbegin_quest(5970) == 1) {
		erasequest 5970;
		setquest 5968;
		getexp 1745867,1163910;
		npctalk "Demon: Did you feed Mimic while you were preparing the meal? You seem to value efficiency.", "", bc_self;
	}

	switch( rand(8) ) {
	case 0:
		unittalk getcharid(3), "" + strcharinfo(0) + " : Why does it change every day?", bc_self;
		sleep2 500;
		npctalk "Demon: What are you talking about? I look like a nice demon every day. Yesterday, today, and tomorrow I am a nice demon.", "", bc_self;
		sleep2 500;
		unittalk getcharid(3), "" + strcharinfo(0) + " : Uh.. yes.. It turns out that you don't even know..", bc_self;
		break;
	case 1:
		unittalk getcharid(3), "" + strcharinfo(0) + " : Why did you cancel the weapon exchange?", bc_self;
		sleep2 500;
		npctalk "Demon: This is my heart. Weapon exchange!", "", bc_self;
		sleep2 500;
		unittalk getcharid(3), "" + strcharinfo(0) + " : well... that's right...", bc_self;
		sleep2 500;
		npctalk "Demon: According to the demon data, they tend not to press the button that says cancel.", "", bc_self;
		break;
	case 2:
		unittalk getcharid(3), "" + strcharinfo(0) + " : What are the free lead and cogs?", bc_self;
		sleep2 500;
		npctalk "Demon: I don't know. There are a lot of them in the box I'm in. I'm throwing away bit by bit to make the box wider.", "", bc_self;
		break;
	case 3:
		unittalk getcharid(3), "" + strcharinfo(0) + " : Speaking of which, what's your name?", bc_self;
		sleep2 500;
		npctalk "Demon: Have you ever thought to ask the great demon's name? My name is Pakazmucci!", "", bc_self;
		sleep2 500;
		unittalk getcharid(3), "" + strcharinfo(0) + " : Difficult?! I'll just call you the demon...", bc_self;
		sleep2 500;
		npctalk "Demon: $%^#$#%#$ Worse than this demon!", "", bc_self;
		break;
	case 4:
		npctalk "Demon: It's weird, I feel like I'm being tricked...", "", bc_self;
		sleep2 500;
		unittalk getcharid(3), "" + strcharinfo(0) + " : I'm in the mood.", bc_self;
		break;
	case 5:
		unittalk getcharid(3), "" + strcharinfo(0) + " : Where did you get the exchanged weapon?", bc_self;
		sleep2 500;
		npctalk "Demon: This is a secret, but if you put lead and cogwheels in the box, there is a vending machine with weapons.", "", bc_self;
		sleep2 500;
		npctalk "Demon: The generous demon is a nuisance, but it's in service, thank you!", "", bc_self;
		sleep2 500;
		unittalk getcharid(3), "" + strcharinfo(0) + " : ah.. yes.. thank you so much", bc_self;
		break;
	case 6:
		unittalk getcharid(3), "" + strcharinfo(0) + " : How did you get into the box?", bc_self;
		sleep2 500;
		npctalk "Demon: You stupid. Would you stay here if I knew that?", "", bc_self;
		sleep2 500;
		unittalk getcharid(3), "" + strcharinfo(0) + " : that's it..", bc_self;
		break;
	case 7:
		unittalk getcharid(3), "" + strcharinfo(0) + " : how did you get a pet mimic?", bc_self;
		sleep2 500;
		npctalk "Demon: I picked it up while playing. I grew it because it ate well.", "", bc_self;
		sleep2 500;
		unittalk getcharid(3), "" + strcharinfo(0) + " : the demon is nice.", bc_self;
		sleep2 500;
		npctalk "Demon: Why are you suddenly swearing at me for what I've done? He's worse than a demon anyway.", "", bc_self;
		break;
	}
	setpcblock PCBLOCK_NPC, false;

	mes "[Demon in the box]";
	mes "I am a generous and forgiving demon.";
	next;
	switch( select( "[Daily] Demon's Meal", "[Daily] Demon's Pet Mimic", "[Daily] Demon's Snack", "[Weekly] Demon's Meal", "Cancel" ) ) {
	case 1:
		switch( .@playtime_5961 ) {
		case -1:
			break;
		case 0:
		case 1:
			emotion ET_FRET;
			mes "[Demon in the box]";
			mes "No matter how hungry I am, two meals at a time is too much";
			close;
		case 2:
			end;	// shouldn't happen
		}
		if (isbegin_quest(5962) == 1 || isbegin_quest(5963) == 1 || isbegin_quest(5964) == 1) {
			mes "[Demon in the box]";
			mes "What are you doing here right now? Don't go quickly and bring me food? It's slow and burst.";
			close;
		}
		mes "[Demon in the box]";
		mes "If you defeat the monster in there, you can get the remnants of the soul. Heck, that's my precious food. How much remnants will you bring me as an offering!";
		next;
		switch( select( "Demon's Breakfast - Hunt 100", "Demon's Lunch - Hunt 300", "Demon's Dinner - Hunt 1000", "Just starve!" ) ) {
		case 1:
			setquest 5962;
			mes "[Demon in the box]";
			mes "This is about the size of breakfast..";
			mes "Eat lightly and something is good. Hurry up and grab it!";
			close;
		case 2:
			setquest 5963;
			mes "[Demon in the box]";
			mes "This is about the size of lunch..";
			mes "A little full is just right for lunch. Hurry up and grab it!";
			close;
		case 3:
			setquest 5964;
			mes "[Demon in the box]";
			mes "This is about the size of dinner..";
			mes "Even if you have a good dinner, you won't be able to sleep well. Hurry up and grab it!";
			close;
		case 4:
			emotion ET_ANGER;
			mes "[Demon in the box]";
			mes "Aaah! It's mean to eat!";
			close;
		}
		end;
	case 2:
		switch( .@playtime_5968 ) {
		case -1:
			break;
		case 0:
		case 1:
			mes "[Demon in the box]";
			mes "I'm relieved to hear that my pet Mimic is all right. If you're not busy, take care of him tomorrow.";
			close;
		case 2:
			end;	// shouldn't happen
		}
		if (isbegin_quest(5969) == 1) {
			mes "[Demon in the box]";
			mes "My poor Mimic must be wandering around inside me, trembling with fear. Ego, poor guy. He met the wrong owner...";
			close;
		}
		mes "[Demon in the box]";
		mes "I'm worried that my pet Mimic is alone in there. Did you eat well? Is anyone bothering you?";
		next;
		mes "[Demon in the box]";
		mes "I'll give him something to eat, so can you go and get some?";
		next;
		if (select( "So what?", "It's annoying." ) == 2) {
			mes "[Demon in the box]";
			mes "Aagh! This bloodless, tearless bastard!!! Didn't you feel sorry for my pet Mimic!";
			close;
		}
		setquest 5969;
		mes "[Demon in the box]";
		mes "My poor Mimic will be wandering all over the place, trembling with fear. Feed him this so he doesn't go hungry.";
		close;
	case 3:
		switch( .@playtime_5967 ) {
		case -1:
			break;
		case 0:
		case 1:
			emotion ET_FRET;
			mes "[Demon in the box]";
			mes "Haven't you already packed today's snacks? Are you trying to make me a pig demon? Unfortunately, I'll refuse.";
			close;
		case 2:
			end;	// shouldn't happen
		}
		mes "[Demon in the box]";
		mes "How can a demon live on rice? Sometimes he needs to eat snacks too.";
		next;
		switch( gettime(DT_DAYOFWEEK) ) {
		case MONDAY:
		case THURSDAY:
			.@item_id = 25506;	// Cute_Broom
			.@exp = 279337600;
			.@jexp = 186224800;
			break;
		case WEDNESDAY:
		case SUNDAY:
			.@item_id = 25507;	// Showy_Coral_Reef
			.@exp = 347798000;
			.@jexp = 231865000;
			break;
		case TUESDAY:
		case FRIDAY:
			.@item_id = 25505;	// Jokers_Wild
			.@exp = 280442400;
			.@jexp = 186961600;
			break;
		case SATURDAY:
			.@item_id = 25509;	// Shining_Round_Shell
			.@exp = 347798000;
			.@jexp = 231865000;
			break;
		}
		if (countitem(.@item_id) < 100) {
			switch( .@item_id ) {
			case 25505:	// Jokers_Wild
				mes "[Demon in the box]";
				mes "So today's snack is " + mesitemlink(.@item_id) + " would be nice.";
				next;
				mes "[" + strcharinfo(0) + "]";
				mes "Huh? Isn't this a card? Does the demon eat cards too?";
				next;
				mes "[Demon in the box]";
				mes "You are curious about useless things, useless curiosity only hastens people!";
				next;
				mes "[Demon in the box]";
				mes "If you bring me 100 " + mesitemlink(.@item_id) + ", I'll make myself a snack." ;
				close;
			case 25506:	// Cute_Broom
				mes "[Demon in the box]";
				mes "So today's snack is " + mesitemlink(.@item_id) + " would be nice.";
				next;
				mes "[" + strcharinfo(0) + "]";
				mes "Huh? Eat a broom?";
				next;
				mes "[Demon in the box]";
				mes "If you take off the stick part and use it for firewood, and fry the remaining part and eat it, there's nothing special in the world.";
				next;
				mes "[Demon in the box]";
				mes "If you bring me 100 " + mesitemlink(.@item_id) + ", I'll treat you to snacks." ;
				close;
			case 25507:	// Showy_Coral_Reef
				mes "[Demon in the box]";
				mes "So today's snack is " + mesitemlink(.@item_id) + " would be nice.";
				next;
				mes "[" + strcharinfo(0) + "]";
				mes "Still, do you think we can eat today?";
				next;
				mes "[Demon in the box]";
				mes "Oh, you guys seem to be eating coral too. I thought you didn't even know the taste, but you know how to enjoy the taste quite a bit, don't you?";
				next;
				mes "[Demon in the box]";
				mes "Bring 100 " + mesitemlink(.@item_id) + ", I will personally eat it as a snack.";
				close;
			case 25509:	// Shining_Round_Shell
				mes "[Demon in the box]";
				mes "How can a demon live on rice? Sometimes he needs to eat snacks too.";
				next;
				mes "[Demon in the box]";
				mes "So today's snack is " + mesitemlink(.@item_id) + " would be nice.";
				next;
				mes "[" + strcharinfo(0) + "]";
				mes "Isn't it a bit stiff?";
				next;
				mes "[Demon in the box]";
				mes "Oops, oops... it's like they don't know the taste... Well, beings with weak teeth like you might find it hard to eat. I couldn't figure it out.";
				next;
				mes "[Demon in the box]";
				mes "But! Isn't that something you eat? Bring me 100 " + mesitemlink(.@item_id) + ". I will personally eat it as a snack.";
				close;
			}
			end;
		}
		mes "[Demon in the box]";
		mes "Ooh Did you get " + mesitemlink(.@item_id) + "?";
		next;
		if (select( "Yes, it's yours", "It's not yours" ) == 2) {
			mes "[Demon in the box]";
			mes "You're worse than a demon.";
			close;
		}
		delitem .@item_id,100;
		setquest 5967;
		getexp .@exp, .@jexp;
		mes "[Demon in the box]";
		mes "Today, I'm going to take a look at the sincerity of bringing snacks, so let's eat them specially.";
		close;
	case 4:
		switch( .@playtime_5965 ) {
		case -1:
			break;
		case 0:
		case 1:
			mes "[Demon in the box]";
			mes "I'm a generous demon, so I'm content with a special meal once a week.";
			close;
		case 2:
			end;	// shouldn't happen
		}
		if (isbegin_quest(5966) == 1) {
			mes "[Demon in the box]";
			mes "Whoops, it's a special meal if you eat it occasionally! If you eat it often, it's not a special meal!";
			close;
		}
		mes "[Demon in the box]";
		mes "He who feeds me, don't you think I need a special meal once a week?";
		next;
		if (select( "Okay. I'll prepare that special meal for you!", "It's annoying." ) == 2) {
			mes "[Demon in the box]";
			mes "These days, the devils are having a serious job shortage, so there must be a reason.";
			close;
		}
		setquest 5966;
		mes "[Demon in the box]";
		mes "Okay! Looking forward to that special!";
		close;
	case 5:
		setpcblock PCBLOCK_NPC, true;
		switch( rand(4) ) {
		case 0:
			npctalk "Demon: Shhh!! Did you bother to notice in the end?", "", bc_self;
			sleep2 500;
			npctalk "Demon: Well, I ask you to feed me, so I'll press the button that says solid.", "", bc_self;
			callshop "barter_clock_ub_solid";
			break;
		case 1:
			npctalk "Demon: Shhh!! Did you bother to notice in the end?", "", bc_self;
			sleep2 500;
			npctalk "Demon: Oh, I've been wanting to press the button that says precision.", "", bc_self;
			callshop "barter_clock_ub_precision";
			break;
		case 2:
			npctalk "Demon: Shhh!! Did you bother to notice in the end?", "", bc_self;
			sleep2 500;
			npctalk "Demon: Just wanted to press the button with Wicked on it, so why don't you take a look?", "", bc_self;
			callshop "barter_clock_ub_wicked";
			break;
		case 3:
			npctalk "Demon: Shhh!! Did you bother to notice in the end?", "", bc_self;
			sleep2 500;
			npctalk "Demon: Great!! I just wanted to press this button...", "", bc_self;
			callshop "barter_clock_ub_gear";
			break;
		}
		setpcblock PCBLOCK_NPC, false;
		end;
	}
	end;

OnHour04:
	// (the NPC sprite seems to change as follows)
	switch( gettime(DT_DAYOFWEEK) ) {
	case MONDAY:
	case THURSDAY:
		setnpcdisplay( strnpcinfo(0), 1882 );	// VAVAYAGA
		break;
	case WEDNESDAY:
	case SUNDAY:
		setnpcdisplay( strnpcinfo(0), 1883 );	// UZHAS
		break;
	case TUESDAY:
	case FRIDAY:
		setnpcdisplay( strnpcinfo(0), 1261 );	// WILD_ROSE
		break;
	case SATURDAY:
		setnpcdisplay( strnpcinfo(0), 2155 );	// BUTOIJO
		break;
	}
	end;

OnInit:
	cloakonnpc();
	questinfo( QTYPE_QUEST, QMARK_YELLOW, "BaseLevel >= 240 && isbegin_quest(5971) == 0" );

	// (no questinfo for item gathering quests completed)
	questinfo( QTYPE_QUEST2, QMARK_YELLOW, "checkquest(5962,HUNTING) == 2" );
	questinfo( QTYPE_QUEST2, QMARK_YELLOW, "checkquest(5963,HUNTING) == 2" );
	questinfo( QTYPE_QUEST2, QMARK_YELLOW, "checkquest(5964,HUNTING) == 2" );
	questinfo( QTYPE_QUEST2, QMARK_YELLOW, "isbegin_quest(5970) == 1" );
	questinfo( QTYPE_QUEST2, QMARK_YELLOW, "checkquest(5966,HUNTING) == 2" );

	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(5961,PLAYTIME) == 2" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(5965,PLAYTIME) == 2" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(5967,PLAYTIME) == 2" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "checkquest(5968,PLAYTIME) == 2" );

	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "isbegin_quest(5971) == 2 && (isbegin_quest(5962) == 0 || isbegin_quest(5963) == 0 || isbegin_quest(5964) == 0 || isbegin_quest(5966) == 0) && checkquest(5961,PLAYTIME) == -1" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "isbegin_quest(5971) == 2 && isbegin_quest(5969) == 0 && isbegin_quest(5970) == 0 && checkquest(5965,PLAYTIME) == -1" );
	questinfo( QTYPE_DAILYQUEST, QMARK_YELLOW, "isbegin_quest(5971) == 2 && isbegin_quest(5966) == 0 && checkquest(5968,PLAYTIME) == -1" );
	// (no questinfo for item gathering quests PLAYTIME -1)

	donpcevent strnpcinfo(0) + "::OnHour04";
	end;
}

c_tower1,246,197,4	script	Giant Spring#D240	HIDDEN_NPC,{
	if (BaseLevel < 240) {
		unittalk getcharid(3), "" + strcharinfo(0) + " : The spring is firmly fixed and does not move, perhaps rusted.", bc_self;
		end;
	}
	mes "It's a huge spring.";
	mes "It seems to be able to move if it is moved with force.";
	next;
	switch( select( "Turn right", "Turn left" ) ) {
	case 1:
		unittalk getcharid(3), "" + strcharinfo(0) + " : Let's struggle to turn the mainspring to the right. Somewhere...", bc_self;
		warp "clock_01",84,44;
		end;
	case 2:
		unittalk getcharid(3), "" + strcharinfo(0) + " : Let's struggle to turn the mainspring to the left. Somewhere...", bc_self;
		warp "clock_01",24,162;
		end;
	}
	end;
}

clock_01,79,39,0	warp2	clock_01_Out_c_tower1	2,2,c_tower1,241,200


clock_01,1,1,8	script	clock_ub_random_mimic	-1,{
	end;
OnInit:
	for ( .@i = 0; .@i < 10; ++.@i )
		disablenpc "Pet Mimic#D240_" + .@i;
	enablenpc "Pet Mimic#D240_" + rand(10);
	end;
}

clock_01,77,79,8	script	Pet Mimic#D240_0	4_MIMIC,{// 508
	if (BaseLevel < 240)
		end;
	if (isbegin_quest(5969) == 1) {
		if (!.npc_change)
			donpcevent strnpcinfo(0) + "::OnRand";	// Shuffle the NPC

		setpcblock PCBLOCK_NPC, true;
		unittalk getcharid(3), "" + strcharinfo(0) + " : A pet mimic that the demon outside the dungeon asked to take care of.", bc_self;
		erasequest 5969;
		specialeffect2 EF_BASH;
		setquest 5970;
		sleep2 500;
		unittalk getcharid(3), "" + strcharinfo(0) + " : Well... I took the food, so it's no problem, right?", bc_self;
		setpcblock PCBLOCK_NPC, false;
		end;
	}
	if (isbegin_quest(5970) == 1) {
		emotion ET_SURPRISE;
		unittalk getcharid(3), "" + strcharinfo(0) + " : Let's leave this place.", bc_self;
		end;
	}
	unittalk getcharid(3), "" + strcharinfo(0) + " : A mimic in a place like this?", bc_self;
	end;

OnRand:
	.npc_change = true;
	initnpctimer;
	end;
OnTimer500:
	disablenpc();
	end;
OnTimer1000:
	.npc_change = false;
	stopnpctimer;
	enablenpc "Pet Mimic#D240_" + rand(10);
	end;
}
clock_01,267,180,2	duplicate(Pet Mimic#D240_0)	Pet Mimic#D240_1	4_MIMIC
clock_01,244,198,4	duplicate(Pet Mimic#D240_0)	Pet Mimic#D240_2	4_MIMIC
clock_01,136,143,2	duplicate(Pet Mimic#D240_0)	Pet Mimic#D240_3	4_MIMIC
clock_01,110,173,2	duplicate(Pet Mimic#D240_0)	Pet Mimic#D240_4	4_MIMIC
clock_01,195,269,8	duplicate(Pet Mimic#D240_0)	Pet Mimic#D240_5	4_MIMIC
clock_01,111,259,8	duplicate(Pet Mimic#D240_0)	Pet Mimic#D240_6	4_MIMIC
clock_01,37,221,2	duplicate(Pet Mimic#D240_0)	Pet Mimic#D240_7	4_MIMIC
clock_01,20,158,4	duplicate(Pet Mimic#D240_0)	Pet Mimic#D240_8	4_MIMIC
clock_01,37,70,6	duplicate(Pet Mimic#D240_0)	Pet Mimic#D240_9	4_MIMIC
