//===== rAthena Script =======================================
//= Eden Group Quests - Mission [56 - 70]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.7
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 56 - 70.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Added checkquest confirmation for -1. [Euphy]
//= 1.3 Fixed Puppet Master's Agony Quest (12217 -> 3259). [Joseph]
//= 1.4 Fixed invalid check (Magic Wand Quest). [Joseph]
//= 1.5 Optimized, based on code by Zopokx. [Euphy]
//= 1.6 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.7 Added use of F_HasEdenGroupMark function. [Ragno]
//= 1.8 Reverting Magic Wand Quest to request Trunks. [Carlosgrr]
//============================================================

moc_para01,42,38,3	script	Mission [56 - 70]	857,{
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "เพื่อให้ได้ภารกิจเหล่านี้ฉันต้องเข้าร่วมกลุ่ม Eden ก่อนฉันต้องพบ Secretary Lime Evenor และเป็นสมาชิก";
		close;
	}
	mes "นี่คือรายการการผจญภัยต่าง ๆ สำหรับนักผจญภัยระดับ 56 ~ 70";
	next;
	mes "ภารกิจมากมายเช่นการล่าสัตว์ประหลาด, การส่งมอบสินค้า, การหาคน";
	next;
	switch(select("หมายเลขภารกิจ 1 ~ 5: หมายเลขภารกิจ 6 ~ 10: หมายเลขภารกิจ 11 ~ 15 ")) {
	case 1:
		switch(select("ผ้าพันแผลนี้คืออะไร:ผู้ชี้ขาดที่เป็นอันตราย:นั่นไม่ใช่นางเงือก...:ประเทศของฉันเรียกฉัน!")) {
			case 1: callsub L_HuntingQuest,3250,15000,5000,930,30;
			case 2: callsub L_HuntingQuest,3251,16000,6000;
			case 3: callsub L_HuntingQuest,3252,17000,8000;
			case 4: callsub L_Quest,3254,18000,8000,7043,10,1056,30;
		}
	case 2:
		switch(select("ตามล่าหมูป่า:เตรียมหน้าร้อน:ความแค้นของผู้หญิง:ส่วนผสมพิเศษ:ความเจ็บปวดของอาจารย์หุ่น")) {
			case 1: callsub L_HuntingQuest,3255,16000,7000;
			case 2: callsub L_Quest,3256,15000,5000,918,30;
			case 3: callsub L_HuntingQuest,3257,17000,8000;
			case 4: callsub L_Quest,3258,16000,7000,7198,40;
			case 5: callsub L_HuntingQuest,3259,17000,7000,1060,30;
		}
	case 3:
		switch(select("น่ากลัวแมลงวัน:อันตราย Munak:ทำให้โลกสีเขียว:ไม้เท้าวิเศษ")) {
			case 1: callsub L_HuntingQuest,3260,16000,6000;
			case 2: callsub L_HuntingQuest,3261,16000,7000;
			case 3: callsub L_Quest,3262,17000,7000,1032,40;
			case 4: callsub L_Quest,3263,18000,8000,1021,30,1019,30;
		}
	}
	end;

L_Quest:
	.@quest_id = getarg(0);
	if (checkquest(.@quest_id) == -1) {
		callsub L_Details, .@quest_id;
		next;
		mes "คุณต้องการยอมรับภารกิจนี้หรือไม่";
		next;
		if(select("ยอมรับภารกิจ: อย่ายอมรับภารกิจ") == 1) {
			if (BaseLevel < 55 || BaseLevel > 70) {
				mes "ภารกิจเหล่านี้ไม่เหมาะกับระดับข้าข้าควรมองหาภารกิจอื่น";
				close;
			}
			setquest .@quest_id;
			mes "ฉันยอมรับภารกิจสำเร็จแล้วฉันต้องกลับมาและยืนยันงานของฉันหลังจากเสร็จภารกิจ";
			next;
			mes "ภารกิจนี้ไม่มีเวลา จำกัด ";
		}
		close;
	}
	if (getargcount() > 5) {
		if (countitem(getarg(5)) < getarg(6))
			set .@items,1; //incomplete
		else
			set .@items,2; //complete
	}
	if (checkquest(.@quest_id) < 2 && (countitem(getarg(3)) < getarg(4) || .@items == 1)) {
		mes "คุณมีภารกิจต่อเนื่องคุณต้องการตรวจสอบรายละเอียดหรือไม่";
		next;
		if(select("ตรวจสอบรายละเอียด: ยกเลิก") == 1)
			callsub L_Details, .@quest_id;
		close;
	} else {
		mes "ฉันทำได้ดีมากสำหรับภารกิจฉันควรรายงานตอนนี้หรือไม่";
		next;
		if(select("รายงานภารกิจ: อย่ารายงานเลย") == 1) {
			delitem getarg(3),getarg(4);
			if (.@items)
				delitem getarg(5),getarg(6);
			getexp getarg(1),getarg(2);
			erasequest .@quest_id;
			mes "คุณทำภารกิจสำเร็จแล้วรับรางวัล";
		}
		close;
	}
	end;

L_HuntingQuest:
	.@quest_id = getarg(0);
	if (checkquest(.@quest_id) == -1) {
		callsub L_Details, .@quest_id;
		next;
		mes "คุณต้องการยอมรับภารกิจนี้หรือไม่";
		next;
		if(select("ยอมรับภารกิจ: อย่ายอมรับภารกิจ") == 1) {
			if (BaseLevel < 55 || BaseLevel > 70) {
				mes "ภารกิจเหล่านี้ไม่เหมาะกับระดับข้าข้าควรมองหาภารกิจอื่น";
				close;
			}
			setquest .@quest_id;
			mes "ฉันยอมรับภารกิจสำเร็จแล้วฉันต้องกลับมาและยืนยันงานของฉันหลังจากเสร็จภารกิจ";
			next;
			mes "ภารกิจนี้ไม่มีเวลา จำกัด ";
		}
		close;
	}
	if (getargcount() > 3) {
		if (countitem(getarg(3)) < getarg(4))
			set .@items,1; //incomplete
		else
			set .@items,2; //complete
	}
	if (checkquest(.@quest_id,HUNTING) < 2 || .@items == 1) {
		mes "คุณมีภารกิจต่อเนื่องคุณต้องการตรวจสอบรายละเอียดหรือไม่";
		next;
		if(select("ตรวจสอบรายละเอียด: ยกเลิก") == 1)
			callsub L_Details, .@quest_id;
		close;
	} else if (checkquest(.@quest_id,HUNTING) == 2) {
		mes "ฉันทำได้ดีมากสำหรับภารกิจฉันควรรายงานตอนนี้หรือไม่";
		next;
		if(select("รายงานภารกิจ: อย่ารายงานเลย") == 1) {
			if (.@items)
				delitem getarg(3),getarg(4);
			getexp getarg(1),getarg(2);
			erasequest .@quest_id;
			mes "คุณทำภารกิจสำเร็จแล้วรับรางวัล";
		}
		close;
	}
	end;

L_Details:
	switch(getarg(0)) {
	case 3250:
		mes "ภารกิจนี้ได้รับมอบหมายจาก Rekenber corporation จาก Lighthalzen ด้านล่างเป็นรายละเอียด";
		next;
		mes "Rekenber corporation กำลังจะเปิดตัวโครงการใหม่ที่ชื่อว่า 'Green Medical Center' สำหรับผลิตภัณฑ์แรกของโครงการนี้เรากำลังเตรียมผ้าพันแผลที่เป็นอยู่ที่ดี";
		next;
		mes "สำหรับโครงการนี้เราต้องทำการวิจัยปฏิกิริยาของสัตว์ประหลาดเมื่อมันถูกห่อด้วยผ้าพันแผลเน่า";
		next;
		mes "หากคุณสนใจโปรดตามล่ามัมมี่ 30 คนและนำผ้าพันแผลเน่า 30 ตัวคุณสามารถพบพวกมันได้ใน Pyramid West of Morocc";
		next;
		mes "โปรดยืนยันให้เราหลังจากที่คุณทำภารกิจเสร็จสิ้นจากนั้นเราจะให้รางวัลที่เหมาะสมแก่คุณ";
		next;
		mes "- Rekenber คอร์ปอเรชั่นหัวหน้าผู้อำนวยการ 'Julie E Delph' -";
		break;
	case 3251:
		mes "ภารกิจนี้ได้รับมอบหมายจากสมาคมสหกรณ์ Comodo ด้านล่างนี้เป็นรายละเอียด";
		next;
		mes "เหล่านักล่าได้ดุร้ายในทุกวันนี้พวกมันโจมตีผู้หญิงและคนเมาเหล้าที่ออกไปเที่ยวใกล้ชายหาด";
		next;
		mes "นี่เป็นสถานการณ์ที่น่าอับอายมากสำหรับพวกเราซึ่งเป็นสถานที่พักผ่อนที่วิเศษที่สุดใน Rune-Midgarts";
		next;
		mes "มันจะมีประโยชน์มากถ้าเจ้าตามล่า Alligators 30 คนเพื่อกำจัดสถานการณ์นี้";
		next;
		mes "โปรดยืนยันให้เราหลังจากที่คุณทำภารกิจเสร็จสิ้นจากนั้นเราจะให้รางวัลที่เหมาะสมแก่คุณ";
		break;
	case 3252:
		mes "ภารกิจนี้ได้รับมอบหมายจากลูกค้าที่ไม่รู้จักจาก Izlude";
		next;
		mes "ข้าชื่นชมชายหาดของ Izlude นับตั้งแต่ข้าเกิดมา";
		next;
		mes "เมื่อฉันยังเด็กฉันพบสิ่งมีชีวิตลึกลับในคุกใต้ดิน Izlude ฉันไม่เคยเห็นอะไรแบบนั้นมาก่อน ... ";
		next;
		mes "ฉันคิดทันทีว่ามันคือนางเงือกฉันประหลาดใจมากฉันได้ศึกษาเกี่ยวกับนางเงือกตลอดชีวิตของฉัน";
		next;
		mes "แต่เมื่อฉันศึกษาให้หนักขึ้นฉันพบว่าสิ่งมีชีวิตนั้นไม่ใช่นางเงือก ... ใช่มันไม่ได้สวยงามเลยเหมือนนางเงือกคนอื่น ๆ ... ";
		next;
		mes "ในที่สุดฉันก็ตามความจริงที่ว่ามันเป็น Merman ไม่ใช่เงือก!";
		next;
		mes "Merman ... ข้าเสียชีวิตไปกับการศึกษาสัตว์ประหลาดตัวนี้ดังนั้นโปรดตามล่ามนุษย์ 30 คนเพื่อทำให้ข้ารู้สึกดีขึ้น";
		next;
		mes "โปรดยืนยันให้เราหลังจากที่คุณทำภารกิจเสร็จสิ้นจากนั้นเราจะให้รางวัลที่เหมาะสมแก่คุณ";
		next;
		mes "- Izlude 'นางเงือกในฝันที่หายไป' -";
		break;
	case 3254:
		mes "ภารกิจนี้ได้รับมอบหมายจากเจ้าหน้าที่ของราชอาณาจักร Rune-Midgarts";
		next;
		mes "เพื่อป้องกันน้ำท่วมใน Prontera เราตัดสินใจสร้างเขื่อนขนาดใหญ่เพื่อควบคุมการจ่ายน้ำ";
		next;
		mes "แต่เราได้รับเสบียงจากเสบียงและเราจำเป็นต้องรวบรวม Fine Sand และ Grit";
		next;
		mes "ดังนั้นเราต้องการขอความช่วยเหลือผู้คนโปรดนำทรายละเอียด 10 เม็ดและเม็ดกรวด 30 เม็ดเพื่อเราจะได้สร้างเขื่อน";
		next;
		mes "คุณจะได้วัสดุจาก Sand Man ฉันขอให้คุณโชคดี!";
		next;
		mes "โปรดยืนยันให้เราหลังจากที่คุณทำภารกิจเสร็จสิ้นจากนั้นเราจะให้รางวัลที่เหมาะสมแก่คุณ";
		next;
		mes "- เจ้าหน้าที่ของราชอาณาจักร Rune-Midgarts, Dufre Kent -";
		break;
	case 3255:
		mes "ภารกิจนี้ได้รับมอบหมายจากศาลากลาง Payon";
		next;
		mes "สัตว์ป่ามักจะสร้างความรำคาญให้กับเกษตรกร";
		next;
		mes "พวกมันโจมตีฟาร์มของเราและทำลายพืชผลทั้งหมด !! เราทนไม่ได้อีกแล้ว!";
		next;
		mes "ดังนั้นเราต้องการขอความช่วยเหลือจากนักผจญภัยผู้กล้าหาญ";
		next;
		mes "ได้โปรดตามล่าหา 30 Savages เพื่อช่วยพืชผลของเรา!";
		next;
		mes "คุณสามารถค้นหาสัตว์ประหลาดเหล่านั้นรอบเมืองและในทุ่งของเราได้";
		next;
		mes "โปรดยืนยันให้เราหลังจากที่คุณทำภารกิจเสร็จสิ้นจากนั้นเราจะให้รางวัลที่เหมาะสมแก่คุณ";
		next;
		mes "- สมาชิกศาลากลาง Payon -";
		break;
	case 3256:
		mes "ภารกิจนี้ได้รับมอบหมายจากช่างตีเหล็ก Morocc Aragham";
		next;
		mes "สวัสดี ~~~~ นักผจญภัย! นี่คือ Aragham, ช่างเหล็กที่ร้อนแรงที่สุดใน Morocc!";
		next;
		mes "อย่างไรก็ตามข้ามีลูกชายชื่อ Aragam Junior คนน่ารักฮ่าฮ่า";
		next;
		mes "เขาจะเข้าค่ายฤดูร้อนช่วงซัมเมอร์นี้ แต่เขาไม่รู้ว่าจะว่ายน้ำอย่างไร";
		next;
		mes "ข้าอยากจะสอนเขา แต่อย่างที่เจ้ารู้ว่าข้าเป็นช่างตีเหล็กห่างไกลจากการว่ายน้ำ!";
		next;
		mes "แต่ฉันคิดว่าถ้าฉันทำครีบว่ายน้ำมันจะดีสำหรับความมั่นใจของเขา ~!";
		next;
		mes "เพื่อที่จะทำมันฉันต้องการ Webfoots เหนียวๆ 30 อันจากกบ Roda เจ้าช่วยพาพวกมันมาให้ฉันได้ไหม?";
		next;
		mes "โปรดยืนยันให้เราหลังจากที่คุณทำภารกิจเสร็จสิ้นจากนั้นเราจะให้รางวัลที่เหมาะสมแก่คุณ";
		next;
		mes "PS. อย่าเปรียบเทียบฉันกับ Hollgrehenn หรือ Antonio !!!!";
		next;
		mes "- ช่างตีเหล็กที่ร้อนแรง, Morocc blacksmith Aragham -";
		break;
	case 3257:
		mes "ภารกิจนี้ได้รับมอบหมายจากนักประดิษฐ์ Dorian จาก Izlude";
		next;
		mes "คุณเคยได้ยินเรื่องที่ว่าถ้าผู้หญิงมีความเสียใจในใจของพวกเขาก็จะนำภัยพิบัติทางธรรมชาติ";
		next;
		mes "ว้าว, ดังนั้นพวกคุณต้องระวังผู้หญิงที่อ่อนไหวเหล่านั้นด้วย!";
		next;
		mes "ผู้หญิงสามารถนำปรากฏการณ์แปลก ๆ มาด้วยได้!";
		next;
		mes "ฉันรู้สึกทึ่งกับเรื่องราวที่ฉันพยายามพิสูจน์ว่ามันเป็นเรื่องจริง";
		next;
		mes "ฉันต้องทำให้ผู้หญิงอารมณ์เสีย! ฮ่าฮ่าฉันรู้ว่ามันมีความคิดอะไรใช่มั้ย";
		next;
		mes "แต่นี่เป็นเพียงการศึกษาอย่างจริงจัง ... ดังนั้นโปรดตามล่าหาโซฮี 30 คนที่ดูเหมือนจะฟื้นจากวิญญาณที่ตกเป็นเหยื่อ";
		next;
		mes "ฉันจะคอยสังเกตเรื่องนี้ต่อไป";
		next;
		mes "โปรดยืนยันให้เราหลังจากที่คุณทำภารกิจเสร็จสิ้นจากนั้นเราจะให้รางวัลที่เหมาะสมแก่คุณ";
		next;
		mes "- นักประดิษฐ์ที่สง่างาม Dorian -";
		break;
	case 3258:
		mes "ภารกิจนี้ได้รับมอบหมายจาก Wallah จาก Payon";
		next;
		mes "สวัสดีนักผจญภัยเจ้าเป็นอย่างไรบ้างสุขภาพของเจ้าเป็นอย่างไรบ้าง";
		next;
		mes "ฉันเป็นเภสัชกรและฉันมีเวลายากที่จะได้รับส่วนผสมพิเศษ";
		next;
		mes "เพราะข้าเป็นเด็กผู้หญิงตัวเล็ก ๆ ข้าจะได้ของเหล่านั้นด้วยตัวเองได้อย่างไร";
		next;
		mes "ได้โปรดช่วยฉันด้วยเพียงแค่นำใบมโหฬาร 40 ใบมาให้ฉันนั่นก็เพียงพอแล้วที่จะทำยาพิเศษของข้า!";
		next;
		mes "ได้โปรดนำมาให้โดยเร็วที่สุดผู้คนต้องการยาของฉัน";
		next;
		mes "โปรดยืนยันให้เราหลังจากที่คุณทำภารกิจเสร็จสิ้นจากนั้นเราจะให้รางวัลที่เหมาะสมแก่คุณ";
		next;
		mes "- Wallah -";
		break;
	case 3259:
		mes "ภารกิจนี้ได้รับมอบหมายจากนายหุ่น Woonute จาก Geffen";
		next;
		mes "เจ้ารู้ว่าการเป็นนายหุ่นนั้นเป็นงานยากที่จะทำให้ผู้คนพอใจ";
		next;
		mes "ผู้คนสามารถค้นหาและดูสิ่งต่าง ๆ มากมายในวันนี้";
		next;
		mes "ความคาดหวังของพวกเขาเริ่มสูงขึ้นฉันไม่สามารถสนองพวกเขาได้อีกต่อไป";
		next;
		mes "แต่ถ้าฉันสามารถสร้างแนวความคิดของเล่นใหม่ ๆ เช่นของเล่นมีชีวิตมันจะต้องแตกหัก";
		next;
		mes "ข้าได้ยินมาว่ามีตุ๊กตาที่เรียกว่า Marionettes อยู่เจ้าช่วยตามล่าข้า 30 Marionettes ให้ข้าได้ไหมและนำ Golden Hair 30 อันมาให้พวกมันซึ่งเป็นวัสดุที่ดีสำหรับของเล่นใหม่";
		next;
		mes "โปรดยืนยันให้เราหลังจากที่คุณทำภารกิจเสร็จสิ้นจากนั้นเราจะให้รางวัลที่เหมาะสมแก่คุณ";
		next;
		mes "- Puppet Master Woonute -";
		break;
	case 3260:
		mes "ภารกิจนี้ได้รับมอบหมายจากผู้ทำลายจาก Prontera";
		next;
		mes "เมื่ออากาศอุ่นขึ้นแมลงก็ทวีมากขึ้นเรื่อย ๆ มันควบคุมไม่ได้แล้ว";
		next;
		mes "ผู้คนใน Prontera นอนไม่หลับเพราะแมลงในบ้านของพวกเขา";
		next;
		mes "งั้นโปรดช่วยพวกเราตามล่านักล่าแมลงวัน 30 ตัวจากนั้นแมลงวันจะลดลงอย่างช้าๆ ";
		next;
		mes "โปรดยืนยันให้เราหลังจากที่คุณทำภารกิจเสร็จสิ้นจากนั้นเราจะให้รางวัลที่เหมาะสมแก่คุณ";
		next;
		mes "- ผู้ทำลายล้าง Prontera -";
		break;
	case 3261:
		mes "ภารกิจนี้ได้รับมอบหมายจากชีส Dongja จาก Payon";
		next;
		mes "เจ้าเคยได้ยินเรื่องเกี่ยวกับ Munak หรือเปล่า?";
		next;
		mes "Munak สัตว์ประหลาดน่าเกลียดกำลังคุกคามผู้คนใน Payon";
		next;
		mes "ดังนั้นเราต้องการนักผจญภัยที่กล้าหาญอย่างคุณ!";
		next;
		mes "โปรดตามล่า 30 Munaks จากนั้น Payon จะอยู่อย่างสงบ";
		next;
		mes "โปรดยืนยันให้เราหลังจากที่คุณทำภารกิจเสร็จสิ้นจากนั้นเราจะให้รางวัลที่เหมาะสมแก่คุณ";
		next;
		mes "- Payon Cheese Dongja -";
		break;
	case 3262:
		mes "ภารกิจนี้ได้รับมอบหมายจากตัวแทนจำหน่ายอัญมณี Ibraham จาก Morocc";
		next;
		mes "ดอกไม้เล็ก ๆ ที่สวยงามในทะเลทรายที่แห้งแล้ง ... มันช่างน่าทึ่งจริง ๆ !";
		next;
		mes "การปลูกดอกไม้สามารถทำให้โลกเป็นสีเขียวและมันจะสวยไปทุกที่";
		next;
		mes "ฉันวางแผนที่จะปลูกดอกไม้ที่แข็งแรงในทะเลทราย Morocc เพื่อให้ดินดีขึ้น";
		next;
		mes "ฉันต้องการ Maneater Blossom 40 ตัวจาก Flora ฉันรู้ว่า Maneater Blossom เป็นดอกไม้ที่น่าเกลียด แต่ก็ยังเป็นต้นไม้";
		next;
		mes "ฉันหวังว่าคุณจะสามารถตามล่า Flora และได้รับ 40 Maneater Blossoms คุณทำให้โลกเป็นสีเขียว! จงภูมิใจ!";
		next;
		mes "โปรดยืนยันให้เราหลังจากที่คุณทำภารกิจเสร็จสิ้นจากนั้นเราจะให้รางวัลที่เหมาะสมแก่คุณ";
		next;
		mes "- Morocc Ibraham -";
		break;
	case 3263:
		mes "ภารกิจนี้ได้รับมอบหมายจากช่างตีเหล็ก Payon Antonio";
		next;
		mes "ไม้เรียววิเศษเจ้าเคยได้ยินเรื่องนี้ไหมมันเป็นไม้เรียวที่เหลือเชื่อ";
		next;
		mes "ถ้าคุณพูดคำวิเศษณ์และแกว่งไม้เรียวมันจะทำให้ความปรารถนาของคุณเป็นจริง";
		next;
		mes "ดังนั้นฉันตัดสินใจที่จะทำด้วยตัวเองแล้วฉันจะรวยและทรงพลัง ~! ฮ่าฮ่า !!";
		next;
		mes "เพื่อให้ไม้เรียวฉันต้องการวัสดุพิเศษ";
		next;
		mes "ได้โปรดนำ Dokebi Horns มาให้ 30 อันและ Bamboo Cut 30 อันถ้าเจ้าทำเช่นนั้นข้าจะยืมไม้เท้าวิเศษให้ข้าสักครั้งฮ่าฮ่าจัดการได้ไหม?";
		next;
		mes "โปรดยืนยันให้เราหลังจากที่คุณทำภารกิจเสร็จสิ้นจากนั้นเราจะให้รางวัลที่เหมาะสมแก่คุณ";
		next;
		mes "- ช่างตีเหล็ก Payon Antonio -";
		break;
	}
	return;
}
