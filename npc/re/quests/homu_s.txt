//===== rAthena Script ======================================= 
//= Homunculus S Quest
//===== By: ================================================== 
//= Masao
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= Evolves an Homunculus which is at least Level 99 to the
//= new Homunculus S Class.
//===== Additional Comments: ================================= 
//= 1.0 First Version. The actual changing dialog is currently
//=     customized. [Masao]
//= 1.1 Cleaning. [Euphy]
//============================================================ 

job3_gen01,12,44,4	script	Viorel#job3_gen01	542,{

	if((Class != Job_Genetic) && (Class != Job_Genetic_T) && (Class != Job_Baby_Genetic)){
		mes "[Viorel]";
		mes "^FF4800Homunculus^000000 research requires a lot of time and funding.";
		mes "However, I believe passion is the most important factor.";
		next;
		switch(select("End conversation.:What are you talking about?:Why are you always standing there?")) {
		case 1:
			mes "[Viorel]";
			mes "So long~!";
			close;
		case 2:
			mes "[Viorel]";
			mes "You see, ^006400Alchemists^000000 have an inquisitive nature.";
			mes "People like me, especially, even think of creating life itself.";
			next;
			mes "[Viorel]";
			mes "You'd have to research ^FF4800Homunculus^000000 to understand.";
			mes "Now, if you'll excuse me~!";
			close;
		case 3:
			emotion 4,0;
			mes "[Viorel]";
			mes "What, is it so strange to see an Alchemist in their own lab?";
			mes "How so?";
			close;
		}
	}
	mes "[Viorel]";
	mes "Hello "+strcharinfo(0)+",";
	mes "what can I do for a fellow Alchemist like you?";
	next;
	switch(select("I want to evolve my Homunculus:About Homunculus S:Cancel")){
	case 1:
		mes "[Viorel]";
		if(!getskilllv("AM_BIOETHICS")){
			mes "I'm sorry, but you must know the Skill Bioethics! Otherwise I can't let your Homunculus evolve into a Homunculus S.";
			close;
		}
		if(!gethominfo(1)){
			mes "You don't even have a Homunculus!";
			close;
		}
		if(gethominfo(1) < 6009){
			mes "You must have an evolved Homunculus, or you can't turn it into the new Homunculus S.";
			close;
		}
		if(gethominfo(6) < 99){
			mes "Your Homunculus must be level 99 to evolve into a Homuculus S!";
			mes "Come back after you've trained your Homunculus a little bit more.";
			close;
		}
		if(gethominfo(1) == 6048 || gethominfo(1) == 6049 || gethominfo(1) == 6050 || gethominfo(1) == 6051 || gethominfo(1) == 6052){
			mes "Your Homunculus S looks great!";
			mes "I hope you will experience many great adventures with it!";
			close;
		}
		mes "Great, it seems like you're all ready to get your Homunculus to the next level!";
		next;
		mes "[Viorel]";
		mes "Now you can either directly change your Homunculus to a random Homunculus S or you can donate a little fee of 50,000z";
		mes "to the Alchemist Guild. For this you'll be able to change your Homunculus into your desired Homunculus S Class!";
		mes "So, what do you think?";
		next;
		switch(Select("I want to donate.:I don't want to donate.:Cancel")){
		case 1:
			if(Zeny < 50000){
				mes "[Viorel]";
				mes "Haha, nice try my friend! But you don't even have the 50,000z to donate!";
				mes "Come back if you have the Zeny.";
				close;
			}
			mes "[Viorel]";
			mes "Alright, now please tell me which Homunculus you'd like to have:";
			next;
			set .@i, select("Eira:Bayeri:Sera:Dieter:Elanor:Cancel");
			if (.@i == 6) {
				mes "[Viorel]";
				mes "So long~!";
				close;
			}
		case 2:
			mes "[Viorel]";
			mes "Ok, it's all set! Now just give me a moment!";
			next;
			mes "[Viorel]";
			mes "Abra...";
			next;
			mes "[Viorel]";
			mes "Kadabra...";
			next;
			mes "[Viorel]";
			mes "Simsala!";
			mes "...";
			mes "Oh, sorry... wrong game!";
			next;
			mes "[Viorel]";
			mes "Here you go! Your new Homunculus S!";
			if (.@i) {
				hommutate 6047+.@i;
				set Zeny,Zeny - 50000;
			}
			else hommutate;
			close;
		case 3:
			mes "[Viorel]";
			mes "So long~!";
			close;
		}
	case 2:
		mes "[Viorel]";
		mes "The Homunculus S are the new and improved versions of the current Homunculus you know.";
		mes "These new Homunculus have been made because the monsters in our world keep getting stronger and";
		mes "stronger, and our current Homunculus are just not able to keep up with them anymore.";
		next;
		mes "[Viorel]";
		mes "But since we're Alchemists and we don't want to abandon our precious Homunculus, we've discovered a way";
		mes "to improve their skills and strength in order to keep them by our side, and joining us once more";
		mes "in battle! Isn't that great?";
		close;
	case 3:
		mes "[Viorel]";
		mes "So long~!";
		close;
	}
}