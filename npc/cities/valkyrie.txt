//===== eAthena Script ======================================= 
//= Valkyrie Realm
//===== By: ==================================================
//= Nana, fixes by Poki
//= finished, optimized and tested by Lupus
//===== Current Version: ===================================== 
//= 2.6a
//===== Compatible With: ===================================== 
//= eAthena 1.0 +
//===== Description: ========================================= 
//= The Jobquest from kRO on how to advance to a Reborn class.
//===== Additional Comments: ================================= 
//= 1.1 Made all into functions,additional checks,quest skills,now Valhallana can return you back. She also makes reborn process only when you got no Zeny/items (as in kRO) [Lupus]
//= 1.2 now alternative classes can use their 1-1 job NPC to become Advanced 1st Class [Lupus]
//= 1.4 added Baby Class support [Lupus]
//= 1.5 Fixed loads of gramatical errors. [Mass Zero]
//= 1.6 Added a func which prevent advanced classes passing 2nd Job Quests again. It also guides adv classes where to go. [Lupus]
//= 1.7 On reborn reset some Job Quest Vars and Reset Girls Counter [Lupus]
//= 1.8 Added Taekwondo classes check [Lupus]
//= 1.9 Added support for Job NPC Fase pics, sorrected one dialogue stuff [Lupus]
//= 2.0 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 2.1 Changed all numbers into constants. [Vicious]
//= 2.2 Now ALL quests skills are saved/restored [Lupus] 2.3 Removed Duplicates [Silent] 2.4 Fixed typos, optimized [Lupus] 2.5 Fixed a stuck bug [Lupus]
//= 2.6a Adding proper dialogues [Lupus]
//============================================================ 


// -== Warp's needed! ==-
valkyrie,48,73,0	warp	valk01	1,1,valkyrie,48,64
valkyrie,48,66,0	warp	valk01	1,1,valkyrie,48,75

// -== Book of Ymir (Heavens Door) ==-
yuno_in02,93,207,0	script	Book of Ymir	111,{
	mes "[Book of Ymir]";
	if(Upper==1 || valkyrie_Q>1){
		mes "...The entrance to the Hall of Honor";
		mes "is open to everyone who will move forward, into their next lives.";
		mes "It is there to help heroes decide what they want to do, and can";
		mes "access every place in this world.";
		next;
		mes "[Book of Ymir]";
		mes "In the Hall of Honor, everything is perfectly prepared for heroes.";
		mes "It is rumored that any wish that";
		mes "cannot be fulfilled in our reality";
		mes "can be realized in the Hall of Honor.";
		next;
		switch(select("Stop reading.:Continue reading.")) {
		case 1:
			mes "[Book of Ymir]";
			mes ". . . . .";
			close;
			break;
		case 2:
			mes "[Book of Ymir]";
			mes "There is a forgotten path which leads to";
			mes "the Hall of Honor, the closest place to the heavens. However, it is forbidden";
			mes "for ordinary people to know about it...";
			close2;
			warp "valkyrie",48,9; //48,8?
			end;
			break;
		}
	}
	if(valkyrie_Q<1 || Upper==2 || BaseClass == Job_Taekwon || BaseJob == Job_Gunslinger || BaseJob == Job_Ninja){
		mes ". . . . .";
	}
	mes "...Therefore, ancient heroes were";
	mes "always in anguish, knowing that";
	mes "eventually, they were mortal and";
	mes "would pass from this realm...";
	next;
	mes "[The Book of Ymir]";
	mes "There were no documents, songs";
	mes "or remaining folklore that";
	mes "had any information on life";
	mes "after death. However, I recently";
	mes "uncovered an old scroll about";
	mes "Valkyrie...";
	next;
	mes "[The Book of Ymir]";
	mes "Valkyrie...";
	mes "The legendary guardian angel";
	mes "angel of Ragnarok.";
	next;
	mes "[The Book of Ymir]";
	mes "Adventurers of great strength";
	mes "and bravery will be lead by";
	mes "Valkyrie to Valhalla, the Hall";
	mes "of Honor. There, they will be";
	mes "given a new life.";
	next;
	mes "[The Book of Ymir]";
	mes "Reborn, they will live again as";
	mes "even greater heroes that will";
	mes "brighten the world. Bodies that";
	mes "were exhausted will be filled";
	mes "with energy...";
	next;
	mes "[The Book of Ymir]";
	mes "And the spirit will be given";
	mes "abilities with the heart of Ymir.";
	mes "However, the heart of Ymir was";
	mes "totally destroyed and scattered";
	mes "all over the world after the battle of the Rune-Midgarts.";
	next;
	mes "[The Book of Ymir]";
	mes "I had found a small amount of";
	mes "Ymir heart pieces over a long";
	mes "long period of time. But I can't";
	mes "confirm if the story of Valkyrie";
	mes "and Valhalla is true or not";
	mes "through scientific tests.";
	next;
	mes "[The Book of Ymir]";
	mes "So, I am leaving this record";
	mes "and hope that someone in the future";
	mes "can confirm that Valkyrie and Valhalla";
	mes "exist through my records.";
	next;
	mes "[The book of Ymir]";
	mes "Let the heroes live new lives";
	mes "so they could protect the world from danger.";
	mes "And then......";
	mes "...........";
	close;
}

// -== The Librarian that watches the "Book of Ymir" ==-
yuno_in02,91,176,5	script	Librarian	754,{
	mes "[Librarian]";
	if(valkyrie_Q){
		mes "Why? You have already paid me.";
		mes "Now go and look for the Book of Ymir.";
		close;
	}
	if(BaseJob >= Job_Knight && Upper==0) goto L_PAY;
	mes "Have a look around, but don't touch the book. Only a few chosen ones can read its wise words.";
	close;

L_PAY:
	mes "You seem like a worthy human.";
	mes "I, the 78th Librarian of the secret order have sworn to protect this book, it won't thread lightly on you.";
	mes "In order to let me allow you to view into this wonderous book, you have to do two things for me.";
	next;
	mes "[Librarian]";
	mes "One. You have to pay me 1,285,000 Zeny. Remember that you need the accurate amount. Not over or under 1,285,000z.";
	mes "Two. You have to venture deep inside of Yuno castle, into the Heart of Ymir.";
	mes "Remember that you cannot view the book unless you have done this.";
	mes "Now go, brave one.";
	next;
	menu "Pay now",-,"Close",M_EXIT;

	mes "[Librarian]";
	if(Zeny != 1285000){
		mes "You didn't bring me the accurate number of zeny I wanted. Bring me back only 1,285,000 zeny!";
		mes "Not more, nor less.";
		close;
	}
	set Zeny,0;
	set valkyrie_Q,1;
	mes "Go now, into Heart of Ymir";
	mes "There, you'll find the last piece of information before you can open the book.";
M_EXIT:
	close;
}

// -== The Heart of Ymir ==-
yuno_in05,50,44,0	script	Heart of Ymir	111,{
	mes "[Heart of Ymir]";
	if(valkyrie_Q == 1){
		mes "Thus upon hard times and when our self esteem is at its lowest, then is when our faith has to be the strongest.";
		mes "For aeons the secret guardians of the path to heaven have protected the secret gate from evil spirits entering it.";
		mes "Thus, the bonds became to weaken over time... That's when the Legion of Sages started recruiting and making young, brave and quick minded mages and sages, teaching them the laws and propositions of our world.";
		next;
		mes "[Heart of Ymir]";
		mes "Aeons passed and the Sages grew stronger in both mind and forces, until they all were feared throughout the monsters' world.";
		mes "Now, after listening to my words of wisdom, please advance to the Book of Ymir and give yourself into Gods light.";
		mes "If you are pure of heart and have no evil intentions, the gates of dawn will open for you and take you in...";
		set valkyrie_Q,2;
		close;
	}
	if(valkyrie_Q == 2){
		mes "I have nothing more to teach you, nor to tell you.";
		close;
	}
	mes ". . .";
	close;
}

// -== Researcher of the Book of Ymir ==-
yuno_in02,90,77,4	script	Researcher#06	744,{
	mes "[Researcher]";
	mes "Argh, where is it!?";
	mes "They said that it would be around here somewhere...";
	mes "Maybe I have to look deeper into this castle...";
	emotion e_what;
	close;
}

// -== Valhallana ==-
valkyrie,48,86,6	script	Valkyrie	811,{
	mes "[Valhallana]";
	if(BaseJob == Job_SuperNovice) goto L_SN;
	if(baseClass == Job_Taekwon || BaseJob == Job_Gunslinger || BaseJob == Job_Ninja) goto L_CANT; //sent back any Taekwondo/GS/NJ classes
	if(Upper == 2) goto L_BABY;
	if(Upper > 0) goto L_ALREADY;
	if(BaseLevel >= 99 && JobLevel >= 50 && BaseJob > Job_Thief) goto L_CHANGE;
	if(BaseJob <= Job_Thief) goto L_NOTHING;

	mes "You need 99 Base Level and 50 Job Level.";
	mes "Also you must get rid of all of your money and items.";
	next;
	goto L_NOTHING;

L_BABY:
	mes "A Baby?! How did you get here?";
	mes "I'm passing you back to your parents.";
	emotion e_what;
	next;
L_Y:
	warp "yuno_in02",93,204;
	close;

L_CANT:
	mes "How did you get here?";
	emotion e_what;
	next;
	goto L_Y;

L_SN:
	mes "Welcome to Valkyrie, "+strcharinfo(0)+".";
	mes "I see you have followed a hard way of the Super Novice.";
	next;
	mes "[Valhallana]";
	mes "I am sorry, but I can't help you.";
	emotion e_sry;
	next;
	goto L_NOTHING;

L_ALREADY:
	mes "I can't help you anymore.";
	if(Class == Job_Novice_High) mes "You have been reborn already.";
	if(Class >= Job_Swordman_High && Class <= Job_Thief_High) mes "Please go over there, to the person representing your class.";
	next;

L_NOTHING:
	mes "Do you wish to go back to your world?";
	next;
	menu "Yes",L_Y,"No",-;

	mes "[Valhallana]";
	mes "As you wish...";
	close;

L_CHANGE:
	mes "Welcome to Valkyrie, "+strcharinfo(0)+". From this point on, there is no returning back.";
	next;
	mes "[Valhallana]";
	if(Zeny || Weight) goto L_ZENYITEMS;
	if(checkcart()) goto L_CART;
	if(checkfalcon()) goto L_FALCON;
	if(checkriding()) goto L_PECO;
	if(skillpoint > 0) goto L_SKILLPNTS;

	mes "Let's start your reincarnation ceremony...";
	next;

	set ADVJOB, Class+Job_Novice_High; //memo the target 3rd Job ID
	if(ADVJOB == Job_Lord_Knight2) set ADVJOB,Job_Lord_Knight;
	if(ADVJOB == Job_Paladin2) set ADVJOB,Job_Paladin;

	callfunc("F_SaveQuestSkills");

	mes "[Valhallana]";
	mes "Done...";
	mes "Good luck.";
	jobchange Job_Novice_High;
	resetlvl(1);
	callfunc "F_ClearJobVar";
	set RES_SKILL,0; //we reset Reset Skills NPC counter
	next;
	if(ADVJOB == Job_Lord_Knight || ADVJOB == Job_Paladin) goto L_Izl;
	if(ADVJOB == Job_High_Priest || ADVJOB == Job_Champion) goto L_Pro;
	if(ADVJOB == Job_High_Wizard || ADVJOB == Job_Professor) goto L_Gef;
	if(ADVJOB == Job_Whitesmith || ADVJOB == Job_Creator) goto L_Alb;
	if(ADVJOB == Job_Sniper || ADVJOB == Job_Clown || ADVJOB == Job_Gypsy) goto L_Pay;
	if(ADVJOB == Job_Assassin_Cross || ADVJOB == Job_Stalker) goto L_Mor;

L_Pro:
	savepoint "prontera",273,354;
	warp "prontera",273,354;
	close;

L_Mor:
	savepoint "morocc",160,94;
	warp "morocc",160,94;
	close;

L_Alb:
	savepoint "alberta",116,57;
	warp "alberta",116,57;
	close;

L_Pay:
	savepoint "payon",155,90;
	warp "payon",155,90;
	close;

L_Izl:
	savepoint "izlude",94,103;
	warp "izlude",94,103;
	close;

L_Gef:
	savepoint "geffen",120,100;
	warp "geffen",120,100;
	close;

L_ZENYITEMS:
	mes "Your money and items do rope you to your routine life.";
	mes "You should get rid of them.";
	close;

L_CART:
	mes "Please, drop your cart and we'll continue.";
	close;

L_FALCON:
	mes "Please, free your Falcon and we'll continue.";
	close;

L_PECO:
	mes "Please, free your PecoPeco and we'll continue.";
	close;

L_SKILLPNTS:
	mes "You will need to use up all of your skill points if you want me to continue.";
	close;
}

// function HIGH NOVICE -> HIGH 1
//getarg(0) - High Job ID
//getarg(1) - High Job Name
//getarg(2) - 3rd Job ID
//getarg(3) - current NPC's name

function	script	F_ToHigh	{
	if(Upper == 2) return; //Baby Class - skip it
	if(ADVJOB == Job_Lord_Knight2) set ADVJOB,Job_Lord_Knight;
	if(ADVJOB == Job_Paladin2) set ADVJOB,Job_Paladin;
	set @fjob,ADVJOB; //alternative classes should pass, too
	if(@fjob == Job_Stalker) set @fjob,Job_Assassin_Cross;
	if(@fjob == Job_Creator) set @fjob,Job_Whitesmith;
	if(@fjob == Job_Clown || @fjob == Job_Gypsy) set @fjob,Job_Sniper;
	if(@fjob == Job_Paladin) set @fjob,Job_Lord_Knight;
	if(@fjob == Job_Champion) set @fjob,Job_High_Priest;
	if(@fjob == Job_Professor) set @fjob,Job_High_Wizard;

	if(Class == getarg(0)) goto L_WELCOME; //3rd Job
	if(Class >= Job_Lord_Knight && Class <= Job_Paladin2) goto L_ALREADY; //already advanced class
	if(Class == Job_Novice_High && @fjob == getarg(2)) goto L_GETHIGH; //High Novice -> High XXXX
	if(Class == Job_Novice_High) {
		mes "Hello, Novice High! If you are going to became a "+getarg(1)+", then go visit your very first job teacher.";
		close;
	}
	return; //this char doesn't want to get HIGH class

L_GETHIGH:
	mes "["+getarg(3)+"]";
	if(JobLevel < 10) goto L_NOTREADY;
	if(checkcart()) goto L_CART;
	if(checkfalcon()) goto L_FALCON;
	if(checkriding()) goto L_PECO;
	if(skillpoint > 0) goto L_SKILLPNTS;

	mes "Hello there, "+strcharinfo(0)+"!";
	mes "You've made a brave choice in coming here to be reborn and stepping forth into the advanced ranks.";
	mes "Now... close your eyes.";
	next;
	mes "["+getarg(3)+"]";
	mes "Open your eyes.";
	mes "You have become a "+getarg(1)+".";
	jobchange getarg(0); //High Class
	next;
	if(ADV_QSK){
		mes "["+getarg(3)+"]";
		mes "Let me just add in the missing Quest Skills you lost under the Reborn process, "+strcharinfo(0)+".";
		next;
		//return learnt quest skills
		callfunc("F_Load1Skills");
	}
	mes "["+getarg(3)+"]";
	mes "I wish you good fortune in the near future!";
	emotion e_grat;
	close;

L_NOTREADY:
	mes "You are not ready to become a "+getarg(1)+".";
	mes "You have to raise your Job Level to 10.";
	emotion e_gasp;
	close;

L_SKILLPNTS:
	mes "You will need to use up all of your skill points if you want me to continue.";
	emotion e_hmm;
	close;

L_CART:
	mes "Please, drop your cart and we'll continue.";
	emotion e_hmm;
	close;

L_FALCON:
	mes "Please, free your Falcon and we'll continue.";
	emotion e_hmm;
	close;

L_PECO:
	mes "Please, free your Pecopeco and we'll continue.";
	emotion e_hmm;
	close;

L_WELCOME:
	mes "["+getarg(3)+"]";
	mes "You are welcome, "+strcharinfo(0)+"!";
	mes "We're always glad to see a "+getarg(1)+" here!";
	close;

L_ALREADY:
	mes "["+getarg(3)+"]";
	mes "It's such a big honor to salute envoys of Valhalla.";
	mes "Come again.";
	emotion e_ho;
	close;
}

// function GET 3rd JOB
//getarg(0) - High Job ID
//getarg(1) - High Job Name
//getarg(2) - 3rd Job ID
//getarg(3) - 3rd Job Name
function	script	F_Rebirth	{
	mes "["+getarg(3)+"]";
	if(Upper == 2) goto L_BABY; //Baby Class - skip it
	if(Class >= Job_Lord_Knight && Class <= Job_Paladin2) goto L_ALREADY; //already advanced class
	if(Class == getarg(0) && ADVJOB == getarg(2)) goto L_GET3RD; //Hight XXXX -> 3rd Job
	mes "Go talk to either Valhallana or one of my collegues...";
	emotion e_sry;
	close;

L_GET3RD:
	if(JobLevel < 45) goto L_NOTREADY;
	if(checkcart()) goto L_CART;
	if(checkfalcon()) goto L_FALCON;
	if(checkriding()) goto L_PECO;
	if(skillpoint > 0) goto L_SKILLPNTS;

	mes "Congratulations!";
	mes "You have trained well. Now stroll here as a "+getarg(3)+"!";
	jobchange getarg(2); //Rebirth Class
	set ADVJOB,0;
	if(ADV_QSK2){
		next;
		mes "["+getarg(3)+"]";
		mes "Let me just add in the missing Quest Skills you lost under the Reborn process, "+strcharinfo(0)+".";
		next;
		//return learnt quest skills
		callfunc("F_Load2Skills");
	}
	close;

L_NOTREADY:
	mes "You have went so far to get here. But I am sorry, you aren't quite ready to become a "+getarg(3)+".";
	mes "You need at least Job Level 45 or higher.";
	emotion e_gasp;
	close;

L_SKILLPNTS:
	mes "You will need to use up all of your skill points if you want me to continue.";
	emotion e_hmm;
	close;

L_CART:
	mes "Please, drop your cart and we'll continue.";
	emotion e_hmm;
	close;

L_FALCON:
	mes "Please, free your Falcon and we'll continue.";
	emotion e_hmm;
	close;

L_PECO:
	mes "Please, free your Pecopeco and we'll continue.";
	emotion e_hmm;
	close;

L_ALREADY:
	mes "Well, hello there! You have been reborn once, there is no second chance.";
	emotion e_ho;
	close;

L_BABY:
	mes "What a lively baby!";
	mes "How did you get here? Go to aunt Valhallana and ask her to take your home.";
	emotion e_pat;
	close;
}

// function GUIDE player to Valhalla for getting 3rd JOB
// this function prevents passing 2nd JOB QUESTS by advanced classes also
//getarg(0) - High Job ID
//getarg(1) - High Job Name
//getarg(2) - 3rd Job ID
//getarg(3) - 3rd Job Name
//getarg(4) - NPC Name
function	script	F_BlockHigh	{
	if(Upper != 1) return; //This func should interact with Advanced classes only
	mes "["+getarg(4)+"]";
	if(Class == getarg(0) && ADVJOB == getarg(2)) goto L_RIGHTHIGH;
	if(Class == getarg(2)) goto L_RIGHT3RD;
	if(Class >= Job_Lord_Knight && Class <= Job_Paladin2) goto L_ALREADY3RD; //already advanced class, but from wrong guild
//this player is a High Novice
	if(Class == Job_Novice_High) mes "Hello, Novice High! If you are going to became a "+getarg(1)+", then go visit your very first job teacher.";
//this player has 1st advanced job, but from wrong guild
	if(Class == getarg(0)) mes "A "+getarg(1)+"?";
	if(Class != Job_Novice_High) mes "Rumors say only Valhallana knows your way...";
	emotion e_sry;
	close;

L_RIGHT3RD:
	mes "Well, hello there! You look... younger.";
	emotion e_hmm;
	next;
	mes "["+getarg(4)+"]";
	mes "You are always welcome here, "+strcharinfo(0)+"!";
	mes "Our good old guild is your second home, isn't it?";
	emotion e_no1;
	close;

L_RIGHTHIGH:
	mes "Hello, "+strcharinfo(0)+"!";
	mes "If you are going to become a "+getarg(3)+", then you should visit Valhalla again.";
	if(JobLevel < 45) mes "But you need at least Job Level 45 or higher.";
	emotion e_gasp;
	close;

L_ALREADY3RD:
	mes "A "+getarg(3)+"?";
	mes "You don't belong to our guild. Begone!";
	emotion e_omg;
	close;
}
