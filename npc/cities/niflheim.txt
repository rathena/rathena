//===== eAthena Script ======================================= 
//= Niflheim Script 
//===== By: ================================================== 
//= Fyrien, Dizzy, PKGINGO
//= Official NPCs translated and re-edited by Celest
//===== Current Version: ===================================== 
//= 1.3a
//===== Compatible With: ===================================== 
//= Any eAthena Version; Niflheim Required
//===== Description: ========================================= 
//= Official NPC's for Niflheim
//= 1.01 Splitted file(guides, shops). Fixed rand() bugs,
//= missing labels bugs, optimization [Lupus]
//= 1.03 fixed end; -> close;
//= 1.04-1.05 fixed several bugs with missing CLOSE button, wrong var name
//=	in Piano Keys quest
//=     fixed zeny/item, exploit in Sairin, some optimizations [Lupus]
//= 1.06 some typos in some variables fixed (thx 2 Dr.Evil) [Lupus]
//= 1.07 Fixed typos [Nexon]
//= 1.08 Commented "Traveler" out as it is not official & we have correct Bungee quest in. 
//=     Also removed TODO, as they are already done. [Vicious_Pucca]
//= 1.1 Replaced nif_q_done with MISC_QUEST & 32. nif_t -> @nif_t [Lupus]
//= 1.2 Removed Duplicates [Silent]
//= 1.3 Moved Book of Devil and Piano quest to quests/quests_niflheim.txt [Evera]
//============================================================ 


niflheim,52,174,3	script	Kirz	796,{
	mes "[Kirz]";
	mes "Humans are not allowed here!";
	mes "Living things-! are not";
	mes "allowed to pass!";
	close;
}

niflheim,213,221,0	script	Spirit::spirits	802,{
	mes "[Spirit]";
	mes "Welcome to my world...";
	mes "This land we call....";
	mes "Niflheim....";
	percentheal -85,-85;
	close;
}

niflheim,39,146,0	duplicate(spirits)	Spirit#2	802
niflheim,95,61,0	duplicate(spirits)	Spirit#3	802
niflheim,313,187,0	duplicate(spirits)	Spirit#4	802
niflheim,212,264,0	duplicate(spirits)	Spirit#5	802
niflheim,247,72,0	duplicate(spirits)	Spirit#6	802
niflheim,130,176,0	duplicate(spirits)	Spirit#7	802
niflheim,173,110,0	duplicate(spirits)	Spirit#8	802
niflheim,182,167,0	duplicate(spirits)	Spirit#9	802


niflheim,195,211,5	script	Gigantia	796,{
	mes "[Gigantia]";
	mes "The Lord of Death always sees everything...";
	close;
}

niflheim,153,215,3	script	Feline	794,{
	mes "[Feline]";
	mes "Why are you in this dangerous village?";
	mes "Better go back fast...";
	close;
}

nif_in,16,27,1	script	Dead Cock	800,{
	mes "[Dead Cock]";
	mes "I was eaten by humans when alive,";
	mes "now it's my turn to eat you! Gugugugu~";
	mes "Aaah, I can't believe there's such a world after death, gugugu";
	percentheal -5,0;
	close;
}

nif_in,16,30,4	script	Vampire Bat	799,{
	mes "[Vampire Bat]";
	mes "Alive or dead, human blood";
	mes "is always the tastiest!";
	mes "What? You want your blood";
	mes "to be sucked by me? Nice nice-!";
	percentheal -5,0;
	close;
}

nif_in,156,93,5	script	Child	793,{
	mes "[Alakina Ann]";
	mes "Where, is this place? I just woke up";
	mes "and found myself here... mommy...";
	mes "have you seen my mommy, hmm? ... sob...";
	mes "I want to go home...";
	next;
	menu "About the witch...",L_Wizzard,"About the curse...",L_Curse,"Stop conversation",L_end;

L_Wizzard:
	mes "[Alakina Ann]";
	mes "A witch? I don't know any witches...";
	mes "although I read about them in storybooks,";
	mes "but I don't know if they're real or not,";
	mes "must be really scary if she is real...";
	next;
	mes "[Alakina Ann]";
	mes "But why is it so cold?";
	if (sex == 1) mes "Aren't you cold, brother? Wierd... Hu~";
	if (sex == 0) mes "Aren't you cold, sister? Wierd... Hu~";
	mes "I miss my warm home,";
	mes "Can you help me please? Hmmm? Hmmm?";
	close;
   
L_Curse:
	mes "[Alakina Ann]";
	mes "Curse... My grandma told me";
	mes "she has a way to avoid any curse";
	mes "unlifteable by holy blessings...";
	next;
	mes "[Alakina Ann]";
	mes "Klaatu...";
	mes "Verata.....";
	mes "And... something... something... what was it?";
	mes "I can't remember the last one.";
	next;
	mes "[Alakina Ann]";
	mes "She said if you can pronounce these magic words";
	mes "to escape a cursed fate.";
	mes "Now I can't remember what she said was important...";
	mes "looks like I forgot them too.";
	close;
   
L_end:
	mes "[Alakina Ann]";
	mes "So cold... so cold... I wish I could";
	mes "go home... if you can please take me with you...";
	mes "it's too scary here...";
	mes "please.... help me....";
	close;
}

niflheim,350,258,5	script	Cursed Soul::cursedsoul	802,{
	killmonster "niflheim","cursedsoul::OnNFMob";
	mes "[Ashe Bruce]";
	mes "I feel a strong curse on youaaaahhhhh";
	mes "I shall deny your existanceahhhhhhh";
	mes "Turn back or you will dieeeaaaahhhh";
	mes "Don't touch my boookkssaaaahhhh";
	next;
	menu "Pick up 1st book",L_book1,"Pick up 2nd book",L_book2,"Pick up 3rd book",L_book3,"Leave silently",L_back;
   
L_book1:
	mes "[Ashe Bruce]";
	mes "Don't you touch my bookssssahhh!!";
	mes "Ahahaha, I curse you for being bitten, torn and clawed at!!";
	mes "Forever binded to this curse!!";
	monster "niflheim",350,258,"Rideword",1478,1,"cursedsoul::OnNFMob";
	close;

L_book2:
	mes "[Ashe Bruce]";
	mes "I said don't touch my booksssahhh...";
	mes "Get lost....";
	mes "Don't disrupt my restingggaaahh!!";
	warp "niflheim",34,162;
	close;

L_book3:
	set @nif_t,0;
	mes "[Ashe Bruce]";
	mes "Ahahaha.... I see you are brave!!";
	mes "Start reading your prayersahahahah!!";
	next;
	if(select("Clover.:Klaatu.:Kleitos.")==2)
		set @nif_t,@nif_t + 10;

	if(select("Verit.:Veritas.:Verata.")==3)
		set @nif_t,@nif_t + 10;

	if(select("Necktie.:Necklace.:Nero.:^FFFFFFNictu.^000000")==4)
		set @nif_t,@nif_t + 10;

	mes "[Ashe Bruce]";
	if(@nif_t == 30){
		if(rand(9)){
			mes "Your prayers were correctaaahhhh...";
			mes "but your curse still remainsaaahhhh!!";
			mes "Haahahahaha.....!!!";
			close;
		}
	       mes "Hahahaha!! Your curse has been lifted!!";
	       mes "Gahahahaha.....!!!";
	       close;
	}
	mes "Hahahaha!! Your prayers were wrong!!";
	mes "Time to face deathohohohoho,";
	mes "May you be cursed forevahahahaha!!";
	monster "niflheim",349,256,"Orc Skeleton",1462,1,"cursedsoul::OnNFMob";
	monster "niflheim",347,258,"Orc Skeleton",1462,1,"cursedsoul::OnNFMob";
	monster "niflheim",347,254,"Orc Skeleton",1462,1,"cursedsoul::OnNFMob";
	monster "niflheim",350,252,"Orc Skeleton",1462,1,"cursedsoul::OnNFMob";
	monster "niflheim",344,255,"Orc Skeleton",1462,1,"cursedsoul::OnNFMob";
	monster "niflheim",348,252,"Orc Skeleton",1462,1,"cursedsoul::OnNFMob";
	monster "niflheim",347,259,"Orc Skeleton",1462,1,"cursedsoul::OnNFMob";
	close;

L_back:
	mes "[Ashe Bruce]";
	mes "Ahahahaha! Not bad thinking at alllhhhaahaaa...";
	mes "I shall pray that you live to returnahhhh....";
	close;
OnNFMob:
	end;
}