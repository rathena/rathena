//===== rAthena Script =======================================
//= Episode 17.1 Sealed OS                                       =|
//================================================================|
//= 52,Sealed OS,7200,300,1@os_b,188,41 ==========================|
//================================================================|

1@os_b,190,44,3	script	Dien#os_ent	4_F_DIENE,{ end; }
1@os_b,190,49,5	script	Cotnes#os_ent	4_F_COATNEIS,{ end; }
1@os_b,188,45,5	script	Ridsh#os_ent	4_F_LEEDSH,{
	.@dien$ =  instance_npcname("Dien#os_ent");
	.@cot$ = instance_npcname("Cotnes#os_ent");
	switch('sealed_os){
		case 0:
			cutin "ep162_rds01",2;
			mes "[ Ridsh ]";
			mes "Oh?";
			mes "What are you doing here?";
			npctalk "Ridsh : Oh? What are you doing here.";
			next;
			cutin "ep162_rds05",2;
			mes "[ Ridsh ]";
			mes "We're in trouble~~";
			mes "We can't have anyone know we're here~~";
			npctalk "Ridsh : We're in trouble~~ We can't have anyone know we're here~~";
			next;
			cutin "ep162_dn03",2;
			mes "[ Dien ]";
			mes "The adventurer is here too!";
			mes "Why are you scared!";
			npctalk "Dien : The adventurer is here too!  Why are you scared!",.@dien$;
			next;
			mes "[ Dien ]";
			mes "Hello " + strcharinfo(0) + ".";
			mes "We've came here to do something.. It was too troublesome to tell the kids.";
			npctalk "Dien : Hello " + strcharinfo(0),.@dien$;
			next;
			mes "[ Dien ]";
			mes "If you keep it a secret that you saw us...";
			npctalk "Dien : If you keep it a secret that you saw us...",.@dien$;
			specialeffect EF_BLASTMINEBOMB,AREA,instance_npcname("#os_lock_01");
			specialeffect EF_BLASTMINEBOMB,AREA,instance_npcname("#os_lock_02");
			next;
			cutin "ep162_rds04",2;
			mes "[ Ridsh ]";
			mes "Sister~~";
			mes "What are you talking about, I don't see the adventurer!";
			npctalk "Ridsh : Sister~~ What are you talking about, I don't see the adventurer!";
			next;
			cutin "ep162_ctn01",2;
			mes "[ Cotnes ]";
			mes "Oh~";
			mes "Yeah?";
			mes "I don't see the adventurer either~";
			npctalk "Cotnes : Oh~ Yeah? I don't see the adventurer either~",.@cot$;
			next;
			mes "[ Cotnes ]";
			mes "So what now?";
			mes "We're already inside.";
			npctalk "Cotnes : So what now? We're already inside.",.@cot$;
			next;
			cutin "ep162_rds05",2;
			mes "[ Ridsh ]";
			mes "It can't be helped~~";
			mes "We have to go with the adventurer.";
			npctalk "Ridsh : It can't be helped~~ We have to go with the adventurer.";
			next;
			cutin "ep162_dn01",2;
			mes "[ Dien ]";	
			mes "It's dangerous, so please come with us.";
			npctalk "Dien :  It's dangerous so please come with us.",.@dien$;
			specialeffect EF_BLASTMINEBOMB,AREA,instance_npcname("#os_lock_01");
			specialeffect EF_BLASTMINEBOMB,AREA,instance_npcname("#os_lock_02");
			'sealed_os = 1;
			close2;
			cutin "",255;
			F_InstanceHide(strnpcinfo(0),1);
			F_InstanceHide("Dien#os_ent",1);
			F_InstanceHide("Cotnes#os_ent",1);
			F_InstanceHide("#os_lock_01",5);
			F_InstanceHide("#os_lock_02",5);
			F_InstanceHide("Dien#os_ev_right",2);
			F_InstanceHide("Ridsh#os_ev_right",2);
			F_InstanceHide("Cotnes#os_ev_right",2);
			F_InstanceEvent("#os_mob_01","OnMobSpawn");
			end;
	}
	end; 
}

1@os_b,199,51,7	script	Dien#os_ev_right	4_F_DIENE,{ end; OnInstanceInit: F_InstanceHide(strnpcinfo(0),1); end; }
1@os_b,201,49,5	script	Cotnes#os_ev_right	4_F_COATNEIS,{ end; OnInstanceInit: F_InstanceHide(strnpcinfo(0),1); end; }
1@os_b,203,51,7	script	Ridsh#os_ev_right	4_F_LEEDSH,{ end; OnInstanceInit: F_InstanceHide(strnpcinfo(0),1); end; }
1@os_b,203,53,5	script	#s_os_dummy00	20357,{ end; }
1@os_b,202,55,3	script	#s_os_dummy01	20357,{ end; }
1@os_b,198,53,3	script	#s_os_dummy02	20355,{ end; }
1@os_b,199,55,5	script	#s_os_dummy03	20355,{ end; }
1@os_b,201,52,3	script	#s_os_dummy04	20363,{ end; }
1@os_b,200,54,3	script	#s_os_dummy05	20361,{ end; }
1@os_b,194,250,5	script	#s_os_dummy06	20357,{ end; }
1@os_b,196,250,3	script	#s_os_dummy07	20355,{ end; }
1@os_b,195,251,3	script	#s_os_dummy08	20363,{ end; }
1@os_b,205,256,4	script	#s_os_dummy09	20365,{ end; }
1@os_b,193,274,4	script	#s_os_dummy10	20365,{ end; }
1@os_b,199,274,4	script	#s_os_dummy11	20361,{ end; }
1@os_b,205,274,4	script	#s_os_dummy12	20363,{ end; }
1@os_b,213,256,4	script	#s_os_dummy13	20357,{ end; }
1@os_b,125,230,4	script	#s_os_dummy14	20365,{ end; }
1@os_b,129,230,4	script	#s_os_dummy15	20361,{ end; }
1@os_b,228,276,4	script	#s_os_dummy16	20361,{ end; }
1@os_b,243,276,4	script	#s_os_dummy17	20361,{ end; }
1@os_b,231,276,4	script	#s_os_dummy18	20365,{ end; }
1@os_b,240,276,4	script	#s_os_dummy19	20365,{ end; }
1@os_b,260,261,4	script	#s_os_dummy20	20365,{ end; }
1@os_b,262,261,4	script	#s_os_dummy21	20363,{ end; }
1@os_b,265,268,4	script	#s_os_dummy22	20361,{ end; }
1@os_b,252,279,5	script	#s_os_dummy23	20363,{ end; }
1@os_b,252,300,5	script	#s_os_dummy24	20363,{ end; }
1@os_b,252,298,5	script	#s_os_dummy25	20361,{ end; }
1@os_b,266,283,3	script	#s_os_dummy26	20355,{ end; }
1@os_b,134,230,4	script	#s_os_dummy27	20363,{ end; }




1@os_b,212,41,0	script	Suspicious Device#os_lock_0	4_ENERGY_BLUE,{
	mes "- There is a pipe going through the floor. -";
	if('sealed_os == 1){
		next;
		if(select("Crush it.:Put it down.") == 2){
			mes "- Nothing happened. -";
			end;
		}
		mes "- Breaking the piple made a strange noise. The machine has stopped. -";
		specialeffect EF_BLASTMINEBOMB;
		close2;
		for(.@i = 0; .@i < 6; .@i++){
			F_InstanceHide("#s_os_dummy0"+.@i,5);
		}
		F_InstanceHide(strnpcinfo(0),5);
		F_InstanceHide("Dien#os_ev_right",5);
		F_InstanceHide("Ridsh#os_ev_right",5);
		F_InstanceHide("Cotnes#os_ev_right",5);
		F_InstanceHide("#os_lock_00",3);
		F_InstanceEvent("Dien#os_ev_mid","OnEnable");
		mapannounce 'os_map$,"Ridsh : Alright! Let's move up along the railroad tracks!",bc_map,0xFF0000;
		'sealed_os = 2;
	}
	end;
	
OnInstanceInit:
	'os_map$ = instance_mapname("1@os_b");
	F_InstanceHide(strnpcinfo(0),1);
	end;
}


1@os_b,1,1,0	script	#os_mob_01	HIDDEN_WARP_NPC,{
OnMobSpawn:
	F_InstanceHide(strnpcinfo(0),4);
	npctalk "Dien : Oh my!",instance_npcname("Dien#os_ev_right");
	.@map$ = 'os_map$;
	monster .@map$,204,46,"Dolor",20361,1,instance_npcname(strnpcinfo(0)) + "::OnMobKill";
	monster .@map$,199,46,"Twin Caput",20365,1,instance_npcname(strnpcinfo(0)) + "::OnMobKill";
	'mob_count = 2;
	sleep 1500;
	npctalk "Dien : We will take care of these, please take care of the gnarly ones!",instance_npcname("Dien#os_ev_right");
	end;
	
OnMobKill:
	'mob_count -= 1;
	if('mob_count == 0){
		F_InstanceHide("Suspicious Device#os_lock_0",2);
		mapannounce 'os_map$,"Ridsh : While we are dealing with the enemies, turn of the device at the bottom.",bc_map,0xFF0000;
	}
	end;

OnInstanceInit:
	F_InstanceHide(strnpcinfo(0),3);
	end;
}


1@os_b,187,52,0	script	#os_lock_00	WARPNPC,2,2,{
	end;
	
OnTouch:
	warp 'os_map$,188,41;
	end;
}

1@os_b,191,46,0	script	#os_lock_01	WARPNPC,2,2,{
	end;
	
OnTouch:
	warp 'os_map$,188,41;
	end;
}

1@os_b,192,49,0	script	#os_lock_02	WARPNPC,2,2,{
	end;
	
OnTouch:
	warp 'os_map$,188,41;
	end;
}

1@os_b,112,108,5	script	Dien#os_ev_mid	4_F_DIENE,{
	cutin "ep162_dn01",2;
	mes "[ Dien ]";
	mes "Oh! What's in front over there?";
	close3;
	
OnEnable:
	F_InstanceHide(strnpcinfo(0),4);
	F_InstanceHide("Dangerous Device#os_lock_0",4);
	F_InstanceHide("Dangerous Device#os_lock_1",4);
	F_InstanceHide("Dangerous Device#os_lock_2",4);
	F_InstanceHide("Thick Wire#os_lock_0",4);
	F_InstanceHide("Thick Wire#os_lock_1",4);
	F_InstanceHide("Thick Wire#os_lock_2",4);
	end;
	
OnUnlock:
	'sealed_os = 3;
	F_InstanceHide(strnpcinfo(0),5);
	F_InstanceHide("Dien#os_ev_gate",4);
	F_InstanceHide("Cotnes#os_ev_gate",4);
	F_InstanceHide("Ridsh#os_ev_gate",4);
	end;
	
OnInstanceInit: 
	F_InstanceHide(strnpcinfo(0),3); 
	end;
}

1@os_b,115,112,0	script	Dangerous Device#os_lock_0	4_PURPLE_WARP,3,2,{
	end;
	
OnTouch:
	mes "[ Dien ]";
	mes "Oh! I think it's a very precise device.";
	npctalk "Dien : Oh! I think it's a very precise device.",instance_npcname("Dien#os_ev_mid"),bc_map;
	next;
	mes "[ Dien ]";
	mes "If you don't break the machine on the other side, you cannot pass!";
	npctalk "Dien : If you don't break the machine on the other side, you cannot pass!",instance_npcname("Dien#os_ev_mid"),bc_map;
	next;
	mes "[ Dien ]";
	mes "It's dangerous to go through right now, so turn around and go to the device!";
	npctalk "Dien : It's dangerous to go through right now, so turn around and go to the device!",instance_npcname("Dien#os_ev_mid"),bc_map;
	close2;
	cutin "",255;
	warp 'os_map$,115,104;
	end;
	
OnInstanceInit: 
	F_InstanceHide(strnpcinfo(0),3); 
	end;
}

1@os_b,115,126,0	duplicate(Dangerous Device#os_lock_0)	Dangerous Device#os_lock_1	4_PURPLE_WARP,3,2
1@os_b,115,138,0	duplicate(Dangerous Device#os_lock_0)	Dangerous Device#os_lock_2	4_PURPLE_WARP,3,2
	

1@os_b,137,107,0	script	Thick Wire#os_lock_0	4_ENERGY_BLUE,{
	'wires +=  1;
	F_InstanceHide("Dangerous Device#os_lock_0",5);
	F_InstanceHide(strnpcinfo(0),5);
	if('wires == 3){
		mapannounce 'os_map$,"Ridsh : That's it! Let's move along the railroad tracks!",bc_map,0xFF0000;
		F_InstanceEvent("Dien#os_ev_mid","OnUnlock");
	} else {
		mapannounce 'os_map$,"Ridsh : The machine hasn't turned off yet. Please take a look above.",bc_map,0xFF0000;
	}
	end;
	
OnInstanceInit: 
	F_InstanceHide(strnpcinfo(0),3); 
	end;
}

1@os_b,137,123,0	script	Thick Wire#os_lock_1	4_ENERGY_BLUE,{
	'wires +=  1;
	F_InstanceHide("Dangerous Device#os_lock_1",5);
	F_InstanceHide(strnpcinfo(0),5);
	if('wires == 3){
		mapannounce 'os_map$,"Ridsh : That's it! Let's move along the railroad tracks!",bc_map,0xFF0000;
		F_InstanceEvent("Dien#os_ev_mid","OnUnlock");
	} else {
		mapannounce 'os_map$,"Ridsh : The machine hasn't turned off yet. Please take a look above.",bc_map,0xFF0000;
	}
	end;

OnInstanceInit: 
	F_InstanceHide(strnpcinfo(0),3); 
	end;	
}

1@os_b,137,139,0	script	Thick Wire#os_lock_2	4_ENERGY_BLUE,{
	'wires +=  1;
	F_InstanceHide("Dangerous Device#os_lock_2",5);
	F_InstanceHide(strnpcinfo(0),5);
	if('wires == 3){
		mapannounce 'os_map$,"Ridsh : That's it! Let's move along the railroad tracks!",bc_map,0xFF0000;
		F_InstanceEvent("Dien#os_ev_mid","OnUnlock");
	} else {
		mapannounce 'os_map$,"Ridsh : The machine hasn't turned off yet. Please take a look above.",bc_map,0xFF0000;
	}
	end;

OnInstanceInit: 
	F_InstanceHide(strnpcinfo(0),3); 
	end;	
}

1@os_b,122,212,5	script	Dien#os_ev_gate	4_F_DIENE,{
	if('sealed_os < 3){
		'sealed_os = 3;
	}
	cutin "ep162_dn01",2;
	mes "[ Dien ]";
	mes "Oh!";
	mes "If you go right, I think you can go back on the railroad without having to break the door.";
	npctalk "Dien : Oh! If you go right, I think you can go back on the railroad without having to break the door";
	if('sealed_os == 3){
		F_InstanceHide("#os_lock_03",3);
		'sealed_os = 4;
	}
	close3;
	
OnInstanceInit:
	F_InstanceHide(strnpcinfo(0),3);
	end;
}

1@os_b,114,217,0	script	Cotnes#os_ev_gate	4_F_COATNEIS,{
	if('sealed_os == 3){
		cutin "ep162_ctn01",2;
		mes "[ Cotnes ]";
		mes "Oh, is this door too hard?";
		mes "It will take some time to break it~";
		next;
	}
	cutin "ep162_dn01",2;
	mes "[ Dien ]";
	mes "Oh!";
	mes "If you go right, I think you can go back on the railroad without having to break the door.";
	npctalk "Dien : Oh! If you go right, I think you can go back on the railroad without having to break the door",instance_npcname("Dien#os_ev_gate");
	if('sealed_os == 3){
		F_InstanceHide("#os_lock_03",3);
		'sealed_os = 4;
	}
	close3;
	
OnInstanceInit:
	F_InstanceHide(strnpcinfo(0),3);
	end;
}

1@os_b,146,221,7	script	Ridsh#os_ev_gate	4_F_LEEDSH,{
	if('sealed_os == 4){
		.@erst$ = instance_npcname("Erst#os_ev_gate");
		.@cot$ = instance_npcname("Cotnes#os_ev_gate_2");
		.@dien$ = instance_npcname("Dien#os_ev_gate_2");
		cutin "ep162_rds04",2;
		mes "[ Ridsh ]";
		mes "Argh!";
		npctalk "Ridsh : Argh!";
		next;
		specialeffect EF_BLASTMINEBOMB,AREA,instance_npcname("#os_dummy_erst_1");
		specialeffect EF_BLASTMINEBOMB,AREA,instance_npcname("#os_dummy_erst_2");
		F_InstanceHide("#os_dummy_erst_1",3);
		F_InstanceHide("#os_dummy_erst_2",3);
		F_InstanceHide("Erst#os_ev_gate",4);
		cutin "ep162_est01",2;
		mes "[ Erst ]";
		mes "Oh...";
		mes "I'm in a hurry, what are you doing here?";
		npctalk "Erst : I'm in a hurry, what are you doing here?",.@erst$;
		next;
		cutin "ep162_est02",2;
		mes "[ Erst ]";
		mes "Please go back and enjoy your tea gracefully.";
		npctalk "Erst : Please go back and enjoy your tea gracefully.",.@erst$;
		next;
		cutin "ep162_rds02",2;
		mes "[ Ridsh ]";
		mes "Huh!";
		mes "How did you sneak in here anyway?";
		npctalk "Ridsh : Huh! How did you sneak in here anyway?";
		next;
		cutin "ep162_est02",2;
		mes "[ Erst ]";
		mes "This is only a surveillance routine.";
		mes "If you close your eyes for a second, you may run into an accident.";
		npctalk "Erst : This is only a surveillance routine.",.@erst$;
		sleep2 2000;
		npctalk "Erst : If you close your eyes for a second, you may run into an accident.",.@erst$;
		next;
		cutin "ep162_rds02",2;
		mes "[ Ridsh ]";
		mes "What makes you think we need your help?";
		npctalk "Ridsh : What makes you think we need your help?";
		next;
		cutin "ep162_est02",2;
		mes "[ Erst ]";
		mes "Didn't you just did now?";
		mes "I don't think I heard that wrong. Did I?";
		next;
		F_InstanceHide("Cotnes#os_ev_gate",5);
		F_InstanceHide("Cotnes#os_ev_gate_2",2);
		cutin "ep162_ctn01",2;
		mes "[ Cotnes ]";
		mes "You two really~";
		mes "Please stop fighting.";
		npctalk "Cotnes : You two really~ Please stop fighting.",.@cot$;
		next;
		F_InstanceHide("Dien#os_ev_gate",5);
		F_InstanceHide("Dien#os_ev_gate_2",2);
		cutin "ep162_dn03",2;
		mes "[ Dien ]";
		mes "How do you always fight like that?";
		npctalk "Dien : How do you always fight like that?",.@dien$;
		next;
		cutin "ep162_ctn01",2;
		mes "[ Cotnes ]";
		mes "I'm so excited about this~ I'm getting old and I'm tired of fighting.";
		npctalk "Cotnes : I'm so excited about this~ I'm getting old and I'm tired of fighting.",.@cot$;
		next;
		cutin "ep162_dn03",1;
		mes "[ Dien ]";
		mes "Is that so?";
		npctalk "Dien : Is that so?",.@dien$;
		next;
		cutin "ep162_ctn01",2;
		mes "[ Cotnes ]";
		mes "Yeah~";
		mes "I'm tired of fighting, let's go back to work.";
		mes "The road is open now.";
		npctalk "Cotnes : Yeah~ I'm tired of fighting, let's go back to work. The road is open now.",.@cot$;
		'sealed_os = 5;
		close2;
		cutin "",255;
		F_InstanceHide(strnpcinfo(0),5);
		F_InstanceHide("Erst#os_ev_gate",5);
		F_InstanceHide("Dien#os_ev_gate_2",5);
		F_InstanceHide("Cotnes#os_ev_gate_2",5);
		F_InstanceHide("#os_lock_05",5);
		F_InstanceHide("Ridsh#os_ev_end",4);
		F_InstanceHide("Erst#os_ev_end",4);
		F_InstanceHide("Dien#os_ev_end",2);
		F_InstanceHide("Cotnes#os_ev_end",4);
		F_InstanceHide("#os_mob_ev_1",4);
		F_InstanceHide("#os_mob_ev_2",4);
		F_InstanceHide("#os_mob_ev_3",4);
		F_InstanceHide("#os_mob_ev_4",4);
		F_InstanceHide("#os_mob_ev_5",4);
		F_InstanceHide("#os_mob_ev_6",4);
		F_InstanceHide("#os_mob_ev_7",4);
		end;
	}
	end;
	
OnInstanceInit:
	F_InstanceHide(strnpcinfo(0),3);
	end;
}

1@os_b,144,221,5	script	#os_dummy_erst_1	20363,{ end; }
1@os_b,148,221,3	script	#os_dummy_erst_2	20365,{ end; }

1@os_b,148,217,3	script	Erst#os_ev_gate	4_F_ESTLOVELOY,{
	end;
	
OnInstanceInit:
	F_InstanceHide(strnpcinfo(0),3);
	end;
}

1@os_b,142,225,5	script	Cotnes#os_ev_gate_2	4_F_COATNEIS,{ 
	end; 
 
OnInstanceInit:
	F_InstanceHide(strnpcinfo(0),1);
	end;
}

1@os_b,143,222,5	script	Dien#os_ev_gate_2	4_F_DIENE,{
	end; 
 
OnInstanceInit:
	F_InstanceHide(strnpcinfo(0),1);
	end;
}

1@os_b,118,209,0	script	#os_lock_03	WARPNPC,2,2,{
	end;
OnTouch:
	cutin "ep162_dn01",2;
	mes "[ Dien ]";
	mes "It would be better to just go around the door.";
	npctalk "Dien : It would be better to just go around the door.";
	close2;
	cutin "",255;
	warp 'os_map$,114,204;
	end;
	
}

1@os_b,119,215,0	script	#os_lock_04	WARPNPC,2,2,{
	end;
OnTouch:
	cutin "ep162_dn01",2;
	mes "[ Dien ]";
	mes "It would be better to just go around the door.";
	npctalk "Dien : It would be better to just go around the door.";
	close2;
	cutin "",255;
	warp 'os_map$,114,204;
	end;
}

1@os_b,147,224,0	script	#os_lock_05	WARPNPC,2,2,{
OnTouch:
	warp 'os_map$,142,220;
	end;
}

1@os_b,259,360,5	script	Dien#os_ev_end	4_F_DIENE,{
	cutin "ep162_dn03",2;
	mes "[ Dien ]";
	mes "Finally..";
	mes "If you cross this door...";
	next;
	cutin "ep162_est01",2;
	mes "[ Erst ]";
	mes "Let's head back now before they gather more troops.";
	next;
	cutin "ep162_dn03",2;
	mes "[ Dien ]";
	mes "Excuse me.";
	next;
	cutin "ep162_est01",2;
	mes "[ Erst ]";
	mes "?";
	next;
	cutin "ep162_rds01",2;
	mes "[ Ridsh ]";
	mes "Why?";
	next;
	cutin "ep162_dn03",2;
	mes "[ Dien ]";
	mes "Oh my, oh my!";
	mes "What should we do!";
	next;
	cutin "ep162_rds04",2;
	mes "[ Ridsh ]";
	mes "Why, what's going on?";
	next;
	cutin "ep162_dn03",2;
	mes "[ Dien ]";
	mes "I think I've left it.";
	next;
	cutin "ep162_rds01",2;
	mes "[ Ridsh ]";
	mes "Left what?";
	next;
	cutin "ep162_dn03",2;
	mes "[ Dien ]";
	mes "My protection charm!";
	mes "I get nervous if I don't have it with me.";
	next;
	cutin "ep162_rds04",2;
	mes "[ Ridsh ]";
	mes "You've been fine so far without it?!";
	next;
	cutin "ep162_dn03",2;
	mes "[ Dien ]";
	mes "...";
	next;
	cutin "ep162_rds01",2;
	mes "[ Ridsh ]";
	mes "Ah... Sorry..";
	mes "Alright.";
	next;
	mes "[ Ridsh ]";
	mes "While we open the last door, would you mind going back to ^FF0000Clana Nemieri^000000?";
	next;
	mes "[ Ridsh ]";
	mes "I have to contact a few people anyway, and Dien won't go anywhere until she gets her protection charm.";
	next;
	mes "[ Ridsh ]";
	mes "Sister, tell me where it is.";
	next;
	cutin "ep162_dn02",2;
	mes "[ Dien ]";
	mes "Oh!";
	mes "Thank you so much for that";
	next;
	mes "[ Dien ]";
	mes "It's hard for me to get it back because it's far...";
	mes "Please do me a favor.";
	next;
	cutin "ep162_dn01",2;
	mes "[ Dien ]";
	mes "^FF0000Go back to my room and look for a chest on the left side of the window, near a vase.";
	next;
	mes "[ Dien ]";
	mes "It should be easy to get since we're here.";
	next;
	cutin "ep162_rds05",2;
	mes "[ Ridsh ]";
	mes "Oh!";
	mes "Shouldn't we tell them where we are?";
	ep_illusion = 32;
	completequest 11600;
	setquest 11601;
	getitem 25669,25;
	getexp 600000,600000;
	close2;
	warp "ein_fild03",258,267;
	end;
	
OnInstanceInit:
	questinfo(QTYPE_QUEST,QMARK_YELLOW,"checkquest(11600) == 1");
	F_InstanceHide(strnpcinfo(0),1);
	end;
}

1@os_b,260,363,5	script	Cotnes#os_ev_end	4_F_COATNEIS,{
	cutin "ep162_ctn01",2;
	mes "[ Cotnes ]";
	mes "It's good sometimes to take a walk like this~";
	close3;
	
OnInstanceInit:
	F_InstanceHide(strnpcinfo(0),3);
	end;
}

1@os_b,262,362,5	script	Erst#os_ev_end	4_F_ESTLOVELOY,{
	cutin "ep162_est01",2;
	mes "[ Erst ]";
	mes "I want to explore the next area.";
	close3;
	
OnInstanceInit:
	F_InstanceHide(strnpcinfo(0),3);
	end;
}

1@os_b,261,358,3	script	Ridsh#os_ev_end	4_F_LEEDSH,{
	cutin "ep162_rds01",2;
	mes "[ Ridsh ]";
	mes "I'm tired!";
	mes "I want to go home.";
	close3;
	
OnInstanceInit:
	F_InstanceHide(strnpcinfo(0),3);
	end;
}

1@os_b,204,266,0	script	#os_mob_ev_1	4_PURPLE_WARP,2,3,{
	end;
	
OnTouch:
	if(getd("'" + strnpcinfo(2))) 
		end;
	.@map$ = 'os_map$;
	setarray .@mob,20361,20363,20365,20355,20357;
	getmapxy(.@map_d$,.@x,.@y,BL_PC);
	for(.@i = 0; .@i < 2; .@i++){
		switch(rand(1,4)){
			case 1:
				monster .@map$,.@x + rand(1,2),.@y + rand(1,2),"--ja--",.@mob[rand(getarraysize(.@mob))],1,instance_npcname(strnpcinfo(0))+"::OnMobKill";
				break;
				
			case 2:
				monster .@map$,.@x + rand(1,2),.@y - rand(1,2),"--ja--",.@mob[rand(getarraysize(.@mob))],1,instance_npcname(strnpcinfo(0))+"::OnMobKill";
				break;
				
			case 3:
				monster .@map$,.@x - rand(1,2),.@y - rand(1,2),"--ja--",.@mob[rand(getarraysize(.@mob))],1,instance_npcname(strnpcinfo(0))+"::OnMobKill";
				break;
				
			case 4:
				monster .@map$,.@x - rand(1,2),.@y + rand(1,2),"--ja--",.@mob[rand(getarraysize(.@mob))],1,instance_npcname(strnpcinfo(0))+"::OnMobKill";
				break;
		}
	}
	setd("'" + strnpcinfo(2),1);
	end;
	
OnMobKill:
	.@map$ = 'os_map$;
	if(mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMobKill") == 0){
		F_InstanceHide(strnpcinfo(0),5);
	}
	end;
}

1@os_b,229,266,0	duplicate(#os_mob_ev_1)	#os_mob_ev_2	4_PURPLE_WARP,2,3
1@os_b,244,266,0	duplicate(#os_mob_ev_1)	#os_mob_ev_3	4_PURPLE_WARP,2,3
1@os_b,261,281,0	duplicate(#os_mob_ev_1)	#os_mob_ev_4	4_PURPLE_WARP,2,3
1@os_b,261,299,0	duplicate(#os_mob_ev_1)	#os_mob_ev_5	4_PURPLE_WARP,2,3
1@os_b,261,314,0	duplicate(#os_mob_ev_1)	#os_mob_ev_6	4_PURPLE_WARP,2,3
1@os_b,261,332,0	duplicate(#os_mob_ev_1)	#os_mob_ev_7	4_PURPLE_WARP,2,3
