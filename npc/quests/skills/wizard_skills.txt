//===== rAthena Script ======================================= 
//= Wizard Skills Quests
//===== By: ================================================== 
//= Lupus, Reddozen
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Quests for skills: Sight Blaster
//===== Additional Comments: ================================= 
//= 1.4 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//= 1.5 Replaced effect numerics with constants. [L0ne_W0lf]
//============================================================

gef_tower,115,36,4	script	Meow#q_wiz	876,{
	if (BaseJob == Job_Wizard) {
		if (WIZ_SK == 100) {
			if (getskilllv("WZ_SIGHTBLASTER") == 0) {
				mes "[Meow]";
				mes "What...?! You forgot the";
				mes "awesome skill I taught you?";
				mes "Transcending's no excuse";
				mes "for forgetting! Alright, so you";
				mes "want me to teach you, eh?";
				mes "Alright, here's your lesson!";
				next;
				mes "^3355FF*BAM! BOOM! CRASH!*^000000";
				specialeffect2 EF_SUI_EXPLOSION;
				next;
				mes "[Meow]";
				mes "Don't you dare get";
				mes "hoity-toity with me, just";
				mes "because you've been reborn!";
				mes "You better remember how to";
				mes "do that skill before I give";
				mes "you another whupping!";
				next;
				mes "^3355FFYou miraculously recall";
				mes "the Sight Blaster skill before";
				mes "Meow can administer another";
				mes "vicious beating to you.";
				specialeffect2 EF_ABSORBSPIRITS;
				skill "WZ_SIGHTBLASTER",1,0;
				next;
				mes "[Meow]";
				mes "Remember now?";
				mes "Great. Now I'm going";
				mes "back to taking a nap.";
				mes "If you even think about";
				mes "bothering me, I'll make";
				mes "you regret it. Mark my word!";
				close;
			}
			else {
				mes "[Meow]";
				mes "Zzz... Zzz...";
				mes "Catherine... I'll";
				mes "make you sorry for";
				mes "turning me into a kitty...";
				mes "I'm gonna... Gonna... Zzz...";
				close;
			}
		}
		else if (WIZ_SK == 0) {
			mes "[Meow]";
			mes "*Yaaawn~*";
			mes "So sleeepy...";
			mes "Hm? Did you want";
			mes "to talk to me? Don't";
			mes "bother me if you've";
			mes "got nothing to say!";
			next;
			if (select("A cat?:Now I'm sleepy, too...") == 1) {
				mes "[Meow]";
				mes "Yeah. Yeah, I'm a cat.";
				mes "For now, anyway. Damn that";
				mes "Catherine! She turned me into";
				mes "this form when I borrowed some";
				mes "gemstones without her consent.";
				mes "I was gonna tell her!";
				next;
				mes "[Meow]";
				mes "Once I become human";
				mes "again and regain my powers,";
				mes "I'm gonna go freakin' buck";
				mes "wild. Catherine, you, everyone.";
				mes "You're all turning into cats!";
				close;
			}
			mes "[Meow]";
			mes "Hah! Yeah...";
			mes "Taking a nap sounds like";
			mes "the best idea I've hear--oh.";
			mes "Wait. You're a Wizard, right?";
			mes "I was so sleepy, I didn't even";
			mes "notice. Ah, this is good~";
			next;
			mes "[Meow]";
			mes "Now, I'm supposed to be";
			mes "teaching Wizards this new";
			mes "skill on behalf of the Wizard";
			mes "Guild. Although I don't really";
			mes "want to, I pretty much don't have a choice. So what do you say?";
			next;
			if (select("Nah, I'm too sleepy...:Sure, I'd like to learn a new skill.") == 1) {
				mes "[Meow]";
				mes "^333333*Yaaaawn*^000000";
				mes "Alright. If you wanna";
				mes "learn it later, then come";
				mes "back and remind me about";
				mes "the skill. Well then, back to";
				mes "napping... Zzz... Zzz...";
				close;
			}
			mes "[Meow]";
			mes "Really? Fine, fine.";
			mes "Just so you know, I'm";
			mes "only teaching this skill so";
			mes "that Catherine will return me";
			mes "to my human form. It's too";
			mes "humiliating to just be a cat!";
			next;
			mes "[Meow]";
			mes "Wait, now that I think";
			mes "about it, that dog Maria has";
			mes "been hanging about Catherine";
			mes "for the longest time, but she's";
			mes "never regained her human form.";
			mes "N-no! I can't lose hope now!";
			next;
			mes "[Meow]";
			mes "Oh, let's just get this over";
			mes "with. Now, you're aware that we";
			mes "Wizards have great long range";
			mes "attack skills, but we're weak";
			mes "in short range battles, right?";
			next;
			mes "[Meow]";
			mes "This new skill that I am";
			mes "going to teach you has been";
			mes "designed to compensate the";
			mes "Wizard class's weakness in";
			mes "close range combat. First,";
			mes "you chant the spell...";
			next;
			mes "[Meow]";
			mes "Then, for the skill's duration,";
			mes "you will be protected by a shield that will push back and damage";
			mes "enemies that approach too closely. Doesn't that sound really useful";
			mes "in heated battle situations?";
			next;
			select("I can push enemies away?");
			mes "[Meow]";
			mes "Sure you can, well, once you";
			mes "learn the skill. Hmm, but you";
			mes "need to have a good grasp on";
			mes "push back theory before you can";
			mes "learn the skill. Hmm, you might";
			mes "need a better explanation...";
			next;
			mes "[Meow]";
			mes "You know what? Knights are";
			mes "pretty much experts with skills";
			mes "that push enemies back, so go";
			mes "and find one who'll explain Push Back theory to you in Prontera.";
			next;
			mes "[Meow]";
			mes "Check the 11 o' clock";
			mes "direction on your Mini-Map";
			mes "in Prontera to find the Knight";
			mes "Chivalry. Someone there should";
			mes "be willing to teach you more";
			mes "about Push Backs in skills...";
			set WIZ_SK,1;
			close;
		}
		else if (WIZ_SK == 1) {
			mes "[Meow]";
			mes "What are you still doing";
			mes "here? Didn't I tell you to go";
			mes "to the Prontera Chivalry and";
			mes "ask someone there to teach";
			mes "you about Push Back theory?";
			close;
		}
		else if (WIZ_SK == 2) {
			mes "[Meow]";
			mes "Hm? Oh good, you learned";
			mes "the basics for pushing back";
			mes "enemies. Now you're almost";
			mes "ready to learn the skill that";
			mes "I'm supposed to teach you.";
			next;
			mes "[Meow]";
			mes "I'm no philanthropist, so";
			mes "I expect some tuition for all";
			mes "of my instruction and effort!";
			mes "Mmm, what would be good for";
			mes "my payment? Ah, I'm low on";
			mes "enchanted stones right now~";
			next;
			mes "[Meow]";
			mes "Go and bring me";
			mes "^FF000010 Crystal Blues^000000,";
			mes "^FF000010 Green Lives^000000,";
			mes "^FF000010 Red Bloods^000000 and";
			mes "^FF000010 Winds of Verdure^000000.";
			next;
			mes "[Meow]";
			mes "It's not that much,";
			mes "so don't complain and";
			mes "come back as soon as you";
			mes "can. While you're out there,";
			mes "I'm going to take another nap~";
			set WIZ_SK,3;
			close;
		}
		else if (WIZ_SK == 3) {
			if (countitem(991) < 10 || countitem(993) < 10 || countitem(990) < 10 || countitem(992) < 10) {
				mes "[Meow]";
				mes "^333333*Yaaaaaawn~*^000000 Hm?";
				mes "H-how dare you come";
				mes "back without everything";
				mes "I asked you for?! You";
				mes "forgot what to bring,";
				mes "didn't you? Grrrr...";
				next;
				mes "[Meow]";
				mes "I said to bring";
				mes "^ff000010 Crystal Blues^000000,";
				mes "^ff000010 Green Lives^000000,";
				mes "^ff000010 Red Bloods^000000 and";
				mes "^ff000010 Winds of Verdure^000000!";
				next;
				mes "[Meow]";
				mes "Don't forget this time!";
				mes "And don't even think of";
				mes "waking me up until you";
				mes "find everything I asked for!";
				close;
			}
			mes "[Meow]";
			mes "*Yaaaawn~* Wh-wha?";
			mes "Oh, it's you. So did you";
			mes "bring the stones I wanted?";
			mes "Mmm. Good, let me see if";
			mes "you brought them all...";
			mes "One, two, three...";
			next;
			mes "[Meow]";
			mes ".................";
			mes "Screw it. I don't feel";
			mes "like counting this all.";
			mes "Let me just take these,";
			mes "and teach you the skill";
			mes "so I can go back to sleep.";
			next;
			mes "[Meow]";
			mes "This skill is called,";
			mes "''^FF0000Sight Blaster^000000.'' Pretty";
			mes "cool, eh? Anyway, it's a Fire";
			mes "attribute skill that requires";
			mes "you to cast Sight before you";
			mes "can activate it. Now try it.";
			next;
			specialeffect2 EF_SUI_EXPLOSION;
			mes "^3355FF*BAM! BOOM!*^000000";
			next;
			mes "[Meow]";
			mes "......";
			mes "Huh. Not exactly right,";
			mes "but not too bad for a first";
			mes "try. Remember to will the";
			mes "push back to happen. Ergh,";
			mes "let's try it once again, okay?";
			next;
			mes "[Meow]";
			mes "You gotta will it.";
			mes "Push! Push with";
			mes "your freakin' mind!";
			mes "Well, push the flames";
			mes "you summoned with Sight";
			mes "with your mind. Not that hard!";
			next;
			mes "[Meow]";
			mes "Finally...!";
			mes "You actually learned it!";
			mes "That took way too much effort,";
			mes "but at least we're done here.";
			specialeffect2 EF_ABSORBSPIRITS;
			delitem 991,10; //Crystal_Blue
			delitem 993,10; //Yellow_Live
			delitem 990,10; //Boody_Red
			delitem 992,10; //Wind_Of_Verdure
			set WIZ_SK,100;
			skill "WZ_SIGHTBLASTER",1,0;
			next;
			mes "[Meow]";
			mes "I'm so tired from all of";
			mes "this teaching. I think I'll";
			mes "take a well deserved nap";
			mes "right now. Go on your way,";
			mes "and if you know any Wizards,";
			mes "send 'em to me, I guess.";
			close;
		}
		else {
			mes "[Meow]";
			mes "Zzz... Zzz...";
			mes "Catherine... I'll";
			mes "make you sorry for";
			mes "turning me into a kitty...";
			mes "I'm gonna... Gonna... Zzz...";
			close;
		}
	}
	else {
		mes "[Meow]";
		mes "*Yaaawn~*";
		mes "So sleeepy...";
		mes "Hm? Did you want";
		mes "to talk to me? Don't";
		mes "bother me if you've";
		mes "got nothing to say!";
		next;
		if (select("A cat?:Now I'm sleepy, too...") == 1) {
			mes "[Meow]";
			mes "Yeah. Yeah, I'm a cat.";
			mes "For now, anyway. Damn that";
			mes "Catherine! She turned me into";
			mes "this form when I borrowed some";
			mes "gemstones without her consent.";
			mes "I was gonna tell her!";
			next;
			mes "[Meow]";
			mes "Once I become human";
			mes "again and regain my powers,";
			mes "I'm gonna go freakin' buck";
			mes "wild. Catherine, you, everyone.";
			mes "You're all turning into cats!";
			close;
		}
		mes "[Meow]";
		mes "Hah! Yeah...";
		mes "Taking a nap sounds like";
		mes "the best idea I've heard";
		mes "all day. Hm. ^333333*Yaaawwn~*^000000";
		mes "There we go, time to sleep...";
		mes "ZzzzzzzZZZzzz... ZzZzzzz...";
		close;
	}
}

prt_in,82,98,3	script	Simon Mayace#q_wiz	46,{
	if (WIZ_SK == 1) {
		mes "[Simon]";
		mes "Ah, you must be a skilled";
		mes "practitioner of magic. Mmm,";
		mes "you must one of those Wizards.";
		mes "High Wizards? I can't really";
		mes "tell the difference. Ah, may";
		mes "I be of service to you?";
		next;
		if (select("I'm fine, thank you.:I want to learn about push backs.") == 1) {
			mes "[Simon]";
			mes "I'm surprised that you chose";
			mes "to practice magic: judging";
			mes "from your build, I'd say that";
			mes "you'd be well suited to wielding a sword. Haha, but that's just";
			mes "a humble Knight's opinion.";
			close;
		}
		mes "[Simon]";
		mes "Wait, are you asking me";
		mes "to teach you a Push-Back";
		mes "attack technique? You can";
		mes "defend yourself using spells";
		mes "like Fire Wall or Fire Bolt";
		mes "much more effectively, right?";
		next;
		select("But at close range...");
		mes "[Simon]";
		mes "Oh, I see. Yeah, Wizards";
		mes "aren't very effective in close";
		mes "range battles. Okay, I can see";
		mes "why you'd want to learn some";
		mes "Push-Back technique. Alright,";
		mes "I can teach you a few basics.";
		next;
		mes "[Simon]";
		mes "Now, every adventurer knows";
		mes "the fundamentals of attacking.";
		mes "You strike, and then ready your";
		mes "next action, which is either";
		mes "another strike, a dodge or a";
		mes "guard. Simple stuff, right?";
		next;
		mes "[Simon]";
		mes "To push an enemy back, you've";
		mes "got to attack using a strike";
		mes "with continuous force. It's all";
		mes "in the follow through. Now, if";
		mes "you strike the head, the push";
		mes "distance will be too short.";
		next;
		mes "[Simon]";
		mes "If you deliver a pushing";
		mes "strike to the legs, your";
		mes "enemy will fall towards you.";
		mes "The best target for pushing";
		mes "attacks is the torso since it";
		mes "allows more control of the push.";
		next;
		mes "[Simon]";
		mes "Well, I've explained";
		mes "pushing as best as I can.";
		mes "Concentrate and practice the";
		mes "theory on your own, and you";
		mes "should be able to do it. Good luck on your travels, adventurer~";
		set WIZ_SK,2;
		close;
	}
	else {
		mes "[Simon]";
		mes "Make no mistake:";
		mes "swordplay is an art.";
		mes "The techniques may seem";
		mes "brutish at first glance, but";
		mes "there's a keen strategy that";
		mes "Knights innately understand.";
		close;
	}
}

//============================================================ 
// Old changelog
//============================================================ 
//= 1.0 for fully working skills only [Lupus]
//= 1.1 Added more new skill quests for more classes [Lupus]
//= Somehow eA engine doesn't let you keep learn't skill V_V'
//= 1.2 Added to correct locations, correct NPC's, fixed
//= some of the items required and made them into real
//= quests. [Reddozen] 
//= 1.3 Fixed bugs and minor typos. Optimized [Lupus]
//= 1.3a fixed an item ID typo, thx 2Spiritual Kid
//= 1.3b Splitted into different files [DracoRPG]
//= 1.3c Removed duplicates [Toms]
//============================================================
