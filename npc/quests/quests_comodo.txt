//===== eAthena Script ======================================= 
//= Item Quest NPCs located in Comodo
//===== By: ================================================== 
//= kobra_k88
//===== Current Version: ===================================== 
//= 1.0 
//===== Compatible With: ===================================== 
//= eAthena 7.15 +
//===== Description: ========================================= 
//= 'Cross Hat', 'Bulb Hairband', 'Striped Hairband', 'Blue Hairband' Quests.
//= 'Mine Helmet', 'Parcel Hat', 'Money Loser's Grief',
//= 'Phantom of the Opera Mask' Quests.
//===== Additional Comments: ================================= 
//= Fully working
//============================================================ 



//============================================================================================//
// 					Hair Ornament Girl 
//============================================================================================//
// Cross Hat, Bulb Hairband, Striped Hairband, Blue Hairband
//==================================================
comodo.gat,227,158,4	script	Hair Ornament Girl	700,{
	mes "[Hair Ornament Girl]";
	if(sex==1)mes "Oh, hi.  Do you need to buy a present for a friend, your mother, or maybe your girl friend? Then get her a Hair Band! It's the best gift you can get her.";
	if(sex==0)mes "Oh, hi.  Do you need to buy a present for a friend, your mother, or maybe yourself? Then get a Hair Band! It's the best gift you can give, or get.";
	next;
	mes "[Hair Ornament Girl]";
	mes "Just by bringing me a few simple items, I can make you a gorgeous Hair Band!  Here, take a look at these....";
M_Menu:
	next;
	menu "-Crosss Hat Hairband",M_Cross, "-Bulb Hairband",M_Bulb, "-Striped Hairband",M_Striped, "-Blue Hairband",M_Blue, "-End Conversation",M_End;

	M_Cross:
		set @BAND$, "Cross Hat Hairband";
		set @ITEMS$, " ^5555FF1 Rosary^000000 and ^5555FF500 Destroyed Armors^000000.";
		if (countitem(2608)>=1 && countitem(7069)>=500) set @GETBAND, 1;
		goto sL_0;

	M_Bulb:
		set @BAND$, "Bulb Hairband";
		set @ITEMS$, "^5555FF1 Slotted Circlet^000000 and ^5555FF20 Glass Beads^000000.";
		if (countitem(2233)>=1 && countitem(746)>=20) set @GETBAND, 2;
		goto sL_0;

	M_Striped:
		set @BAND$, "Striped Hairband";
		set @ITEMS$, "^5555FF1500 Worn-out Prison Uniforms^000000.";
		if (countitem(1099)>=1500) set @GETBAND, 3;
		goto sL_0;

	M_Blue:
		set @BAND$, "Blue Hairband";
		set @ITEMS$, "^5555FF1 Bandana^000000, ^5555FF1 Cobaltblue Dyestuffs^000000, and ^5555FF300 Anolian Skins^000000.";
		if (countitem(2211)>=1 && countitem(978)>=1 && countitem(7003)>=300) set @GETBAND, 4;

		sL_0:
			mes "[Hair Ornament Girl]";
			mes "So you're interested in the "+@BAND$+" huh? Here are the items you'll need for the "+@BAND$+":";
			mes @ITEMS$;
			next;
			if (@GETBAND > 0) goto sL_GetBand;

			mes "[Hair Ornament Girl]";
			mes "Bring me all of these items and I will be able to make you a "+@BAND$+".";
			goto M_Menu;

		sL_GetBand:
			mes "[Hair Ornament Girl]";
			mes "I see that you have enough items for a "+@BAND$+".  Would you like me to make you one?";
			next;
			menu "Yes, I would.",sM_Yes, "No thank you.",M_End;

			sM_Yes:
				if (@GETBAND == 1) goto ssL_Cross;
				if (@GETBAND == 2) goto ssL_Bulb;
				if (@GETBAND == 3) goto ssL_Striped;
				if (@GETBAND == 4) goto ssL_Blue;

				ssL_Cross:
					delitem 2608,1;
					delitem 7069,500;
					getitem  5036,1;
					goto ssL_End;
				ssL_Bulb:
					delitem 2233,1;
					delitem 746,20;
					getitem 5034,1;
					goto ssL_End;
				ssL_Striped:
					delitem 1099,1500;
					getitem 5049,1;
					goto ssL_End;
				ssL_Blue:
					delitem 2211,1;
					delitem 978,1;
					delitem 7003,300;
					getitem 5052,1;
				ssL_End:
					mes "[Hair Ornament Girl]";
					mes "Here is your "+@BAND$+".  Thank you.";
					set @GETBAND, 0;
					close;

	M_End:
		mes "[Hair Ornament Girl]";
		mes "Hope to see you again soon.";
		close;

}


//===================================================================================================//
//						Mari Isac
//===================================================================================================//
// 'Mine Helmet', 'Parcel Hat', 'Money Loser's Grief', 'Phantom of the Opera Mask'
//===============================================================//
comodo.gat,274,137,5	script	Mari Isac	702,{
	mes "[Mari Isac]";
	mes "I travel all over the world and get to hear many interesting stories from the people I meet.";
	mes "Sometimes they even teach me how to make special items";
	next;
	mes "[Mari Isac]";
	mes "Would you be interested in some of the items I've learned to make?";
M_Menu:
	next;
	menu "Tell me about them.",M_0, "Make me one.",M_1;

	M_0:
		menu "-Mine Helmet",sM_Mine, "-Parcel Hat",sM_Parcel, "-Money Loser's Grief",sM_Money, "-Phantom of the Opera Mask",sM_Phantom;

		sM_Mine:
			mes "[Mari Isac]";
			mes "I learned how to make the Mine Helmet when I traveled to the old coal mines near Geffen.";
			next;
			mes "[Mari Isac]";
			mes "The 2 most important things a miner needs are light, and head protection.";
			mes "A very inventive miner was able to create something that dealt with both issues.";
			next;
			mes "[Mari Isac]";
			mes "He attached a light to his safety helmet and called it a Mine Helmet.";
			mes "This meant that miners no longer had to worry about carrying around lamps and could work more efficiently.";
			next;
			mes "[Mari Isac]";
			mes "Anyways, if you want me to make you a Mine Helmet you'll need to bring me these items:";
			mes "^5555FF1 Safety Helmet^000000,";
			mes "^5555FF1 Candle^000000,";
			mes "^5555FF1 Crystal Mirror^000000,";
			mes "^5555FF25 Steel^000000.";
			next;
			goto M_0;
		sM_Parcel:
			mes "[Mari Isac]";
			mes "I learned how to make the Parcel Hat when I traveled to Alberta.  There they have a unique way of distributing mail and goods.";
			next;
			mes "[Mari Isac]";
			mes "Instead of using a pouch or a sack to carry items for delivery, they use a special kind of hat, called a Parcel Hat.";
			mes "This hat allows the delivery personel to carry numerous items on their heads.  Of course they have to have good balance to use it.";
			next;
			mes "[Mari Isac]";
			mes "Anyways, if you want me to make you a Parcel Hat, you'll need to bring me these items.";
			mes "^5555FF150 Fabric^000000,";
			mes "^5555FF100 Resin^000000,";
			mes "^5555FF1 Cobaltblue Dyestuffs^000000.";
			next;
			goto M_0;
		sM_Money:
			mes "[Mari Isac]";
			mes "I learned how to make the Money Loser's Grief when I traveled to Morroc.";
			next;
			mes "[Mari Isac]";
			mes "A long time ago an honest merchant got into a conflict with a crooked guild.";
			mes "Unlike the dirty guild, the merchant did not scam or cheat people.";
			next;
			mes "[Mari Isac]";
			mes "Because of this they attacked the merchants shop and took all of his goods.";
			next;
			mes "[Mari Isac]";
			mes "The merchant was devastated by the loss of his shop.";
			mes "In his sadness he used the bits and pieces that were left from his shop to create a new item.";
			next;
			mes "[Mari Isac]";
			mes "He created the Money Loser's Grief, a symbol of his great sadness.  When people see it, they can't help but feel sympathetic to the wearer.";
			next;
			mes "[Mari Isac]";
			mes "Many merchants now wear it to help themselves get more business.  Of course it is also a reminder of the honest merchants sad tale.";
			next;
			mes "[Mari Isac]";
			mes "Anyways, if you want me to make you a Money Losers Grief, you'll need to bring me these items.";
			mes "^5555FF1 Circlet with a single slott^000000,";
			mes "^5555FF1 Gold^000000,";
			mes "^5555FF20 Steel^000000,";
			mes "^5555FF80 Feathers^000000,";
			mes "^5555FF800 Sticky Mucus^000000.";
			next;
			goto M_0;
		sM_Phantom:
			mes "[Mari Isac]";
			mes "I learned how to make the Phantom of the Opera Mask when I traveled to Al De Baran.";
			mes "The story behind this item is that of a man's pure love........";
			next;
			mes "[Mari Isac]";
			mes "There once was a man who was in love with a beautiful opera singer.";	
			mes "Unfortunately the man had a terribly disfigured face which forced to him to wear a mask.";
			next;
			mes "[Mari Isac]";
			mes "He was ashamed of his appearance and could never bring himself to face the singer.";
			mes "The man instead, watched her from a far, admiring every nuance of her beauty and grace.";
			next;
			mes "[Mari Isac]";
			mes "He lurked in the bowles of the opera house and from time to time he would sing his sad love song.";
			mes "Many people heard it and believed that there was a phantom haunting the opera house.";
			next;
			mes "[Mari Isac]";
			mes "The singer also heard his song, and through it she was able to feel his pain and his love for her.";
			mes "The two eventually met, and when they did she was able to look past his appereance and accept him for who he was.";
			next;
			mes "[Mari Isac]";
			mes "Too this day, the tale of the masked 'phantom' is still romantacised.";
			next;
			mes "[Mari Isac]";
			mes "Anyways, if you want me to make you a Phantom of the Opera Mask, you'll need to bring me these items.";
			mes "^5555FF1 Opera Masque^000000,";
			mes "^5555FF50 Horrendous Hairs^000000.";
			goto M_0;

	M_1:
		menu "-Mine Helmet",sM_MakeMine, "-Parcel Hat",sM_MakePar, "-Money Loser's Grief",sM_MakeMon, "-Phantom of the Opera Mask",sM_MakePhan;

		sM_MakeMine:
			if (countitem(5009)<1 || countitem(5028)<1 || countitem(747)<1 || countitem(999)<25) goto ssL_NoItems;
			mes "[Mari Isac]";
			mes "Here you go.  Enjoy your Mine Helmet!";
			delitem 5009, 1;
			delitem 5028, 1;
			delitem 747, 1;
			delitem 999, 25;
			getitem 5031,1;
			close;
		sM_MakePar:
			if (countitem(1059)<150 || countitem(907)<100 || countitem(978)<1) goto ssL_NoItems;
			mes "[Mari Isac]";
			mes "Here you go.  Enjoy your Parcel Hat!";
			delitem 1059, 150;
			delitem 907, 100;
			delitem 978, 1;
			getitem 5023,1;
			close;
		sM_MakeMon:
			if (countitem(2233)<1 || countitem(969)<1 || countitem(999)<20 || countitem(949)<80 || countitem(938)<800) goto ssL_NoItems;
			mes "[Mari Isac]";
			mes "Here you go.  Enjoy your Money Loser's Grief!";
			delitem 2233, 1;
			delitem 969, 1;
			delitem 999, 20;
			delitem 949, 80;
			delitem 938, 800;
			getitem 5021,1;
			close;
		sM_MakePhan:
			if (countitem(2281)<1 || countitem(1048)<50) goto ssL_NoItems; 
			mes "[Mari Isac]";
			mes "Here you go.  Enjoy your Phantom of the Opera Mask!";
			delitem 2281,1;
			delitem 1048, 50;
			getitem 5043, 1;
			close;

			ssL_NoItems:
				mes "[Mari Isac]";
				mes "You don't seem to have enough items for me to make that hat.  Please come back when you do.";
				close;

}
