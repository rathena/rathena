//===== eAthena Script ======================================= 
//= Item Quest NPCs located in Morroc
//===== By: ================================================== 
//= kobra_k88
//===== Current Version: ===================================== 
//= 1.0 
//===== Compatible With: ===================================== 
//= eAthena 7.15 +
//===== Description: ========================================= 
//= Stop Post and Binoculars Quest.
//===== Additional Comments: ================================= 
//= Fully working
//============================================================ 



//==================================================================================//
// 				Stop Post Quest
//==================================================================================//
morocc_in.gat,142,100,4	script	William	89,{
	mes "[William]";
	mes "Welcome to MacMillan's ^3355FF'Post'^000000 Workshop .";
	next;
	mes "[William]";
	mes "My Clan, MacMillan, Has been producing professional grade Traffic Signal Posts for more than 250 years.";
	mes "Nowadays we are taking special orders for our very unique, ^3355FF'Stop Post'^000000.";
	next;
	menu "Could I order a ^3355FF'Stop Post'^000000?",M_Order, "I'm not interested.",M_End;

	M_Order:
		mes "[William]";
		mes "MacMillan's one of a kind ^3355FF'Stop Post'^000000 fits comfortably on a persons head.";
		mes "Not only does it make an interesting head accessory, but it can actually be used as a stop sign as well.";
		next;
		mes "[William]";
		mes "I garauntee that our ^3355FF'Stop Post'^000000 will last for well over 100 years, or my name isn't MacMillan!!";
		next;
		mes "[William]";
		mes "For us to make you a ^3355FF'Stop Post'^000000, we will need:";
		mes "^3355FF91100 Zeny,";
		mes "50 Trunks,";
		mes "and 1 Black Dyestuffs^000000.";
		next;
		if((countitem(1019) >= 50) && (countitem(983) >= 1) && (Zeny >= 91100)) goto sL_GetPost;
		mes "[William]";
		mes "Come back when you have all of the items.";
		close;

		sL_GetPost:
			mes "[William]";
			mes "Here you are! A hand made, one of a kind, ^3355FF'Stop Post '^000000! Thank you for your patronage!";
			getitem 2272,1;
			delitem 1019,50;
			delitem 983,1;
			set Zeny, Zeny - 91100;
			emotion 15;
			close;

	M_End:
		mes "[William]";
		mes "Thanks for stopping by the MacMillan's Workshop.  I hope to see you soon.";
		close;
}


//==================================================================================//
//				Binoculars Quest
//==================================================================================//
morocc_in.gat,76,163,4	script	Alchemist	64,{
	mes "[Marius]";
	mes "Howdy! A new customer........ Wait!!! I know what you're here for....... You're interested in those magical goggles aren't you?";
	next;
	menu "What is that ?",M_What, "Make",M_Make, "Ignore him",M_End;

	M_What:
		mes "[Marius]";
		mes "It is actually called ^3355FF'Binoculars'^000000! It is an opitcal device that allows the wearer to see far away objects.";
		next;
		mes "[Marius]";
		mes "You see, when two lens are placed next to each other in front of your eyes, your brain interperets this image........ blah blah...... blah....";
		next;
		mes "[Marius]";
		mes "............... What's this? You don't seem interested in what I have to say?? I don't think you realize what I'm talking about!";
		emotion 20;
		next;
		mes "[Marius]";
		mes "For us to make you a ^3355FF'Binoculars'^000000, we will need:";
		mes "^3355FF50,000 Zeny,";
		mes "100 Steel,";
		mes "and 1 Geek Glasses.^000000.";
		next;
		mes "[Marius]";
		mes "Oh c'mon! Don't be a jerk! What I'm talking about is REALLY COOL!!";
		close;

	M_Make:
		mes "[Marius]";
		if((countitem(2243) < 1) || (countitem(999) < 100) || (Zeny < 50000)) goto sL_NotEnuf;
		mes "Gr~~~~eat ! You'll definitely enjoy a pair of these!";
		next;
		mes "[Marius]";
		mes "Here it is ! The Binoculars !";
		getitem 2296,1;
		delitem 2243,1;
		delitem 999,100;
		set Zeny, Zeny - 50000;
		next;
		mes "[Marius]";
		mes "But before you go out and use them I have to tell you......";
		if(sex==0) mes "DON'T USE THEM TO BE A PEEPING TOM!!!";
		if(sex==1) mes "Becarefull where you look..... you might see someting you didn't want to........";
		close;

		sL_NotEnuf:
			mes "Argggghhhhhhh !! You didn't bring enough items!! You need:";
			mes "^3355FF1 pair of Geek Glasses, 50,000 Zeny and 100 Steel^000000.";
			emotion 6;
			close;

	M_End:
		mes "[Marius]";
		mes "Hey you! You DARE IGNORE ME!! I am MARIUS the alchemist of ALCHEMISTS!!!!!";
		emotion 23;
		close;
}
