//===== eAthena Script ======================================= 
//= Quest NPCs related to Aldebaran 
//===== By: ================================================== 
//= eAthena Dev Team
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= eAthena 7.15 +
//===== Description: ========================================= 
//= 'Doctor Band', 'Feather Bonnet', 'Opera Masque', 'Sakkat Hat' Quests.
//===== Additional Comments: ================================= 
//= Fully working
//= 1.1 Fixed Doctor Band items [Lupus]
//= 1.2 Removed Duplicates [Silent]
//============================================================ 


//=======================================================================================================//
//			'Doctor Band', 'Feather Bonnet', 'Opera Masque', 'Sakkat Hat' Quest
//=======================================================================================================//
aldeba_in,152,166,4	script	Trader#01	86,{
	mes "[Trader]";
	mes "Muhahaha! I am the Very Famous Enigmatic Dealer! I am always hustling and bustling in and out of Rune Midgard!";
	mes "Take a look at my RARE and UNIQUE items from ALL OVER the WORLD!!";
	next;
	mes "(1)  ^3355FFDoctor Band^000000 :";
	mes "^FF55331 Red Bandana + 50 Iron + 1 Cracked Diamond + 3500 Zeny^000000.";
	mes "(2)  ^3355FFFeather Bonnet^000000 :";
	mes "^FF55331 Romantic Gent + 300 Feather of Birds + 500 Zeny^000000.";
	mes "(3)  ^3355FFOpera Masque^000000 :";
	mes "^FF553320 Iron + 1 Singing Plant + 5000 Zeny^000000.";
	mes "(4)  ^3355FFSakkat Hat^000000 :";
	mes "^FF5533120 Trunk + 10000 Zeny^000000.";
	next;
	menu "Doctor Band",-, "Feather Bonnet",M_1, "Opera Masque",M_2, "Sakkat",M_3;
		
		mes "[Trader]";
		if(countitem(2275) == 0 || countitem(998) < 50 || countitem(733) == 0 || Zeny < 3500) goto L_CantMake;
		delitem 2275,1;//Items: Red Bandana,
		delitem 998,50;//Items: Iron,
		delitem 733,1;//Items: Cracked deamond
		set Zeny,Zeny - 3500;
		mes "Hmm.... Do you have a.... MEDICAL LICENSE?!";
		emotion e_what;
		next;
		mes "[Trader]";
		mes "I've heard about a well-known unlicensed physician 'Cuwaki'... I hope you don't get caught....";
		mes "Anyways it's your own buisness what you do... Take this.";
		getitem 2273,1;//Items: Doctor Band,
		close;
	M_1:
		mes "[Trader]";
		if(countitem(2247) == 0 || countitem(916) < 300 || Zeny < 500) goto L_CantMake;//Items: Romantic Gent, Feather of Birds,
		delitem 2247,1;//Items: Romantic Gent,
		delitem 916,300;//Items: Feather of Birds,
		set Zeny,Zeny - 500;
		mes "Umhahaha. You have good fashion sense.  I know you had a hard time collecting these items, but this bonnet is definately worth it.  Take it.";
		emotion e_heh;
		getitem 5018,1;//Items: Feather Bonnet,
		close;
	M_2:
		mes "[Trader]";
		if(countitem(998) < 20 || countitem(707) == 0 || Zeny < 5000) goto L_CantMake;//Items: Iron, Singing Plant,
		delitem 998,20;//Items: Iron,
		delitem 707,1;//Items: Singing Plant,
		set Zeny,Zeny - 5000;
		mes "This is a pretty nice item.  A little bit creepy looking though.... I think it gives off some kinda weird vibe.  What do you think?";
		next;
		mes "[Trader]";
		mes ".. You like it ..Alright.. Take it!";
		getitem 2281,1;//Items: Opera Masque,
		close;
	M_3:
		mes "[Trader]";
		if(countitem(1019) < 120 || Zeny < 10000) goto L_CantMake;//Items: Trunk,
		delitem 1019,120;//Items: Trunk,
		set Zeny,Zeny - 10000;
		mes "If you have a chance to visit the Village of Payon, please go meet the Sakkat Craftsman.";
		mes "He's never sold a Sakkat to any other dealer but to me.";
		next;
		mes "[Trader]";
		mes "Because only I can recognize a Sakkat hats' quality.  Due to the fact that it's so rare, the Sakkat Hat has become a very popular item!";
		next;
		mes "[Trader]";
		mes "OK! Take it!";
		getitem 2280,1;//Items: Sakkat,
		close;

	L_CantMake:
		mes "You, fool! Check the requirements again..... you're not some kinda idiot are you? C'mon..... give me a break.";
		emotion e_an;
		close;
}