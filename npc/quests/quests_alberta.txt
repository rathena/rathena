//===== eAthena Script ======================================= 
//= Items Quest NPC's located in Alberta
//===== By: ================================================== 
//= kobra_k88
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= eAthena 1.0
//===== Description: ========================================= 
//= Dolls Quest
//= Boy's Cap Quest
//= Antlers Quest
//= Bao Bao, Cresent Hairpin, Fashionbale Glasses, Heart Hairpin Quests
//= Hat of Sun God, Sunday Hat, Mage Hat, Magician Hat Quests
//===== Additional Comments: ================================= 
//= Fully working
//= 1.1 Fixed Spore Doll exploit [Lupus], 1.2 Gramp's Tiger skin label bug
//= 1.3 Fixed item ID 7031 -> 7013
//============================================================ 



//=======================================================================================================//
//						Doll Quest
//=======================================================================================================//
alberta.gat,117,134,8	script	Elin	96,
{
	
	mes "[Elin]";
	if(Sex == 0) mes "Hello, miss. Since you're a girl, you must like dolls, don't you?";
	if(Sex == 1) mes "Hello, mister. Hmm, since you're a boy, you don't like dolls, do you?";
	next;
	mes "[Elin]";
	mes "I LOVE dolls!!";
	emotion 14;
	next;
	mes "[Elin]";
	mes "Ya know..... I REALLY, REALLY, want a new doll.  I hope daddy is going to buy me one for my birthday.....";
	next;
	menu "Aww, you must be happy...",M_1,"How about I give you one now?",M_2;

	M_1:
		mes "[Elin]";
		mes "Yeah, this time I'm hoping for a Yoyo doll.  Hehe, they're so cute! They're my FAVORITE!";
		emotion 33;
		close;
	M_2:
		mes "[Elin]";
		mes "Oh my goodness! Really?? You're such a sweetie.... gimme, gimme!!";
		mes "What kinda doll are you going to give me? Are you really gonna give me one?";
		emotion 0;
		next;
		menu "Puppet",sM_1,"Poring doll",sM_2,"Chonchon doll",sM_3,"Rocker doll",sM_4, "Spore doll",sM_5,
		           "Osiris doll",sM_6,"Baphomet doll",sM_7,"Racoon doll",sM_8,"Yoyo doll",sM_9, "I am as adorable as a doll!",sM_10;
		
		sM_1:
			if(countitem(740) < 1) goto L_Liar;
			mes "[Elin]";
			mes "Wow--! It's a rabbit! It's SOOOO cute!! Thank you SOOO much!";
			emotion 14;
			next;
			mes "[Elin]";
			mes "It's SOOO fluffy... Oh yeah! Let me give you a present too!";
			next;
			mes "(diggs around in her pockets)";
			next;
			mes "[Elin]";
			mes "Ummm ... found it! Here take this!";
			next;
			delitem 740,1;
			getitem 530,1;
			mes "[Elin]";
			mes "I got this from Santa. It's a candy cane~";
			next;
			mes "[Elin]";
			mes "Thanks again for the rabbit doll! I'm gonna HUG this to sleep EVERY night~";
			emotion 15;
			close;


		sM_2:
			if(countitem(741) < 1) goto L_Liar;
			mes "[Elin]";
			mes "Yay! It's a poring doll! It's SOOOO cute! Since you gave me a doll I'll give you this....";
			emotion 14;
			next;
			delitem 741,1;
			getitem 529,1;
			mes "[Elin]";
			mes "Thank you for the doll!";
			emotion 15;
			close;


		sM_3:
			if(countitem(742) < 1) goto L_Liar;
			mes "[Elin]";
			mes "Ew whats this?.... a Chonchon ... doll? ...";
			emotion 16;
			next;
			mes "[Elin]";
			mes "Oh well, since you're giving it to me, I guess it's ok ....";
			next;
			mes "[Elin]";
			mes "Well here's a little something for giving me the doll.  Don't eat too much or you'll get cavities.";
			delitem 742,1;
			getitem 530,1;
			next;
			mes "[Elin]";
			mes ".... chonchon.... (~grumble, grumble~)";
			emotion 7;
			close;

		sM_4:
			if(countitem(752) < 1) goto L_Liar;
			mes "[Elin]";
			mes "Oh! a Rocker doll.  I hate hopping things but this doll's kinda cute.";
			next;
			mes "[Elin]";
			mes "Thank you.  Let me give you something too.";
			next;
			mes "[Elin]";
			mes "Hmm, didn't I get some stuff from my uncle ...";
			next;
			mes "[Elin]";
			mes "Here it is.  We've got tons of it at home so I'll share some with you~";
			delitem 752,1;
			getitem 532,7;
			next;
			mes "[Elin]";
			mes "Thanks again for the doll!";
			emotion 15;
			close;

		sM_5:
			if(countitem(743) < 1) goto L_Liar;
			mes "[Elin]";
			mes "Blehh .... It's a mushroom... Mom's always scolding me for not eating mushrooms ...";
			emotion 4;
			next;
			mes "[Elin]";
			mes "I'll take it anyway, since the doll looks cute.  But I still won't eat mushrooms though ...";
			next;
			mes "[Elin]";
			mes "Hmm, I'll need to give you a present too.";
			next;
			mes "[Elin]";
			mes "My mom made this so please take it.";
			delitem 743,1;
			getitem 538,5;
			next;
			mes "[Litte Kid]";
			mes "Thank you for the doll~";
			emotion 15;
			close;

		sM_6:
			if(countitem(751) < 1) goto L_Liar;
			mes "[Elin]";
			mes "Yay! It's an Osiris doll!";
			next;
			mes "[Elin]";
			mes "Here's something for you too.";
			delitem 751,1;
			getitem 522,2;
			mes "[Elin]";
			mes "Thank you for the doll!";
			close;

		sM_7:
			if(countitem(750) < 1) goto L_Liar;
			mes "[Elin]";
			mes "Yay! It's a Baphomet doll!";
			next;
			mes "[Elin]";
			mes "Here's something for you too.";
			delitem 750,1;
			getitem 525,5;
			mes "[Elin]";
			mes "Thank you for the doll!";
			emotion 15;
			close;

		sM_8:
			if(countitem(754) < 1) goto L_Liar;
			mes "[Elin]";
			mes "Ah, it's a Smokie doll.  I hate Smokies, but the doll's really cute! Thank you!";
			emotion 33;
			next;
			mes "[Elin]";
			mes "Let me give you some of Grandma's home-made cake!";
			delitem 754,1;
			getitem 539,3;
			next;
			mes "[Elin]";
			mes "They're really good so make sure you eat them ok.";
			next;
			mes "[Elin]";
			mes "Thanks again for this cute doll!";
			emotion 15;
			close;

		sM_9:
			if(countitem(753) < 1) goto L_Liar;
			mes "[Elin]";
			mes "WOW--!!! OH MY GOSH! It's a Yoyo doll! This is what I REALLY wanted!! It's SOOOO cute......";
			emotion 14;
			next;
			mes "[Elin]";
			mes "As a thank you, I'll give you this.";
			delitem 753,1;
			getitem 608,1;
			next;
			mes "[Elin]";
			mes "Dad picked them up during his travels.  It's a seed of some kind.";
			mes "We tried planting them at home, but it doesn't seem to grow.";
			next;
			mes "[Elin]";
			mes "Anyway, thank you SOOO MUCH for the Yoyo doll!!!!";
			emotion 15;
			close;

		sM_10:
			mes "[Elin]";
			mes "BLAAAAAAAAHHH!! Don't make fun of me cuz I'm young!";
			emotion 32;
			next;
			mes "[Elin]";
			mes "(mumbles) .....'stupid'.....";
			close;

		L_Liar:
			mes "[Elin]";
			mes "HEY!! You don't have any dolls... I can't believe you would lie to a little kid!!!";
			emotion 0;
			next;
			mes "[Elin]";
			mes "You're a MEANIE!! I HATE YOU!!!..... sniff... sniff.... Waaaaaaaaaaaaaaaaaaaahhhhhh!!";
			emotion 28;
			close;
}


//=======================================================================================================//
//						Boy's Cap' Quest
//=======================================================================================================//
alberta_in.gat,28,145,4	script	Grampa	120,{
	mes "[Grampa]";
	mes "Gasp ..Gasp";
	next;
	mes "[Grampa]";
	mes "When I look back on my younger days... I regret not making better use of my time...";
	next;
	mes "[Grampa]";
	mes "Even so I STILL feel young at heart..... unfortunately my body is very old and is in poor condtion...... Cough !! Cough .. !!";
	next;
	if(countitem(1030) >= 10) menu "Talk.",M_Talk, "Show him Tiger's Footskin.",M_Show, "Cancel.",M_End;
	menu "Talk",M_Talk, "Cancel",M_End;
	
	M_Talk:
		mes "[Grampa]";
		mes "Listen to me..... You must take care of your body as best you can....";
		mes "If you aren't serious about staying healthy now..... you will surely regret it as you get older.";
		next;
		mes "[Grampa]";
		mes "I've tried to restore my body to its youthful, healthy state with all of the best herbs and medicines that money can buy....";
		next;
		mes "[Grampa]";
		mes "Not only have none of them worked, but their emense cost have made me bankurpt!";
		next;
		mes "[Grampa]";
		mes "I have all but given up on trying to restore my youth.... there IS still one last thing I have not yet tried.....";
		next;
		mes "[Grampa]";
		mes "It is the ^3355FFTiger's Footskin^000000, the legendary king of rejuvination!!"; 
		emotion 0;
		next;
		mes "[Grampa]";
		mes "Have you heard of the Tiger's Footskin???.....";
		emotion 1;
		next;
		mes "[Grampa]";
		mes "Just by LOOKING at it, your white hair turns Black again! By SMELLING it, your reflexes become 10x faster!";
		mes "And just a single TASTE can turn and 80 year old man.... INTO AN 18 BOY!!!!";
		next;
		mes "[Grampa]";
		mes "It truely is amazing! Ah, if I could only get my hands on some Tiger's Footskin.... my wishes would at last come true.....";
		next;
		mes "[Grampa]";
		mes "For whoever brings me ^5555FF10 Tiger's Footskins^000000, I would gladly give that person my precious ^3355FFBoys Cap^000000.";
		close;

	M_Show:
		mes "[Grampa]";
		mes "Ohhh !! Th-This is the... this is the legendary.... TIGERS FOOTSKIN!!";
		next;
		mes "[Grampa]";
		mes "Just by LOOKING at it, your white hair turns Black again! By SMELLING it, your reflexes become 10x faster!";
		mes "And just a single TASTE can turn and 80 year old man.... INTO AN 18 BOY!!!!";
		next;
		mes "[Grampa]";
		mes "It is the real Tiger's Footskin !! In my wildest dreams I never thought...... P-P-Please.... I beg you.... let me have it.....";
		next;
		menu "-Give him the Tiger's Footskin.", sM_Give, "Walk away.", sM_End;

		sM_Give:
			mes "[Grampa]";
			mes "T-Thank YOU!! Thank you SO VERY MUCH!!";
			delitem 1030,10;
			getitem 5016,1;
			emotion 15;
			next;
			mes "[Grampa]";
			mes "Now my body can be reinvigorated!!! I will eat it right away!";
			close;

		sM_End:
			mes "[Grampa]";
			mes "H-He-Hey!! Young one!! W-Wa-it! Please.... come back... I beg you.... come... back.... ooohh... foot.... sk... i... n....";
			close;

	M_End:
		mes "[Grampa]";
		mes "Cough Cough !! .. Tiger ....Tiger's ..... Foot ..... skin .....Cough Cough !!";
		close;


}


//=======================================================================================================//
//						Antlers Quest 
//=======================================================================================================//
alberta_in.gat,122,53,4	script	Cherokee	47,{
	mes "[Cherokee]";
	mes "Hey there, I am a Horn Collector.  Sounds great, huh?";
	next;
	mes "[Cherokee]";
	mes "What do you think about Animal horns? I think they are very valuable and are well worth collecting.";
	mes "You can wear them on your head, or even decorate your house with them.";
	next;
	mes "[Cherokee]";
	mes "You know.... there has been a particular horn that I have not been able to find.  It is the ^5555FF'Evil Horn '^000000..";
	next;
	mes "[Cherokee]";
	mes "Some say that an Evil Horn is really not an animal's horn, but one that comes from a Devil!";
	mes "Even so, I really wish I could get my hands on one.";
	next;
	mes "[Cherokee]";
	mes "Say.... do you have any ^5555FF'Evil Horns'^000000?";
	mes "If you offer me ^5533FF20 Evil Horns^000000, I will give you my precious and famed, ^FF3355'Antlers'^000000.  Is it a deal?";
	next;
	menu "You got it!",M_0, "Shut up Dumbo.",M_End;

	M_0:
		mes "[Cherokee]";
		if(countitem(923) < 20) goto L_NotEnuf;
		delitem 923,20;
		mes "Whoah~! This is the first time I've ever seen a real ^3355FF'Evil Horn'^000000!!";
		emotion 5;
		next;
		mes "[Cherokee]";
		mes "Thank you! Here are the Antlers just as I promised you!";
		emotion 15;
		getitem 2284,1;
		next;
		mes "[Cherokee]";
		mes "With your great help I was finally able to realize my long time dream.  I appreciate what you have done! God Bless You ..";
		close;

		L_NotEnuf:
			mes "Hmm.... like I said, I need ^FF5533'20 Evil Horns'^000000.";
			close;

	M_End:
		mes "[Cherokee]";
		mes "Well aren't you a rude person.... I will forgive you however, seeing as we will meet again, I'm sure....";
		close;
}


//=======================================================================================================//
//			Bao Bao, Cresent Hairpin, Fashionbale Glasses, Heart Hairpin
//=======================================================================================================//
alberta.gat,120,53,3	script	Zic	51,
{
	mes "[Zic]";
	mes "Yay! A cool breeze! That's why I love the sea!!";
	emotion 2;
	next;
	mes "[Zic]";
	mes "Huh?... Do you have any business with me? You came to me because I'm a merchant right?";
	emotion 20;
	next;
	mes "[Zic]";
	mes "~Sigh!~ I get tired of my reputation.... I can't get any peace or quite around here.....";
	next;
	mes "[Zic]";
	mes "So what do you wanna get? Tell me, tell me! It's so easy to guess what you want.....";
	next;
	menu "-Bao Bao",M_0, "-Cresent Hairpin",M_1, "-Fashionable Glasses",M_2, "-Heart Hairpin",M_3;

	M_0:
		mes "[Zic]";
		mes "To make a Bao Bao I will need:";	//5042
		mes "- ^5555FF1 Silk Ribbon^000000,";	//10007
		mes "- ^5555FF50 Herioc Emblems^000000.";	//968
		next;
		mes "[Zic]";
		mes "Would you like me to make you one?";
		next;
		menu "Sure.",sM_0a, "No thanks.",M_End;

		sM_0a:
			if (countitem(10007)<1 || countitem(968)<50) goto sL_NotEnuf;
			mes "[Zic]";
			mes "Please wait while I make your Bao Bao......";
			next;
			mes "[Zic]";
			mes "There you are, one Bao Bao.  Enjoy.";
			getitem 5042, 1;
			delitem 10007, 1;
			delitem 968, 50;
			close;
	M_1:
		mes "[Zic]";
		mes "To make a Cresent Hairpin I will need:";	//5048
		mes "- ^5555FF1 Heart Hair Pin^000000,";	//5041
		mes "- ^5555FF10 Steel^000000.";	//999
		next;
		mes "[Zic]";
		mes "Would you like me to make you one?";
		next;
		menu "Sure.",sM_1a, "No thanks.",M_End;

		sM_1a:
			if (countitem(5041)<1 || countitem(999)<10) goto sL_NotEnuf;
			mes "[Zic]";
			mes "Please wait while I make your Cresent Hairpin......";
			next;
			mes "[Zic]";
			mes "There you are, one Cresent Hairpin.  Enjoy.";
			getitem 5048, 1;
			delitem 5041, 1;
			delitem 999, 10;
			close;
	M_2:
		mes "[Zic]";
		mes "To make a pair of Fashionable Glasses I will need:";	//5047
		mes "- ^5555FF1 Jack a Dandy^000000,";	//2271
		mes "- ^5555FF1 Scarlet Dyestuffs^000000.";	//975
		next;
		mes "[Zic]";
		mes "Would you like me to make you one?";
		next;
		menu "Sure.",sM_2a, "No thanks.",M_End;

		sM_2a:
			if (countitem(2271)<1 || countitem(975)<1) goto sL_NotEnuf;
			mes "[Zic]";
			mes "Please wait while I make your Fashionable Glasses......";
			next;
			mes "[Zic]";
			mes "There you are, one Fashionable Glasses.  Enjoy.";
			getitem 5047, 1;
			delitem 2271, 1;
			delitem 975, 1;
			close;
	M_3:
		mes "[Zic]";
		mes "Are you sure you want a simple item like this? Oh well... To make a Heart Hairpin I will need:";	//5041
		mes "- ^5555FF1200 Coral Reefs^000000.";	//7013
		emotion 20;
		next;
		mes "[Zic]";
		mes "Would you like me to make you one?";
		next;
		menu "Sure.",sM_3a, "No thanks.",M_End;

		sM_3a:
			if (countitem(7013)<1200) goto sL_NotEnuf;
			mes "[Zic]";
			mes "Please wait while I make your Hear Hairpin......";
			next;
			mes "[Zic]";
			mes "There you are, one Heart Hairpin.  Enjoy.";
			getitem 5041, 1;
			delitem 7013, 1200;
			close;

		sL_NotEnuf:
			mes "[Zic]";
			mes "Hmm.... you don't have enough items for me to make it.  Come back some other time.";
			close;

	M_End:
		mes "[Zic]";
		mes "See ya around.";
		close;
}


//=======================================================================================================//
//				Hat of Sun God, Sunday Hat, Mage Hat, Magician Hat
//=======================================================================================================//
alberta.gat,135,79,3	script	Tempestra	71,
{
	mes "[Tempestra]";
	mes "Ah...... Such a cool breeze.  It's good to be next to the sea.  I think it was the right choice for me to take a break from my business and come here.";
	emotion 2;
	if(TEMPESTRA == 1) goto sM_Menu;
	next;
	mes "[Tempestra]";
	mes "Oh the sun is so bright today.  I'm glad I brought my hat.  If my skin is exposed to the sun like this everyday, I'm sure to get a sunburn.";
	next;
	mes "[Tempestra]";
	mes "Boy, the heat is making me thirsty.  Nothing would be better right now, than sipping on a perfectly chilled yellow potion.....";
	next;
	menu "Here you go, my treat.",M_0, "Then go get one, sheesh....",M_1;

	M_0:
		if(countitem(503) < 1) goto L_NoPot;
		set TEMPESTRA, 1;
		mes "[Tempestra]";
		mes "Oooh! Thank you so much.  I'm so glad to meet such a friendly person here....";
		emotion 15;
		next;
		mes "[Tempestra]";
		mes "(~gulp~gulp~)";
		next;
		mes "[Tempestra]";
		mes "Hyaaaaaaa!! It's sooo cold!! Thank you.";
		sM_Menu:
		next;
		menu "The weather is really hot, isn't it...",sM_0, "You should wear a hat.",sM_1;

		sM_0:
			mes "[Tempestra]";
			mes "Yes, it's very hot indeed.....";
			next;
			mes "[Tempestra]";
			mes "Luckly I brought a lot of hats along.  I have";
			mes "a ^5555FF'Sunday Hat'^000000,";
			mes "a ^5555FF'Mage Hat'^000000,";
			mes "a ^5555FF'Magician Hat'^000000, and more in my room...";
			next;
			mes "[Tempestra]";
			mes "I brought these hats with me so that I could sell them and use the money I made for my vacation expenses....";
			mes "but now it looks like I'll have to wear one.....";
			ssM_Menu:
			next;
			menu "Sunday Hat?",ssM_0a, "Mage Hat?",ssM_0b, "Magician Hat?",ssM_0c, "End Conversation.",ssM_End;

			ssM_0a:
				mes "[Tempestra]";
				mes "A Sunday Hat is made up of:";
				mes "- ^5555FF250 Fabric^000000,";
				mes "- ^5555FF1 Slotted Hat^000000,";
				mes "- ^5555FF1 Slotted Cap^000000,";
				mes "- ^5555FF600 Soft Feathers^000000.";
				mes "If you want, I can make one for you.  Just bring me all of those items.";
				if (countitem(1059)<250 || countitem(2221)<1 || countitem(2227)<1 || countitem(7063)<600) goto ssM_Menu;
				next;
				menu "Make one for me.",-, "Cancel",ssM_End;

					mes "[Tempestra]";
					mes "Please wait just a moment while I make it........";
					next;
					getitem 5032, 1;
					delitem 1059, 250;
					delitem 2221, 1;
					delitem 2227, 1;
					delitem 7063, 600;
					mes "[Tempestra]";
					mes "All done... one Sunday Hat just for you.  Thank you and come back any time.";
					emotion 15;
					close;
			ssM_0b:
				mes "[Tempestra]";
				mes "A Mage Hat is made up of:";
				mes "- ^5555FF1 Wizard Hat^000000,";
				mes "- ^5555FF400 Dragon Scales^000000,";
				mes "- ^5555FF50 Mould Powder^000000,";
				mes "- ^5555FF1 Elder Wilow Card^000000.";
				mes "If you want, I can make one for you.  Just bring me all of those items.";
				if (countitem(2252)<1 || countitem(1036)<400 || countitem(7001)<50 || countitem(4052)<1) goto ssM_Menu;
				next;
				menu "Make one for me.",-, "Cancel",ssM_End;

					mes "[Tempestra]";
					mes "Please wait just a moment while I make it........";
					next;
					getitem 5027, 1;
					delitem 2252, 1;
					delitem 1036, 400;
					delitem 7001, 50;
					delitem 4052, 1;
					mes "[Tempestra]";
					mes "All done... one Mage Hat just for you.  Thank you and come back any time.";
					emotion 15;
					close;
			ssM_0c:
				mes "[Tempestra]";
				mes "A Magician Hat is made up of:";
				mes "- ^5555FF1 Wizard Hat^000000,";
				mes "- ^5555FF450 Ancient Lips^000000,";
				mes "- ^5555FF1200 Solid Shells^000000,";
				mes "If you want, I can make one for you.  Just bring me all of those items.";
				if (countitem(2252)<1 || countitem(1054)<450 || countitem(943)<1200) goto ssM_Menu;
				next;
				menu "Make one for me.",-, "Cancel",ssM_End;

					mes "[Tempestra]";
					mes "Please wait just a moment while I make it........";
					next;
					getitem 5045, 1;
					delitem 2252, 1;
					delitem 1054, 450;
					delitem 943, 1200;
					mes "[Tempestra]";
					mes "All done... one Magician just for you.  Thank you and come back any time.";
					emotion 15;
					close;
			ssM_End:
				close;

		sM_1:
			mes "[Tempestra]";
			mes "Oh but the hats I have are for sale.... if I use them for myself I won't be able to sell them to my customers.";
			next;
			mes "[Tempestra]";
			mes "Because you gave me the potion, I would like to make something for you.  Unfortuately I don't have any materials right now.";
			next;
			mes "[Tempestra]";
			mes "Hmm... If you have:";
			mes "^5555FF1 Amblem of the Sun God";
			mes "10 Gold";
			mes "40 Steel";
			mes "50 Coal";
			mes "and 2 Oridecon^000000..........";
			next;
			mes "[Tempestra]";
			mes "I can make you a ^5555FF'Hat of the Sun God'^000000!! It's a fantastic piece of work! How's that sound?";
			next;
			menu "Alright.",ssM_1a, "Not right now thanks.",ssM_1b;

			ssM_1a:
				if(countitem(7086)<1 || countitem(969)<10 || countitem(999)<40 || countitem(1003)<50 || countitem(984)<2) goto ssL_NotEnuf;
				mes "[Tempestra]";
				mes "Please wait just a moment while I make it........";
				next;
				getitem 5022, 1;
				delitem 7086, 1;
				delitem 969, 10;
				delitem 999, 40;
				delitem 1003, 50;
				delitem 984, 2;
				mes "[Tempestra]";
				mes "All done... one Hat of the Sun God just for you.  Thank you again for the potion, and please come back any time.";
				emotion 15;
				close;

				ssL_NotEnuf:
					mes "[Tempestra]";
					mes "Um.... where are those items exactly???";
					emotion 20;
					close;
			ssM_1b:
				mes "[Tempestra]";
				mes "Come back anytime when you've changed your mind.";
				close;

		L_NoPot:
			mes "[Tempestra]";
			mes "Umm... excuse me but.... where is the potion??";
			emotion 20;
			close;

	M_1:
		mes "[Tempestra]";
		mes "..... How RUDE!!";
		emotion 0;
		close;
}
