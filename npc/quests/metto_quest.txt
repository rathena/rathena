//===== eAthena Script ======================================= 
//= Metto Quest
//===== By: ================================================== 
//= I have no idea.
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= eAthena 7.15 +
//===== Description: ========================================= 
//= Metto Quest
//===== Additional Comments: ================================= 
//= Separated from cities/yuno.txt
//============================================================ 


yuno_in03,25,39,3	script	Metto	709,{
	mes "[Metto]";
	if(metto_q == 2 || metto_q == 4 || metto_q == 6) goto L_Check;
	if(metto_q == 1) goto L_Stang;
	if(metto_q == 3) goto L_Kato;
	if(metto_q == 5) goto L_Cici;
	if(metto_q == 7) goto L_Ruined;
	if(metto_q > 7) goto L_Done;
	mes "Whether it be today, tomorrow, or the next day, I spend every moment I have on my research.";
	mes "My whole life has been dedicated to making............";
	next;
	mes "[Metto]";
	mes "NEW YUNO, my own kingdom.  In order to do that I must research how this city floats in the air.";
	mes "If I'm able to do so..... my goals will no longer be a mere dream, but a reality!!";
	next;
	mes "[Metto]";
	mes "That ^5544FFWagan^000000 thinks my research means nothing......";
	mes "But he doesn't realize that my ideas have been blessed by the Gods themselves!";
	close;

L_Stang:
	mes "I'm going to ask my colleague ^5533FFStangckle^000000 to help me.  I am sure he will be more than willing to help out an old friend.";
	next;
	mes "[Metto]";
	mes "Hmmm.... If I had that fellow's help, this would be much easier.... Pray! Pray to whatever God you believe in that we will succeed!";
	set metto_q, 2;
	close;
L_Kato:
	mes "What??!! That fool Stangckle said he doesn't want to help me?? This is not good...... My research will slow down considerably.";
	mes "......... What will I do now?!?!?";
	emotion e_swt2;
	next;
	mes "[Metto]";
	mes "Luckily I have a backup plan.  I'm certain that my friend and colleague ^5533FFKato^000000 will lend me a hand.";
	next;
	mes "[Metto]";
	mes "If I could only get a chance to study his super robot, the 'Great Z', I could continue my research.......";
	next;
	mes "[Metto]";
	mes "That Stangckle can have it his way for all I care.  Even if he doesn't help me, my research will still go on.... it must!";
	next;
	mes "[Metto]";
	mes "Expect great things from me young friend..... MUHAHAHAHAH!!!!";
	set metto_q, 4;
	emotion e_gg;
	close;
L_Cici:
	mes "WHAT!?!? You're telling me that the very thing I need to continue my research with... the Great Z.... has been DESTROYED!?!?";
	emotion e_wah;
	next;
	mes "[Metto]";
	mes "NOOOOOOO!!! Why?? How?? Why would he do that? Are the Gods cursing me?!?";
	emotion e_an;
	next;
	mes "[Metto]";
	mes "Wait.... I need to calm down...";
	mes "If I can just get the help of professor ^5533FFCiCi^000000 with his 'Riding Engine Theory', I may be able to finish my research.......";
	next;
	mes "[Metto]";
	mes "*sigggghhhh* I still have hope...... YES I DO!! HAHAHA!!!";
	set metto_q, 6;
	emotion e_gg;
	close;
L_Check:
	mes "I better get going so I can check this new thing out..... What are you still doing here?";
	mes "I'll be leaving in a bit.  I need to get ready so why don't you go ahead and just take off.......";
	close;
L_Ruined:
	mes "............................";
	next;
	mes "[Metto]";
	mes "How could something like this happen?? All 3 of my colleagues abandoning me all at once?!?!";
	mes "It's true....... the Gods HAVE forsaken me..... I'm ruined...... RUINED!!!";
	emotion e_sob;
	next;
	mes "[Metto]";
	mes "What else can I do now? Has the time really come for me to let go of all of my research?....";
	mes "Those many, many years of research?........";
	emotion e_sob;
	next;
	mes "[Metto]";
	mes "You must be disappointed...... but please, forget about all of this... forget about me........";
	mes "Who would do this to me? What kind of malicious person would shatter a man's dreams??";
	next;
	mes "[Metto]";
	mes "I suddenly feel tired now....... It's time for me to give up....... It's all over.  Please don't be too disappointed.......";
	set metto_q, 8;
	close;
L_Done:
	mes "Maybe I should look into getting a job change.  I hear it's a popular thing to do these days.  I wonder what profession I should go into?";
	mes "All I know for sure is that I don't want to be a scientist anymore.";
	next;
	mes "[Metto]";
	mes "I'm done with research forever.............";
	close;
}

//=====================================================================
yuno_in01,18,95,5	script	Stangckle	99,{
	mes "[Stangckle]";
	if(metto_q > 2) goto L_Done;
	if(metto_q == 2) goto L_Start;
	mes "I am one of the scientists in this city.  My name is ^5533FFStangckle^000000.";
	mes "Remember my name for it may be useful to you in the future.  Who knows, I may just invent something incredible.";
	close;
L_Start:
	mes "What can I do for you? My colleague Metto is coming here soon, so I don't have much time.";
	mes "So what did you say you needed??";
	next;
	menu "I need to talk to you about Metto!!",-, "Actually I don't need anything.",M_End;

		mes "[Stangckle]";
		mes "......................";
		next;
		mes "[Stangckle]";
		mes "..................................";
		next;
		mes "[Stangckle]";
		mes "Is this true?? Metto doesn't think of me as a colleague, and is only USING me?";
		mes "He sees me as a lowly assistant and is going to STEAL all the CREDIT for our discoveries?!!";
		emotion e_omg;
		next;
		mes "[Stangckle]";
		mes "I can't believe I trusted him..... the rumors were true.... he IS a selfish mad man!!!.......";
		mes "And to think I respected a scumbag like that!!";
		emotion e_pif;
		next;
		mes "[Stangckle]";
		mes "I don't think I can even bear to see his disgusting face anymore.....";
		next;
		mes "[Stangckle]";
		mes "I'm glad that an honest and trustworthy person such as yourself had the guts to tell me the ugly truth about Metto.";
		mes "Thank you for sharing that information with me.";
		next;
		mes "[Stangckle]";
		mes "Now if you'll excuse me, I have some work to do.";
		set metto_q, 3;
		close;
	M_End:
		mes "[Stangckle]";
		mes "What are you doing here then? Stop wasting my time.";
		emotion e_swt;
		close;
L_Done:
	mes "So Metto was truly a mad scientist after all...... I guess he will be the one who will ultimately pay for that choice.......";
	close;
}

//===================================================================
yuno_in01,19,182,3	script	Kato	55,{
	mes "[Kato]";
	if(metto_q > 4) goto L_Done;
	if(metto_q == 4) goto L_Start;
	mes "I am a scientist that specializes in ^5533FFArtificial Intelligence and Robotics^000000.";
	mes "Although I'm not working on a specific project at this moment, my latest creation the 'Great Z' is truly a work of sheer genius!";
	close;

L_Start:
	mes "As a scientist I research many different things.... of course this research is private.  What can I do for you?";
	next;
	menu "I have news from Metto!",-, "Just wanted to say hello.",M_End;

		mes "[Kato]";
		mes "WHAT!!?? Metto said my Great Z is INFERIOR to Stangckles research!!??";
		mes "He.... he said I should rename it to 'LAME Z'??.......";
		emotion e_omg;
		next;
		mes "[Kato]";
		mes "*Sniff* I... I don't believe it.... *sob* how could he say that?..... If... if my invention is no good.....";
		mes "Then I guess I should destroy it.... my Great Z.... *sigh*";
		emotion e_sob;
		next;
		mes "[Kato]";
		mes "How could that Metto be such a heartless bastard?? Thank you for letting me know Metto's TRUE feelings about my work.";
		set metto_q, 5;
		close;
	M_End:
		mes "[Kato]";
		mes "........... Okay.... Um.... Hey........";
		close;

L_Done:
	mes "I am interested in researching artificial intelligence.  You know, like robots.";
	mes "I know that it is something very difficult to accomplish, but where there's a will, there's a way.";
	next;
	mes "[Kato]";
	mes "If you ever see something amazing that is similar to what I described, you'll know that it's my handwork.";
	close;
}

//===================================================================
yuno_in03,179,43,5	script	CiCi	121,{
	mes "[CiCi]";
	if(metto_q > 6) goto L_Done;
	mes "My name is ^5533FFCiCi^000000.  I am one of the ^5533FF3 great scientists^000000 that live in this city.";
	mes "Although there are many great scientists in this town, being one of the top 3 is a true honor don't you think??";
	if(metto_q == 6) goto L_Start;
	close;

L_Start:
	next;
	mes "[CiCi]";
	mes "Heh, I guess I was being a little to egoistical there.... anyways what can I do for you?";
	next;
	menu "Metto wanted me to tell you this......",-, "Nothing, just saying hey.",M_End;

		mes "[CiCi]";
		mes "Hmm?? Metto said that he doesn't need my Riding Engine Theory?? Hmphf! How dare he!!!";
		emotion e_pif;
		next;
		mes "[CiCi]";
		mes "I have half a mind to teach him a little something about TRUE science and INTELLIGENT research!!";
		emotion e_ag;
		next;
		mes "[CiCi]";
		mes "... Whatever.... I don't have the necessary equipment to help him anyways.";
		mes "I guess, either way, he didn't have a chance.";
		next;
		mes "[CiCi]";
		mes "Let him know that I didn't really want to be a part of what he was doing in the first place.";
		mes "He isn't even doing the type of research that could be publicized.........";
		set metto_q, 7;
		close;

	M_End:
		mes "[CiCi]";
		mes "What? That's it? You just wanted to waste some time?...........";
		emotion e_swt;
		close;

L_Done:
	mes "Hmm... I want to improve the Riding Engine but I lack the necessary parts.  What a headache....";
	mes "Metto never offered me any help at all.  This is not going to be easy.......";
	close;
}

//===================================================================
yuno_in01,18,30,3	script	Wagan	85,{
	mes "[Wagan]";
	if(metto_q > 0) goto L_Check;
	mes "I'm sure you know that our village has been able to prosper for many years because of the brilliant research done here.";
	mes "We owe a great deal to those who have dedicated their lives to research and study.";
	next;
	mes "[Wagan]";
	mes "It is because of them that we can enjoy such a wonderful quality of life here.";
	mes "They have been able to work together and support each other which is great to see.";
	next;
	mes "[Wagan]";
	mes "Unfortunately there are a few 'bad apples' that cause problems within this solid community of researchers.";
	next;
	mes "[Wagan]";
	mes "These people just drive me crazy!";
	emotion e_an;
	next;
	mes "[Wagan]";
	mes "They think only of themselves and their crazy actions make it much more difficult for everyone else.";
	mes "Out of those few selfish mad men, ^FF3333'Metto'^000000 must be the worst by far.";
	next;
	mes "[Wagan]";
	mes "This disturbed individual does nothing but destroy public facilities, create harmful pollutants, disrupt other peoples lives......";
	next;
	mes "[Wagan]";
	mes "..... the list goes on and on.  What's worse..... He tries to justify his dreadful actions by calling it 'research'!!!!";
	mes "He has definitely lost his sanity and has gone too far!!";
	emotion e_ag;
	next;
	mes "[Wagan]";
	mes "If only there was a way to stop this mad man from destroying our community......";
	mes "I just wish there was someone capable and brave enough to put an end to ^FF3333Metto's^000000 insanity.......";
	next;
	menu "Leave it to me.",-, "I wish your city the best of luck...",M_End;

		mes "[Wagan]";
		mes "Oh?? Are you really going to help us stop Metto? Our city would be very grateful if you could do this.......";
		mes "But how exactly do you plan on accomplishing this rather difficult task?";
		emotion e_what;
		next;
		menu "Yeah it's pretty hard....",M_End, "Well, I guess I need to think of a plan now.....",-;

			mes "[Wagan]";
			mes "Yes a solid plan will be necessary to put a stop to Metto.  You will need to trick him into quitting his evil experiments somehow......";
			mes "It will have to be something subtle like....... hmm........";
			next;
			mes "[Wagan]";
			mes "... Sabotaging his experiments in a way that makes it look like he was destined to fail.";
			mes "He is talkative so you may be able to get him to tell you everything you need to know about his work.";
			next;
			mes "[Wagan]";
			mes "To be honest.... I have been thinking about this plan for a while.........";
			set metto_q, 1;
			close;
	M_End:
		mes "[Wagan]";
		mes "We will have to hope that fate will be on our side as we wait for things to unfold......";
		mes "Come to think of it.... Metto is a strong believer in fate..... If we could only use that to your advantage somehow....";
		close;
L_Check:
	if(metto_q == 8) goto L_Thank;
	if(metto_q == 9) goto L_Done;
	mes "How goes your work on sabotaging Metto's experiments?  Hopefully the plan is going well.";
	mes "I'm sorry to involve you in our city's problems, but we really do appreciate you efforts.";
	close;
L_Thank:
	mes "Is this true? You were able to shut down Metto's research? Thank you sooo much for your help.";
	mes "Without Metto causing trouble we have a lot less to worry about.";
	emotion e_ic;
	next;
	mes "[Wagan]";
	mes "The people of this city are grateful to you.  Here is a little token of our appreciation......";
	next;
	mes "[Wagan]";
	mes "Thank you once again for your assistance! I wish you the best of luck in all of your future endeavors!!!";
	getitem 715,10;
	emotion e_thx;
	set metto_q, 9;
	close;
L_Done:
	mes "Things have been much more peaceful around here now that Metto has been shut down.  Thank you once again for your help.";
	emotion e_thx;
	close;
}
