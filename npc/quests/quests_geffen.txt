//===== eAthena Script ======================================= 
//= Quest NPCs related to Geffen
//===== By: ================================================== 
//= kobra_k88 & Samuray22
//===== Current Version: ===================================== 
//= 1.3a
//===== Compatible With: ===================================== 
//= eAthena 7.15 +
//===== Description: ========================================= 
//= Welding Mask and Headset Quests.
//===== Additional Comments: ================================= 
//= 1.1 Fixed exploits [Lupus]
//= 1.2 Removed Duplicates [Silent]
//= 1.3 Updated "Welding Mask" Quest to the Official one. [Samuray22]
//= 1.3a replaced item "names" with item id. Added missing [] [Lupus]
//============================================================ 



//==============================================================================================//
// 					Blacksmith: 'Welding Mask' Quest
//==============================================================================================//
geffen_in,144,166,5	script	Blacksmith	63,{
	mes "[Blacksmith]";
	mes "I am the best Blacksmith! Do you know that?";
	mes "I've been working for 30 years in this hot, bloody Hellfire!";
	mes " I have been a Blacksmith for many years, I invented something really incredible. Wanna Know about it?";
	next;
	switch( select( "Tip", "Not much help to me though...")) {
	case 1:
		mes "[Blacksmith]";
		mes "As Blacksmith, we melt steel into various metal product with incredible high temperature.";
		mes "So we have to face the heat and metallic fluid all the time, our face are almost burnt down and sometimes injured during working... ";
		mes "But with this new item... You don't have to worry any more. Introducing ^3355FF' Welding Mask '^000000 !!";
		next;
		mes "[Blacksmith]";
		mes "It protect your face from the burning heat.";
		mes "This is the most wanted item for anyone!";
		mes "So? If you offer me  ^2244FF'50 Steel '^000000 and ^4422FF2000 Zeny^000000, I will make it for you right away!";
		next;
		switch( select( "Not bad! I will take one.", "It doesn't seem useful.")) {
			case 1:
				if (countitem(999) > 49 && Zeny > 1999) {
					mes "[Blacksmith]";
					mes "Great! I will do it right now!!";
					next;
					delitem 999, 50;
					set Zeny, Zeny-2000;
					getitem 2292, 1;
					mes "[Blacksmith]";
					mes "Now you can enjoy the heat!";
					mes "Good bye...";
					close;
				}
					mes "[Blacksmith]";
					mes "Look! You don't have enough steel and zeny. I can't do it!";
					mes "Do you know about alchemy? It is wHat you need now!";
					mes "Find the steel and zeny quickly then you can do alchemy.";
					close;
			case 2:
				mes "[Blacksmith]";
				mes "Uh-uh... You don't know wHat you have missed!";
				mes "Young man, you should take good care of your face before it gets shrinking.";
				close;
			}
	case 2:
		mes "[Blacksmith]";
		mes "Never mind... I am not ganna waste any more my time talking to you! Leave now! ";
		close;
	}
}



//=============================================================================================//
//					Eric: Headset Quest
//=============================================================================================//
geffen_in,30,71,4	script	Eric	83,{
	mes "[Eric]";
	mes "Please listen to the story of my blessed grand father...";
	emotion e_sob;
	next;
	menu "Listen.",-, "Ask about Items needed.",M_Ask, "Make.",M_Make, "End Conversation.",M_End;

		mes "[Eric]";
		mes "My grand father passed away few years ago.... he was very special to me.....";
		next;
		mes "[Eric]";
		mes "For as long as I can remember, he was always there for me.";
		mes "He even took better care of me than did my own father, who was always busy with work.";
		next;
		mes "[Eric]";
		mes "To me, my grand father's library was always like a 'Fairyland'.";
		mes "I spent most of my time there and I learned a lot of things from his vast collection of books";
		next;
		mes "[Eric]";
		mes "Grand father was interested in alchemy and music, so his library was filled with many books on these subjects.";
		next;
		mes "[Eric]";
		mes "One day while I was in grand fathers library, I discovered an interesting design sheet.....";
		mes "It was a design for what look liked 'Ear Muffs'.... underneath the drawing was a description that read:";
		next;
		mes "[Eric]";
		mes "'When worn on over the ears, anything from music to the faintest whispers can be heard.";
		mes "The sounds are transmitted to the ears through a thin wire.  Project Name: ^5555FFHeadset^000000";
		next;
		mes "[Eric]";
		mes "It was a project that my grand father was working on before he passed away.... unfortunately he was never able to finish it.....";
		next;
		mes "[Eric]";
		mes "That's why I made up my mind to pick up where he left off and finish his beloved project!";
		mes "I met with a few Alchemists and asked them to create a working prototype based on his plans.";
		next;
		mes "[Eric]";
		mes "But they were not able to understand my grand fathers concepts and therefore could not help me.";
		next;
		mes "[Eric]";
		mes "Even so I did not give up and decided to make it myself.  So I studied and researched for many years....";
		mes "And now I have a full working knowledge of my grand fathers invention, the ^5555FFHeadset^000000!!";
		next;
		mes "[Eric]";
		mes "Yet there is still one obsticale that is keeping me from making it.... I do not have the materials necessary to make my grand fathers invention!";
		next;
		mes "[Eric]";
		mes "My only goal in life is to realize my grand fathers ideas and pay tribute to the man I hold so dear to my heart.......";
		emotion e_sob;
		next;
		mes "[Eric]";
		mes "If only I had those materials.... Hmm... would you be iterested in this project? All I ask is that you gather the right items for me....";
		emotion e_hmm;
		close;

	M_Ask:
		mes "[Eric]";
		mes "These are items needed for grand father's Headset:";
		mes "^3355FFSteel^000000 ^FF555540^000000";
		mes "^3355FFOridecon^000000 ^FF55551^000000";
		mes "^3355FFAlcohol^000000 ^FF55551^000000";
		mes "^3355FFCoal^000000 ^FF55551^000000";
		close;

	M_Make:
		mes "[Eric]";
		if(countitem(999) < 40 || countitem(984) < 1 || countitem(970)<1 || countitem(1003) < 1) goto L_NotEnuf;
		delitem 999,40;
		delitem 984,1;
		delitem 970,1;
		delitem 1003,1;
		mes "Oh this is GREAT!! You have all of the materials I need to make my grandfather's Headset!";
		emotion e_ic;
		next;
		mes "[Eric]";
		mes "Give me a few minutes to assemble it...............";
		next;
		getitem 5001,1;
		mes "[Eric]";
		mes "FINALLY!! It's complete! My grand father's Headset! Please take them... without your help this wouldn't have been possible.";
		emotion e_gasp;
		next;
		mes "[Eric]";
		mes "Thank so very much! Because of you, I could fullfill my grand father's wish.  I just know he's smiling down on me from Heaven.....";
		emotion e_thx;
		close;

		L_NotEnuf:
			mes "Thank you for trying to help me but..... you don't seem to have all of the items I need.....";
			emotion e_swt;
			close;
	M_End:
		mes "[Eric]";
		mes "Farewell..... boo hoo....";
		emotion e_sob;
		close;
}
