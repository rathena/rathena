//===== eAthena Script ======================================= 
//= Quest NPCs related to Geffen
//===== By: ================================================== 
//= kobra_k88
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= eAthena 7.15 +
//===== Description: ========================================= 
//= Welding Mask and Headset Quests.
//===== Additional Comments: ================================= 
//= 1.1 Fixed exploits [Lupus]
//= 1.2 Removed Duplicates [Silent]
//============================================================ 



//==============================================================================================//
// 					Blacksmith: 'Welding Mask' Quest
//==============================================================================================//
geffen_in,144,166,4	script	Blacksmith#01	63,{
	mes "[Blacksmith]";
	mes "Who am I?!........";
	emotion e_gasp;
	next;
	mes "[Blacksmith]";
	mes "Iam the one and only Veteran Blacksmith here!! Haven't you heard???";
	mes "I've been working for 30 years in this hot, bloody, Hellfire!";
	emotion e_no1;
	next;
	mes "[Blacksmith]";
	mes "Since I became a Blacksmith, I invented something really incredible..... wanna hear about it?";
	next;
	menu "I'd love to.",-, "Well, not really....",M_No;

		mes "[Blacksmith]";
		mes "Blacksmiths are always welding metals under the HOTEST and must grueling conditions!";
		mes "The tremendous heat from the metals is so intense that it almost feels as if your face is going to burn off....";
		emotion e_omg;
		next;
		mes "[Blacksmith]";
		mes "But with my great invention, Blacksmiths no longer have to fear their faces catching on fire! I call it the ^3355FF' Welding Mask'^000000!!";
		emotion e_ic;
		next;
		mes "[Blacksmith]";
		mes "By wearing this over ones face, one will be fully protected from scorching heat and flying debirs.";
		next;
		mes "[Blacksmith]";
		mes "So what do you think? For ^4455FF'50 Steels'^000000 and only ^4455FF2000 Zeny^000000, I can make you one.";
		next;
		menu "Hm.. Not bad. Alright.",-, "No thanks.  I'd have no use for it.",sM_No;

			mes "[Blacksmith]";
			if(countitem(999) < 50 || Zeny < 2000) goto sL_NotEnuf;
			delitem 999,50;
			set Zeny, Zeny - 2000;
			mes "Great! I'll make one right away.......";
			next;	
			mes "~!clonk!~!clank!~!bang!~ ~!clonk!~!clank!~!bang!~";
			next;
			mes "[Blacksmith]";
			mes "Here you are, your very own Welding Mask! It was a pleasure doing buisness with you!";
			getitem 2292,1;
			close;

			sL_NotEnuf:
				mes "Listen here.... I said I needed ^4455FF'50 Steels'^000000 and ^4455FF2000 Zeny^000000 to make you a Welding Mask.";
				emotion e_swt;
				close;

		sM_No:
			mes "[Blacksmith]";
			mes "Oh man... Your gonna regret not getting one.  Trust me sooner or later you're gonna need a ^5555FFWelding Mask^000000.";
			mes "Everybody does.....";
			close;

	M_No:
		mes "[Blacksmith]";
		mes "Well then, stop wasting my time!";
		emotion e_pif;
		close;
}


//=============================================================================================//
//					Eric: Headset Quest
//=============================================================================================//
geffen_in,30,71,4	script	Eric	83,{
	mes "[Eric]";
	mes "Please listen to the story of my blessed grand father...";
	emotion e_sob;
	next;
	menu "Listen.",-, "Ask about Items needed.",M_Ask, "Make.",M_Make, "End Conversation.",M_End;

		mes "[Eric]";
		mes "My grand father passed away few years ago.... he was very special to me.....";
		next;
		mes "[Eric]";
		mes "For as long as I can remember, he was always there for me.";
		mes "He even took better care of me than did my own father, who was always busy with work.";
		next;
		mes "[Eric]";
		mes "To me, my grand father's library was always like a 'Fairyland'.";
		mes "I spent most of my time there and I learned a lot of things from his vast collection of books";
		next;
		mes "[Eric]";
		mes "Grand father was interested in alchemy and music, so his library was filled with many books on these subjects.";
		next;
		mes "[Eric]";
		mes "One day while I was in grand fathers library, I discovered an interesting design sheet.....";
		mes "It was a design for what look liked 'Ear Muffs'.... underneath the drawing was a description that read:";
		next;
		mes "[Eric]";
		mes "'When worn on over the ears, anything from music to the faintest whispers can be heard.";
		mes "The sounds are transmitted to the ears through a thin wire.  Project Name: ^5555FFHeadset^000000";
		next;
		mes "[Eric]";
		mes "It was a project that my grand father was working on before he passed away.... unfortunately he was never able to finish it.....";
		next;
		mes "[Eric]";
		mes "That's why I made up my mind to pick up where he left off and finish his beloved project!";
		mes "I met with a few Alchemists and asked them to create a working prototype based on his plans.";
		next;
		mes "[Eric]";
		mes "But they were not able to understand my grand fathers concepts and therefore could not help me.";
		next;
		mes "[Eric]";
		mes "Even so I did not give up and decided to make it myself.  So I studied and researched for many years....";
		mes "And now I have a full working knowledge of my grand fathers invention, the ^5555FFHeadset^000000!!";
		next;
		mes "[Eric]";
		mes "Yet there is still one obsticale that is keeping me from making it.... I do not have the materials necessary to make my grand fathers invention!";
		next;
		mes "[Eric]";
		mes "My only goal in life is to realize my grand fathers ideas and pay tribute to the man I hold so dear to my heart.......";
		emotion e_sob;
		next;
		mes "[Eric]";
		mes "If only I had those materials.... Hmm... would you be iterested in this project? All I ask is that you gather the right items for me....";
		emotion e_hmm;
		close;

	M_Ask:
		mes "[Eric]";
		mes "These are items needed for grand father's Headset:";
		mes "^3355FFSteel^000000 ^FF555540^000000";
		mes "^3355FFOridecon^000000 ^FF55551^000000";
		mes "^3355FFAlcohol^000000 ^FF55551^000000";
		mes "^3355FFCoal^000000 ^FF55551^000000";
		close;

	M_Make:
		mes "[Eric]";
		if(countitem(999) < 40 || countitem(984) < 1 || countitem(970)<1 || countitem(1003) < 1) goto L_NotEnuf;
		delitem 999,40;
		delitem 984,1;
		delitem 970,1;
		delitem 1003,1;
		mes "Oh this is GREAT!! You have all of the materials I need to make my grandfather's Headset!";
		emotion e_ic;
		next;
		mes "[Eric]";
		mes "Give me a few minutes to assemble it...............";
		next;
		getitem 5001,1;
		mes "[Eric]";
		mes "FINALLY!! It's complete! My grand father's Headset! Please take them... without your help this wouldn't have been possible.";
		emotion e_gasp;
		next;
		mes "[Eric]";
		mes "Thank so very much! Because of you, I could fullfill my grand father's wish.  I just know he's smiling down on me from Heaven.....";
		emotion e_thx;
		close;

		L_NotEnuf:
			mes "Thank you for trying to help me but..... you don't seem to have all of the items I need.....";
			emotion e_swt;
			close;
	M_End:
		mes "[Eric]";
		mes "Farewell..... boo hoo....";
		emotion e_sob;
		close;
}
