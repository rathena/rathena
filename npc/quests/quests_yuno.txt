//===== eAthena Script ======================================= 
//= Quest NPCs related to Yuno
//===== By: ================================================== 
//= eAthena Dev Team
//===== Current Version: ===================================== 
//= 1.4
//===== Compatible With: ===================================== 
//= eAthena 7.15 +
//===== Description: ========================================= 
//= Help the bros and get some rubies or gold. This quest is
//= also used in part of the Alchemist job quest.
//===== Additional Comments: ================================= 
//= v1.1 Re-did the text and re-organized the script.  Corrected some items. [kobra_k88]
//= v1.2 Added part to Bro's for alchemist quest [Darkchild]
//= 1.3 fixed that part of Alch Job Quest. [Lupus]
//= 1.4 Fixed exploit [Lupus]
//============================================================ 


//===========================================================================
yuno_in01,103,158,4	script	Bain	98,{
	set @name$, "[Bain]";
	
OnStart:
	mes @name$;
	if(bros_q == 1) goto L_Check;
	mes "We are going to invent a doll that can talk and communicate! Well, at least that's what I'm trying to do....";
	mes "The idea came from an old tale about a doll that acted like a real person.";
	next;
	mes @name$;
	mes "I figure if someone had the ability to make such a doll back then, then it should definitely be possible today.";
	next;
	mes @name$;
	mes "Unfortunately I lack the proper supplies and parts to actually try and build the doll.";
	mes "Although I can buy some of the things I need, many of these items can only be taken from monsters......";
	next;
	mes @name$;
	mes "I am much to weak to defeat these monsters....... How will I be able to continue this project??.....";
	next;
	menu "I'll get the items for you.",-, "Good luck.",M_End;

		mes @name$;
		mes "Really? Such benevolence is rare in this day and age.  We would be grateful for you assisstance.";
		mes "Here are the items that we will be needing.......";
		callsub sF_List;
		set bros_q, 1;
		close;

	M_End:
		mes @name$;
		mes "We'll need a lot of luck if this is going to happen.  If you know of anyone who can help us, please let us know.";
		close;

L_Check:
	if(countitem(974) < 1 || countitem(612) < 5 || countitem(7068) < 5 || countitem(1003) < 5 || countitem(7043) < 5 || 
		countitem(757) < 3 || countitem(756) < 3 || Zeny < 2000) goto L_NotEnuf;
	delitem 974,1;
	delitem 612,5;
	delitem 7068,5;
	delitem 1003,5;
	delitem 7043,5;
	delitem 757,3;
	delitem 756,3;
	set Zeny, Zeny-2000;
	mes "Thank you very much! You found all of the items I needed.  We could not have completed this project without your help";
	next;
	mes @name$;
	mes "Okay, let's try this out!";
	mes "Hold on for just a second!";
	next;
	mes "-Vrrrmrmrmrmrmrmrrmrmrmmrmrmrmrmmm";
	next;
	mes "-Pakakakakakakakakakakakakakak";
	next;
	mes "-Shakakakakakakakakakakaakakak";
	next;
	mes @name$;
	mes "AHHH it is a success!";
	mes "I don't know if we can do anything else, but this time we have created";
	set @wowitem,rand(2);
	if(@wowitem == 0) mes "5 Ruby!";
	if(@wowitem == 0) getitem 723,5;
	if(@wowitem == 1) mes "3 Gold!";
	if(@wowitem == 1) getitem 969,3;
	if(ALCH_Q == 6) set ALCH_Q2, 2;	//for Alchemist Job Quest
	set bros_q, 0;
	next;
	mes @name$;
	mes "Now that we know that it works,";
	mes "I don't have any need for this.";
	mes "Why don't you take it? Oh,";
	mes "and if you find anymore parts,";
	mes "bring them to me again. Heheheheh.";
	next;
	mes @name$;
	mes "Thanks for helping me out.";
	mes "You have my eternal gratitude.";
	mes "I wish you luck in your journeys!";
	close;

	L_NotEnuf:
		mes "In case you forgot, these are the items that we will need.  I suggest you write them down......";
		callsub sF_List;
		close;

sF_List:
	next;
	mes @name$;
	mes "^5533FF1 Mixture";
	mes "5 Mini-Furnaces";
	mes "5 Burnt Trees";
	mes "5 Coal";
	mes "5 Fine Sand";
	mes "3 Rough Elunium";
	mes "3 Rough Oridecon^000000";
	next;
	mes @name$;
	mes "We will also need ^5533FF2,000 zeny^000000.";
	mes "The money will help pay for the minor expenses invovled in the undertaking of this project.  Good luck and please be safe.";
	return;
}

//=====================================================================
yuno_in01,99,153,4	script	Bajin	47,{
	set @name$, "[Bajin]";
	doevent "Bain::OnStart";
	close;
}