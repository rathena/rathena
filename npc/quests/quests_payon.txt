//===== eAthena Script ======================================= 
//= Quest NPCs related to Payon
//===== By: ================================================== 
//= kobra_k88
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= eAthena 7.15 +
//===== Description: ========================================= 
//= Pretend Murder, Ear Muffs, Oxygen, Derivuchi Cap, Helm of Angel Quests.
//===== Additional Comments: ================================= 
//= Fully working
//= 1.1 Helm of Angel bug fixed [Lupus]
//= 1.2 Fixed skirt of virgin [Lupus]
//= 1.3 Fixed exploits [Lupus]
//= 1.4 Removed Duplicates [Silent]
//= 1.5 Updated Dialogs from "Pretend Murdered Quest" [Samuray22]
//= 1.5a replaced item "names" with item id. [Lupus]
//= 1.6 Updated Dialogs from "Oxygen Mask Quest" [Samuray22]
//============================================================ 



//==========================================================================================//
//				Pretend Murdered quest
//==========================================================================================//
payon,154,170,4	script	Granny	78,{
	if(countitem(1049) > 3 ) {
		mes "[Granny in Payon]";
		mes "Hey... Is this the item for Skirt of Virgin tHat my children will wear for the coming festival...";
		mes "Could you give me some ^3355FF' Skirt of Virgin '^000000 for young girl... I need four of it... please ?";
		mes "I am begging you, please!";
		next;
		switch(select( "Give it to her.","No way!")) {
		case 1:
			delitem 1049,4;
			mes "[Granny in Payon]";
			mes "Uh-uh... Thank you very much! Now I can make Skirt of Virgin for my children.";
			mes "Hey... Oh! Yeah! I was in a stage play when I was young...";
			mes "This is the overcoat I kept when I left the troupe after I got married...";
			next;
			mes "[Granny in Payon]";
			mes "Take it if you like it... please!";
			mes "Good bye! Nice meeting you...";
			getitem 2293,1;
			close;
		case 2:
			mes "[Granny in Payon]";
			mes "Uh-uh... How can I get  ^3355FF'Skirt of Virgin '^000000 by myself... I am so weak and tired.";
			mes "It's such a cold world...";
			close;
		}
	}
		mes "[Granny in Payon]";
		mes "I gather Mushrooms on the Mountain of Payon Everyday for my living. I don't know how I will survive this month.";
		mes "... But festive is coming next month!";
		mes "My children requested for Skirt of Virgin for this festival.";
		mes "For Skirt of Virgin, I need ^3355FF' Skirt of Virgin '^000000 ...";
		next;
		mes "[Granny in Payon]";
		mes "Young man, I have a favor to ask... If you ever find ^3355FF' Skirt of Virgin '^000000... ";
		mes "Please come back to me. I will never forget your kindness...";
		close;
}


//========================================================================================//
//					Ear Muffs Quest
//========================================================================================//
payon_in01,18,10,4	script	Mystic Lady	75,{
	mes "[Mystic Lady]";
	mes "Hi there.";
	next;
	menu "Hello.",-,"Please make me a pair of Ear Muffs.",M_Muffs, "End",M_End;

		mes "[Mystic Lady]";
		mes "My family and I recently came to Payon.";
		mes "Where we came from the weather was very cold so we started a business hand making a special type of ^5555FF'Ear Muffs'^000000";
		next;
		mes "[Mystic Lady]";
		mes "I have tried to continue the family business here in Payon but it has proven to be difficult because of the warm whether.";
		next;
		mes "[Mystic Lady]";
		mes "Although the 'Ear Muffs' aren't very useful here, if you plan on going somewhere that's cold our 'Ear Muffs' are a must have.";
		next;
		mes "[Mystic Lady]";
		mes "All I need are:";
		mes "^5555FF1 Cursed Ruby,";
		mes "1 Headset,";
		mes "200 Feathers,";
		mes "and a 5,000 zeny fee^000000 for my labor.";
		next;
		mes "[Mystic Lady]";
		mes "With those items I can make you a pair of 'Ear Muffs'.  Believe me, they are of the highest quality and look good on just about anybody!";
		close;
	
	M_Muffs:
		if(countitem(724)<1 || countitem(5001)<1 || countitem(949)<200 || zeny<5000) goto sL_NotEnuf;
		delitem 724,1;
		delitem 5001,1;
		delitem 949,200;
		set Zeny, Zeny-5000;
		mes "[Mystic Lady]";
		mes "Oh, I see that you have brought all of the required items.  Just a moment please...";
		next;
		mes "[Mystic Lady]";
		mes "... this goes here... that goes there...... some glue here... a couple stitches over there..........";
		next;
		getitem 2283,1;
		mes "[Mystic Lady]";
		mes "Ah, there you are! One pair of my familys' specialty Ear Muffs.  Enjoy!";
		close;

		sL_NotEnuf:
			mes "[Mystic Lady]";
			mes "I'm sorry but you don't have the required items for me to make you a pair of my familys' specialty Ear Muffs.";
			close;
	M_End:
		close;

}


//========================================================================================//
//					Oxygen Mask Quest
//========================================================================================//
payon_in02,25,71,3	script	Young man#11	86,{
	if(countitem(701) > 4) {
		mes "[Young man]";
		mes "What! Oh! No! No way!";
		mes "Huh... You think that guy is cut... No! No way!!";
		next;
		switch(select( "Show him Ora Ora.","Give him Ora Ora.","Cancel.")) {
		case 1:
			mes "[Young man]";
			mes "Wow! Wow! Wow!";
			mes "This is Ora Ora...";
			mes "I will speak frankly! If you are giving me this I will give you my treasure!";
			mes "How about it? Don't you want it?";
			close;
		case 2:
			delitem 701, 5;
			mes "[Young man]";
			mes "Ha! Ha! Ha! I finally got it! Ora_Ora!";
			mes "Damn Thief Bug!! Now I can kill you all!";
			next;
			mes "[Young man]";
			mes "Ha! Ha! ... Ha! Ha!";
			mes "I am sorry! I am too excited... Mm... I will give you my treasure as I promised!";
			next;
			getitem 5004, 1;
			mes "[Young man]";
			mes "If you breathe through this mouth piece... you can have fresh air all the time!";
			mes "I used to worked by the sea and I brought one back with me.";
			mes "I will give it to you for your kindness since I don't need it any more.";
			next;
			mes "[Young man]";
			mes "Hey! Hey! THat's it! I want to try Ora Ora now!";
			close;
		case 3:
			mes "[Young man]";
			mes "Uh... If it doesn't work on Thief bug...";
			mes "Then I will have no choice but try to like Thief Bug... Hey...";
			close;
		}
	}
		mes "[Young man]";
		mes "Uh...Ah! I can't take it any more!";
		mes "Damn Thief Bug!!";
		next;
		if( select( "Continue talking.","Cancel.") == 1) {
			mes "[Young man]";
			mes "My life was really rough.";
			mes "My parents died when I was little... I had to work really hard to earn my living.";
			next;
			mes "[Young man]";
			mes "It's been ten years now... I got some saving... I dream to have my own house.";
			next;
			mes "[Young man]";
			mes "I can't afford a nice house even I spend all my saving on it...";
			mes "However... I have a big but cheap house now... There weren't many choices for me...";
			next;
			mes "[Young man]";
			mes "And now it is taken over by the damn Thief Bug as their den.";
			next;
			mes "[Young man]";
			mes "I tried anything I could... To hunt them... To drive them away... Nothing really worked. They always come back!";
			mes "I even burned my own house.";
			next;
			mes "[Young man]";
			mes "Ah... I really want them to disappear... Do you think^3355FF Ora Ora^000000 is the answer?";
			mes "I heard it is possible to find it in other countries...";
			mes "If only I got five of it... I can get rid of them forever...";
			close;
		}
			mes "[Young man]";
			mes "I just can't make them disappear no matter how... I am so tired of it... I can't take it any more... WHatever...";
			close;
}


//========================================================================================//
//			Derivuchi Cap(Hat of Petite Diablo), Helm of Angel
//========================================================================================//
payon_in01,56,12,4	script	Young Man#12	89,{
	mes "[Young Man]";
	mes "What is it?............";
	next;
	menu "Can you make me a special item?",-, "Nothing.",M_End;

		mes "[Young Man]";
		mes "I see.... so you know about me huh?.... Ok, tell me what you want.";
		next;
		menu "-Helm of Angel",-, "-Deviruchi Hat",sM_Devir, "-I'll come back later.",M_End;

			mes "[Young Man]";
			mes "For the Helm of Angel I will need:";
			mes "- ^5555FF1 Helm (slotted)^000000.";
			mes "- ^5555FF1 Angel Wing^000000.";
			mes "- ^5555FF5 Fang of Garm^000000.";
			set @HAT, 1;
			next;
			goto sL_GetHat;
		sM_Devir:
			mes "[Young Man]";
			mes "For the Deviruchi Hat I will need:";
			mes "- ^5555FF600 Little Evil Horn^000000.";
			mes "- ^5555FF40 Talon of Griffon^000000.";
			set @HAT, 2;

			sL_GetHat:
				mes "[Young Man]";
				mes "Wait a moment!  Be careful about what items you give me.";
				mes "I do not distinguish between equipment that has been forged or has cards attached to them.";
				mes "If you don't want to loose a precious card or piece of equipment, make sure you don't have it equiped.";
				next;
				mes "[Young Man]";
				mes "Do you have all of the items needed?";
				menu "-Give him the items.",-, "Nevermind.",M_End;

				if(@HAT == 2) goto ssL_Devir;

					if(countitem(2229)<1 || countitem(2254)<1 || countitem(7036)<5) goto ssL_NotEnuf;
					delitem 2229, 1;
					delitem 2254, 1;
					delitem 7036, 5;
					mes "[Young Man]";
					mes "Here is your Helm of Angel.";
					getitem 5025, 1;
					close;
				ssL_Devir:
					if(countitem(1038)<600 || countitem(7048)<40) goto ssL_NotEnuf;
					delitem 1038, 600;
					delitem 7048, 40;
					mes "[Young Man]";
					mes "Here is your Deviruchi Hat.";
					getitem 5038, 1;
					close;
				ssL_NotEnuf:
					mes "[Young Man]";
					mes "You don't have enough items for me to make what you want.";
					close;

	M_End:
		mes "[Young Man]";
		mes "Fine then.....";
		close;
}
