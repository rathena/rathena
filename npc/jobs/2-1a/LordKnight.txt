//===== rAthena Script ======================================= 
//= Advance Class jobchanger after kRO Normals.
//===== By: ==================================================
//= Nana
//= finished, optimized and tested by Lupus
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Advance Class jobchanger after kRO Normals.
//===== Additional Comments: ================================= 
//= 1.1 Made all into functions, additional checks, etc. [Lupus]
//= 1.2 Made numbers into constants. [Vicious]
//= 1.3 Now saves/restores all the quest skills [Lupus]
//============================================================ 

valkyrie,44,39,5	script	Lord Knight#Valkyrie	56,{
	if (ADVJOB == 0 || Upper != 1) {
		set .@karma_d,rand(1,10);
		if (.@karma_d > 4) {
			mes "[Lord Knight]";
			mes "Congratulations.";
			mes "Honor to the warriors!";
			close;
		}
		mes "[Lord Knight]";
		mes "We Knights have an";
		mes "awesome responsibility...";
		mes "To serve and protect.";
		next;
		mes "[Lord Knight]";
		mes "Even at the cost";
		mes "of our own lives,";
		mes "we must safeguard the";
		mes "well being of our comrades.";
		close;
	}
	else if (ADVJOB == Job_Lord_Knight && Class == Job_Swordman_High && JobLevel > 39) {
		mes "[Lord Knight]";
		mes "Your time has come!";
		mes "The world still needs you.";
		mes "Please continue your life";
		mes "as a hero with a new appearance.";
		next;
		mes "[Lord Knight]";
		mes "Would you like";
		mes "to become a Lord Knight?";
		next;
		if (select("No.:Yes.") == 1) {
			mes "[Lord Knight]";
			mes "When you're ready,";
			mes "feel free to come back.";
			mes "Honor to the warriors!";
			close;
		}
		if (SkillPoint) {
			mes "[Lord Knight]";
			mes "It is still possible for you to learn more skills. Please use";
			mes "all of your remaining Skill Points before returning to me.";
			close;
		}
		jobchange Job_Lord_Knight;
		set ADVJOB,0;
		mes "[Lord Knight]";
		mes "Congratulations!";
		mes "As a Lord Knight,";
		mes "I hope that you will be";
		mes "at the forefront of battle,";
		mes "and lead your allies to victory!";
		close;
	}
	mes "[Lord Knight]";
	mes "Welcome";
	mes "to Valhalla,";
	mes "the Hall of Honor.";
	next;
	mes "[Lord Knight]";
	mes "Please make";
	mes "yourself comfortable";
	mes "while you are here.";
	mes "Honor to the warriors!";
	close;
}
