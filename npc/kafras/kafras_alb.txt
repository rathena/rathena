//===== eAthena Script ============
//= Alberta Kafras
//===== By: =========================
//= eAthena Dev Team
//===== Current Version: ===================
//= 2.1b
//===== Compatible With: =====================
//= eAthena 1.0
//===== Description: ============================================
//= Description of argument settings for callfunc "F_Kafra".
//=  arg(0): When set at 0 the default Kafra message is displayed.
//=    When set to 1 the Niflhiem Kafra message is displayed.
//=    When set to 2 the Guild Kafra message is displayed. 
//=  arg(1): Set to 1 to disable teleport menu option. Otherwise set to 0.
//=  arg(2): Set to 1 to disable info menu.  Otherwise set to 0.
//===== Additional Comments: ==========================================
//= v1.1 Now using functions :)
//= v2.1b Minor changes to function calls. Using arguments.
//= This version uses arrays .[kobra_k88]
//=====================================================================


// NorthWest -----------------------------------------------------------------
alberta.gat,28,229,8	script	Kafra	115,{
	cutin "kafra_03",2;
	callfunc "F_KafSetAlb";
	callfunc "F_Kafra",0,0,0;

	M_Save:
		menu "-Save Outside City.",sM_Out, "-Save inside City",sM_In;
		
		sM_Out:
			savepoint "pay_fild03.gat",386,76;
			callfunc "F_KafEnd",0,1;
		sM_In:
			savepoint "alberta.gat",31,231;
			callfunc "F_KafEnd",0,1;
}

// South ---------------------------------------------------------------------
alberta.gat,113,60,5	script	Kafra	112,{
	cutin "kafra_06",2;
	callfunc "F_KafSetAlb";
	callfunc "F_Kafra",0,0,0;

	M_Save:
		savepoint "alberta.gat",117,57;
		callfunc "F_KafEnd",0,1;
}


// Function: Sets variables for Ablerta Kafras -------------------------------
function	script	F_KafSetAlb	{

	setarray @wrpP[0], 900, 1400, 1700, 2400;
	setarray @wrpD$[0], "Payon", "Prontera", "Morroc", "Comodo";
	set @wrpC$[0], @wrpD$[0]+" -> "+@wrpP[0];
	set @wrpC$[1], @wrpD$[1]+" -> "+@wrpP[1];
	set @wrpC$[2], @wrpD$[2]+" -> "+@wrpP[2];
	set @wrpC$[3], @wrpD$[3]+" -> "+@wrpP[3];
	set @wrpC$[4], "Cancel";
	set @wrpC$[5], "";
	set @wrpC$[6], "";
	setarray @viewpX[0], 28, 113, 0, 0;
	setarray @viewpY[0], 229, 60, 0, 0;
	return;
}
