//===== eAthena Script ============
//= Alberta Kafras
//===== By: =========================
//= eAthena Dev Team
//===== Current Version: ===================
//= 2.3
//===== Compatible With: =====================
//= eAthena 1.0
//===== Description: ============================================
//= Description of argument settings for callfunc "F_Kafra".
//=  arg(0): When set at 0 the default Kafra message is displayed.
//=    When set to 1 the Niflhiem Kafra message is displayed.
//=    When set to 2 the Guild Kafra message is displayed. 
//=  arg(1): Set to 1 to disable teleport menu option. Otherwise set to 0.
//=  arg(2): Set to 1 to disable info menu.  Otherwise set to 0.
//===== Additional Comments: ==========================================
//= v1.1 Now using functions :)
//= v2.1b Minor changes to function calls. Using arguments.
//= This version uses arrays .[kobra_k88]
//= 2.2 New Teleport list and prices [Lupus]
//= 2.3 Can't save outside city anymore [Evera]
//=====================================================================


// NorthWest -----------------------------------------------------------------
alberta.gat,28,229,8	script	Kafra::kaf_alberta	115,{
	cutin "kafra_03",2;
	callfunc "F_KafSetAlb";
	callfunc "F_Kafra",0,0,0;

	M_Save:
		savepoint "alberta.gat",31,231;
		callfunc "F_KafEnd",0,1;
}

// South ---------------------------------------------------------------------
alberta.gat,113,60,5	script	Kafra::kaf_alberta2	112,{
	cutin "kafra_06",2;
	callfunc "F_KafSetAlb";
	callfunc "F_Kafra",0,0,0;

	M_Save:
		savepoint "alberta.gat",117,57;
		callfunc "F_KafEnd",0,1;
}


// Function: Sets variables for Ablerta Kafras -------------------------------
function	script	F_KafSetAlb	{
	cleararray @wrpC$[0],"",getarraysize(@wrpC$);
	setarray @wrpP[0], 1200, 1800, 1800;
	setarray @wrpD$[0], "Payon", "Morroc", "Prontera";
	set @wrpC$[0], @wrpD$[0]+" -> "+@wrpP[0];
	set @wrpC$[1], @wrpD$[1]+" -> "+@wrpP[1];
	set @wrpC$[2], @wrpD$[2]+" -> "+@wrpP[2];
	set @wrpC$[3], "Cancel";
	setarray @viewpX[0], 28, 113, 0, 0;
	setarray @viewpY[0], 229, 60, 0, 0;
	return;
}
